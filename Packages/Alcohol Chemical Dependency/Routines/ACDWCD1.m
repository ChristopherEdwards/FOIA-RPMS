ACDWCD1 ;IHS/ADC/EDE/KML - CLIENT DET OP FOR IN/RE/IR/FU/OT;
 ;;4.1;CHEMICAL DEPENDENCY MIS;;MAY 11, 1998
 ;*********************************************************************
 ;//^ACDWDRV3, ^ACDWIIF, ^ACDPFACE
 ;*********************************************************************
 K ACDQUIT
 S ACDP1="" F  S ACDP1=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1)) Q:ACDP1=""  D  Q:$D(ACDQUIT)
 . F ACDP2=0:0 S ACDP2=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2)) Q:'ACDP2  S ACDDA=ACDP2 D ^ACDWIIF S ACDDA=^ACDIIF(ACDP2,"BWP") D ^ACDWVIS D P1 Q:$D(ACDQUIT)
 . Q
 D ^ACDWK
 Q
 ;
P1 ;EP-Output
 ;//^ACDPFACE
 I $D(ACDPFACE),$D(IOF),$E(IOST,1,2)'="P-" W @IOF
 D F W !,"CLIENT NAME:",?40,ACDDFN
 D F W !,"  VETERAN STATUS:",?40,ACDVET
 D F W !,"  AGE/SEX:",?40,$S($G(ACDAGE)'="":ACDAGE,1:"UNKNOWN - ")," YEARS OLD  / ",ACDSEX
 S ACDTRIBN=$O(^AUTTTRI("C",ACDTRIB,0)) I ACDTRIBN S ACDTRIBN=$P(^AUTTTRI(ACDTRIBN,0),U)
 S ACDSTATN=$O(^DIC(5,"C",ACDSTATE,0)) I ACDSTATN S ACDSTATN=$P(^DIC(5,ACDSTATN,0),U)
 D F W !,"  STATE/STATE CODE:",?40,$S($G(ACDSTATN)'="":$E(ACDSTATN,1,35),1:"UNKNOWN")," / ",ACDSTATE
 D F W !,"  TRIBE/TRIBE CODE",?40,$S($G(ACDTRIBN)'="":$E(ACDTRIBN,1,35),1:"UNKNOWN")," / ",ACDTRIB
 D F W !!,"VISIT DATE:",?40,ACDCLIV
 D F W !,"COMPONENT:",?40,ACDCOMCL," / ",ACDCOMTL
 D F W !,"TYPE CONTACT:",?40,ACDCONTL
 D F W !!,"PROVIDER:",?40,ACDPROV
 D F W !,"PRIMARY PROBLEM:",?40,ACDPROBP
 D F W !,"OTHER PROBLEMS:" S ACDLP="" F  D F S ACDLP=$O(ACDPTA(ACDLP)) Q:ACDLP=""  W ?40,ACDLP,!
 D F W !,"TOBACCO USE:",?40,ACDTOB
 D F W !,"DAYS USED ALCOHOL:",?40,ACDDUA,$S(ACDDUA=999:"=UNKNOWN",1:"")
 D F W !,"DAYS USED DRUGS:",?40,ACDDUD,$S(ACDDUD=999:"=UNKNOWN",1:"")
 D F W !,"DRUG TYPE:" S ACDLP="" F  D F S ACDLP=$O(ACDDTA(ACDLP)) Q:ACDLP=""  W ?40,ACDLP,!
 D F W !,"DAYS HOSPITALIZED:",?40,ACDDH,$S(ACDDH=999:"=UNKNOWN",1:"")
 D F W !,"DRUG/ALCOHOL RELATED ARREST:",?40,ACDAAR
 D F W !!,"CLIENT STAGE ALC/SUB:",?40,ACDSUS
 D F W !,"CLIENT STAGE PHYSICAL:",?40,ACDPS
 D F W !,"CLIENT STAGE EMOTIONAL:",?40,ACDES
 D F W !,"CLIENT STAGE SOCIAL:",?40,ACDSS
 D F W !,"CLIENT STAGE CULTURAL:",?40,ACDCS
 D F W !,"CLIENT STAGE BEHAVIORAL:",?40,ACDBS
 D F W !,"CLIENT STAGE VOC/ED:",?40,ACDBV
 D F W !,"STAGING AVERAGE:",?40,ACDSAVG
 D F W !,"CLIENT STATUS:",?40,$S(ACDSTAT="UNKNOWN":"",1:ACDSTAT)
 D F W !!,"RECOMMENDED PLACEMENT:",?40,ACDPLAR_" / "_ACDPLARL
 D F W !,"ACTUAL PLACEMENT:",?40,ACDPLAA_" / "_ACDPLAAL
 D F W !,"DIFFERENCE REASON:",?40,ACDDIF
 D F W !!,"HOURS:",?40,ACDHRS
 W !!,"..........................................",!
 ;
 Q
F ;Form feed
 I $Y+4>IOSL D
 . I '$D(ACDPFACE) D F^ACDWUTL Q
 . I '$D(ZTQUEUED),'$D(ACDSLAVE),$E(IOST,1,2)'="P-" D PAUSE^ACDDEU S:$D(DIRUT) ACDQUIT=1,ACDQ=1
 . W @IOF
 . Q
 Q
