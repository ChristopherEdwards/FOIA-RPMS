ACDWDRV1 ;IHS/ADC/EDE/KML - DRV1 2941117; 
 ;;4.1;CHEMICAL DEPENDENCY MIS;;MAY 11, 1998
 F  K ACDQUIT D D I $D(ACDQUIT) D PRHDR^ACD G ^ACDWK
D ;
 S ACDWDRV(1)=1
 D PRHDR^ACD
 ;S:'$D(ACD27) $P(ACD27,"*",27)="*"
 ;W !,ACD27,!,"* INI/INF/FU/REPORTS *",!,ACD27
 F I=1:1:15 W !,I,"  ",$P($T(@I),";",3) I I=15 S DIR(0)="L^1:15^S ACDRPTS=Y",DIR("A")="RUN REPORT # " W ! D ^DIR S:X["^"!($D(DTOUT)) ACDQUIT=1 Q:$D(ACDQUIT)
 I $D(ACDQUIT) K ACDWDRV Q
 D ^ACDWRQ I $D(ACDQUIT) K ACDQUIT Q
 D ^ACDWQ ;        call to XBDBQUE
 Q
 ;
L ;EP - FOR TASKMAN
 S ACDNW(1)=ACDTO,ACDNW(2)=ACDFR,ACDNW(3)=ACDLOC,ACDNW(4)=ACDRPTS
 S ACDTO=ACDNW(1),ACDFR=ACDNW(2),ACDLOC=ACDNW(3)
 D ACDV
 Q
ACDV ;
 K ^TMP("ACD",ACDJOB,ACDBT)
 S ACDDO=0
 F ACD=ACDFR-.01:0 S ACD=$O(^ACDVIS("B",ACD)) Q:'ACD!(ACD>ACDTO)  D
 . S ACDV=0 F  S ACDV=$O(^ACDVIS("B",ACD,ACDV)) Q:'ACDV  D
 .. Q:$P(^ACDVIS(ACDV,0),U,4)=""  ; prevent subscript error
 .. Q:'$D(ACDCRST($P(^ACDVIS(ACDV,0),U,4)))  ;  type not wanted
 .. S ^TMP("ACD",ACDJOB,ACDBT,"HIT",ACDV)=""
 .. S ACD1=0 F  S ACD1=$O(^ACDIIF("C",ACDV,ACD1)) Q:'ACD1  D
 ... S ^TMP("ACD",ACDJOB,ACDBT,"HIT",ACDV,ACD1)=""
 ... Q
 .. Q
 . Q
 Q
 ;
P ;EP - PRINT REPORT
 K DIRUT
 S ACDNW(1)=ACDTO,ACDNW(2)=ACDFR,ACDNW(3)=ACDLOC,ACDNW(4)=ACDRPTS
 F ACDNW(0)=1:1:$L(ACDNW(4),",")-1 S ACDTO=ACDNW(1),ACDFR=ACDNW(2),ACDLOC=ACDNW(3),(ACDTG,ACDTGSUB)=$P(ACDNW(4),",",ACDNW(0)) D P2 Q:$D(DIRUT)
 Q
 ;
P2 ;
 S ACDV=0
 F  S ACDV=$O(^TMP("ACD",ACDJOB,ACDBT,"HIT",ACDV)) Q:'ACDV  D  Q:$D(DIRUT)
 . S ACDDA=ACDV D ^ACDWVIS
 . S ACD1=0
 . F  S ACD1=$O(^TMP("ACD",ACDJOB,ACDBT,"HIT",ACDV,ACD1)) Q:'ACD1  D  Q:$D(DIRUT)
 .. S ACDDA=ACD1 D ^ACDWIIF
 .. I ACDOK,ACDDFNP D @ACDTG
 .. Q
 . Q
 Q:$D(DIRUT)
 D HED
 Q
 ;
HED ;
 S ACDH(0)=$P($T(@ACDTG),";",3)_U_ACDLOC,ACDWDRV(1)=1 D HV^ACDWUTL
 U IO D H S ACDTG="G"_ACDTG D @ACDTG
 Q
H ;EP
 D H^ACDWDRVA
 Q
1 ;;COMPONENT CODE-TYPE/ SEX/ HOURS/ AGE RANGE/ UNIQ CLI/ # VIS (NEW V4)
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCOMCL_"/"_ACDCOMT,ACDSEX,ACDOTHRS,ACDAGER,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1 Q
2 ;;CT'S BY CONTACT TYPE
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCONTL,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1 Q
3 ;;CT'S BY DRUG TYPE
 D C^ACDWUTL
 S ACDDT="" F  S ACDDT=$O(ACDDTA(ACDDT)) Q:ACDDT=""  D
 .S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDDT,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1
 Q
4 ;;CT'S BY ACTUAL PLACEMENT
 S ACDCBO=ACDPLAA_"/"_ACDPLAA1
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCBO,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1 Q
5 ;;CT'S BY RECOMMENDED PLACEMENT
 S ACDCBO=ACDPLAR_"/"_ACDPLAR1
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCBO,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1 Q
6 ;;CT'S BY PRIMARY PROBLEM
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDPROBP,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1 Q
7 ;;CT'S BY OTHER PROBLEM
 D C1^ACDWUTL
 S ACDPT="" F  S ACDPT=$O(ACDPTA(ACDPT)) Q:ACDPT=""  D
 .S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDPT,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1
 Q
8 ;;CT'S FOR OTHER PROBLEM WITHIN PRIMARY PROBLEM
 ;D C1^ACDWUTL
 S ACDPT="" F  S ACDPT=$O(ACDPTA(ACDPT)) Q:ACDPT=""  D
 .S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDPROBP,ACDPT,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1
 Q
9 ;;CT'S FOR AVE DAYS USED DRUGS WITHIN CONTACT TYPE
 Q:$G(ACDDUD)=999
 S:'ACDDUD ACDDUD=.1 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCONTL,ACDDUD)) ^(ACDDUD)="" S ^(ACDDUD)=^(ACDDUD)+1
 Q
10 ;;CT'S FOR AVE DAYS USED ALCOHOL WITHIN CONTACT TYPE
 Q:$G(ACDDUA)=999
 S:'ACDDUA ACDDUA=.1 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCONTL,ACDDUA)) ^(ACDDUA)="" S ^(ACDDUA)=^(ACDDUA)+1
 Q
11 ;;CT'S FOR DIFFERENCE REASONS WITHIN CONTACT TYPE
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCONTL,ACDDIF,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+1 Q
13 ;;AVE CLIENT STAGE BY COMPONENT
 S ACDADD=0,ACDADD=ACDSUS+ACDPS+ACDES+ACDSS+ACDBS+ACDCS Q:'ACDADD
 S ACDCBO=ACDCOMCL_"/"_ACDCOMT
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCBO,ACDADD)) ^(ACDADD)="" S ^(ACDADD)=^(ACDADD)+6 Q
14 ;;PLACEMENT DIFFERENCE VOLUME
 S ACDCBO=ACDPLAA_"/"_ACDPLAA1,ACDCOMB=ACDPLAR_"/"_ACDPLAR1
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDCOMB,ACDCBO,ACDDIF)) ^(ACDDIF)="" S ^(ACDDIF)=^(ACDDIF)+1 Q
12 ;;DRUG TYPE/ AGE RANGE/ SEX/ ARRESTS / UNIQ CLI  (NEW V4) (****)
 Q:$G(ACDAAR)=999
 S ACDDT="" F  S ACDDT=$O(ACDDTA(ACDDT)) Q:ACDDT=""  D
 . S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDDT,ACDAGER,ACDSEX,ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+ACDAAR Q
 Q
15 ;;CT'S BY HOSPITAL DAYS
 Q:$G(ACDDH)=999
 S:'$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDDFNP)) ^(ACDDFNP)="" S ^(ACDDFNP)=^(ACDDFNP)+ACDDH Q
G2 S ACDC(1)=ACDH(9),ACDC(2)=ACDH(6),ACDC(3)=ACDH(7),ACDC(4)=ACDH(8) D ^ACDWRP5 Q
G3 S ACDC(1)="DRUG TYPE",ACDC(2)=ACDH(6),ACDC(3)=ACDH(7),ACDC(4)=ACDH(8) D ^ACDWRP5 Q
G4 S ACDC(1)=ACDH(10),ACDC(2)=ACDH(6),ACDC(3)=ACDH(7),ACDC(4)=ACDH(8) D ^ACDWRP5 Q
G5 S ACDC(1)=ACDH(11),ACDC(2)=ACDH(6),ACDC(3)=ACDH(7),ACDC(4)=ACDH(8) D ^ACDWRP5 Q
G6 S ACDC(1)=ACDH(51),ACDC(2)=ACDH(6),ACDC(3)=ACDH(7),ACDC(4)=ACDH(8) D ^ACDWRP5 Q
G7 S ACDC(1)=ACDH(52),ACDC(2)=ACDH(6),ACDC(3)=ACDH(7),ACDC(4)=ACDH(8) D ^ACDWRP5 Q
G1 S ACDC(1)=ACDH(54),ACDC(2)=ACDH(53),ACDC(3)="HOURS",ACDC(4)=ACDH(21),ACDC(5)=ACDH(59),ACDC(6)=ACDH(7) D ^ACDWRP91 Q
G8 S ACDC(1)=ACDH(51),ACDC(2)=ACDH(52),ACDC(3)=ACDH(6),ACDC(4)=ACDH(7),ACDC(5)=ACDH(8) D ^ACDWRP6 Q
G9 S ACDC(1)=ACDH(9),ACDC(2)=ACDH(12) D ^ACDWRP14 Q
G10 S ACDC(1)=ACDH(9),ACDC(2)=ACDH(13) D ^ACDWRP14 Q
G11 S ACDC(1)=ACDH(9),ACDC(2)=ACDH(14),ACDC(3)=ACDH(6),ACDC(4)=ACDH(7),ACDC(5)=ACDH(8) D ^ACDWRP6 Q
G13 S ACDC(1)=ACDH(55),ACDC(2)=ACDH(56) D ^ACDWRP13 Q
G14 S ACDC(1)=ACDH(11),ACDC(2)=ACDH(10),ACDC(3)=ACDH(14),ACDC(4)=ACDH(6) D ^ACDWRP15 Q
G12 S ACDC(1)="DRUG TYPE",ACDC(2)=ACDH(21),ACDC(3)=ACDH(53),ACDC(4)=ACDH(60),ACDC(5)=ACDH(59) D ^ACDWRP99 Q
G15 S ACDC(1)=ACDH(59),ACDC(2)=ACDH(61),ACDC(3)=ACDH(58) D ^ACDWRP4 Q
 ;
EOJ ;EP - EOJ FOR XBDBQUE
 K ^TMP("ACD",ACDJOB,ACDBT)
 LOCK -^TMP("ACD",ACDJOB,ACDBT)
 K ACDNW,ACDQ,ACDFAC,ACDAREA,ACDSU,ACDTRB,ACDSTA,ACDCRST
 K ACDAGE,ACDBT,ACDFOLL,ACDH,ACDHRS,ACDJOB,ACDOK,ACDOPT,ACDPLAAL,ACDPLARL,ACDPT,ACDSTAT,ACDTG,ACDTGSUB,ACDVET,ACDWDRV
 Q
