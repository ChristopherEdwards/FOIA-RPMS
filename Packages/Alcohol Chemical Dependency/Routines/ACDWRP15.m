ACDWRP15 ;IHS/ADC/EDE/KML - REPORT GENERATOR;
 ;;4.1;CHEMICAL DEPENDENCY MIS;;MAY 11, 1998
 ;Reports 14,62
 S (ACDIFCNT,ACDSCNT)=0
 I '$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL")) D ^ACDWK Q
 D C S ACDP1="" F  D F Q:$D(DIRUT)  S ACDP1=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1)) Q:ACDP1=""  W !,$E(ACDP1,1,24) D P1 Q:$D(DIRUT)
 I $D(DIRUT) D ^ACDWK Q
 W !!,"CLIENTS WITH NO DIFFERENCE REASON = ",ACDSCNT
 W !!,"CLIENTS WITH A  DIFFERENCE REASON = ",ACDIFCNT
 D ^ACDWK Q
P1 ;
 S ACDP2="" F  S ACDP2=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2)) Q:ACDP2=""  W ?26,$E(ACDP2,1,24) D P2 Q:$D(DIRUT)
 Q
P2 ;
 S ACDP3="" F  S ACDP3=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3)) Q:ACDP3=""  S ACDN0=^(ACDP3) W ?52,$E(ACDP3,1,19),?75,$J(ACDN0,4),! D F Q:$D(DIRUT)  S:ACDP1=ACDP2 ACDSCNT=ACDSCNT+ACDN0 S:ACDP1'=ACDP2 ACDIFCNT=ACDIFCNT+ACDN0
 Q
F ;Form feed
 I $Y+4>IOSL D F^ACDWUTL D:'$D(DIRUT) C
 Q
C ;Column
 W !!,ACDC(1),?26,ACDC(2),?52,ACDC(3),?72,ACDC(4),!,ACDH(1),!
