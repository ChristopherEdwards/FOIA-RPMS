ACDWRP91 ;IHS/ADC/EDE/KML - REPORT GENERATOR; 
 ;;4.1;CHEMICAL DEPENDENCY MIS;;MAY 11, 1998
 ;Report 1
 I '$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL")) D ^ACDWK Q
 S (ACDGTOT,ACDGTOT1)=0
 D C S ACDP1="" F  D F Q:$D(DIRUT)  S ACDP1=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1)) Q:ACDP1=""  W !,$E(ACDP1,1,24) D P1 Q:$D(DIRUT)
 D ^ACDWK Q
P1 ;
 S ACDP2="" F  D F Q:$D(DIRUT)  S ACDP2=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2)) Q:ACDP2=""  W ?26,$E(ACDP2) D P2 Q:$D(DIRUT)
 Q
P2 ;
 S ACDP3="" F  D F Q:$D(DIRUT)  S ACDP3=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3)) Q:ACDP3=""   W ?31,ACDP3 D P3 Q:$D(DIRUT)
 Q
P3 ;
 S ACDP4="" F  D F Q:$D(DIRUT)  S ACDP4=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3,ACDP4)) Q:ACDP4=""   W ?41,ACDP4 D P4 Q:$D(DIRUT)
 Q
 ;
P4 ;
 S (ACDSTOT,ACDSTOT1)=0 F ACDP5=0:0 D F Q:$D(DIRUT)  S ACDP5=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3,ACDP4,ACDP5)) Q:'ACDP5  D P5 Q:$D(DIRUT)
 Q:$D(DIRUT)
 W ?53,ACDSTOT,?70,ACDSTOT1,!
 Q
P5 ;
 S ACDSTOT=ACDSTOT+1,ACDSTOT1=ACDSTOT1+^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3,ACDP4,ACDP5)
 Q
F ;Form feed
 I $Y+4>IOSL D F^ACDWUTL D:'$D(DIRUT) C
 Q
C ;Column
 W !!,ACDC(1),?26,ACDC(2),?31,ACDC(3),?41,ACDC(4),?53,ACDC(5),?70,ACDC(6),!,ACDH(1),!
 Q
