ACDWRP99 ;IHS/ADC/EDE/KML - REPORT GENERATOR; 
 ;;4.1;CHEMICAL DEPENDENCY MIS;;MAY 11, 1998
 ;Reports 12,15
 I '$D(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL")) D ^ACDWK Q
 S (ACDGTOT,ACDGTOT1)=0
 D C S ACDP1="" F  D F Q:$D(DIRUT)  S ACDP1=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1)) Q:ACDP1=""  W !,$E(ACDP1,1,24) D P1 Q:$D(DIRUT)
 D ^ACDWK
 Q
P1 ;
 S ACDP2="" F  D F Q:$D(DIRUT)  S ACDP2=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2)) Q:ACDP2=""  W ?26,ACDP2 D P2 Q:$D(DIRUT)
 Q
P2 ;
 S ACDP3="" F  D F Q:$D(DIRUT)  S ACDP3=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3)) Q:ACDP3=""   W ?45,$E(ACDP3) D P3 Q:$D(DIRUT)
 Q
P3 ;
 S (ACDSTOT,ACDARR)=0,ACDP4="" F  D F Q:$D(DIRUT)  S ACDP4=$O(^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3,ACDP4)) Q:'ACDP4  D P4  Q:$D(DIRUT)
 Q:$D(DIRUT)
 W ?57,ACDARR,?70,ACDSTOT,!
 Q
 ;
P4 ;
 S ACDSTOT=ACDSTOT+1,ACDARR=ACDARR+^TMP("ACD",ACDJOB,ACDBT,ACDTGSUB,"VAL",ACDP1,ACDP2,ACDP3,ACDP4)
 Q
 ;
F ;Form feed
 I $Y+4>IOSL D F^ACDWUTL D:'$D(DIRUT) C
 Q
C ;Column
 W !!,ACDC(1),?26,ACDC(2),?45,ACDC(3),?57,ACDC(4),?70,ACDC(5),!,ACDH(1),!
 Q
