LAZABL3 ;SLC/RWF- ABL3 BLOOD GAS INSTRUMENT ; 4/13/87  16:40 ; [ 09/07/90  3:41 PM ]
 ;V~4.08~
 ;CROSS LINK BY ID OR IDE
LA1 S LANM=$T(+0),TSK=$N(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S @TRAP
LA2 K TV,Y S TOUT=0,A=1 D IN G QUIT:TOUT S V=IN D NUM S IN=+V G LA2:'IN
 S Y(1)=IN,TOUT=0 F A=2:1:25 S TOUT=0 D IN,QC G QUIT:TOUT
 F I=0:0 S I=$N(TC(I)) Q:I<1  X TC(I,2) D NUM S @TC(I,1)=V ;V COULD BE MODIFIED ON THIS LINE
 S V=Y(1) D NUM S ID=+V,TRAY=1 S V=$S($D(Y("PTID")):$E(Y("PTID"),7,21),1:"") D NUM S (CUP,IDE)=+V
LA3 X LAGEN G LA2:'ISQN
 S Z=TSK_">" F I=0:0 S I=$N(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1),Z=Z_TV(I,1)_" "
 D WRITE G LA2
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S V=$E(IN,1,6) D NUM S:V]"" Y(V)=IN Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0 I ECHOALL S Z=IN D WRITE
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
WRITE I IO]"" O IO::1 IF $T U IO W:'ECHOALL *7 W !,Z C IO
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK) I $D(ZTSK) K ^%ZTSK(ZTSK)
 Q
TRAP K ^LA("LOCK",TSK) S T=TSK D SET^LAB,ERROR^LASET G @(U_LANM) ;ERROR TRAP
