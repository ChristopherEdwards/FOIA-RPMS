LAEKT7 ;SLC/RWF- KODAK EKTACHEM 700 ROUTINE ; 5/19/87  15:20 ;
 ;;V~4.08~
 ;CROSS LINK BY ID OR IDE
LA1 S LANM=$T(+0),TSK=$N(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 D ^LASET Q:'TSK  S @TRAP,ID=0 S:'$D(LALCT) LALCT="" Q:LALCT="N"
LA2 S TOUT=0 D IN G QUIT:TOUT,LA2:$E(IN,1)'="!" D QC,HDR:LATYPE="a",DATA:LATYPE="f",CALC:LATYPE="g",LA3:LATYPE="h" G LA2
HDR S V=$E(IN,30,44) D NUM S (ID,IDE)=+V,V=$E(IN,47,48) D NUM S CUP=+V,V=$E(IN,49,63) D NUM S TRAY=+V K TV Q
DATA S V=$E(IN,6,9) D NUM S NM=V,V=$E(IN,10,17) D NUM Q:$E(IN,26)>2&($E(IN,26)<7)!($E(IN,27)#2)
DA2 I LALCT="U" F I=0:0 S I=$N(^UTILITY("LA",$J,I)) Q:I<1  I ^UTILITY("LA",$J,I,4)=NM,V<99999.99 S @^UTILITY("LA",$J,I,1)=V ;Param 3 has the report name.
 I LALCT'="U" F I=0:0 S I=$N(TC(I)) Q:I<1  I TC(I,4)=NM,V<99999.99 S @TC(I,1)=V
 Q
CALC S V=$E(IN,6,9) D NUM S NM=V,V=$E(IN,10,18) D NUM Q:$E(IN,27)>2  G DA2
LA3 Q:ID=""  X LAGEN
 S Z=ID_"> " F I=0:0 S I=$N(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1),Z=Z_TV(I,1)_" "
 S ID="" K TV
 Q
QC ;QC TESTING HERE; S BAD=1 IF DONT STORE
 S LATYPE=$E(IN,5)
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0 I ECHOALL S Z=IN D WRITE
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
WRITE I IO]"" O IO::1 IF $T U IO W:'ECHOALL *7 W !,Z C IO
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK) I $D(ZTSK) K ^%ZTSK(ZTSK)
 Q
TRAP K ^LA("LOCK",TSK) S T=TSK D SET^LAB,ERROR^LASET G @(U_LANM) ;ERROR TRAP
