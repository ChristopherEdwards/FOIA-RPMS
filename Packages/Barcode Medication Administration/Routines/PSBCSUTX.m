PSBCSUTX ;BIRMINGHAM/TEJ- BCMA-HSC COVER SHEET UTILITIES 2 ;Mar 2004
 ;;3.0;BAR CODE MED ADMIN;**16**;Mar 2004
 ;
 ; Reference/IA
 ; File 200/10060
 ; $$GET1^DIQ/2056
 ; $$SCH^XLFDT/10103
 ; $$FMADD^XLFDT/10103
 ;
ADD ; output:  ORD-ORC-DD-ADD-SOL-ID-ADM-CMT-END segmnts
 K PSBDONE S PSBRECHD="ORD",PSBDONE=0,PSBCNT1=^TMP("PSB",$J,PSBTAB,0),PSBCNT2=1,$P(^TMP("PSB",$J,"CVRSHT2",0),U)=0
 F PSBI1=1:1:PSBCNT1 D  Q:PSBDONE
 .I PSBCNT1'>1 S PSBDONE=1 Q
 .I PSBI1=1 S ^TMP("PSB",$J,"CVRSHT2",PSBCNT2)=^TMP("PSB",$J,"CVRSHT",1) Q
 .I ^TMP("PSB",$J,PSBTAB,PSBI1)="END" S PSBRECHD="ORD",PSBCNT2=PSBCNT2+1,^TMP("PSB",$J,"CVRSHT2",PSBCNT2)="END"  Q
 .I PSBRECHD="ORD" D ORD Q
 .I PSBRECHD="ORC" D ORC Q
 .I PSBRECHD="MED" D MED Q
 S $P(^TMP("PSB",$J,"CVRSHT2",0),U)=PSBCNT2
 M ^TMP("PSB",$J,PSBTAB)=^TMP("PSB",$J,"CVRSHT2") K PSBNXTDU D ADM
 K ^TMP("PSB",$J,PSBTAB) M ^TMP("PSB",$J,PSBTAB)=^TMP("PSB",$J,"CVRSHT2") K ^TMP("PSB",$J,"CVRSHT2") D FINALPAS
 K ^TMP("PSB",$J,PSBTAB) M ^TMP("PSB",$J,PSBTAB)=^TMP("PSB",$J,"CVRSHT2") K ^TMP("PSB",$J,"CVRSHT2")
 Q
ORD ; Ordr
 S PSBCNT2=PSBCNT2+1,(PSBORREC,PSBXREC)=^TMP("PSB",$J,PSBTAB,PSBI1)
 S ($P(PSBXREC,U,12),$P(PSBXREC,U,23),$P(PSBXREC,U,24),PSBSCHTM,PSBONMBR,PSBIENX,PSBBAGID,PSBACT,PSBACTBY,PSBACTDT,PSBACTPT,PSBPRNRE,PSBXX,PSBXXX)=""
 S ^TMP("PSB",$J,"CVRSHT2",PSBCNT2)="ORD",$P(^TMP("PSB",$J,"CVRSHT2",PSBCNT2),U,2)=PSBXREC
 S PSBSCHTM=$P(PSBORREC,U,14),PSBONMBR=$P(PSBORREC,U,2),PSBIENX=$P(PSBORREC,U,12),PSBLRGIV=0
 D CLEAN^PSBVT,PSJ1^PSBVT(DFN,PSBONMBR) S:(PSBONMBR["V")&'$$IVPTAB^PSBVDLU3(PSBOTYP,PSBIVT,PSBISYR,PSBCHEMT,$G(PSBMR)) PSBLRGIV=1
 I '$D(PSBLST4X(PSBONMBR)) S PSBXX="" F PSBI=1:1 S PSBXX=$O(PSBADMX(PSBONMBR,PSBXX),-1) Q:PSBXX=""  S PSBXXX="" D  Q:$G(PSBLST4X(PSBONMBR))=4
 .F  S PSBXXX=$O(PSBADMX(PSBONMBR,PSBXX,PSBXXX)) Q:PSBXXX=""  D  Q:$G(PSBLST4X(PSBONMBR))=4
 ..I $$GET1^DIQ(53.79,PSBXXX_",","ACTION STATUS","I")'="N"  S PSBLST4X(PSBONMBR,PSBXXX)="",PSBLST4X(PSBONMBR)=$G(PSBLST4X(PSBONMBR))+1
 I PSBIENX]"",$D(PSBLST4X(PSBONMBR,PSBIENX)) D
 .S PSBBAGID=$$GET1^DIQ(53.79,PSBIENX_",","IV UNIQUE ID")
 .S PSBACTDT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION DATE/TIME","I")
 .S PSBACT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION STATUS","I")
 .S PSBPRNRE=$$GET1^DIQ(53.79,PSBIENX_",","PRN REASON")
 .S PSBACTBY=$$GET1^DIQ(53.79,PSBIENX_",","ACTION BY:INITIAL") S:PSBACTBY']"" PSBACTBY="***"
 .S PSBACTPT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION BY","I")
 .I '$D(PSBDONE(PSBIENX)) D
 ..I PSBLRGIV,(PSBFON]"") Q
 ..S PSBADMS(PSBONMBR,PSBSCHTM_PSBIENX)=PSBSCHTM_"^"_PSBBAGID_"^"_PSBIENX_"^"_PSBACT_"^"_PSBACTDT_"^"_PSBACTBY_"^"_PSBACTPT_"^"_PSBPRNRE
 ..I PSBLRGIV D
 ...I PSBOSP<PSBNOW S PSBADMS(PSBONMBR,"EXP")=""
 ...S PSBADMS(PSBONMBR,PSBSCHTM_PSBIENX,1)=1
 ..S PSBDONE(PSBIENX)="" K PSBADMX(PSBONMBR,PSBSCHTM,PSBIENX) D
 ...S PSBXX="" F  S PSBXX=$O(PSBADMX(PSBONMBR,PSBXX)) Q:PSBXX=""  I $D(PSBADMX(PSBONMBR,PSBXX,PSBIENX)) K PSBADMX(PSBONMBR,PSBXX,PSBIENX)
 I PSBIENX']"" D
 .S PSBADMS(PSBONMBR,PSBSCHTM_PSBIENX)=PSBSCHTM_"^^^^^^^"
 .I PSBLRGIV S PSBADMS(PSBONMBR,PSBSCHTM,1)=1
 I '$D(PSBADMS(PSBONMBR)) S PSBADMS(PSBONMBR,PSBSCHTM_PSBIENX)=PSBSCHTM_"^^^^^^^"
 S PSBRECHD="ORC" K PSBSCHTM S PSBXREC=""
 Q
ORC ; Ordr commnts
 S PSBCNT2=PSBCNT2+1,^TMP("PSB",$J,"CVRSHT2",PSBCNT2)="ORC",$P(^TMP("PSB",$J,"CVRSHT2",PSBCNT2),U,2)=^TMP("PSB",$J,PSBTAB,PSBI1),PSBRECHD="MED"
 Q
MED ; Cnstr DD,ADD,SOL,ID segms
 S PSBXREC=^TMP("PSB",$J,PSBTAB,PSBI1)
 Q:$$QUT^PSBCSUTY()
 I $P(PSBXREC,U)="ID" S PSBUSED=$$USED^PSBCSUTY()  Q:PSBUSED
 S PSBCNT2=PSBCNT2+1,^TMP("PSB",$J,"CVRSHT2",PSBCNT2)=^TMP("PSB",$J,PSBTAB,PSBI1)
 Q
ADM ; AdmnInfo
 K PSBDONE S (PSBONMBR,PSBSCHTM)="" F PSBI1=2:1:$P(^TMP("PSB",$J,PSBTAB,0),U) D
 .I $P(^TMP("PSB",$J,PSBTAB,PSBI1),U)="ORD" S PSBONMBR=$P(^TMP("PSB",$J,PSBTAB,PSBI1),U,3),$P(^TMP("PSB",$J,"CVRSHT2",PSBI1),U,15)=""
 .S (PSBXX,PSBXXX)="" F  S PSBXX=$O(PSBADMX(PSBONMBR,PSBXX)) Q:PSBXX=""  F  S PSBXXX=$O(PSBADMX(PSBONMBR,PSBXX,PSBXXX)) Q:PSBXXX=""  D
 ..S PSBSCHTM=PSBXX,PSBIENX=PSBXXX
 ..Q:'$D(PSBLST4X(PSBONMBR,PSBIENX))
 ..S PSBACT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION STATUS","I")
 ..I PSBACT']"" S PSBACT="* UNKNOWN *"
 ..S PSBACTDT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION DATE/TIME","I")
 ..S PSBBAGID=$$GET1^DIQ(53.79,PSBIENX_",","IV UNIQUE ID")
 ..S PSBPRNRE=$$GET1^DIQ(53.79,PSBIENX_",","PRN REASON")
 ..S PSBACTBY=$$GET1^DIQ(53.79,PSBIENX_",","ACTION BY:INITIAL") S:PSBACTBY']"" PSBACTBY="***"
 ..S PSBACTPT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION BY","I")
 ..S PSBADMS(PSBONMBR,PSBSCHTM_PSBIENX)=PSBSCHTM_"^"_PSBBAGID_"^"_PSBIENX_"^"_PSBACT_"^"_PSBACTDT_"^"_PSBACTBY_"^"_PSBACTPT_"^"_PSBPRNRE
 ..I PSBIENX]"" K PSBADMX(PSBONMBR,PSBSCHTM,PSBIENX)
 .I '$D(PSBADMS(PSBONMBR)) K ^TMP("PSB",$J,"CVRSHT2",PSBI1) Q
 .I $P(^TMP("PSB",$J,PSBTAB,PSBI1),U)="END" K PSBADMS(PSBONMBR) Q
 .I $P(^TMP("PSB",$J,PSBTAB,PSBI1+1),U)="END" D  Q
 ..S PSBCNT2=1,PSBSCHTM=""
 ..F  S PSBSCHTM=$O(PSBADMS(PSBONMBR,PSBSCHTM)) Q:+$G(PSBSCHTM)=0  D
 ...S PSBIENX=$P(PSBADMS(PSBONMBR,PSBSCHTM),U,3)
 ...I PSBIENX]"",'$D(PSBDONE(PSBIENX))  D
 ....S PSBACT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION STATUS","I")
 ....I PSBACT']"" S PSBACT="* UNKNOWN *"
 ....S PSBACTDT=$$GET1^DIQ(53.79,PSBIENX_",","ACTION DATE/TIME","I")
 ....Q:PSBACT="N"
 ....Q:$D(PSBADMS(PSBONMBR,"EXP"))&("SI"'[PSBACT)
 ....S $P(PSBADMS(PSBONMBR,PSBSCHTM),U,4)=PSBACT
 ....S ^TMP("PSB",$J,"CVRSHT2",PSBI1,PSBCNT2)="ADM^"_PSBADMS(PSBONMBR,PSBSCHTM)_"^"_$$NEXTADM(PSBDFNX,PSBONMBR),PSBCNT2=PSBCNT2+1
 ....S PSBDONE(PSBIENX)=""
 ....D CMT^PSBCSUTY
 ...I (PSBIENX']"")&($G(PSBADMS(PSBONMBR,PSBSCHTM,1))'=1) S ^TMP("PSB",$J,"CVRSHT2",PSBI1,PSBCNT2)="ADM^"_PSBADMS(PSBONMBR,PSBSCHTM)_"^"_$$NEXTADM(PSBDFNX,PSBONMBR),PSBCNT2=PSBCNT2+1
 K PSBSCHTM
 Q
FINALPAS ;
 S PSBI1="^TMP(""PSB"",$J,""CVRSHT"")",PSBCNT1=0
 F  S PSBI1=$Q(@PSBI1) Q:PSBI1["CVRSHT2"  D
 .I $QS(PSBI1,4)'>1 S ^TMP("PSB",$J,"CVRSHT2",PSBCNT1)=@PSBI1,PSBCNT1=PSBCNT1+1 Q
 .K PSBX2 M PSBX2=^TMP("PSB",$J,"CVRSHT",$QS(PSBI1,4))
 .I $QS(PSBI1,5)="" S ^TMP("PSB",$J,"CVRSHT2",PSBCNT1)=@PSBI1,PSBCNT1=PSBCNT1+1 Q
 .K PSBDONE
 .I '$D(PSBX2(1)) S ^TMP("PSB",$J,"CVRSHT2",PSBCNT1)=@PSBI1,PSBCNT1=PSBCNT1+1 Q
 .F PSBI2=1:1 Q:'$D(PSBX2(PSBI2))  D  ; Sort actn/commnt revrs chrono
 ..Q:$D(PSBDONE(PSBI2))
 ..S PSBXDTTM=(-1*($P(PSBX2(PSBI2),U,6)))_+($E($P(PSBX2(PSBI2),U,4),$L($P(PSBX2(PSBI2),U,4))-6,999)),PSBMCODE=$P(PSBX2(PSBI2),U)
 ..D:(+PSBXDTTM<0)&(PSBMCODE["ADM")
 ...S PSBX3(+PSBXDTTM,-999)=PSBX2(PSBI2),PSBDONE(PSBI2)="" K ^TMP("PSB",$J,"CVRSHT",$QS(PSBI1,4),PSBI2)
 ...F PSBI3=1:1 Q:'$D(PSBX2(PSBI2+PSBI3))  Q:$P(PSBX2(PSBI2+PSBI3),U)'["CMT"  D
 ....S PSBX3(+PSBXDTTM,-1*PSBI3)=PSBX2(PSBI2+PSBI3),PSBDONE(PSBI2+PSBI3)="" K ^TMP("PSB",$J,"CVRSHT",$QS(PSBI1,4),PSBI2+PSBI3)
 ..D:(+PSBXDTTM=0)&(PSBMCODE["ADM")
 ...S PSBX3(PSBI2,0)=PSBX2(PSBI2),PSBDONE(PSBI2)="" K ^TMP("PSB",$J,"CVRSHT",$QS(PSBI1,4),PSBI2)
 .I $D(PSBX3) D  K PSBX3
 ..S PSBI2="" F  S PSBI2=$O(PSBX3(PSBI2)) Q:PSBI2=""  S PSBI3="" F  S PSBI3=$O(PSBX3(PSBI2,PSBI3)) Q:PSBI3=""  D
 ...S ^TMP("PSB",$J,"CVRSHT2",PSBCNT1)=PSBX3(PSBI2,PSBI3),PSBCNT1=PSBCNT1+1
 S $P(^TMP("PSB",$J,"CVRSHT2",0),U)=PSBCNT1-1
 Q
NEXTADM(XX,YY) ;
 S NEXTADM=""
 I $D(PSBNXTDU(YY)) S NEXTADM=PSBNXTDU(YY) Q NEXTADM
 D:YY'["P"
 .S PSBPATX=XX,PSBORXX=YY D CLEAN^PSBVT,PSJ1^PSBVT(XX,YY)
 .Q:(PSBORXX["V")&'$$IVPTAB^PSBVDLU3(PSBOTYP,PSBIVT,PSBISYR,PSBCHEMT,$G(PSBMR))
 .S PSBGSCH=PSBADST,XX=PSBPATX,YY=PSBORXX,(NEXTADM,X,Y)="",X=$O(^PSB(53.79,"AORD",XX,YY,X),-1)
 .I X]"" S Y=$O(^PSB(53.79,"AORD",XX,YY,X,Y),-1) I ($F("NM",$P(^PSB(53.79,Y,0),U,9))>1)!($P(^PSB(53.79,Y,0),U,9)="") S NEXTADM=X
 .D:X']""
 ..S Y="",X=$O(^PSB(53.79,"AORDX",XX,YY,X),-1)
 ..I X]"" S Y=$O(^PSB(53.79,"AORDX",XX,YY,X,Y),-1) I $F("NM",$P(^PSB(53.79,Y,0),U,9))>1!($P(^PSB(53.79,Y,0),U,9)="") S NEXTADM=$P(^PSB(53.79,Y,0),U,6)
 .D:NEXTADM=""
 ..S PSBGOTY=Y,PSBFREQ=$$GETFREQ^PSBVDLU1(XX,YY)
 ..S PSBFREQ=$S(PSBFREQ="O":1440,PSBFREQ="D":"",1:PSBFREQ)
 ..S (PSBXSCH,LSTTIME,LSTIEN)=""
 ..S:PSBGOTY]"" LSTTIME=$O(^PSB(53.79,"AORD",PSBPATX,PSBORXX,LSTTIME),-1) I LSTTIME]"" S LSTIEN=$O(^PSB(53.79,"AORD",PSBPATX,PSBORXX,LSTTIME,""),-1)
 ..I LSTIEN]"" S:$P(^PSB(53.79,LSTIEN,0),U,9)']"" LSTTIME=""
 ..S:LSTTIME="" LSTTIME=$$FMADD^XLFDT(PSBOST,,,,-0.1)
 ..I +PSBFREQ>0 S PSBXSCH=(+PSBFREQ/60)_"H"
 ..S X=LSTTIME
 ..F PSBIX1=1:1:($L(PSBGSCH,"-")+1) D  Q:NEXTADM>LSTTIME
 ...I ($P(PSBGSCH,"-",PSBIX1))']"" D  Q
 ....I PSBIX1=1 D  Q
 .....I X<PSBOST S NEXTADM=PSBOST Q
 .....S X=PSBOST F  S X=$$SCH^XLFDT(PSBXSCH,X) S Y="" S Y=$O(^PSB(53.79,"AORD",PSBPATX,PSBORXX,Y),-1)  I X>Y S NEXTADM=X Q
 ....I PSBGSCH]"" D  Q
 .....I (+PSBFREQ'>1440) F I=0:1 S PSBDTXX=$$FMADD^XLFDT(PSBOST,I) S $P(PSBDTXX,".",2)=($P(PSBGSCH,"-")) I PSBDTXX>LSTTIME S NEXTADM=PSBDTXX Q
 .....I (+PSBFREQ'<1440),(1440#PSBFREQ=1440) F I=0:1 S PSBDTXX=$$FMADD^XLFDT(PSBOST,(I*(PSBFREQ\1440))) S $P(PSBDTXX,".",2)=($P(PSBGSCH,"-")) I PSBDTXX>LSTTIME S NEXTADM=PSBDTXX Q
 ....S $P(X,".",2)=$P(PSBGSCH,"-"),NEXTADM=$$SCH^XLFDT(PSBXSCH,X) Q
 ...S $P(X,".",2)=$P(PSBGSCH,"-",PSBIX1),NEXTADM=X
 .I $$PSBDCHK1^PSBVT1(PSBSCH) D
 ..S YY=PSBORXX,XX=PSBPATX
 ..I $G(LSTTIME)]"" S NEXTADM=$S(LSTTIME'<PSBOST:LSTTIME,NEXTADM>LSTTIME:NEXTADM,1:PSBOST)
 ..I PSBFREQ="" S PSBDTX=$P(NEXTADM,".") F PSBIX3=0:1 S X=$$FMADD^XLFDT(PSBDTX,PSBIX3) Q:X>PSBOSP  D  Q:$G(PSBYS)
 ...S PSBNXTDT=X,PSBDY="" D DW^%DTC F PSBIX2=1:1 S PSBDY=$P($P(PSBSCH,"@"),"-",PSBIX2) Q:PSBDY=""  S PSBYS=0 I $F($E(X_"S",1,$L(PSBDY)),PSBDY)>1 S PSBYS=1 Q
 ...I PSBYS S PSBSCTM=$$GETADMIN^PSBVDLU1(XX,YY,PSBNXTDT,"","") K ^TMP("PSB",$J,"GETADMIN") D
 ....F PSBIX4=1:1 S PSBTX=$P(PSBSCTM,"-",PSBIX4) Q:PSBTX=""  D  Q:PSBYS
 .....I NEXTADM>(PSBNXTDT_"."_PSBTX) S PSBYS=0 Q
 .....S NEXTADM=PSBNXTDT,$P(NEXTADM,".",2)=PSBTX
 .....I NEXTADM]"" I (NEXTADM<PSBOST)!$D(^PSB(53.79,"AORD",PSBPATX,PSBORXX,+NEXTADM))!(NEXTADM>PSBOSP) S PSBYS=0,NEXTADM="" Q
 .....S PSBYS=1
 .S PSBNXTDU(PSBORXX)=NEXTADM
 .D CLEAN^PSBVT
 Q NEXTADM
