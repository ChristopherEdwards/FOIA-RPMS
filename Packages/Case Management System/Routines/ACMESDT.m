ACMESDT ; IHS/TUCSON/TMJ - DATA ENTRY OF CMS SUPPORTING DATA ;
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;;JAN 10, 1996
 ;DATA ENTRY FOR CMS SYSTEMS DATA
EN ;
READ ;get user entered value
 K DIC
 D ESDT
 S DIC(0)="AEQOL",DIC("A")=$P(ACMENTRY,";;",5)
 ;Screen for lookup of Register Lists
 I '$P(^ACM(41.1,ACMRG,0),U,8) S:ACMDIC'=41&(ACMDIC'=46)&(ACMDIC'=57)&(ACMDIC'=50) DIC("S")="I $D(@ACMDIC2@(+Y,""RG"",""B"",ACMRG))"
 ;I '$T!(ACMY="^")!(ACMY="") K ACMDM S ACMQUIT="" G EXIT ;timed out,entered ^ or entered ""
 ;I ACMY["?" S DIC(0)="MQ",D="B",DZ="??" D DQ^DICQ K DIC("S") D PAUSE G READ
 ;S X=ACMY,DIC(0)="EMQL" 
 D DIC
 I $E(X)=U!(X="") K ACMDM S ACMQUIT="" G EXIT
 I Y=-1 W !!,"Invalid value for a ",$P($P(ACMENTRY,";;",5),":"),"!  TRY AGAIN." K DIC D PAUSE G READ
 I DR[".03" S DR=DR_$S($D(ACMRGNA):"///"_ACMRGNA,1:"")
 D DIE:'$D(ACMQUIT)!((ACMDIC=41.1)&'$D(ACMMGR))
 ;I '$D(ACMQUIT),ACMDIC=44.1 S ACMDXDA=ACMDA D EN11^ACMDXIT K ACMQUIT
 G EN:'$D(ACMQUIT)
EXIT K ACMENTRX,ACMTITLE
 K ACMX,ACMZ,ACMDR,ACMRGMGR,ACMRGUSR,ACMDIC,ACMDIC1,ACMDIC2,ACMY
 Q
ESDT D HEAD^ACMMENU
 K ACMQUIT
 S ACMTITLE=$P(ACMENTRY,";;",2),ACMDIC=$P(ACMENTRY,";;",3),(DIC,DIE)="^ACM("_ACMDIC_",",DR=$P(ACMENTRY,";;",4),ACMDIC2="^ACM("_ACMDIC_")"
HEAD S ACMX=ACMTITLE_" MANAGEMENT UTILITY"
 W !?80-$L(ACMX)\2,ACMX,!!?14,"Update ",@ACMRVON,ACMTITLE,@ACMRVOFF
 Q
PAUSE ;
 Q:$E(IOST)'="C"!(IO'=IO(0))
 W ! S DIR(0)="EO",DIR("A")="Hit return to continue...." D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 Q
DIC W ! D ^DIC K DIC Q
DIE S (ACMDA,DA)=+Y D ^DIE K DIC,DIE,DA,DR Q
ENTRY ;EP;TO SET SEQUENCE PROCESSING VARIABLE
 S ACMENTRY=$T(@ACMENTRY^ACMCTRL1) G EN
