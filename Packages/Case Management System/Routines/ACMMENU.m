ACMMENU ; IHS/TUCSON/TMJ - UTILITY ROUTINES MENU & SCREEN HANDLING ;
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;;JAN 10, 1996
 ;
TITLE D HEAD
 W:ACMY'="" !!?80-$L(ACMY)/2,ACMY
 W:ACMY="" !!?21,@ACMRVON,"DATA ENTRY",@ACMRVOFF,!?17,"for ",@ACMRVON,ACMPTNA2,@ACMRVOFF
 K ACMY
 Q
ENTRY ;EP;THIS SECTION OF LINE LABELS ARE ALL ENTRY POINTS USED IN MENU
 ;HEADER PRESENTATION
MM ;EP - called from option
 K ACMESQK,ACMRG,ACMRGNA S ACMY="MAIN MENU" G TITLE
 ;EP;ALL LINE LABELS MGR - DM ARE EXTERNAL ENTRY POINTS TO CONTROL HEADER DISPLAY
MGR S ACMY="MANAGER'S MENU" G TITLE
LSS S ACMY="LOCAL SYSTEMS SETUP" G TITLE
SCS S ACMY="SETUP LIST CATEGORIES" G TITLE
PDE S ACMY="" G TITLE
 S ACMY="PATIENT AND STATISTICAL REPORT GENERATION" G TITLE
PR ;EP - CALLED FROM OPTION
SR S ACMY="STATISTICAL REPORTS" G TITLE
LOR ;EP - called from option
 S ACMY="LOCAL OPTION REPORTS" G TITLE
DM ;EP -call from option
 S ACMY="PATIENT DATA MANAGEMENT" G TITLE
RES  ;EP - CALLED FROM OPTION
 S ACMY="RESOURCE DIRECTORY LISTING" G TITLE
LTR ;EP - called from option
 S ACMY="RECALL LETTER MANAGEMENT" G TITLE
 ;
HEAD ;EP;TO DISPLAY HEADER
 I $E(IOST,1,2)'="P-" W:$D(IOF) @IOF
 F ACMJ=1:1:3 S ACMX=$T(TEXT+ACMJ),ACMX=$P(ACMX,";;",2) S:ACMJ=2&$D(ACMRG) ACMX=$S(ACMRGNA'="":ACMRGNA,ACMX:ACMX,1:"")_" REGISTER" S:ACMJ=2&$D(ACMRG)&$D(ACMRGCUS) ACMX="CUSTOMIZE "_ACMX W !?80-$L(ACMX)\2,ACMX K ACMX
 K ACMJ
 I $D(ACMY),ACMY="MAIN MENU" D HDVS
 I $D(ACMY),ACMY="RECALL LETTER MANAGEMENT" D HDVS
 I $D(ACMY),ACMY="LOCAL OPTION REPORTS" D HDVS
 I $D(ACMY),ACMY="PATIENT DATA MANAGEMENT" D HDVS
 I $D(ACMY),ACMY="RESOURCE DIRECTORY LISTING" D HDVS
 I $D(ACMY),ACMY="PATIENT AND STATISTICAL REPORT GENERATION" D HDVS
 Q
 ;
TEXT ;
 ;;**************************************
 ;;**      CASE MANAGEMENT SYSTEM      **
 ;;**************************************
HDVS ;
 I $D(DUZ(2))#2,DUZ(2) S:$D(^DIC(4,DUZ(2),0)) ACMX=$P(^(0),U)
 S ACMZ=$O(^DIC(9.4,"C","ACM",0))
 Q:'ACMZ
 S ACMZ=$P(^DIC(9.4,ACMZ,"VERSION"),U),ACMZ="VERSION "_ACMZ
DISP ;
 I IOST'["P-" W:$D(ACMZ) !?80-$L(ACMZ)\2,ACMZ
 W:$D(ACMX) !?80-$L(ACMX)\2,ACMX
EXIT ;
 K ACMX,ACMZ
 Q
PAUSE ;EP;USED FOR ALL PAUSES FOR SCREEN CONTROL
 S DIR(0)="EOA",DIR("A")="Press <ENTER> to continue..... "
 D ^DIR K DIR
 Q
 ;
