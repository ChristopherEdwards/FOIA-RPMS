ACMPPDT ; IHS/TUCSON/TMJ - PRINT CONTROL CASE SUMMARY ; [ 02/02/2009  7:32 AM ]
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;**2,8**;JAN 10, 1996
 ;IHS/CMI/LAB - added browsing to individual case summary
START D EN
EXIT D ^%ZISC
 K ACM2,ACMS,ACMX,ACMA,ACMB,^UTILITY("DIQ1",$J),ACMDIC,DIC,DR,DA,ACMRAGE,ACMMHS,DC
 Q
EN I $P(^ACM(41.1,ACMRG,0),U,10)=1 D
 .S DIR(0)="YO",DIR("A")="Include PCC HEALTH SUMMARY",DIR("B")="NO"
 .W !
 .D ^DIR K DIR
 .I Y=1 S ACMMHS="" D SELTYP^ACMPPDTX
BRPR ;
 S DIR(0)="S^P:PRINT Output;B:BROWSE Output on Screen",DIR("A")="Do you wish to ",DIR("B")="P" K DA D ^DIR K DIR
 I $D(DIRUT) G DONE
 G:Y="B" BROWSE
 K IOP
 S %ZIS="PMQ"
 W !
 D ^%ZIS
 G:POP DONE
 I IO=IO(0),$D(IO("Q")) W !,"Cannot Queue to Screen or Slave Printer!",! K IO("Q") G EN
 I $D(IO("Q")) S ZTRTN="DQ^ACMPPDT",ZTSAVE("ACM*")="",ZTDESC="CMS DATA SHEET PRINT" D ^%ZTLOAD W !,"REQUEST QUEUED!" K IO("Q"),ZTSK G DONE
 ;I IOST["C-" S ^UTILITY($J,1)="R ""Strike <CR> to continue. "",X:DTIME W:$D(IOF) @IOF"
 K DIR S DIR(0)="E",DIR("A")="Strike <ENTER> to continue." D ^DIR K DIR W:$D(IOF) @IOF
DQ ;EP - U IO
 U IO
 Q:'$D(ACMRGDFN)
 S D0=ACMRGDFN,ACMCTRL=ACMCTRLP,ACMCTRL="PAGE;"_ACMCTRL
 S:ACMCTRL["APPL" ACMCTRL=$P(ACMCTRL,";APPL")_$P(ACMCTRL,";APPL",2)
 S:ACMCTRL["CRL" ACMCTRL=$P(ACMCTRL,";CRL")_$P(ACMCTRL,";CRL",2)
 S ACMCNT=$L(ACMCTRL,";")
 F ACMJ=1:1:ACMCNT S ACMLINER=$P(ACMCTRL,";",ACMJ),DC="," K DXS D @ACMLINER
 K ACMJ,ACMCNT
 I IOST["C-",'$D(IO("Q")),IO=IO(0) D PAUSE
 W:IOST["P-" @IOF
 I $D(ACMMHS) D PCCHS^ACMPPDTX
DONE D ^%ZISC
 K %ZIS,DXS,^UTILITY($J)
 S ZTREQ="@"
 S IOP=ION
 D ^%ZIS
 W @IOF
 Q
BROWSE ;
 ;
 I $T(VIEWR^XBLM)="" W !!!?5,"*** Browser Utility not available!! ***" Q
 D VIEWR^XBLM("DQ^ACMPPDT")
 G DONE
 ;
PAGE D ^ACMRAGE Q
REG D ^ACMLCMS,ACMPT^ACMLCMS Q
PROB D ^ACMPROB Q
CH D ^ACMRCH Q
DC D:$D(^ACM(51,"AC",ACMRG,ACMPTNO)) ^ACMRDC Q
DX D:$D(^ACM(44,"AC",ACMRG,ACMPTNO)) ^ACMRDX Q
ET D:$D(^ACM(53,"AC",ACMRG,ACMPTNO)) ^ACMRET Q
CMP D:$D(^ACM(42,"AC",ACMRG,ACMPTNO)) ^ACMRCMP Q
CP D:$D(^ACM(48,"AC",ACMRG,ACMPTNO)) ^ACMRCP Q
AP D:$D(^ACM(43,"AC",ACMRG,ACMPTNO)) ^ACMRAP Q
RF D:$D(^ACM(45,"AC",ACMRG,ACMPTNO)) ^ACMROF Q
FM D:$D(^ACM(46,"AC",ACMRG,ACMPTNO)) ^ACMRFM Q
MEAS D:$D(^ACM(57,"AC",ACMRG,ACMPTNO)) ^ACMRMS Q
MD D:$D(^ACM(54,"AC",ACMRG,ACMPTNO)) ^ACMRMD Q
SV D:$D(^ACM(47,"AC",ACMRG,ACMPTNO)) ^ACMRSV Q
APPT D:$D(^ACM(49,"AC",ACMRG,ACMPTNO)) ^ACMRAPP Q
CR D:($P(^ACM(41,ACMRGDFN,"DT"),U,8)'="")!($P(^("DT"),U,9)'="") ^ACMRNT Q
CT D:$D(^ACM(41,ACMRGDFN,1)) ^ACMRCMT Q
PAUSE W !!,"End of report for ",ACMPTNA2
 S DIR(0)="EOA",DIR("A")="Press <ENTER> to continue..... "
 D ^DIR K DIR
 Q
