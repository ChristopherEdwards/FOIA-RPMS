ACMPPDTX ; IHS/TUCSON/TMJ - HEALTH SUMMARY DISPLAYS ; [ 11/02/05  1:10 PM ]
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;*1,4,6,8*;JAN 10, 1996
 ;;Patch #1 - not quitting when printing multiple PCC Health Summaries
START Q:$G(ACMQUIT)=1  ;IHS/CMI/TMJ PATCH #6
 D EN
 I $D(ACMMHS) D PCCHS
 Q  ;IHS/OHPRD/TMJ Patch #1 added Quit
EXIT K ACM2,ACMS,ACMX,ACMA,ACMB,^UTILITY("DIQ1",$J),ACMDIC,ACMPAGE,ACMMHS
 Q
EN ;
 S ACMQUIT="" ;IHS/CMI/TMJ PATCH #6
DQ I $D(D0) S ACMRGDFN=D0,ACMRG=$P(^ACM(41,D0,0),U),ACMPTNO=$P(^(0),U,2),ACMPTNA=$P(^DPT(ACMPTNO,0),U)
 S ACMCTRL=ACMCTRLP,ACMCTRL="PAGE;"_ACMCTRL
 S:ACMCTRL["APPL" ACMCTRL=$P(ACMCTRL,";APPL")_$P(ACMCTRL,";APPL",2)
 S:ACMCTRL["CRL" ACMCTRL=$P(ACMCTRL,";CRL")_$P(ACMCTRL,";CRL",2)
 S ACMCNT=$L(ACMCTRL,";")
 F ACMJ=1:1:ACMCNT S ACMLINER=$P(ACMCTRL,";",ACMJ),DC="," K DXS D @ACMLINER
 K ACMJ,ACMCNT
 I IOST["C-",('$D(IO("Q"))!(IO=IO(0))) D PAUSE
 I $G(DIRUT)=1 S ACMQUIT=1 Q  ;IHS/CMI/TMJ PATCH #6 11/2/05
 W:IOST["P-" @IOF
DONE Q
CMGT Q
 ;
PAGE D ^ACMRAGE Q
REG D ^ACMLCMS,ACMPT^ACMLCMS Q
PROB D ^ACMPROB Q
CH D ^ACMRCH Q
DC D:$D(^ACM(51,"AC",ACMRG,ACMPTNO)) ^ACMRDC Q
DX D:$D(^ACM(44,"AC",ACMRG,ACMPTNO)) ^ACMRDX Q
ET D:$D(^ACM(53,"AC",ACMRG,ACMPTNO)) ^ACMRET Q
CMP D:$D(^ACM(42,"AC",ACMRG,ACMPTNO)) ^ACMRCMP Q
CP D:$D(^ACM(48,"AC",ACMRG,ACMPTNO)) ^ACMRCP Q
AP D:$D(^ACM(43,"AC",ACMRG,ACMPTNO)) ^ACMRAP Q
RF D:$D(^ACM(45,"AC",ACMRG,ACMPTNO)) ^ACMROF Q
FM D:$D(^ACM(46,"AC",ACMRG,ACMPTNO)) ^ACMRFM Q
MD D:$D(^ACM(54,"AC",ACMRG,ACMPTNO)) ^ACMRMD Q
SV D:$D(^ACM(47,"AC",ACMRG,ACMPTNO)) ^ACMRSV Q
APPT D:$D(^ACM(49,"AC",ACMRG,ACMPTNO)) ^ACMRAPP Q
MEAS D:$D(^ACM(57,"AC",ACMRG,ACMPTNO)) ^ACMRMS Q
CR D:($P(^ACM(41,ACMRGDFN,"DT"),U,8)'="")!($P(^("DT"),U,9)'="") ^ACMRNT Q
CT D:$D(^ACM(41,ACMRGDFN,1)) ^ACMRCMT Q
PAUSE W !!,"End of report for ",ACMPTNA
PAUSE1 ;EP;TO HANDLE SCREEN PAUSE
 S ACMQUIT=""
 S DIR(0)="EOA",DIR("A")="Press <ENTER> to continue..... "
 D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 Q
 ;
PCCHS ;EP;FOR MULTIPLE PCC HEALTH SUMMARIES
 Q:'$D(D0)!'$D(ACMSTYP)
 ;I $D(DIRUT) Q
 S APCHSPAT=$P(^ACM(41,D0,0),U,2),APCHSTYP=ACMSTYP
 D EN^APCHS
 I $E(IOST,1,2)="C-" D PAUSE1
 Q
SELTYP ;PEP;TO SELECT HEALTH SUMMARY TYPE
 K DIC
 S DIC="^APCHSCTL(",DIC("A")="Select health summary type: ",DIC(0)="AEQM",DIC("B")="ADULT REGULAR"
 W !
 D ^DIC
 K DIC,DA,DR
 I Y<0 G EXIT:U[X,SELTYP
 S ACMSTYP=+Y
 Q
