ACMPROB ; IHS/TUCSON/TMJ - DISPLAY OF PCC PROBLEM LIST ; [ 07/11/1999  8:59 PM ]
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;**1**;JAN 10, 1996
 ;IHS/CMI/LAB - y2k patch 1
 ;Called from CASE MANAGEMENT SYSTEM to display PCC problems
 ;ACMPTNO must equal the patient DFN
 ;EP;ENTRY POINT
 ;begin Y2K
 ;S ACMTCVD="S:Y]"""" Y=+Y,Y=$E(Y,4,5)_""/""_$E(Y,6,7)_""/""_$E(Y,2,3)",ACMTTAT="A" ;Y2000
 S ACMTCVD="S:Y]"""" Y=+Y,Y=$E(Y,4,5)_""/""_$E(Y,6,7)_""/""_(1700+$E(Y,1,3))",ACMTTAT="A" ;Y2000
 ;end Y2K
 D COMMON
 S ACMTTAT="I"
 D COMMON
 K ACMTCVD,ACMTQ,Y
 D PROBX
 Q
COMMON ;
 I '$D(^AUPNPROB("AC",ACMPTNO)) W !!?4,"********** No ",$S(ACMTTAT="A":"  ACTIVE",1:"INACTIVE")," Problems in PCC file for this Patient **********",!?4,"(To update the PCC problem list use PCC encounter form.)",! Q
 K ACMTDFT
 S ACMTNDF=0,ACMTFAC=""
 F ACMTQ=0:0 S ACMTFAC=$O(^AUPNPROB("AA",ACMPTNO,ACMTFAC)) Q:'ACMTFAC  D PROBSCH
 I ACMTNDF=0 W !!?4,"********** No ",$S(ACMTTAT="A":"  ACTIVE",1:"INACTIVE")," Problems in PCC file for this Patient **********",!?4,"(To update the PCC problem list use PCC encounter form.)",! Q
 W !!?4,"****************"_$S(ACMTTAT="A":"    ACTIVE ",1:"  INACTIVE ")_"PCC PROBLEMS AND NOTES  *********************",!?4,"(To update PCC problem list use PCC encounter form.)",!
 S ACMTFPP=""
 F ACMTQ=0:0 S ACMTFPP=$O(ACMTDFT(ACMTFPP)) Q:ACMTFPP=""  S ACMTDFN=ACMTDFT(ACMTFPP) D PROBDSP
PROBX K ACMTDFT,ACMTNDF,ACMTFPP,ACMTFAC,ACMTPLN,ACMTPBN,ACMTDTM,ACMTDTN,ACMTPRB,ACMTTAT,ACMTNFP,ACMTNRQ,ACMTPNM,ACMTDFN,ACMTFCN,ACMTICD,ACMTICL,ACMTILN,ACMTN,ACMTNFL,ACMTNSH,ACMTNAB,ACMTVSC,ACMTITE
 Q
PROBSCH ;
 S ACMTPRB=""
 F ACMTQ=0:0 S ACMTPRB=$O(^AUPNPROB("AA",ACMPTNO,ACMTFAC,ACMTPRB)) Q:ACMTPRB=""  S ACMTDFN=$O(^(ACMTPRB,"")) S:$P(^AUPNPROB(ACMTDFN,0),U,12)=ACMTTAT ACMTNDF=ACMTNDF+1,ACMTDFT(ACMTFAC_ACMTPRB)=ACMTDFN
 Q
PROBDSP ;
 S ACMTN=^AUPNPROB(ACMTDFN,0),ACMTNRQ=$P(ACMTN,U,5)
 D GETNARR
 E  S ACMTNRQ=""
 S ACMTITE=$P(ACMTN,U,6)
 D GETSITE
 S ACMTPNM=$P(ACMTN,U,7),ACMTPNM=ACMTNAB_ACMTPNM,Y=$P(ACMTN,U,3)
 X ACMTCVD
 S ACMTDTM=Y,Y=$P(ACMTN,U,8)
 X ACMTCVD
 S ACMTDTN=Y,ACMTPLN=ACMTPNM_$E("         ",1,12-$L(ACMTPNM))_ACMTDTM
 W ?3,ACMTPLN
 S ACMTICL=24,ACMTILN=58
 D PRTICD,NOTEDSP
 Q
NOTEDSP ; DISPLAY NOTES UNDER PROBLEM
 S ACMTNFP=0
 F ACMTQ=0:0 S ACMTNFP=$O(^AUPNPROB(ACMTDFN,11,ACMTNFP)) Q:'ACMTNFP  D DSPFACN
 Q
DSPFACN ; DISPLAY NOTES FOR SELECTED APSHCFACILITY
 Q:$D(^AUPNPROB(ACMTDFN,11,ACMTNFP,11,0))'=1!($O(^(0))="")
 S ACMTITE=^AUPNPROB(ACMTDFN,11,ACMTNFP,0)
 D GETSITE
 S ACMTFCN=ACMTNAB,ACMTNDF=0
 F ACMTQ=0:0 S ACMTNDF=$O(^AUPNPROB(ACMTDFN,11,ACMTNFP,11,ACMTNDF)) Q:'ACMTNDF  D DSPN ; ACC
 Q
DSPN ; DISPLAY SINGLE NOTE
 S ACMTN=^AUPNPROB(ACMTDFN,11,ACMTNFP,11,ACMTNDF,0) ;/IHS/OHPRD/TMJ 9/20/95
 Q:$P(ACMTN,U,4)="I"
 F ACMTQ=0:0 Q:$E(ACMTFCN)'=" "  S ACMTFCN=$E(ACMTFCN,2,99)
 W ?3,ACMTPNM," (",ACMTFCN,$P(ACMTN,U,1),")",?29,$P(ACMTN,U,3),!
 Q
 ;
PRTICD ;
 S:ACMTNRQ="" ACMTNRQ="<no narrative provided>"
 S ACMTICD="",ACMTTXT=ACMTICD
 D PRTTXT
 Q
 ;
PRTTXT ; GENERALIZED TEXT PRINTER
 S ACMTDLT=1,ACMTILN=80-ACMTICL-1
 F ACMTQ=0:0 S:ACMTNRQ]""&(($L(ACMTNRQ)+$L(ACMTTXT)+2)<255) ACMTTXT=$S(ACMTTXT]"":ACMTTXT_"; ",1:"")_ACMTNRQ,ACMTNRQ="" Q:ACMTTXT=""  D:ACMTQ>15 PAUSE D PRTTXT2
 K ACMTILN,ACMTDLT,ACMTF,ACMTC,ACMTTXT
 Q
PRTTXT2 D GETFRAG
 W ?ACMTICL+5,ACMTF,!
 S ACMTICL=ACMTICL+ACMTDLT,ACMTILN=ACMTILN-ACMTDLT,ACMTDLT=0
 Q
GETFRAG I $L(ACMTTXT)<ACMTILN S ACMTF=ACMTTXT,ACMTTXT="" Q
 F ACMTC=ACMTILN:-1:1 Q:$E(ACMTTXT,ACMTC)=" "
 S ACMTF=$E(ACMTTXT,1,ACMTC-1),ACMTTXT=$E(ACMTTXT,ACMTC+1,255)
 Q
 ;
GETNARR ;
 I ACMTNRQ]"" S ACMTNRQ=$S($D(^AUTNPOV(ACMTNRQ)):$P(^AUTNPOV(ACMTNRQ,0),U,1),1:"***** "_ACMTNRQ_" *****")
 E  S ACMTNRQ=""
 Q
 ;
GETSITE ;
 S:ACMTITE="" ACMTITE="null"
 S %=$S($D(^AUTTLOC(ACMTITE,0)):^(0),1:""),ACMTNFL=$P(%,U,1),ACMTNFL=$S($D(^DIC(4,ACMTITE,0)):$P(^(0),U,1),1:"<"_ACMTITE_">"),ACMTNSH=$P(%,U,2)
 I ACMTNSH="" S ACMTNSH="<"_ACMTITE_">"
 S ACMTNAB=$J($P(%,U,7),4)
 I ACMTNAB="" S ACMTNAB="<"_ACMTITE_">"
 Q
PAUSE ;
 I IOST["C-" S DIR(0)="EOA",DIR("A")="Press <ENTER> to continue..... " D ^DIR K DIR
 Q
