ACMRL1 ; IHS/TUCSON/TMJ - PROCESS LISTER ; [ 06/01/1999  1:48 PM ]
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;**1**;JAN 10, 1996
 ;IHS/CMI/LAB - patch 1 tmp to xtmp
 ;
 ;
 ;
START ;
 S (ACMBT,ACMBTH)=$H,ACMJOB=$J,ACMRCNT=0
 S ^XTMP("ACMRL",0)=$$FMADD^XLFDT(DT,14)_U_DT_U_"CASE MANAGEMENT GENERAL RETRIEVAL"
 D P
 Q
 ;
P ;
 S DFN=0 F  S DFN=$O(^ACM(41,"AC",DFN)) Q:DFN'=+DFN  I $D(^ACM(41,"AC",DFN,ACMRG)),'$P(^DPT(DFN,0),U,19) S ACMIFN=^ACM(41,"AC",DFN,ACMRG) D PROC
 Q
 ;
 ;
END ;
 S ACMET=$H
 D EOJ
 Q
EOJ ;
 Q
PROC ;
 D SCREENS
 Q:$D(ACMSKIP)
 K ACMSRT,ACMPRNT S ACMCRIT=ACMSORT,ACMX=0
 X:$D(^ACM(58.1,ACMSORT,4)) ^ACM(58.1,ACMSORT,4) I $G(ACMPRNT)="" S ACMPRNT="--"
 S ACMSRT=ACMPRNT
 S ^XTMP("ACMRL",ACMJOB,ACMBTH,"DATA HITS",ACMSRT,DFN)="",ACMRCNT=ACMRCNT+1
 Q:$D(^XTMP("ACMRL",ACMJOB,ACMBTH,"PATIENTS",DFN))!($D(ACMSCNT))
 S ^XTMP("ACMRL",ACMJOB,ACMBTH,"PATIENTS",DFN)="",ACMPTCT=ACMPTCT+1
 Q
SCREENS ;
 K ACMSKIP
 S ACMI=0 F  S ACMI=$O(^ACM(58.8,ACMRPT,11,ACMI)) Q:ACMI'=+ACMI!($D(ACMSKIP))  D
 .I '$P(^ACM(58.1,ACMI,0),U,8) D SINGLE Q
 .D MULT
 .Q
 Q
SINGLE ;
 K X,ACMSPEC S X="",ACMX=0
 X:$D(^ACM(58.1,ACMI,1)) ^(1)
 I X="" S ACMSKIP="" Q
 I '$D(ACMSPEC),'$D(^ACM(58.8,ACMRPT,11,ACMI,11,"B",X)) S ACMSKIP="" Q
 Q
MULT ;
 K ACMFOUN,ACMSKIP,ACMSPEC,X S ACMX=0,X=""
 X:$D(^ACM(58.1,ACMI,1)) ^(1)
 I $O(X(""))="" S ACMSKIP="" Q
 I '$D(ACMSPEC) S Y="" F  S Y=$O(X(Y)) Q:Y=""  I $D(^ACM(58.8,ACMRPT,11,ACMI,11,"B",Y)) S ACMFOUN="" Q
 I $D(ACMSPEC),$D(X) S ACMFOUN=1 Q
 S:'$D(ACMFOUN) ACMSKIP=""
 Q
XIT ;EP - CALLED FROM ACMRL
 K ACMBD,ACMBDD,ACMED,ACMEDD,ACMSD,ACMSORT,ACMSORV,ACMTCW,ACMRPT,ACMLHDR,ACMDISP,%H,ACMET,ACMLINE,ACMPRNM,ACMPRNT,ACMSKIP,ACMTYPE,ACMSPAG,ACMEN1,ACMSEAT,ACMPROC,ACM,ACMCAND,ACMHDR,ACMHEAD,ACMIFN,ACMNCAN
 K ACMCTYP,ACMFLG,ACMG,ACMNAME,ACMNIFN,ACMSAVE,ACMTITL,ACMQUIT,ACMPCNT,ACMQFLG,ACMPTCT,ACMTL,ACMSRTR,ACMSRTV
 K C,D,D0,DA,DIC,DD,DFN,DIADD,DLAYGO,DICR,DIE,DIK,DINUM,DIQ,DIR,DIRUT,DUOUT,DHD,DTOUT,DR,J,I,J,K,M,S,TS,X,Y,DIG,DIH,DIV,DQ,DDH,AMQQEN3,AMQQLX,DHIT
XIT1 ;EP
 K ACMANS,ACMBTH,ACMC,ACMCNT,ACMCRIT,ACMCUT,ACMD,ACMDISP,ACMDONE,ACMHIGH,ACMI,ACMJOB,ACMQMAN,ACMSEL,ACMTEXT,ACMVAR,ACMSKIP,ACMPRNT,ACMPRNM,ACMLINE,ACMRCNT,ACMSCNT,ACMDFET,ACMY,DFN
 K X,X1,X2,IO("Q"),%,Y,POP,DIRUT,ZTSK,ZTQUEUED,H,S,TS,M,ZTIO,DUOUT,DIR,DTOUT,V,Z,I,DIC,DIK,DIADD,DLAYGO,DA,DR,DIE,DIU,AMQQTAX,DINUM,ACMPACK,ACMEP1,ACMEP2,D,ACMLENG,ACMLHDR,ACMSAVE
 Q
