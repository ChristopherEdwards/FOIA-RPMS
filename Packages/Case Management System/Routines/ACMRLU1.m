ACMRLU1 ; IHS/TUCSON/TMJ - GEN RETR UTILITIES ; [ 07/15/1999  7:57 AM ]
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;**1**;JAN 10, 1996
 ;IHS/CMI/LAB - patch 1 Y2K
 ;
MCR ;display all current medicare data
 NEW ACMMIFN
 I '$D(^DPT(P,0)) G MCRX
 I $P(^DPT(P,0),U,19) G MCRX
 I '$D(^AUPNPAT(P,0)) G MCRX
 I '$D(^AUPNMCR(P,11)) G MCRX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCRX
 S ACMMIFN=0 F  S ACMMIFN=$O(^AUPNMCR(P,11,ACMMIFN)) Q:ACMMIFN'=+ACMMIFN  D
 .Q:$P(^AUPNMCR(P,11,ACMMIFN,0),U)>D
 .I $P(^AUPNMCR(P,11,ACMMIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 .S ACMPCNT=ACMPCNT+1,ACMPRNM(ACMPCNT)=$P(^AUPNMCR(DFN,0),U,3)_" ["_$S($P(^(0),U,4)]"":$P(^AUTTMCS($P(^(0),U,4),0),U),1:"-")_"]"
 .;being Y2K
 .;S ACMPCNT=ACMPCNT+1,Y=$P(^AUPNMCR(DFN,11,ACMMIFN,0),U),Z=$P(^(0),U,2),ACMPRNM(ACMPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3)_"-" I Z]"" S ACMPRNM(ACMPCNT)=ACMPRNM(ACMPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_$E(Y,2,3) ;Y2000
 .S ACMPCNT=ACMPCNT+1,Y=$P(^AUPNMCR(DFN,11,ACMMIFN,0),U),Z=$P(^(0),U,2),ACMPRNM(ACMPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_(1700+$E(Y,1,3))_"-" ;Y2000
 .I Z]"" S ACMPRNM(ACMPCNT)=ACMPRNM(ACMPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_(1700+$E(Z,1,3)) ;Y2000
 .;end Y2K
 .Q
MCRX ;
 K Y,Z
 Q
 ;
MCD ;
 NEW ACMMIFN,ACMNIFN
 I '$D(^DPT(P,0)) G MCDX
 I $P(^DPT(P,0),U,19) G MCDX
 I '$D(^AUPNPAT(P,0)) G MCDX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCDX
 S ACMMIFN=0 F  S ACMMIFN=$O(^AUPNMCD("B",P,ACMMIFN)) Q:ACMMIFN'=+ACMMIFN  D
 .Q:'$D(^AUPNMCD(ACMMIFN,11))
 .S ACMNIFN=0 F  S ACMNIFN=$O(^AUPNMCD(ACMMIFN,11,ACMNIFN)) Q:ACMNIFN'=+ACMNIFN  D
 ..Q:ACMNIFN>D
 ..I $P(^AUPNMCD(ACMMIFN,11,ACMNIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 ..S ACMPCNT=ACMPCNT+1,ACMPRNM(ACMPCNT)=$P(^AUPNMCD(ACMMIFN,0),U,3)_"/"_$S($P(^AUPNMCD(ACMMIFN,0),U,2)]"":$P(^AUTNINS($P(^AUPNMCD(ACMMIFN,0),U,2),0),U),1:"<>")
 ..;begin Y2K
 ..;S ACMPCNT=ACMPCNT+1,Y=$P(^AUPNMCD(ACMMIFN,11,ACMNIFN,0),U),Z=$P(^(0),U,2),ACMPRNM(ACMPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3)_"-" I Z]"" S ACMPRNM(ACMPCNT)=ACMPRNM(ACMPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_$E(Z,2,3) ;Y2000
 ..S ACMPCNT=ACMPCNT+1,Y=$P(^AUPNMCD(ACMMIFN,11,ACMNIFN,0),U),Z=$P(^(0),U,2),ACMPRNM(ACMPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_(1700+$E(Y,1,3))_"-" ;Y2000
 ..I Z]"" S ACMPRNM(ACMPCNT)=ACMPRNM(ACMPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_(1700+$E(Z,1,3)) ;Y2000
 ..;end Y2K
 ..Q
 .Q
 ;
MCDX ;
 Q
 ;
PI ;
 NEW ACMMIFN,ACMFLG
 I '$D(^DPT(P,0)) G PIX
 I $P(^DPT(P,0),U,19) G PIX
 I '$D(^AUPNPAT(P,0)) G PIX
 I '$D(^AUPNPRVT(P,11)) G PIX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G PIX
 S ACMMIFN=0 F  S ACMMIFN=$O(^AUPNPRVT(P,11,ACMMIFN)) Q:ACMMIFN'=+ACMMIFN  D
 .Q:$P(^AUPNPRVT(P,11,ACMMIFN,0),U)=""
 .S ACMNAME=$P(^AUPNPRVT(DFN,11,ACMMIFN,0),U) Q:ACMNAME=""
 .Q:$P(^AUTNINS(ACMNAME,0),U)["AHCCCS"
 .Q:$P(^AUPNPRVT(P,11,ACMMIFN,0),U,6)>D
 .I $P(^AUPNPRVT(P,11,ACMMIFN,0),U,7)]"",$P(^(0),U,7)<D Q
 .S ACMPCNT=ACMPCNT+1,ACMPRNM(ACMPCNT)=$P(^AUTNINS($P(^AUPNPRVT(P,11,ACMMIFN,0),U),0),U)
 .;begin Y2K
 .;S ACMPCNT=ACMPCNT+1,Y=$P(^AUPNPRVT(DFN,11,ACMMIFN,0),U,6),Z=$P(^(0),U,7),ACMPRNM(ACMPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3)_"-" I Z]"" S ACMPRNM(ACMPCNT)=ACMPRNM(ACMPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_$E(Y,2,3) ;Y2000
 .S ACMPCNT=ACMPCNT+1,Y=$P(^AUPNPRVT(DFN,11,ACMMIFN,0),U,6),Z=$P(^(0),U,7) I Y]"" S ACMPRNM(ACMPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_(1700+$E(Y,1,3))_"-" ;Y2000
 .I Z]"" S ACMPRNM(ACMPCNT)=ACMPRNM(ACMPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_(1700+$E(Z,1,3)) ;Y2000
 .;end Y2K
 .Q
PIX ;
 Q
