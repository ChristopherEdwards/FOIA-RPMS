ACMTMPI ; IHS/TUCSON/TMJ - UTILITY TO CHANGE PRINT TEMPLATE DFN'S ;
 ;;2.0;ACM CASE MANAGEMENT SYSTEM;;JAN 10, 1996
 ;UTILITY TO CHANGE DFN'S FOR ASSOCIATED PRINT TEMPLATES FOR THE
 ;ACMSRT SORTING UTILITY WHEN SORT TEMPLATES TRANSFERRED TO NEW SYSTEM
 ;EP;ENTRY POINT
EN I '$D(U) S U="^"
 D WAIT^DICD
 S ACMY(1)=""
 F  S ACMY(1)=$O(^ACM(48.5,"B",ACMY(1))) Q:ACMY(1)=""  D SET
EXIT ;
 K ^ACM(48.5,"B")
 S DIK="^ACM(48.5," D IXALL^DIK
 K ACMTMP,ACMTMPN,ACMY
 W *7,*7,!!?5,"ASSOCIATED PRINT TEMPLATE POINTERS IN THE ACM SORT FILE HAVE BEEN RESET."
 W !?5,"INITIALIZATION OF THE CASE MANAGEMENT SYSTEM IS COMPLETE."
 Q
SET S ACMY(2)=0
 F  S ACMY(2)=$O(^ACM(48.5,"B",ACMY(1),ACMY(2))) Q:'ACMY(2)  K ^ACM(48.5,ACMY(2),2,"B") S ACMY(3)=0 F  S ACMY(3)=$O(^ACM(48.5,ACMY(2),2,ACMY(3))) Q:'ACMY(3)  S ACMTMP=$P(^ACM(48.5,ACMY(2),2,ACMY(3),0),U,2) I ACMTMP'="" D
 .S ACMTMPN=0
 .F  S ACMTMPN=$O(^DIPT("B",ACMTMP,ACMTMPN)) Q:'ACMTMPN  S $P(^ACM(48.5,ACMY(2),2,ACMY(3),0),U)=ACMTMPN
 Q
