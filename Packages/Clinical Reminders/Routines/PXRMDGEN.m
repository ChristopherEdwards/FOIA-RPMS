PXRMDGEN ; SLC/PKR - Handle VA GENERIC drug findings. ;07/18/2002
 ;;1.5;CLINICAL REMINDERS;**2,5,11**;Jun 19, 2000
 ;
 ;=======================================================================
EVALFI(DFN,FIEVAL) ;Evaluate VA GENERIC (file #50.6) findings.
 N FIND0,FINDING,VAGIEN
 S VAGIEN=""
 F  S VAGIEN=$O(^PXD(811.9,PXRMITEM,20,"E","PSNDF(50.6,",VAGIEN)) Q:+VAGIEN=0  D
 . S FINDING=""
 . F  S FINDING=$O(^PXD(811.9,PXRMITEM,20,"E","PSNDF(50.6,",VAGIEN,FINDING)) Q:+FINDING=0  D
 .. S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 .. D FIEVAL(DFN,VAGIEN,FIND0,"",FINDING,.FIEVAL)
 Q
 ;
 ;=======================================================================
EVALTERM(DFN,FINDING,TERMIEN,TFIEVAL) ;Evaluate VA GENERIC (file #50.6) terms.
 N FIND0,TFIND0,TFINDING,VAGIEN
 S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 S VAGIEN=""
 F  S VAGIEN=$O(^PXRMD(811.5,TERMIEN,20,"E","PSNDF(50.6,",VAGIEN)) Q:+VAGIEN=0  D
 . S TFINDING=""
 . F  S TFINDING=$O(^PXRMD(811.5,TERMIEN,20,"E","PSNDF(50.6,",VAGIEN,TFINDING)) Q:+TFINDING=0  D
 .. S TFIND0=^PXRMD(811.5,TERMIEN,20,TFINDING,0)
 .. D FIEVAL(DFN,VAGIEN,FIND0,TFIND0,TFINDING,.TFIEVAL)
 Q
 ;
 ;=======================================================================
FIEVAL(DFN,VAGIEN,FIND0,TFIND0,FINDING,FIEVAL) ;
 N DRUG,DRUGIEN,DSUP,LDATE,RDATE,RXTYPE,SDATE,STATUS,VAGEN,VALID
 S RXTYPE=$P(TFIND0,U,13)
 I RXTYPE="" S RXTYPE=$P(FIND0,U,13)
 I RXTYPE="B" S RXTYPE=""
 S VAGEN=$$VAGN^PSNAPIS(VAGIEN)
 ;Use the "AND" cross-reference on file 50 to find all drugs in this
 ;generic group.
 S DATE=0
 S DRUGIEN=0
 F  S DRUGIEN=$O(^PSDRUG("AND",VAGIEN,DRUGIEN)) Q:+DRUGIEN=0  D
 . S DRUG=$$GET1^DIQ(50,DRUGIEN,.01)
 . D LDATE^PXRMDRUG(DFN,DRUG,RXTYPE,.DSUP,.LDATE,.RDATE,.SDATE,.STATUS)
 . I LDATE>DATE D
 .. K FIEVAL(FINDING)
 .. S DATE=LDATE
 .. S FIEVAL(FINDING,"DATE")=DATE
 .. S FIEVAL(FINDING,"DRUG")=DRUG
 .. I DSUP>0 S FIEVAL(FINDING,"DSUP")=DSUP
 .. I RDATE>0 S FIEVAL(FINDING,"RDATE")=RDATE
 .. I SDATE>0 S FIEVAL(FINDING,"SDATE")=SDATE
 .. S FIEVAL(FINDING,"STATUS")=STATUS
 ;If the last date is 0 then there is no release date and the finding
 ;is false.
 I +DATE=0 D  Q
 . K FIEVAL(FINDING)
 . S FIEVAL(FINDING)=0
 ;Save the rest of the finding information.
 S FIEVAL(FINDING)=1
 S FIEVAL(FINDING,"GENERIC")=VAGEN
 S FIEVAL(FINDING,"DATE")=DATE
 S FIEVAL(FINDING,"FINDING")=VAGIEN_";PSNDF(50.6,"
 ;Determine if the finding has expired.
 S VALID=$$VALID^PXRMDATE(FIND0,TFIND0,DATE)
 I 'VALID D
 . S FIEVAL(FINDING)=0
 . S FIEVAL(FINDING,"EXPIRED")=""
 Q
 ;
 ;=======================================================================
OUTPUT(NLINES,TEXT,FINDING,FIEVAL) ;Produce the clinical
 ;maintenance output.
 N DATE,DRUG,TEMP,VAGEN
 S DATE=FIEVAL(FINDING,"DATE")
 S TEMP=$$EDATE^PXRMDATE(DATE)
 S TEMP=TEMP_" VA GENERIC Drug: "
 S VAGEN=FIEVAL(FINDING,"GENERIC")
 S TEMP=TEMP_VAGEN
 S DRUG=FIEVAL(FINDING,"DRUG")
 S TEMP=TEMP_" (Drug: "_DRUG_")"
 ;If the finding has expired add "EXPIRED"
 I $D(FIEVAL(FINDING,"EXPIRED")) S TEMP=TEMP_" - EXPIRED"
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 S TEMP="Status: "_FIEVAL(FINDING,"STATUS")
 I $D(FIEVAL(FINDING,"RDATE")) S TEMP=TEMP_" Last release date:"_$$EDATE^PXRMDATE(FIEVAL(FINDING,"RDATE"))
 I $D(FIEVAL(FINDING,"DSUP")) S TEMP=TEMP_" Days supply:"_FIEVAL(FINDING,"DSUP")
 I $D(FIEVAL(FINDING,"SDATE")) S TEMP=TEMP_" Stop date:"_$$EDATE^PXRMDATE(FIEVAL(FINDING,"SDATE"))
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 I $D(PXRMDEV) D
 . N UID
 . S UID="VA GENERIC DRUG "_VAGEN
 . S ^TMP(PXRMPID,$J,PXRMITEM,UID)=TEMP
 Q
 ;
