PXRMDRCL ; SLC/PKR - Handle VA DRUG CLASS findings. ;07/18/2002
 ;;1.5;CLINICAL REMINDERS;**2,5,11**;Jun 19, 2000
 ;
 ;=======================================================================
EVALFI(DFN,FIEVAL) ;Evaluate drug class (file #50.605) findings.
 N DRCLIEN,FIND0,FINDING
 S DRCLIEN=""
 F  S DRCLIEN=$O(^PXD(811.9,PXRMITEM,20,"E","PS(50.605,",DRCLIEN)) Q:+DRCLIEN=0  D
 . S FINDING=""
 . F  S FINDING=$O(^PXD(811.9,PXRMITEM,20,"E","PS(50.605,",DRCLIEN,FINDING)) Q:+FINDING=0  D
 .. S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 .. D FIEVAL(DFN,DRCLIEN,FIND0,"",FINDING,.FIEVAL)
 Q
 ;
 ;=======================================================================
EVALTERM(DFN,FINDING,TERMIEN,TFIEVAL) ;Evaluate drug class (file #50.605) terms.
 N DRCLIEN,FIND0,TFIND0,TFINDING
 S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 S DRCLIEN=""
 F  S DRCLIEN=$O(^PXRMD(811.5,TERMIEN,20,"E","PS(50.605,",DRCLIEN)) Q:+DRCLIEN=0  D
 . S TFINDING=""
 . F  S TFINDING=$O(^PXRMD(811.5,TERMIEN,20,"E","PS(50.605,",DRCLIEN,TFINDING)) Q:+TFINDING=0  D
 .. S TFIND0=^PXRMD(811.5,TERMIEN,20,TFINDING,0)
 .. D FIEVAL(DFN,DRCLIEN,FIND0,TFIND0,TFINDING,.TFIEVAL)
 Q
 ;
 ;=======================================================================
FIEVAL(DFN,DRCLIEN,FIND0,TFIND0,FINDING,FIEVAL) ;
 N DATE,DRUGCLAS,DRUG,DRUGIEN,DSUP,LDATE,RDATE,RXTYPE,SDATE,STATUS,VALID
 S RXTYPE=$P(TFIND0,U,13)
 I RXTYPE="" S RXTYPE=$P(FIND0,U,13)
 I RXTYPE="B" S RXTYPE=""
 S DRUGCLAS=$$CLASS2^PSNAPIS(DRCLIEN)
 ;Use the "VAC" cross-reference on file 50 to find all drugs in this
 ;class
 S DATE=0
 S DRUGIEN=0
 F  S DRUGIEN=$O(^PSDRUG("VAC",DRCLIEN,DRUGIEN)) Q:+DRUGIEN=0  D
 . S DRUG=$$GET1^DIQ(50,DRUGIEN,.01)
 . D LDATE^PXRMDRUG(DFN,DRUG,RXTYPE,.DSUP,.LDATE,.RDATE,.SDATE,.STATUS)
 . I LDATE>DATE D
 .. K FIEVAL(FINDING)
 .. S DATE=LDATE
 .. S FIEVAL(FINDING,"DATE")=DATE
 .. S FIEVAL(FINDING,"DRUG")=DRUG
 .. I DSUP>0 S FIEVAL(FINDING,"DSUP")=DSUP
 .. I RDATE>0 S FIEVAL(FINDING,"RDATE")=RDATE
 .. I SDATE>0 S FIEVAL(FINDING,"SDATE")=SDATE
 .. S FIEVAL(FINDING,"STATUS")=STATUS
 ;If the last date is 0 then there is no release date or stop date
 ;and the finding is false.
 I +DATE=0 D  Q
 . K FIEVAL(FINDING)
 . S FIEVAL(FINDING)=0
 ;Save the rest of the finding information.
 S FIEVAL(FINDING)=1
 S FIEVAL(FINDING,"DRUG CLASS")=DRUGCLAS
 S FIEVAL(FINDING,"FINDING")=DRCLIEN_";PS(50.605,"
 ;Determine if the finding has expired.
 S VALID=$$VALID^PXRMDATE(FIND0,TFIND0,DATE)
 I 'VALID D
 . S FIEVAL(FINDING)=0
 . S FIEVAL(FINDING,"EXPIRED")=""
 Q
 ;
 ;=======================================================================
OUTPUT(NLINES,TEXT,FINDING,FIEVAL) ;Produce the clinical
 ;maintenance output.
 N DATE,DRUG,DRUGCLAS,TEMP
 S DATE=FIEVAL(FINDING,"DATE")
 S TEMP=$$EDATE^PXRMDATE(DATE)
 S TEMP=TEMP_" Drug Class: "
 S DRUGCLAS=FIEVAL(FINDING,"DRUG CLASS")
 S TEMP=TEMP_$P(DRUGCLAS,U,1)_"-"_$P(DRUGCLAS,U,2)
 S DRUG=FIEVAL(FINDING,"DRUG")
 S TEMP=TEMP_" (Drug: "_DRUG_")"
 ;If the finding has expired add "EXPIRED"
 I $D(FIEVAL(FINDING,"EXPIRED")) S TEMP=TEMP_" - EXPIRED"
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 S TEMP="Status: "_FIEVAL(FINDING,"STATUS")
 I $D(FIEVAL(FINDING,"RDATE")) S TEMP=TEMP_" Last release date:"_$$EDATE^PXRMDATE(FIEVAL(FINDING,"RDATE"))
 I $D(FIEVAL(FINDING,"DSUP")) S TEMP=TEMP_" Days supply:"_FIEVAL(FINDING,"DSUP")
 I $D(FIEVAL(FINDING,"SDATE")) S TEMP=TEMP_" Stop date:"_$$EDATE^PXRMDATE(FIEVAL(FINDING,"SDATE"))
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 I $D(PXRMDEV) D
 . N UID
 . S UID="DRUG CLASS "_DRUGCLAS
 . S ^TMP(PXRMPID,$J,PXRMITEM,UID)=TEMP
 Q
 ;
