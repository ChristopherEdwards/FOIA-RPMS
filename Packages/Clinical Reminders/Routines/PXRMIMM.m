PXRMIMM ; SLC/PKR - Handle immunization findings. ;01/22/2002
 ;;1.5;CLINICAL REMINDERS;**2,8**;Jun 19, 2000
 ;
 ;=======================================================================
EVALFI(DFN,FIEVAL) ;Evaluate immunization findings.
 N FIND0,FIND3,FINDING,IMMIEN,INVDATE
 S IMMIEN=""
 F  S IMMIEN=$O(^PXD(811.9,PXRMITEM,20,"E","AUTTIMM(",IMMIEN)) Q:+IMMIEN=0  D
 . S INVDATE=$O(^AUPNVIMM("AA",DFN,IMMIEN,""))
 . S FINDING=""
 . F  S FINDING=$O(^PXD(811.9,PXRMITEM,20,"E","AUTTIMM(",IMMIEN,FINDING)) Q:+FINDING=0  D
 .. S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 .. S FIND3=$G(^PXD(811.9,PXRMITEM,20,FINDING,3))
 .. D FIEVAL(DFN,IMMIEN,INVDATE,FIND0,FIND3,"","",FINDING,.FIEVAL)
 Q
 ;
 ;=======================================================================
EVALTERM(DFN,FINDING,TERMIEN,TFIEVAL) ;Evaluate immunization terms.
 N FIEVAL,FIND0,FIND3,IMMIEN,INVDATE,TFIND0,TFIND3,TFINDING
 S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 S FIND3=$G(^PXD(811.9,PXRMITEM,20,FINDING,3))
 S IMMIEN=""
 F  S IMMIEN=$O(^PXRMD(811.5,TERMIEN,20,"E","AUTTIMM(",IMMIEN)) Q:+IMMIEN=0  D
 . S INVDATE=$O(^AUPNVIMM("AA",DFN,IMMIEN,""))
 . S TFINDING=""
 . F  S TFINDING=$O(^PXRMD(811.5,TERMIEN,20,"E","AUTTIMM(",IMMIEN,TFINDING)) Q:+TFINDING=0  D
 .. S TFIND0=^PXRMD(811.5,TERMIEN,20,TFINDING,0)
 .. S TFIND3=$G(^PXRMD(811.5,TERMIEN,20,TFINDING,3))
 .. D FIEVAL(DFN,IMMIEN,INVDATE,FIND0,FIND3,TFIND0,TFIND3,TFINDING,.TFIEVAL)
 Q
 ;
 ;=======================================================================
FIEVAL(DFN,IMMIEN,INVDATE,FIND0,FIND3,TFIND0,TFIND3,FINDING,FIEVAL) ;
 N CONVAL,CONTRA,DATE,IEN,IND,REACTION,SERIES
 N TEMP,VALID,VIEN
 I INVDATE="" S FIEVAL(FINDING)=0 Q
 S IEN=$O(^AUPNVIMM("AA",DFN,IMMIEN,INVDATE,""))
 S TEMP=$G(^AUPNVIMM(IEN,0))
 S REACTION=$P(TEMP,U,6)
 S SERIES=$P(TEMP,U,4)
 S VIEN=$P(TEMP,U,3)
 S CONTRA=+$P(TEMP,U,7)
 S DATE=$$VDATE^PXRMDATE(VIEN)
 ;Save the rest of the finding information.
 S FIEVAL(FINDING)=1
 S FIEVAL(FINDING,"CONTRAINDICATED")=CONTRA
 S FIEVAL(FINDING,"DATE")=DATE
 S FIEVAL(FINDING,"FINDING")=IMMIEN_";AUTTIMM("
 S FIEVAL(FINDING,"REACTION")=REACTION
 S FIEVAL(FINDING,"SOURCE")=IEN_";AUPNVIMM("
 S FIEVAL(FINDING,"SERIES")=SERIES
 S FIEVAL(FINDING,"VALUE")=SERIES
 S FIEVAL(FINDING,"VIEN")=VIEN
 ;If this is being called as part of a term evaluation we are done.
 I TFIND0'="" Q
 ;Determine if the finding has expired.
 S VALID=$$VALID^PXRMDATE(FIND0,TFIND0,DATE)
 I 'VALID D  Q
 . S FIEVAL(FINDING)=0
 . S FIEVAL(FINDING,"EXPIRED")=""
 ;If there is a condition for this finding evaluate it.
 S CONVAL=$$COND^PXRMUTIL(FIND3,TFIND3,SERIES)
 I CONVAL'="" D
 . I CONVAL D
 .. S FIEVAL(FINDING)=CONVAL
 .. S FIEVAL(FINDING,"CONDITION")=CONVAL
 . E  D
 .. K FIEVAL(FINDING)
 .. S FIEVAL(FINDING)=0
 I CONTRA S ^TMP(PXRMPID,$J,PXRMITEM,"CONTRAINDICATED")=1
 Q
 ;
 ;=======================================================================
OUTPUT(NLINES,TEXT,FINDING,FIEVAL) ;Produce the clinical
 ;maintenance output.
 N IMMIEN,EM,FIEN,IND,PNAME,REACTION,SERIES,TEMP,VDATE
 S IMMIEN=$P(FIEVAL(FINDING,"SOURCE"),";",1)
 S FIEN=$P(FIEVAL(FINDING,"FINDING"),";",1)
 S VDATE=FIEVAL(FINDING,"DATE")
 S REACTION=$G(FIEVAL(FINDING,"REACTION"))
 S SERIES=$G(FIEVAL(FINDING,"VALUE"))
 S TEMP=$$EDATE^PXRMDATE(VDATE)
 S TEMP=TEMP_" Immunization: "
 S IND=$P(^AUPNVIMM(IMMIEN,0),U,1)
 S PNAME=$P(^AUTTIMM(FIEN,0),U,1)
 S TEMP=TEMP_PNAME
 I $L(SERIES)>0 D
 . S TEMP=TEMP_"; series - "
 . S TEMP=TEMP_$$EXTERNAL^DILFD(9000010.11,.04,"",SERIES,.EM)
 I $L(REACTION)>0 D
 . S TEMP=TEMP_"; reaction - "
 . S TEMP=TEMP_$$EXTERNAL^DILFD(9000010.11,.06,"",REACTION,.EM)
 I FIEVAL(FINDING,"CONTRAINDICATED") D
 . S TEMP=TEMP_" - CONTRAINDICATED"
 ;If the finding has expired add "EXPIRED"
 I $D(FIEVAL(FINDING,"EXPIRED")) S TEMP=TEMP_" - EXPIRED"
 ;If the finding is false because of the value add the reason.
 I $G(FIEVAL(FINDING,"CONDITION"))=0 S TEMP=TEMP_$$ACTFT^PXRMOPT
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 I $D(PXRMDEV) D
 . N UID
 . S UID="IMM "_PNAME
 . S ^TMP(PXRMPID,$J,PXRMITEM,UID)=TEMP
 ;
 ;For historical encounters display the location and comments.
 I VDATE["E" D
 . N INDEX,VIEN
 . S INDEX="IMM-"_PNAME
 . S VIEN=FIEVAL(FINDING,"VIEN")
 . D HLCV^PXRMVSIT(.NLINES,.TEXT,VIEN,INDEX)
 Q
 ;
