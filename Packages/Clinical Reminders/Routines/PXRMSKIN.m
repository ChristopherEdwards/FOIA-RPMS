PXRMSKIN ; SLC/PKR - Handle skin test findings. ;01/22/2002
 ;;1.5;CLINICAL REMINDERS;**2,8**;Jun 19, 2000
 ;
 ;=======================================================================
EVALFI(DFN,FIEVAL) ;Evaluate skin test findings.
 N FIND0,FIND3,FINDING,INVDATE,SKINIEN
 S SKINIEN=""
 F  S SKINIEN=$O(^PXD(811.9,PXRMITEM,20,"E","AUTTSK(",SKINIEN)) Q:+SKINIEN=0  D
 . S INVDATE=$O(^AUPNVSK("AA",DFN,SKINIEN,""))
 . S FINDING=""
 . F  S FINDING=$O(^PXD(811.9,PXRMITEM,20,"E","AUTTSK(",SKINIEN,FINDING)) Q:+FINDING=0  D
 .. S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 .. S FIND3=$G(^PXD(811.9,PXRMITEM,20,FINDING,3))
 .. D FIEVAL(DFN,SKINIEN,INVDATE,FIND0,FIND3,"","",FINDING,.FIEVAL)
 Q
 ;
 ;=======================================================================
EVALTERM(DFN,FINDING,TERMIEN,TFIEVAL) ;Evaluate skin test terms.
 N FIND0,FIND3,INVDATE,LFIEVAL,TFIND0,TFIND3,TFINDING,SKINIEN
 S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 S FIND3=$G(^PXD(811.9,PXRMITEM,20,FINDING,3))
 S SKINIEN=""
 F  S SKINIEN=$O(^PXRMD(811.5,TERMIEN,20,"E","AUTTSK(",SKINIEN)) Q:+SKINIEN=0  D
 . S INVDATE=$O(^AUPNVSK("AA",DFN,SKINIEN,""))
 . S TFINDING=""
 . F  S TFINDING=$O(^PXRMD(811.5,TERMIEN,20,"E","AUTTSK(",SKINIEN,TFINDING)) Q:+TFINDING=0  D
 .. S TFIND0=^PXRMD(811.5,TERMIEN,20,TFINDING,0)
 .. S TFIND3=$G(^PXRMD(811.5,TERMIEN,20,TFINDING,3))
 .. D FIEVAL(DFN,SKINIEN,INVDATE,FIND0,FIND3,TFIND0,TFIND3,TFINDING,.TFIEVAL)
 Q
 ;
 ;=======================================================================
FIEVAL(DFN,SKINIEN,INVDATE,FIND0,FIND3,TFIND0,TFIND3,FINDING,FIEVAL) ;
 N CONVAL,DATE,IEN,IND,READING,RESULTS
 N TEMP,VALID,VIEN
 I INVDATE="" S FIEVAL(FINDING)=0 Q
 S IEN=$O(^AUPNVSK("AA",DFN,SKINIEN,INVDATE,""))
 S TEMP=$G(^AUPNVSK(IEN,0))
 S RESULTS=$P(TEMP,U,4)
 S READING=$P(TEMP,U,5)
 S VIEN=$P(TEMP,U,3)
 S DATE=$$VDATE^PXRMDATE(VIEN)
 ;Save the rest of the finding information.
 S FIEVAL(FINDING)=1
 S FIEVAL(FINDING,"DATE")=DATE
 S FIEVAL(FINDING,"FINDING")=SKINIEN_";AUTTSK("
 S FIEVAL(FINDING,"SOURCE")=IEN_";AUPNVSK("
 S FIEVAL(FINDING,"READING")=READING
 S FIEVAL(FINDING,"RESULTS")=RESULTS
 S FIEVAL(FINDING,"VALUE")=RESULTS
 S FIEVAL(FINDING,"VIEN")=VIEN
 ;If this is being called as part of a term evaluation we are done.
 I TFIND0'="" Q
 ;Determine if the finding has expired.
 S VALID=$$VALID^PXRMDATE(FIND0,TFIND0,DATE)
 I 'VALID D  Q
 . S FIEVAL(FINDING)=0
 . S FIEVAL(FINDING,"EXPIRED")=""
 ;If there is a condition for this finding evaluate it.
 S CONVAL=$$COND^PXRMUTIL(FIND3,TFIND3,RESULTS)
 I CONVAL'="" D
 . I CONVAL D
 .. S FIEVAL(FINDING)=CONVAL
 .. S FIEVAL(FINDING,"CONDITION")=CONVAL
 . E  D
 .. K FIEVAL(FINDING)
 .. S FIEVAL(FINDING)=0
 Q
 ;
 ;=======================================================================
OUTPUT(NLINES,TEXT,FINDING,FIEVAL) ;Produce the clinical
 ;maintenance output.
 N EM,FIEN,IND,PNAME,READING,RESULTS,SKINIEN,TEMP,VDATE
 S SKINIEN=$P(FIEVAL(FINDING,"SOURCE"),";",1)
 S FIEN=$P(FIEVAL(FINDING,"FINDING"),";",1)
 S VDATE=FIEVAL(FINDING,"DATE")
 S READING=$G(FIEVAL(FINDING,"READING"))
 S RESULTS=$G(FIEVAL(FINDING,"RESULTS"))
 S TEMP=$$EDATE^PXRMDATE(VDATE)
 S TEMP=TEMP_" Skin Test: "
 S IND=$P(^AUPNVSK(SKINIEN,0),U,1)
 S PNAME=$P(^AUTTSK(FIEN,0),U,1)
 S TEMP=TEMP_PNAME
 I $L(RESULTS)>0 D
 . S TEMP=TEMP_"; results - "
 . S TEMP=TEMP_$$EXTERNAL^DILFD(9000010.12,.04,"",RESULTS,.EM)
 I $L(READING)>0 D
 . S TEMP=TEMP_"; reading - "
 . S TEMP=TEMP_$$EXTERNAL^DILFD(9000010.12,.05,"",READING,.EM)
 ;If the finding has expired add "EXPIRED"
 I $D(FIEVAL(FINDING,"EXPIRED")) S TEMP=TEMP_" - EXPIRED"
 ;If the finding is false because of the value add the reason.
 I $G(FIEVAL(FINDING,"CONDITION"))=0 S TEMP=TEMP_$$ACTFT^PXRMOPT
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 I $D(PXRMDEV) D
 . N UID
 . S UID="SKIN "_PNAME
 . S ^TMP(PXRMPID,$J,PXRMITEM,UID)=TEMP
 ;
 ;For historical encounters display the location and comments.
 I VDATE["E" D
 . N INDEX,VIEN
 . S INDEX="SKIK-"_PNAME
 . S VIEN=FIEVAL(FINDING,"VIEN")
 . D HLCV^PXRMVSIT(.NLINES,.TEXT,VIEN,INDEX)
 Q
 ;
