PXRMTERM ; SLC/PKR - Handle reminder terms. ;18-Apr-2006 17:39;MGH
 ;;1.5;CLINICAL REMINDERS;**1,7,8,1003,1004**;Jun 19, 2000
 ;IHS/CIA/MGH Modified to add lookups for files not used by VA
 ;=======================================================================
EVALFI(DFN,FIEVAL) ;Evaluate reminder terms.
 N CONVAL,DATE,FIND0,FIND3,FINDING,IND
 N TFIND0,TFIND3,TERMIEN,TFI,TFIEVAL,VALID,VALUE
 S TERMIEN=""
 F  S TERMIEN=$O(^PXD(811.9,PXRMITEM,20,"E","PXRMD(811.5,",TERMIEN)) Q:+TERMIEN=0  D
 . S FINDING=""
 . F  S FINDING=$O(^PXD(811.9,PXRMITEM,20,"E","PXRMD(811.5,",TERMIEN,FINDING)) Q:+FINDING=0  D
 .. S FIEVAL(FINDING)=0
 .. K TFIEVAL,TSORT
 .. D EVALTERM(DFN,FINDING,TERMIEN,.TFIEVAL)
 ..;Use the most recent of the term findings as the value for the
 ..;term.
 .. S (DATE,IND,TFI)=0
 .. F  S IND=$O(TFIEVAL(IND)) Q:+IND=0  D
 ... I 'TFIEVAL(IND) Q
 ... I TFIEVAL(IND,"DATE")>DATE D
 .... S DATE=TFIEVAL(IND,"DATE")
 .... S TFI=IND
 .. I TFI=0 S FIEVAL(FINDING)=0 Q
 ..;Use the most recent finding for the value of the term.
 .. M FIEVAL(FINDING)=TFIEVAL(TFI)
 .. S FIEVAL(FINDING,"TERM")=^PXRMD(811.5,TERMIEN,0)
 .. S FIND0=^PXD(811.9,PXRMITEM,20,FINDING,0)
 .. S FIND3=$G(^PXD(811.9,PXRMITEM,20,FINDING,3))
 .. S TFIND0=^PXRMD(811.5,TERMIEN,20,TFI,0)
 .. S TFIND3=$G(^PXRMD(811.5,TERMIEN,20,TFI,3))
 ..;Determine if the finding has expired.
 .. S VALID=$$VALID^PXRMDATE(FIND0,TFIND0,DATE)
 .. I 'VALID D  Q
 ... S FIEVAL(FINDING)=0
 ... S FIEVAL(FINDING,"EXPIRED")=""
 ..;If there is a condition for this finding evaluate it.
 .. S VALUE=$G(FIEVAL(FINDING,"VALUE"))
 .. S CONVAL=$$COND^PXRMUTIL(FIND3,TFIND3,VALUE)
 .. I CONVAL'="" D
 ... I CONVAL D
 .... S FIEVAL(FINDING)=CONVAL
 .... S FIEVAL(FINDING,"CONDITION")=CONVAL
 ... E  D
 .... K FIEVAL(FINDING)
 .... S FIEVAL(FINDING)=0
 Q
 ;
 ;=======================================================================
EVALTERM(DFN,FINDING,TERMIEN,TFIEVAL) ;Evaluate all the findings in a term.
 ;Use the "E" cross-reference just like the finding evaluation.
 I '$D(^PXRMD(811.5,TERMIEN,20,"E")) D  Q
 . S ^TMP(PXRMPID,$J,PXRMITEM,"WARNING","NOFI")="Warning no findings items in reminder term "_$P(^PXRMD(811.5,TERMIEN,0),U,1)
 N FTYPE
 S FTYPE=""
 F  S FTYPE=$O(^PXRMD(811.5,TERMIEN,20,"E",FTYPE)) Q:FTYPE=""  D
 . I FTYPE="AUTTEDT(" D EVALTERM^PXRMEDU(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="AUTTEXAM(" D EVALTERM^PXRMEXAM(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="AUTTHF(" D EVALTERM^PXRMHF(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="AUTTIMM(" D EVALTERM^PXRMIMM(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="AUTTSK(" D EVALTERM^PXRMSKIN(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="GMRD(120.51," D EVALTERM^PXRMMEAS(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="LAB(60," D EVALTERM^PXRMLAB(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="ORD(101.43," D EVALTERM^PXRMORDR(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="PXD(811.2," D EVALTERM^PXRMTAX(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="PXRMD(811.4," D EVALTERM^PXRMCF(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="PXRMD(811.5," D EVALTERM^PXRMTERM(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="PS(50.605," D EVALTERM^PXRMDRCL(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="PSDRUG(" D EVALTERM^PXRMDRUG(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="PSNDF(50.6," D EVALTERM^PXRMDGEN(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="RAMIS(71," D EVALTERM^PXRMRAD(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="YTT(601," D EVALTERM^PXRMMH(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . ;IHS/CIA/MGH  - 10/19/05 patch 1003,1004
 . ;Calls to resolve finding files form IHS that are not part of VA
 . I FTYPE="AUTTMSR(" D EVALTERM^BPXRMEA(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 . I FTYPE="AUTTREFT(" D EVALTERM^BPXRMREF(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 Q
 ;
 ;=======================================================================
OUTPUT(NLINES,TEXT,FINDING,FIEVAL) ;Produce the clinical
 ;maintenance output.
 N TEMP
 S TEMP="Reminder Term: "_$P(FIEVAL(FINDING,"TERM"),U,1)
 S NLINES=NLINES+1
 S TEXT(NLINES)=TEMP
 Q
 ;
 ;=======================================================================
PATLIST(INDEX,TERMIEN,FINDING,FIND0,FIND3,FIEVAL) ;Build a list
 ;of patients for all the findings in a term.
 I $D(PXRMPID),'$D(^PXRMD(811.5,TERMIEN,20,"E")) D  Q
 . S ^TMP(PXRMPID,$J,PXRMITEM,"WARNING","NOFI")="Warning no findings items in reminder term "_$P(^PXRMD(811.5,TERMIEN,0),U,1)
 N DATE,FIEN,FTYPE,TFIEVAL,TFIND0,TFIND3,TFINDING
 S FTYPE=""
 F  S FTYPE=$O(^PXRMD(811.5,TERMIEN,20,"E",FTYPE)) Q:FTYPE=""  D
 . S FIEN=0
 . F  S FIEN=+$O(^PXRMD(811.5,TERMIEN,20,"E",FTYPE,FIEN)) Q:FIEN=0  D
 .. S TFINDING=+$O(^PXRMD(811.5,TERMIEN,20,"E",FTYPE,FIEN,""))
 .. S TFIND0=^PXRMD(811.5,TERMIEN,20,TFINDING,0)
 .. S TFIND3=$G(^PXRMD(811.5,TERMIEN,20,TFINDING,3))
 .. S TFIEVAL(TFINDING,"FINDING")=FIEN_";"_FTYPE
 .. I FTYPE="AUTTEDT(" D PATLIST^PXRMEDU("PXRMTERM",FIEN,TFINDING,FIND0,FIND3,TFIND0,TFIND3,.TFIEVAL) Q
 .. I FTYPE="AUTTEXAM(" D PATLIST^PXRMEXAM("PXRMTERM",FIEN,TFINDING,FIND0,FIND3,TFIND0,TFIND3,.TFIEVAL) Q
 .. I FTYPE="AUTTHF(" D PATLIST^PXRMHF("PXRMTERM",FIEN,TFINDING,FIND0,FIND3,TFIND0,TFIND3,.TFIEVAL) Q
 .. ;I FTYPE="AUTTIMM(" D PATLIST^PXRMIMM(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="AUTTSK(" D PATLIST^PXRMSKIN(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="GMRD(120.51," D PATLIST^PXRMMEAS(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="LAB(60," D PATLIST^PXRMLAB(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="ORD(101.43," D PATLIST^PXRMORDR(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="PXD(811.2," D PATLIST^PXRMTAX(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="PXRMD(811.4," D PATLIST^PXRMCF(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="PS(50.605," D PATLIST^PXRMDRCL(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="PSDRUG(" D PATLIST^PXRMDRUG(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="PSNDF(50.6," D PATLIST^PXRMDGEN(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="RAMIS(71," D PATLIST^PXRMRAD(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 .. ;I FTYPE="YTT(601," D PATLIST^PXRMMH(DFN,FINDING,TERMIEN,.TFIEVAL) Q
 ;Process the lists and return most recent finding for each patient
 ;as the result for the term.
 S DFN=0
 F  S DFN=+$O(^TMP("PXRMTERM",$J,DFN)) Q:DFN=0  D
 . S DATE=$O(^TMP("PXRMTERM",$J,DFN,""),-1)
 . S TFINDING=$O(^TMP("PXRMTERM",$J,DFN,DATE,""))
 . S ^TMP(INDEX,$J,DFN,DATE,FINDING)=^TMP("PXRMTERM",$J,DFN,DATE,TFINDING)
 K ^TMP("PXRMTERM",$J)
 Q
 ;
