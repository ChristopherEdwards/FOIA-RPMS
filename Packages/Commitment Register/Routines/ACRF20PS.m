ACRF20PS ;IHS/OIRM/DSD/AEF - ARMS PATCH 20 POST INSTALL ROUTINE [ 07/24/2006  11:08 AM ]
 ;;2.1;ADMINISTRATIVE RESOURCE MGMT SYSTEM;**20**;MAY 27, 2004
 ;
EN ;EP -- MAIN ENTRY POINT
 ;
 D ^XBKVAR
 D HOME^%ZIS
 D AUDIT
 D COMPP
 Q
COMPP ;----- RECOMPILE PRINT TEMPLATES
 ;
 N ACRPTEMP,I,X
 ;
 D BMES^XPDUTL("Recompiling print templates...")
 ;
 F I=1:1 S X=$T(PTEMP+I) Q:X["$$END"  S ACRPTEMP($P(X,";;",2))=""
 ;
 S ACRPTEMP=""
 F  S ACRPTEMP=$O(ACRPTEMP(ACRPTEMP)) Q:ACRPTEMP']""  D
 . D ONEP(ACRPTEMP)
 Q
ONEP(ACRPTEMP)     ;
 ;----- RECOMPILE ONE PRINT TEMPLATE
 ;
 ;      ACRPTEMP  =  PRINT TEMPLATE NAME
 ;
 N ACRFILE,ACRIEN,DMAX,X,Y
 ;
 S ACRFILE=$P(ACRPTEMP,U,2)
 S ACRPTEMP=$P(ACRPTEMP,U)
 S ACRIEN=0
 F  S ACRIEN=$O(^DIPT("B",ACRPTEMP,ACRIEN)) Q:'ACRIEN  D
 . Q:$P($G(^DIPT(ACRIEN,0)),U,4)'=ACRFILE
 . S X=$P($G(^DIPT(ACRIEN,"ROU")),U,2)
 . Q:X']""
 . S Y=ACRIEN
 . S DMAX=$$ROUSIZE^DILF
 . D EN^DIPZ
 Q
 ; 
PTEMP ;----- PRINT TEMPLATES TO BE COMPILED
 ;;ACR TRAVEL ORDER^9002196
 ;;ACR ORDER FOR SUP HEAD^9002196
 ;;ACR PROGRAM INFO^9999999.62
 ;;ACR PURCHASE ORDER AMENDMENT^9002196
 ;;ACR PURCHASE ORDER INFO^9002196
 ;;ACR ARMS USER ADDRESS^9002185.3
 ;;ACR REQUISITION-TX^9002196
 ;;ACR SEPARATE TRAVEL ITINERARY^9002196
 ;;ACR TRAINING EVALUATION^9002191.6
 ;;ACR TRAVEL ITINERARY^9002196
 ;;ACR TRAVEL ORDER SUMMARY^9002196
 ;;ACR TRAVEL VOUCHER^9002196
 ;;ACR TRAVEL VOUCHER SUMMARY^9002196
 ;;ACR TRAVELER BANK INFO^9002185.3
 ;;ACR ARMS CAN^9002186.5
 ;;ACR DEPARTMENT ACCOUNT INFO^9002188
 ;;ACR CONTROL SEQUENCE INFO^9002190
 ;;ACR SIGNATURE SUMMARY^9002190
 ;;ACR CERTIFICATION^9002197.1
 ;;ACR REQUEST CONTROLLER AUDIT^9002190
 ;;ACR REQUISITION TAIL^9002196
 ;;ACR REQUISITION DISPLAY-2^9002196
 ;;ACR SF-44 DISPLAY^9002196
 ;;ACR TRAVEL ORDER DISPLAY^9002196
 ;;ACR PURCHASE ORDER DISPLAY^9002196
 ;;ACR CONTRACT DISPLAY^9002196
 ;;ACR TRIBAL CONTRACT DISPLAY^9002196
 ;;ACR REQUISITION HEAD^9002196
 ;;ACR REQUISTION-TX SUMMARY^9002196
 ;;ACR SEPARATE TRAVEL ITINERARY^9002196
 ;;ACR TRAVEL VOUCHER AUDIT LIST^9002196
 ;;ACR TRAINING REQUEST DISPLAY^9002196
 ;;ACR PURCHASING OFFICE^9002199.4
 ;;$$END
 Q
 ;
AUDIT ;CLEAN OUT THE VENDOR EDIT AUTHORITY FIELD IN ARMS USER FILE
 D BMES^XPDUTL("Removing VENDOR EDIT AUTHORITY from ARMS USER file...")
 N ACRX,ACR1,DA,DIE,DR
 S ACRX=0
 F  S ACRX=$O(^ACRAU(ACRX)) Q:'ACRX  D
 .S ACR1=$G(^ACRAU(ACRX,1))
 .Q:$P(ACR1,U,17)']""
 .S DA=ACRX
 .S DIE="^ACRAU("
 .S DR="17///@"
 .D DIE^ACRFDIC
 .W !," FOR: ",^ACRAU(ACRX,0)
 D BMES^XPDUTL("VENDOR EDIT AUTHORITY cleanup completed...")
 Q
