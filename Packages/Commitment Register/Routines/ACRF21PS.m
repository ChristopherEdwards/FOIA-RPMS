ACRF21PS ;IHS/OIRM/DSD/AEF - ARMS PATCH 21 POST INSTALL ROUTINE [ 01/13/2006  11:08 AM ]
 ;;2.1;ADMINISTRATIVE RESOURCE MGMT SYSTEM;**21**;MAY 27, 2004
 ;
EN ;EP -- MAIN ENTRY POINT -- ACR*2.1*21
 ;
 D ^XBKVAR
 D HOME^%ZIS
 D COMPP
 D CIS56                       ; ADDED 1/13/2006 - EFG
 Q
COMPP ;----- RECOMPILE PRINT TEMPLATES
 ;
 N ACRPTEMP,I,X
 ;
 D BMES^XPDUTL("Recompiling print templates...")
 ;
 F I=1:1 S X=$T(PTEMP+I) Q:X["$$END"  S ACRPTEMP($P(X,";;",2))=""
 ;
 S ACRPTEMP=""
 F  S ACRPTEMP=$O(ACRPTEMP(ACRPTEMP)) Q:ACRPTEMP']""  D
 . D ONEP(ACRPTEMP)
 Q
ONEP(ACRPTEMP)     ;
 ;----- RECOMPILE ONE PRINT TEMPLATE
 ;
 ;      ACRPTEMP  =  PRINT TEMPLATE NAME
 ;
 N ACRFILE,ACRIEN,DMAX,X,Y
 ;
 S ACRFILE=$P(ACRPTEMP,U,2)
 S ACRPTEMP=$P(ACRPTEMP,U)
 S ACRIEN=0
 F  S ACRIEN=$O(^DIPT("B",ACRPTEMP,ACRIEN)) Q:'ACRIEN  D
 . Q:$P($G(^DIPT(ACRIEN,0)),U,4)'=ACRFILE
 . S X=$P($G(^DIPT(ACRIEN,"ROU")),U,2)
 . Q:X']""
 . S Y=ACRIEN
 . S DMAX=$$ROUSIZE^DILF
 . D EN^DIPZ
 Q
CIS56 ; THIS CONVERSION IS NECESSARY FOR THE DCIS PROJECT.
 ; IT IS ELEMENT 11B - SUBCONTRACTING PLAN
 ; IT WILL CONVERT FIELD 56 (SUBCONTRACTING PLAN REQ)
 ;    IN THE CIS CONTRACTS AND GRANTS FILE.
 ;    IF FIELD 56=1 (PLAN REQUIRED) CONVERT TO C
 ;    IF FIELD 56=2 (PLAN NOT REQUIRED) CONVERT TO B
 S ACRCGIEN=0
 F  S ACRCGIEN=$O(^ACGS(ACRCGIEN)) Q:'ACRCGIEN  D
 .Q:'$G(^ACGS(ACRCGIEN,"DT3"))
 .I $P($G(^ACGS(ACRCGIEN,"DT3")),U)'=1,$P($G(^ACGS(ACRCGIEN,"DT3")),U)'=2 Q
 .I $P($G(^ACGS(ACRCGIEN,"DT3")),U)=1 D
 ..S $P(^ACGS(ACRCGIEN,"DT3"),U)="C"
 ..;W !,ACRCGIEN," :  ",$P($G(^ACGS(ACRCGIEN,"DT3")),U)
 .;I $P($G(^ACGS(ACRCGIEN,"DT3")),U)=2 D
 .;S $P(^ACGS(ACRCGIEN,"DT3"),1)="B"
 K ACRCGIEN
 Q
 ; 
PTEMP ;----- PRINT TEMPLATES TO BE COMPILED
 ;;ACR TRAVEL ORDER^9002196
 ;;ACR ORDER FOR SUP HEAD^9002196
 ;;ACR PROGRAM INFO^9999999.62
 ;;ACR PURCHASE ORDER AMENDMENT^9002196
 ;;ACR PURCHASE ORDER INFO^9002196
 ;;ACR ARMS USER ADDRESS^9002185.3
 ;;ACR REQUISITION-TX^9002196
 ;;ACR SEPARATE TRAVEL ITINERARY^9002196
 ;;ACR TRAINING EVALUATION^9002191.6
 ;;ACR TRAVEL ITINERARY^9002196
 ;;ACR TRAVEL ORDER SUMMARY^9002196
 ;;ACR TRAVEL VOUCHER^9002196
 ;;ACR TRAVEL VOUCHER SUMMARY^9002196
 ;;ACR TRAVELER BANK INFO^9002185.3
 ;;ACR ARMS CAN^9002186.5
 ;;ACR DEPARTMENT ACCOUNT INFO^9002188
 ;;ACR CONTROL SEQUENCE INFO^9002190
 ;;ACR SIGNATURE SUMMARY^9002190
 ;;ACR CERTIFICATION^9002197.1
 ;;ACR REQUEST CONTROLLER AUDIT^9002190
 ;;ACR REQUISITION TAIL^9002196
 ;;ACR REQUISITION DISPLAY-2^9002196
 ;;ACR SF-44 DISPLAY^9002196
 ;;ACR TRAVEL ORDER DISPLAY^9002196
 ;;ACR PURCHASE ORDER DISPLAY^9002196
 ;;ACR CONTRACT DISPLAY^9002196
 ;;ACR TRIBAL CONTRACT DISPLAY^9002196
 ;;ACR REQUISITION HEAD^9002196
 ;;ACR REQUISTION-TX SUMMARY^9002196
 ;;ACR SEPARATE TRAVEL ITINERARY^9002196
 ;;ACR TRAVEL VOUCHER AUDIT LIST^9002196
 ;;ACR TRAINING REQUEST DISPLAY^9002196
 ;;ACR PURCHASING OFFICE^9002199.4
 ;;$$END
