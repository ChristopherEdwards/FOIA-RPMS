ACRF22PS ;IHS/OIRM/DSD/AEF - ARMS PATCH 22 POST INSTALL ROUTINE [ 04/02/2007  11:08 AM ]
 ;;2.1;ADMINISTRATIVE RESOURCE MGMT SYSTEM;**22**;MAY 27, 2004
 ;
EN ;EP -- MAIN ENTRY POINT --
 ;
 D ^XBKVAR
 D HOME^%ZIS
 D COMPP                               ;USE TO RECOMPILE PRINT TEMPLATES
 D ADDMENU                             ;USE TO ADD NEW MENU OPTIONS
 D OPT                                 ;ADD KEY TO NEW MENU OPTION
 Q
COMPP ;----- RECOMPILE PRINT TEMPLATES
 ;
 N ACRPTEMP,I,X
 ;
 D BMES^XPDUTL("Recompiling print templates...")
 ;
 F I=1:1 S X=$T(PTEMP+I) Q:X["$$END"  S ACRPTEMP($P(X,";;",2))=""
 ;
 S ACRPTEMP=""
 F  S ACRPTEMP=$O(ACRPTEMP(ACRPTEMP)) Q:ACRPTEMP']""  D
 . D ONEP(ACRPTEMP)
 Q
ONEP(ACRPTEMP)     ;
 ;----- RECOMPILE ONE PRINT TEMPLATE
 ;
 ;      ACRPTEMP  =  PRINT TEMPLATE NAME
 ;
 N ACRFILE,ACRIEN,DMAX,X,Y
 ;
 S ACRFILE=$P(ACRPTEMP,U,2)
 S ACRPTEMP=$P(ACRPTEMP,U)
 S ACRIEN=0
 F  S ACRIEN=$O(^DIPT("B",ACRPTEMP,ACRIEN)) Q:'ACRIEN  D
 . Q:$P($G(^DIPT(ACRIEN,0)),U,4)'=ACRFILE
 . S X=$P($G(^DIPT(ACRIEN,"ROU")),U,2)
 . Q:X']""
 . S Y=ACRIEN
 . S DMAX=$$ROUSIZE^DILF
 . D EN^DIPZ
 Q
ADDMENU ; ADD AN OPTION TO A MENU
 ;
 ;SUBSCRIPTS FOR ADD^XPDMENU UTILITY:
 ;           1)  MENU NAME TO RECEIVE NEW OPTION
 ;           2)  OPTION NAME TO PUT ON MENU
 ;           3)  SYNONYM TO DISPLAY FOR NEW OPTION
 ; IF THE SYNONYM IS NOT PASSED, THE OPTION WILL USE THE SYNONYM THAT WAS
 ; ORIGINALLY ESTABLISHED ON THE SENDING SYSTEM
 ;
 ;***** MENU OPTION TO ADD *****
 ;
 NEW HEREYAGO
 D BMES^XPDUTL("Adding ACRF UFMS READINESS REPORTS to ACRF M VENDOR MENU.")
 S HEREYAGO=$$ADD^XPDMENU("ACRF M VENDOR MENU","ACRF UFMS READINESS REPORTS","URR")
 ;
 I HEREYAGO=1 D
 . D OKAY("ACRF UFMS READINESS REPORTS added to ACRF M VENDOR MENU.")
 ;
 I HEREYAGO'=1 D
 .S HEREYAGO=$P($$UP^XLFSTR(HEREYAGO),"^",2)      ; Uppercase Error Message
 .D TABMENU("Error in adding ACRF UFMS READINESS REPORTS to ACRF M VENDOR MENU.")
 .I $L(HEREYAGO)>0 D TABMENU("Error Message: "_$P(HERYAGO,"^",2),10)
 ;
 ;***** ADDITIONAL MENU OPTION TO ADD *****
 ;
 NEW HEREYAGO
 D BMES^XPDUTL("Adding ACRF UFMS CORE OPEN DOC RPTS to ACRF M VENDOR MENU.")
 S HEREYAGO=$$ADD^XPDMENU("ACRF M VENDOR MENU","ACRF UFMS CORE OPEN DOC RPTS","OD")
 ;
 I HEREYAGO=1 D
 . D OKAY("ACRF UFMS CORE OPEN DOC RPTS added to ACRF M VENDOR MENU.")
 ;
 I HEREYAGO'=1 D
 .S HEREYAGO=$P($$UP^XLFSTR(HEREYAGO),"^",2)      ; Uppercase Error Message
 .D TABMENU("Error in adding ACRF UFMS CORE OPEN DOC RPTS to ACRF M VENDOR MENU.")
 .I $L(HEREYAGO)>0 D TABMENU("Error Message: "_$P(HERYAGO,"^",2),10)
 ;
 ;***** ADDITIONAL MENU OPTION TO ADD *****
 ;
 NEW HEREYAGO
 D BMES^XPDUTL("Adding ACRF M VENDOR MENU to ACRMENU.")
 S HEREYAGO=$$ADD^XPDMENU("ACRMENU","ACRF M VENDOR MENU","VM")
 ;
 I HEREYAGO=1 D
 . D OKAY("ACRF M VENDOR MENU added to ACRMENU.")
 ;
 I HEREYAGO'=1 D
 .S HEREYAGO=$P($$UP^XLFSTR(HEREYAGO),"^",2)      ; Uppercase Error Message
 .D TABMENU("Error in adding ACRF M VENDOR MENU to ACRMENU.")
 .I $L(HEREYAGO)>0 D TABMENU("Error Message: "_$P(HERYAGO,"^",2),10)
 Q
OKAY(MSG,TAB) ;
 ; MESSAGE WITH "OK" AT THE END
 NEW MESSAGE
 I $G(TAB)="" S TAB=5
 S MESSAGE=$J("",TAB)_MSG_" OK."
 D MES^XPDUTL(MESSAGE)
 Q
TABMENU(MSG,TAB,TAIL) ;
 ; GENERIC MESSAGE OUTPUT
 NEW MESSAGE
 I $G(TAB)="" S TAB=5
 S MESSAGE=$J("",TAB)_MSG
 I $G(TAIL)'="" S MESSAGE=MESSAGE_" "_TAIL
 D MES^XPDUTL(MESSAGE)
 Q
OPT ;----- ADD KEY TO OPTION
 ;
 ;
 N DIC,X,Y
 S DIC="^DIC(19,"
 S DIC(0)=""
 S X="ACRF M VENDOR MENU"
 D ^DIC
 Q:+Y'>0
 D BMES^XPDUTL("Adding Vendor Menu key ACRZ VENDOR to Vendor Menu option")
 S DA=+Y
 S DIE=DIC
 S DR="3///^S X=""ACRZ VENDOR"""
 D ^DIE
 Q
 ; 
PTEMP ;----- PRINT TEMPLATES TO BE COMPILED
 ;;ACR CREDIT CARD VENDOR^9999999.11
 ;;$$END
