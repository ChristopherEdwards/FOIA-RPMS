ACRFAS ;IHS/OIRM/DSD/THL,AEF - UTILITY TO SET UP AREA SYSTEMS DATA; [ 09/22/2005   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**19**;NOV 05, 2001
 ;;ROUTINE USED TO ENTER AND EDIT AREA OFFICE SPECIFIC DEFAULT DATA
EN D EN1
 D:'$D(ACRQUIT) SYSCHK
EXIT K ACRQUIT
 Q
EN1 ;
 W @IOF
 W !?20,"AREA SYSTEM SETUP"
 W !
 D AREA
 Q:$D(ACRQUIT)
 I '$D(^ACRSYS(ACRADA,0)) D
 .S X=ACRADA
 .S DIC="^ACRSYS("
 .S DIC(0)="L"
 .D FILE^ACRFDIC
 S DA=ACRADA
 S DIE="^ACRSYS("
 S DR="[ACR SYSTEMS DEF]"
 D DDS^ACRFDIC
 I $D(ACRSCREN) K ACRSCREN D
 .F ACRDA=16,27,31,20,35,1,15,18,25,26,28,30,34,44 Q:$D(ACRQUIT)  D
 ..S DA=ACRDA
 ..S ACR=$P($T(@DA),";;",2)
 ..S ACRPC=$P($T(@DA),";;",3)
 ..S DIC="^VA(200,"
 ..S DIC(0)="AEMQ"
 ..S DIC("A")=ACR_": "
 ..;I $D(^ACRSYS(ACRADA,"DT")),$P(^("DT"),U,ACRPC),$D(^VA(200,$P(^("DT"),U,ACRPC),0)) S DIC("B")=$P(^(0),U)  ;ACR*2.1*19.02 IM16848
 ..I $D(^ACRSYS(ACRADA,"DT")),$P(^("DT"),U,ACRPC),$D(^VA(200,$P(^("DT"),U,ACRPC),0)) S DIC("B")=$$NAME2^ACRFUTL1($P(^ACRSYS(ACRADA,"DT"),U,ACRPC))  ;ACR*2.1*19.02 IM16848
 ..D DIC^ACRFDIC
 ..Q:$D(ACRQUIT)!$D(ACROUT)
 ..I +Y>0 D
 ...S ACRDAX=+Y
 ...I $D(^VA(200,+Y,0)),'$D(^ACRAPL("AC",+Y,ACRDA)) D
 ....S X=+Y
 ....S DIC="^ACRAPL("
 ....S DIC(0)="L"
 ....S DIC("DR")=".02////"_ACRDA
 ....D FILE^ACRFDIC
 ...I ACRDAX>0 D
 ....S X=ACRDA
 ....S DA=ACRADA
 ....S DIE="^ACRSYS("
 ....S DR=$S(X=16:1,X=27:2,X=31:11,X=20:3,X=35:14,X=1:4,X=15:5,X=18:6,X=25:7,X=26:8,X=28:9,X=30:10,X=34:12,X=44:15)
 ....S DR=DR_"////"_ACRDAX
 ....D DIE^ACRFDIC
 Q:$D(ACROUT)
 K ACRQUIT
 S DA=ACRADA
 S DIE="^ACRSYS("
 S DR="[ACR SYSTEMS DEF-2]"
 D DDS^ACRFDIC
 I $D(ACRSCREN) K ACRSCREN D DIE^ACRFDIC
 I $D(^ACRPO(1,0))&$D(^ACRPO(2,0)) D SUPER Q
 F ACRI=1,2,3 Q:$D(ACRQUIT)  D
 .I '$D(^AUTTPRG(ACRI,0)) D
 ..S X="AREA "_$S(ACRI=1:"PROCURMENT AND CONTRACTING",ACRI=2:"FINANCE OFFICE",1:"PROPERTY AND SUPPLY")
 ..S DINUM=ACRI
 ..S DIC="^AUTTPRG("
 ..S DIC(0)="L"
 ..D FILE^ACRFDIC
 .W !!?25,"Enter Data for Area "
 .W $S(ACRI=1:"Procurement",ACRI=2:"Finance",1:"Property and Supply")
 .W " Department."
 .W !?25,"------------------------------------------"
 .S DA=ACRI
 .S DIE="^AUTTPRG("
 .S DR="[ACR PROGRAM]"
 .W !
 .D DDS^ACRFDIC
 .Q:'$D(ACRSCREN)
 .K ACRSCREN
 .D DIE^ACRFDIC
 Q:$D(ACROUT)
SUPER ;ENTER NAMES OF AREA CONTRACT OFFICER AND PURCHASING SUPERVISOR
 I $D(^ACRPO(1,0)),$P(^(0),U,3)&$P(^(0),U,11) Q
 K ACRQUIT
 F ACRI=1,2 Q:$D(ACRQUIT)  D
 .W !!?25,"Enter name of the Area "
 .W $S(ACRI=1:"Contract Officer.",1:"Purchasing Supervisor.")
 .W !?25,"------------------------------------------"
 .S DIC="^ACRPA("
 .S DIC(0)="AELMQZ"
 .S DIC("A")=$S(ACRI=1:"Area Contract Officer..: ",1:"Area Purchasing Supervisor: ")
 .D DIC^ACRFDIC
 .Q:+Y<1
 .S DA=+Y
 .S DIE="^ACRPA("
 .S DR=".02T"
 .D DIE^ACRFDIC
 Q
DHR ;EP;TO DISPLAY AND EDIT SYSTEMS DHR INTERFACE INFO
 F  D DHR1 Q:$D(ACRQUIT)!$D(ACROUT)
 K ACRQUIT,ACROUT
 Q
DHR1 W @IOF
 W !?20,"DHR INTERFACE SUMMARY"
 W !!
 N DXS,DIP
 S D0=1
 D ^ACRPDHR
 S DIR(0)="YO"
 S DIR("A")="Edit this data"
 W !
 D DIR^ACRFDIC
 I $G(Y)'=1 S ACRQUIT="" Q
 S DA=1
 S DIE="^ACRSYS("
 S DR="[ACR SYSTEMS DHR SETUP]"
 D DDS^ACRFDIC
 Q:'$D(ACRSCREN)
 K ACRSCREN
 W !
 D DIE^ACRFDIC
 Q
31 ;;AREA PROPERTY CLEARANCE..;;11
20 ;;AREA PROPERTY MANAGEMENT.;;3
35 ;;AREA SUPPLY OFFICER......;;14
1 ;;AREA CONTRACT OFFICER....;;4
15 ;;AREA FINANCE OFFICER.....;;5
16 ;;AREA INFO SYSTEMS COORD..;;1
27 ;;AREA TELECOMMUNICATION...;;2
30 ;;AREA ADP SECURITY OFFICER;;10
34 ;;AREA M&M OFFICER.........;;12
18 ;;HQ OIRM..................;;6
25 ;;HQ TELECOMMUNICATIONS....;;7
26 ;;HQ DIV ADMIN SERVICES....;;8
28 ;;HQ PROPERTY MANAGEMENT...;;9
44 ;;DIR HQ OPERATIONS........;;15
 ;;
SYSCHK ;EP;TO ENSURE AREA SETUP IS COMPLETE
 I '$D(^ACRSYS(ACRADA,0))!'$D(^ACRSYS(ACRADA,"DT"))!$G(^ACRSYS(ACRADA,"DT"))="" D  Q
 .W !!,*7,*7,"AREA SYSTEM SETUP must be completed before you proceed with any ARMS functions."
 S ACRX=$G(^ACRSYS(ACRADA,"DT"))
 F ACRI=1:1:18 I $P(ACRX,U,ACRI)="" D SC1
 I $D(ACRQUIT) D
 .W !!,*7,*7,"You must complete this data befor you proceed with any ARMS functions."
 .D PAUSE^ACRFWARN
 Q
SC1 W !!,*7,*7,@ACRON,$P($T(MD+ACRI),";;",2),@ACROF," data is missing."
 S ACRQUIT=""
 Q
AREA ;EP;TO SELECT AREA SETUP
 K ACRADA
 S DIC="^ACRSYS("
 S DIC(0)=$S('$D(DIC(0))#2:"AELMQZ",1:DIC(0))
 S DIC("A")="Setup for which AREA OFFICE: "
 S DIC("B")=$P(^AUTTAREA(+^ACRSYS(1,0),0),U)
 W !
 D DIC^ACRFDIC
 I $D(ACRQUIT)!$D(ACROUT)!(+Y<1) S ACRQUIT="" Q
 S ACRADA=+Y
 Q
MD ;MISSING DATA HEADINGS
 ;;AREA INFO SYSTEMS COORD
 ;;AREA TELECOMMUNICATION
 ;;AREA PROPERTY MANAGEMENT
 ;;AREA CONTRACT OFFICER
 ;;AREA FINANCE OFFICER
 ;;HQ OIRM
 ;;HQ TELECOMMUNICATIONS
 ;;HQ DIV ADMIN SERVICES
 ;;HQ PROPERTY MANAGEMENT
 ;;AREA ADP SECURITY OFFICER
 ;;AREA PROPERTY CLEARANCE
 ;;AREA M&M OFFICER
 ;;STANDARD MILEAGE RATE
 ;;AREA SUPPLY OFFICER
 ;;DIR HQ OPERATIONS
 ;;ATM SERVICE CHARGE RATE
 ;;DAILY PHONE CALL AMOUNT
 ;;AIRLINE TICKET CHARGED TO GOVT CC
 ;;EXCEED ESTIMATE BY PERCENT
 ;;EXCEED ESTIMATE BY MAXIMUM AMOUNT
 ;;CONUS LODGING
 ;;CONUS PERDIEM
