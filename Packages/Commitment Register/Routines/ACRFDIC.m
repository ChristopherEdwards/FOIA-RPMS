ACRFDIC ;IHS/OIRM/DSD/THL,AEF - FILEMAN INTERFACE UTILITY; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;UTILITY ROUTINE USED TO MANAGE ALL INTERFACE TO FILEMAN
 ;;WITH FEW EXCEPTIONS, THIS IS THE ONLY PLACE IN ARMS FROM WHICH
 ;;FILEMAN ENTRY POINTS ARE CALLED
DIC ;EP;
 Q:$D(ACROUT)
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 D ^DIC
 S:$D(DUOUT) ACRQUIT=""
 S:$D(DTOUT)!(X="^^") (ACRQUIT,ACROUT)=""
 K DIC,DA,DD,DR,DINUM,D,DLAYGO
 Q
MIX ;EP;
 Q:$D(ACROUT)
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 D MIX^DIC1
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACRQUIT,ACROUT)=""
 K DIC,DA,DD,DR,DINUM,D,DLAYGO
 Q
IX ;EP;
 Q:$D(ACROUT)
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 D IX^DIC
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACRQUIT,ACROUT)=""
 K DIC,DA,DD,DR,DINUM,D,DLAYGO
 Q
DIE ;EP;
 Q:$D(ACROUT)
 Q:'DA
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 L +@(DIE_DA_")"):4 I '$T S ACRQUIT="" Q:'$D(IOST)#2  W:$E(IOST,1,2)="C-" !!,"Entry being edited by another user.  Please try Later.",! H 3 Q
 S ACRDIEDA=DA
 D ^DIE
 L -@(DIE_ACRDIEDA_")"):0
 I $D(DTOUT)!$D(DUOUT) S (ACRQUIT,ACROUT)=""
 K DIE,DA,DR,ACRDIEDA
 Q
DDS ;EP;FOR SCREENMAN CALLS
 D SCREEN^ACRFAU
 I '$D(ACRSCREN)!'$D(^DIST(.403,"B",$TR(DR,"[]",""))) S ACRSCREN="" Q
 S DDSFILE=DIE
 D ^DDS
 K DDSFILE,ACRSCREN
 Q
FILE ;EP;
 K DD,DO,DTOUT,DUOUT,ACRQUIT,ACROUT
 D FILE^DICN
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACRQUIT,ACROUT)=""
 K DIC,DA,DD,DR,DINUM,DLAYGO
 Q
DIR ;EP;
 I $D(ACROUT) K DIR S Y="" Q
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 D ^DIR
 S ACRY=Y
 S:$D(DIRUT)!$D(DIROUT)!$D(DUOUT) ACRQUIT=""
 S:X="^^"!$D(DTOUT) (ACRQUIT,ACROUT)=""
 K DIR,DIRUT,DIROUT,DUOUT,DTOUT
 Q
DIK ;EP;
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 D ^DIK
 K DIK
 Q
IX1 ;EP;
 Q:$D(ACROUT)
 K DTOUT,DUOUT,ACRQUIT,ACROUT
 D IX1^DIK
 K DIK,DA
 Q
DIQ1 ;EP;
 D EN^DIQ1
 K DIC,DA,DR,DIQ
 Q
WP ;EP;TO BROWSE WORD PROCESSING FIELD
 Q:'$D(ACRFILE)#2!'$D(ACRIEN)#2!'$D(ACRFIELD)
 D SCREEN^ACRFAU
 I '$D(ACRSCREN) S ACRSCREN="" Q
 K ACRSCREN
 D WP^DDBR(ACRFILE,ACRIEN,ACRFIELD,"N",$G(ACRTITLE))
 Q
OBJ ;EP;TO SCREEN OBJECT CODES BY TYPE OF TRANSACTION
 Q:'$D(ACRLBDA)
 Q:'$D(ACRDOCDA)
 I $P($G(^AUTTOBJC(+$G(Y),0)),U,12)=1
 E  Q
 N ACRTXDA
 S ACRTXDA=$P(^ACRDOC(ACRDOCDA,0),U,4)
 Q:'ACRTXDA
 Q:ACRTXDA=11
 S ACRGREF="^AUTTOBJC"
 G OBJ3:$D(ACRSTOCK)
OBJ1 I $D(^ACRTXTYP(ACRTXDA,10,+Y))
 Q
OBJ3 I $P(^AUTTOBJC(+Y,0),U,8)="S" Q
 Q
CAN Q:$G(ACRLBDA)<1
 I $D(^ACRLOCB("CAN",+Y,ACRLBDA))!$D(^ACRLOCB("DCAN",+Y,ACRLBDA))
 Q
DICS() ;EP TO SET DIC("S") FOR CAN LOOKUP FOR SUPPLIES/SERVICES
 S DIC("S")="Q:'$G(ACRLBDA)!'$G(ACRDOCDA)  I $D(^ACRLOCB(""DCAN"",+Y,ACRLBDA))!$D(^ACRLOCB(""OCAN"",+Y,ACRLBDA))!($P($G(^ACRDOC(ACRDOCDA,0)),U,4)=36!($P($G(^(0)),U,4)=37)&($D(^ACRLOCB(""DCAN"",+Y))))"
 Q DIC("S")
LBCAN() ;EP TO WRITE THE DEFAULT CAN FOR THE DEPARTMENT ACCOUNT
 S Y=$P($G(^AUTTCAN(+$P($G(^ACRLOCB(+$G(D0),"DT")),U,9),0)),U)
 Q ""
SSDA() ;EP TO WRITE DEPARTMENT ACCOUNT CAN
 S Y=$P($G(^AUTTCAN(+$P($G(^ACRLOCB(+$P($G(^ACRSS(+$G(DA),0)),U,6),"DT")),U,9),0)),U)
 Q ""
