ACRFEA3 ;IHS/OIRM/DSD/THL,AEF - CUSTOMIZED DOCUMENT EDITING UTILITY;  [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;CONTINUATION OF ACRFEA
 ;;ROUTINE USED TO CUSTOMIZE DOCUMENT EDITING
EN K ACRPARS
 D ENX
EXIT S:$D(ACRREFX) ACRREF=ACRREFX
 K ACRX,ACRI,DIE,DR,DA,ACRJ,ACRL,ACRPARS
 Q
ENX N ACRI,ACRY,ACRX
 S:ACRREF=600 ACRREF=130,ACRREFX=600
 S:ACRREF=210 ACRREF=103,ACRREFX=210
 S ACRL=$T(@ACRREF^ACRFEA31)
 S ACRL=$P(ACRL,";;",2)
 S DIR(0)="LOB^1:"_$S($D(ACRL):ACRL,1:ACRI)
 S DIR("A")="       Edit which field(s)?"
 D DIR^ACRFDIC
 I $D(ACRQUIT)!$D(ACROUT) Q
 S ACRX=Y(0)
 S:$E(ACRX,$L(ACRX))="," ACRX=$E(ACRX,1,$L(ACRX)-1)
 S ACRFEA3=0
EN1 ;EP;
 D HOME^ACRFMENU
 N ACRY
 W !
 F ACRI=1:1:$L(ACRX,",") D EN11
 I $D(ACRAUTOD) D DELAUTO^ACRFSS41 K ACRAUTOD
 Q
EN11 K ACRREFZ
 S:ACRREF=210 ACRREF=103,ACRREFZ=210
 S ACRY=$P(ACRX,",",ACRI)
 S ACRMESS="[ "_ACRY_" ]"
 S ACRDDIE="HEAD^ACRFEA3"
 S DA=ACRDOCDA
 S DIE=ACRDIE
 S DR=$T(@ACRREF+ACRY^ACRFEA31)
 I ACRREF=999&(ACRY>1) D
 .S DIE="^ATAEMP("
 .S DA=$P(^ACRDOC(DA,"PR"),U)
 S DR=$P(DR,";;",2)_$S(DR'["]":"T",1:"")
 N ACRTXDA
 I ACRREF=116,+DR=.04 S ACRTXDA=$P(^ACRDOC(DA,0),U,4)
 D DIE^ACRFDIC
 I $D(ACRTXDA),$P(^ACRDOC(ACRDOCDA,0),U,4)'=ACRTXDA D
 .I $P(^ACRDOC(ACRDOCDA,0),U,4)=35,$E($G(^ACROBL(ACRDOCDA,"APV")))="A" Q
 .;IF CHANGED TO CREDIT CARD PURCHASE DURING PURCHASE ORDER PROCESSING
 .;DO NOT RESET PREVIOUS APPROVALS
 .S ACRSCHK=""
 .D APPROVE^ACRFSCHK
 I $D(ACRREFZ) S ACRREF=ACRREFZ K ACRREFZ
 I $D(ACRNEWOB),ACRREF=130,$P($G(^ACRDOC(ACRDOCDA,"TO")),U,9) D
 .;THIS SEQUENCE WILL SET TRAVEL AUTHORIZATION TO 'JTR' FOR
 .;COMMISSIONED OFFICERS
 .S ACRDUZ=$P(^ACRDOC(ACRDOCDA,"TO"),U,9)
 .Q:$P($G(^ACRAU(ACRDUZ,1)),U,3)'="CO"
 .S DA=ACRDOCDA
 .S DIE="^ACRDOC("
 .S DR="130121////J"
 .D DIE^ACRFDIC
 I ACRREF=148 D
 .N ACRTP
 .S ACRTP=$P($G(^ACRDOC(+$G(ACRDOCDA),"TRNG")),U,2)
 .Q:'ACRTP
 .S DR=""
 .D EOD1^ACRFTPA1
 .S DA=ACRDOCDA
 .S DIE="^ACRDOC("
 .S DR=$E(DR,2,999)
 .D DIE^ACRFDIC
 Q
HEAD N ACRX,ACRI
 W @IOF
 W ?4,"DOCUMENT NO:  ",@ACRON,ACRDOC,@ACROF
 W $$DASH^ACRFMENU
 N DXS,DIP,DC,DN
 S D0=ACRDOCDA
 D @ACRRTN2
 W $$DASH^ACRFMENU
 W !
 Q
