ACRFIRS8 ;IHS/OIRM/DSD/AEF - LIST VENDORS BY SELECTED PARAMETERS [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;
EN ;EP -- MAIN ENTRY POINT      
 ;
 D SORT(.ACRSORT)
 Q:$G(ACRSORT)']""
 D PRINT(.ACRPRT)
 Q:$G(ACRPRT)']""
 D DIP(ACRSORT,ACRPRT)
 Q
SORT(ACRSORT)      ;
 ;----- HOW TO SORT
 ;
 N DIR,DIRUT,DTOUT,DUOUT,X,Y
 S DIR(0)="S^NAME:List Alphabetically by Name;EIN:List by EIN#;YTD:List by YTD PAID;YTDS:List by YTD PAID Within STATE;1099:List Vendors With '1099 Y/N' = 'Y'"
 S DIR("A")="    SELECTION"
 S DIR("B")="NAME"
 D ^DIR
 Q:$D(DTOUT)!($D(DUOUT))!($D(DIRUT))
 Q:Y']""
 I Y="NAME" S ACRSORT="[ACRF 1099 VENDOR BY NAME]"
 I Y="EIN" S ACRSORT="[ACRF 1099 VENDOR BY EIN]"
 I Y="YTD" S ACRSORT="[ACRF 1099 VENDOR BY YTD]"
 I Y="YTDS" S ACRSORT="[ACRF 1099 VENDOR BY STATE]"
 I Y=1099 S ACRSORT="[ACRF 1099 VENDOR 1099 YES]"
 Q
PRINT(ACRPRT)      ;
 ;----- WHAT TO PRINT
 ;
 N DIR,DIRUT,DTOUT,DUOUT,X,Y
 S DIR(0)="S^M:Provide MAILING ADDRESS Only;B:Provide BILLING ADDRESS Only;R:Provide REMIT TO ADDRESS Only;A:Provide ALL ADDRESSES on Listing"
 S DIR("A")="     SELECTION"
 S DIR("B")="M"
 D ^DIR
 Q:$D(DTOUT)!($D(DUOUT))!($D(DIRUT))
 Q:Y']""
 I Y="M" S ACRPRT="[ACRF 1099 VENDOR MAIL ADD]"
 I Y="B" S ACRPRT="[ACRF 1099 VENDOR BILL ADD]"
 I Y="R" S ACRPRT="[ACRF 1099 VENDOR REMIT ADD]"
 I Y="A" S ACRPRT="[ACRF 1099 VENDOR ALL ADD]"
 Q
 Q
DIP(ACRSORT,ACRPRT)          ;
 ;----- CALL FILEMAN TO PRINT REPORT
 ;
 N BY,DIC,FLDS,L,X,Y
 S L=0
 S DIC="^AUTTVNDR("
 S BY=ACRSORT
 S FLDS=ACRPRT
 D EN1^DIP
 Q
