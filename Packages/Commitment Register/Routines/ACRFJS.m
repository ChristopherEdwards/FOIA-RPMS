ACRFJS ;IHS/OIRM/DSD/THL,AEF - PRINT JUSTIFICATION AND SPECIAL REMARKS; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;ROUTINE TO PRINT JUSTIFICATION AND SPECIAL REMARKS
DISPLAY ;EP;TO DISPLAY NARRATIVE AND JUSTIFICATION
 Q:$D(ACROUT)
 I $D(^ACROBL(ACRDOCDA,"NOTES")) S ACRX=^("NOTES") D:ACRX]""
 .N ACRI
 .W !,"BRIEF NARRATIVE:--------------------------------------------------------------"
 .D W
 I $D(^ACROBL(ACRDOCDA,"NOTES2")) S ACRX=^("NOTES2") D W:ACRX]""
 I $D(^ACROBL(ACRDOCDA,"JST")) S ACRX=^("JST") D
 .W !,"JUSTIFICATION:------------------------------------------------------------------"
 .D W
 .I $D(^ACROBL(ACRDOCDA,"JST2")) S ACRX=^("JST2") D W:ACRX]""
 D VEND
EXIT D PAUSE^ACRFWARN
 K ACRX
 Q
W F ACRI=1:1:5 W:$P(ACRX,U,ACRI)]"" !?4,$P(ACRX,U,ACRI)
 Q
VENDOR ;EP;TO DISPLAY REQUESTED VENDOR DATA
 Q:$D(ACROUT)
 N ACRFVNDA
 S ACRFVNDA=$P(ACRDOCPO,U,5)
 I ACRFVNDA,$D(^AUTTVNDR(ACRFVNDA,0)) D
 .S ACRVN0=^AUTTVNDR(ACRFVNDA,0)
 .S ACRVN11=$G(^AUTTVNDR(ACRFVNDA,11))
 .S ACRVN13=$G(^AUTTVNDR(ACRFVNDA,13))
 .W !!,"Requested Vendor: ",$P(ACRVN0,U)
 .W !?18,$P(ACRVN13,U)
 .W !?18,$P(ACRVN13,U,2),", ",$P(^DIC(5,$P(ACRVN13,U,3),0),U,2),"  ",$P(ACRVN13,U,4)
 .W !,"Attention.......: ",$P(ACRVN13,U,5)
 .W !,"Phone...........: ",$P(ACRVN11,U,9)
 .W !
 .K ACRVN0,ACRVN11,ACRVN13
 Q
NSVEND ;EP;TO PRINT NON-STANDARD VENDOR DATA
 Q:$D(ACROUT)
 S ACRVND1=$S($D(^ACRDOC(ACRDOCDA,3)):$P(^(3),U,1,8),1:"NO VENDOR SPECIFIED")
 W !?10,"(THIS VENDOR DATA NOT FROM STANDARD VENDOR FILE)"
 F ACRK=1:1:4 W:$P(ACRVND1,U,ACRK)]"" !?10,$P(ACRVND1,U,ACRK)
 W:$P(ACRVND1,U,5)]"" ", ",$P(^DIC(5,$P(ACRVND1,U,5),0),U,2),"  ",$P(ACRVND1,U,6)
 W !?10,"EIN.:",$P($G(^ACRDOC(ACRDOCDA,14)),U,10)
 W:$P(ACRVND1,U,7)]"" !?10,"ATTN: ",$P(ACRVND1,U,7)
 W:$P(ACRVND1,U,8)]"" !?10,"PHONE: ",$P(ACRVND1,U,8)
 W !
 D:ACRVND1]"" PAUSE^ACRFWARN
 Q
VEND ;EP
 W !,"REQUESTED VENDOR:---------------------------------------------------------------"
 I $D(ACRDOCVN),ACRDOCVN D  I 1
 .N D0,DXS,DIP,DC,DN
 .S D0=ACRDOCVN
 .D ^ACRPRVN
 E  I $P(ACRDOCPO,U,5)="",$D(^ACRDOC(ACRDOCDA,3)),$P(^(3),U)]"" D NSVEND I 1
 E  W !!,"NO VENDOR HAS BEEN SPECIFIED FOR THIS REQUEST."
 Q
