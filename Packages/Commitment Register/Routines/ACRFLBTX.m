ACRFLBTX ;IHS/OIRM/DSD/THL,AEF - ADD TRANSACTION TYPES FOR DEPARTMENT ACCOUNTS; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;ROUTINE USED TO ADD TRANSACTION TYPES FOR DEPARTMENT ACCOUNTS
EN F  D EN1 Q:$D(ACRQUIT)!$D(ACROUT)
EXIT K ACRLBTX
 Q
EN1 K ACRQUIT
 S ACRLBTX=""
 D DISPLAY^ACRFTXTP
 D ADD
 D SELECT:'$D(ACRQUIT)
 Q
ADD W !!,"You may ADD or REMOVE Transaction which can be accessed by this department."
 W !,"(** indicates that department has access to this type of transaction.)"
 S DIR(0)="SO^1:ADD Transaction Types;2:REMOVE Transaction Types"
 D DIR^ACRFDIC
 Q:$D(ACRQUIT)!$D(ACROUT)
 S:Y=2 ACRTXDEL=""
 Q
SELECT S DIR(0)="LO^1:"_ACRJ
 S DIR("A")="Select TRANSACTIONS to be used by this account"
 D DIR^ACRFDIC
 I $D(ACRQUIT)!$D(ACROUT) K ACRQUIT Q
 N ACRI,ACRX,ACRY
 S ACRY=Y
 F ACRI=1:1 S ACRX=$P(ACRY,",",ACRI) Q:ACRX=""  D
 .S (DINUM,X,DA)=+ACRTX(ACRX)
 .I $D(ACRTXDEL) D
 ..S DA(1)=ACRZDA
 ..S DIK="^ACRLOCB("_DA(1)_",2,"
 ..D DIK^ACRFDIC
 .I '$D(ACRTXDEL),'$D(^ACRLOCB(ACRZDA,2,X)) D
 ..S:'$D(^ACRLOCB(ACRZDA,2,0)) ^ACRLOCB(ACRZDA,2,0)="^9002188.02P"
 ..S DA(1)=ACRZDA
 ..S DIC="^ACRLOCB("_DA(1)_",2,"
 ..S DIC(0)="L"
 ..D FILE^ACRFDIC
 K ACRTXDEL
 Q
