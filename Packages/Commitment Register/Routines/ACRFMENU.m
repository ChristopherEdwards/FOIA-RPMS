ACRFMENU ;IHS/OIRM/DSD/THL,AEF - CONTROL HEADINGS AND MENUS;  [ 12/13/2006   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**19,20,21,22**;NOV 05, 2001
 ;;ROUTINE TO CONTROL HEADINGS AND MENUS
TITLE ;EP;TO DISPLAY TITLE
 I $D(ACRNOFM) D ^XUSCLEAN Q           ;ACR*2.1*22.03 IM22653
 S ACRFINSS=""
 S IOP="HOME"
 D ^%ZIS
 D HOME
 D HEAD
EXIT K ACRX,ACRZ,ACRTRM
 I $D(ACRY),ACRY]"" W !!?80-$L(ACRY)/2,ACRY
 D ^ACRFEXIT
 D NSV
 Q
HEAD ;EP;TO DISPLAY HEADER
 W @IOF
HEAD1 F ACRJ=0:1:4 D H1
 D HDVS
 I DUZ D
 .;S ACRX=$P(^VA(200,DUZ,0),U)  ;ACR*2.1*19.02 IM16848
 .S ACRX=$$NAME2^ACRFUTL1(DUZ)  ;ACR*2.1*19.02 IM16848
 .S ACRX=$P($P(ACRX,",",2)," ")_" "_$P(ACRX,",")
 .S ACRX="CURRENT USER: "_ACRX
 .W !?80-$L(ACRX)\2,ACRX
 .I $P(^VA(200,DUZ,0),U,4)'["A"&($P(^(0),U,4)'["@") D
 ..D WARNING^ACRFWARN
 ..W !!,"You do NOT have the correct FILE MANAGER ACCESS CODE to use ARMS."
 ..W !,"Contact the ARMS Manager at the Area Office immediately.  DO NOT use"
 ..W !,"ARMS until you have been assigned the proper FILE MANAGER ACCESS CODE."
 ..D PAUSE^ACRFWARN
 ..S ACRNOFM=""                        ;ACR*2.1*22.03 IM22653
 D:ACRON=""!(ACROF="") RV
 Q
H1 S ACRX=$T(TEXT+ACRJ),ACRX=$P(ACRX,";;",2)
 W !?80-$L(ACRX)\2,ACRX
 Q
HDVS I $D(DUZ(2))#2,DUZ(2) S:$D(^DIC(4,DUZ(2),0)) ACRX=$P(^(0),U)
 I $D(^DIC(9.4,"C","ACR")) D
 .;BEGIN OLD CODE ACR*2.1*22.08 IM23060
 .;S ACRZ=$O(^DIC(9.4,"C","ACR",0))
 .;S ACRZ=$P(^DIC(9.4,ACRZ,"VERSION"),U)
 .;BEGIN MOD TO DISPLAY PATCH # ON MENUS  ACR*2.1*21.01 IM22090
 .;S ACRPKG=$O(^DIC(9.4,"C","ACR",0))
 .;S ACRPKG=$P($G(^DIC(9.4,ACRPKG,0)),U)
 .;S ACRPATCH=$$LAST^XPDUTL(ACRPKG,ACRZ)
 .;S ACRZ="VERSION "_ACRZ
 .;S ACRZ="VERSION "_ACRZ_"   PATCH "_$P(ACRPATCH,U)
 .;K ACRPKG,ACRPATCH
 .;END MOD TO DISPLAY PATCH # ON MENUS  ACR*2.1*21.01 IM22090
 .;I $E(IOST,1,2)="C-" W:$D(ACRZ) !?80-$L(ACRZ)\2,ACRZ
 .;W:$D(ACRX) !?80-$L(ACRX)\2,ACRX
 .;END OLD CODE ACR*2.1*22.08 IM23060
 .;BEGIN NEW CODE ACR*2.1*22.08 IM23060
 .N ACRLPAT
 .S ACRLPAT=""
 .D CURRENT^ACRFPAH
 .I $E(IOST,1,2)="C-" W:$D(ACRZ) !?80-$L(ACRZ)\2,ACRZ
 .W:$D(ACRX) !?80-$L(ACRX)\2,ACRX
 ;END NEW CODE ACR*2.1*22.08 IM23060
 Q
MM ;EP
 S ACRY="MANAGER MENU" G TITLE
EA ;EP
 S ACRY="FUND DISTRIBUTION" G TITLE
OF ;EP
 S ACRY="REQUESTS" G TITLE
FR ;EP
 S ACRY="REPORTS" G TITLE
FM ;EP
 S ACRY="USER MENU" G TITLE
SR ;EP
 S ACRY="SYSTEM REPORTS" G TITLE
SF ;EP
 S ACRY="STATUS OF FUNDS" G TITLE
SS ;EP
 S ACRY="SYSTEM SETUP" G TITLE
SD ;EP
TM ;EP
 S ACRY="TABLE MAINTENANCE" G TITLE
VM ;EP
 S ACRY="VENDOR MENU" G TITLE          ;ACR*2.1*20.14
PT ;EP
 D PAUSE^ACRFWARN
PT1 ;EP
 S ACRY="PRINT TABLE LISTS"
 G TITLE
SU ;EP
 S ACRY="UTILITIES" G TITLE
EDI ;EP
 S ACRY="EDI SYSTEM MANAGEMENT" G TITLE
FQS ;EP
 S ACRY="FINANCIAL QUERY SYSTEM" G TITLE
IRS ;EP
 S ACRY="IRS 1099 MENU" G TITLE
DHR ;EP
 S ACRY="DHR MANAGEMENT MENU" G TITLE
DHRM ;EP    
 S ACRY="DHR MANAGEMENT" G TITLE
DHRE ;EP
 S ACRY="DHR ENTER/EDIT" G TITLE
DHRX ;EP    
 S ACRY="DHR EXPORT" G TITLE
ODD ;EP
 S ACRY="OPEN DOCUMENT DOWNLOAD" G TITLE
YTF ;EP -- YOUR TRAVEL FORMS
 ;
 ;FOR FUTURE RELEASE
 Q
NQ ;;
HOME ;EP;TO SET TERMINAL CONTROL VARIABLES
 S ACRTRM=^%ZIS(2,IOST(0),5)
 S ACRXY=$G(^%ZIS(2,IOST(0),"XY"))
 S ACRON=$P(ACRTRM,U,4)
 S ACROF=$P(ACRTRM,U,5)
 D:ACRON=""!(ACROF="") RV
 D ENS^%ZISS
 Q
CLEAN ;EP;TO KILL SELECTD VARIABLES
 K ACRON,ACROF,ACRZ,ACRY,ACRTRM,ACRFINSS,ACRPPO,ACRPTX,ACRREV,ACRRR,ACRSOF,ACRTRN,ACRBOX
 Q
RV ;WARNING MESSAGE WHEN REVERSE VIDEO NOT DEFINED
 W @IOF
 W !?10,"WARNING     WARNING     WARNING     WARNING     WARNING"
 W *7,*7
 W !!,"Your DEVICE and/or TERMINAL TYPE are not properly defined to work with the ARMS"
 W !,"system.  The REVERSE VIDEO ON and/or REVERSE VIDEO OFF are not properly defined."
 W !,"This is the responsibility of the AREA OFFICE computer site manager."
 W !!,"Please contact this individual for assistance.  Let him/her know that you"
 W !,"are logged in on device NO.: ",$I
 D P1^ACRFWARN
 Q
B1 ;EP;TO TURN BLINK ON
 W:$D(IOBON)&$D(IOBOFF) IOBON
 Q
B2 ;EP;TO TURN BLINK OFF
 W:$D(IOBOFF) IOBOFF
 Q
U1 ;EP;TO TURN UNDERLINE ON
 W:$D(IOUON)&$D(IOUOFF) IOUON
 Q
U2 ;EP;TO TURN UNDERLINE OFF
 W:$D(IOUOFF) IOUOFF
 Q
DASH(Z) ;EP;TO WRITE LINE OF DASHES
 S:'$G(Z) Z=80
 W !
 W $$DASH1(Z)
 Q ""
DASH1(Z) ;EP;TO WRITE DASHES WITHOUT LINE FEED
 S:'$G(Z) Z=80
 N I
 F I=1:1:Z W "-"
 Q ""
NSV ;EP;TO CHECK FOR SECURITY VIOLATIONS
 N ACRLVL
 S ACRLVL=$O(^ACRUAL("LVL",DUZ,0))
 Q:ACRLVL=3!(ACRLVL=11)
 N X
 S X=0
 F  S X=$O(^ACRACC(3,"SEC","C",1,X)) Q:'X  I $D(^VA(200,DUZ,51,X)) D  Q
 . S ACRDUZ=DUZ
 .D KILLSEC^ACRFACC
 .W !!,"You have ARMS ACCESS LEVEL ",ACRLVL
 .W !,"However, somehow you have gotten access to secured functions"
 .W !,"within ARMS which can only be accessed by someone with"
 .W !,"ACESS LEVEL 10."
 .W !!,"All your access to ARMS has been temporarily suspended."
 .W !,"Please see your ARMS Manager to re-establish an appropriate"
 .W !,"ACCESS LEVEL for yourself."
 .D PAUSE^ACRFWARN
 Q
TEXT ;;WARNING: RESTRICTED GOVERNMENT FINANCIAL DATA, UNAUTHORIZED
 ;;ENTRY INTO THIS SYSTEM OR USE OF THIS DATA IS A FEDERAL CRIME
 ;;****************************************************
 ;;**    ADMINISTRATIVE RESOURCE MANAGEMENT SYSTEM   **
 ;;****************************************************
