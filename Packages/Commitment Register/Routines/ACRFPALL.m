ACRFPALL ;IHS/OIRM/DSD/THL,AEF - PRINT SELECTED DOCUMENTS; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;ROUTINE TO PRINT SELECTED DOCUMENTS;
EN D EN1
EXIT K ACR,ACRPRTX
 Q
EN1 S DIR(0)="LOA^1:99999"
 S DIR("A")="Enter ID number(s): "
 W !
 D DIR^ACRFDIC
 Q:$D(ACRQUIT)!$D(ACROUT)
EN2 ;EP;TO PRINT MULTIPLE DOCUMENTS
 S ACRPAL=ACRY
 S ACRPALX=""
 F ACRPAL1=1:1 S ACRPAL2=$P(ACRPAL,",",ACRPAL1) Q:'ACRPAL2  I $D(ACRPRTX(ACRPAL2)) S ACRPALX=ACRPALX_(ACRPAL2)_","
 S ACRPAL=ACRPALX
 I $L(ACRPAL,",")>11 D
 .S ACRPAL=$P(ACRPAL,",",1,10)
 .W !!,"ONLY 10 REPORTS CAN BE PRINTED AT ONCE.  THE FOLLOWING HAVE BEEN SELECTED: "
 .W !?5,ACRPAL
 S ACRRTN="PALL^ACRFPALL"
 S ZTDESC="PRINT SELECTED DOCUMENTS"
 D ^ACRFZIS
 Q
PALL F ACRPAL1=1:1 S ACRPAL2=$P(ACRPAL,",",ACRPAL1) Q:'ACRPAL2  I $D(ACRPRTX(ACRPAL2)),$D(^ACRDOC(ACRPAL2,0)) K ACRPRTX(ACRPAL2) D
 .S (ACRDOCDA,ACRDOCDA)=ACRPAL2
 .D SETDOC^ACRFEA1
 .S ACRLBDA=$P(ACROBL0,U,3)
 .I $D(ACRREQST),ACRREF'=116&(ACRREF'=130) S ACRREF=$S(ACRREF=103!(ACRREF=349)!(ACRREF=326)!(ACRREF=210):116,ACRREF=600:130,ACRREF=148:148,1:"")
 .S ACRREFX=ACRREF
 .D ^ACRFQ
 Q
