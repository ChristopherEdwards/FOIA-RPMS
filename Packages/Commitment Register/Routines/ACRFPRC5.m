ACRFPRC5 ;IHS/OIRM/DSD/THL,AEF - EDIT TRAINING VENDOR;  [ 05/10/2005  11:53 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**3,16,17**;NOV 05, 2001
 ;;UTILITY FOR ACTIONS NEEDED DURING DOCUMENT SIGNATURE PROCESSING
AUX ;EP;TO PROCESS AUXILLIARY FUNCTIONS DURING APPROVAL PROCESS
 I ACRAPVT=31!(ACRAPVT=6) S ACRREFX=116 D
 .;OFFICER TO EDIT ITEMS DURING APPROVAL SEQUENCE
 .N X
 .S X=0
 .K ACRQUIT,ACRREV
 .D ^ACRFSS
 .S ACRREV=""
 ;I ACRAPVT=39,$P(^ACRSYS(1,"DT"),U,99) D VOUCHER^ACRFSS41 ; REMOVE LINE ;ACR*2.1*3.09
 I ACRAPVT=14 D TVENDOR
 I $G(ACRREF)=148,$P($G(^ACRAPVS(ACRAPDA,"DT")),U,5)="Y" D TVENDOR ;ACR*2.1*3.18
 I ACRAPVT=38!(ACRAPVT=39),$P($G(^ACRAPVS(+$G(ACRAPDA),"DT")),U,5)="Y" D TPAY
 I ACRAPVT=38!(ACRAPVT=39),ACRREFX'=148,$P(^ACRSYS(1,"DT"),U,25)=1,'$P(^("DT"),U,37),'$P($G(^ACRSYS(1,"DT1")),U,9) D 38^ACRFPRC4
 I ACRAPVT=39,$P($G(ACRAPV0),U,11)=ACRDOCDA,$P($G(^ACROTA(ACRDOCDA,0)),U,3) W !!,"Edit TRAVEL ADVANCE",! D E11^ACRFTA
 Q
TVENDOR ;EP;TO ADD/EDIT THE TRAINING VENDOR
 F  D T1 Q:$D(ACRQUIT)
 I +$G(^ACRDOC(ACRDOCDA,"TRNG3")) K ACRQUIT Q
 W !!,"NO vendor from the STANDARD VENDOR file was added to this 350."
 W !,"A vendor must be added before the requrest can be signed."
 S DIR(0)="YO"
 S DIR("A")="Add the vendor now"
 S DIR("B")="YES"
 W !
 D DIR^ACRFDIC
 I +Y'=1 S ACRQUIT=""
 G:'$D(ACRQUIT) TVENDOR
 Q
T1 D TDISP
 S DIR(0)="SO^1:Add or Change VENDOR on this 350;2:Add or Edit VENDOR on STANDARD VENDOR File;3:Return Request for Change/Clarification"
 W !
 D DIR^ACRFDIC
 I +$G(Y)<1 S ACRQUIT="" Q
 I Y=1 D CHANGE Q
 I Y=2 D VEND Q
 I Y=3 S ACRAPDAS="R" D RETURN^ACRFPRC1 S ACRQUIT="" Q
 Q
CHANGE ;CHANGE VENDOR ON 350
 S DA=ACRDOCDA
 S DIE="^ACRDOC("
 S DR="148181T"
 W !
 D DIE^ACRFDIC
 Q
TDISP ;DISPLAY VENDOR DATA
 K ACRDOCVN
 I '+$G(^ACRDOC(ACRDOCDA,"TRNG3")),$P($G(^(3)),U)="" D
 .W !!,"NO vendor information on file for this 350."
 W @IOF
 I +$G(^ACRDOC(ACRDOCDA,"TRNG3")) S ACRDOCVN=+^("TRNG3")
 D VEND^ACRFJS
 Q
VEND ;ADD/EDIT STANDARD VENDOR FILE
 N AUTVDA
 D ^ACRFVEND
 I $G(AUTVDA),$D(^AUTTVNDR(AUTVDA,0)),$D(^AUTTVNDR(AUTVDA,11)) D
 .S DIR(0)="YO"
 .S DIR("A")="Make "_X_" the vendor for this 350"
 .S DIR("B")="NO"
 .W !
 .D DIR^ACRFDIC
 .I +Y=1 D
 ..S DA=ACRDOCDA
 ..S DIE="^ACRDOC("
 ..S DR="148181"
 ..D DIE^ACRFDIC
 Q
TPAY ;EP;TO EDIT PRELIMINARY DATA FOR TRAVEL PAYMENT VIA 1166
 Q:$P($G(^ACRSYS(1,"DT1")),U,9)<2
 S:ACRREFX'=148 ACRDUZ=$P(^ACRDOC(ACRDOCDA,"TO"),U,9)
 S:ACRREFX=148 ACRVDA=+^ACRDOC(ACRDOCDA,"TRNG3")
 S ACRBTYP=$S(ACRREFX'=148:"T",1:"V")
 S DIR("B")="ACH Non-Grouped"
 D SCHT^ACRFIV4  ;commented out ACR*2.1*16.06 IM15505 ;put back ACR*2.1*17.07 IM17139
 ;D SCHT^ACRFIV4(.ACRACHX,.ACRACH,.ACRBTYP,.ACRVDA,.ACRDUZ,.ACRQUIT) ;ACR*2.1*16.06 IM15505; ACR*2.1*17.07 IM17139
 Q
