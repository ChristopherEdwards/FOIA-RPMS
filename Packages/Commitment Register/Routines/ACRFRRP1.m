ACRFRRP1 ;IHS/OIRM/DSD/THL,AEF - CALCULATE ACCOUNTING DATA FOR RECEIVING REPORT; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;UTILITY TO CALCULATE ACCOUNTING DATA FOR RECEIVING REPORT
EN ;EP;TO CALCULATE ACCOUNTING DATA FOR RECEIVING REPORT
 S ACR=^ACRSS(ACRSSDA,0)
 S ACRLBDA=$P(ACR,U,6)
 D SETACT^ACRFPSS
 S ACR1=$P(ACR,U,4)
 S ACR2=$P(ACR,U,5)
 S ACR=$P(ACR,U)
 I ACR1=""!(ACR2="") S ACRSSRQD="" Q
 S ACR3=$P(ACRRRDT,U,3)*$P($G(^ACRSS(ACRSSDA,"DT")),U,3)
SETS I '$D(ACROBJ(ACRACT,ACR2,ACR1)) D
 .S ACROBJ(ACRACT,ACR2,ACR1)=0
 .S ACROBJ(ACRACT,ACR2,ACR1,"I")=""
 S ACROBJ(ACRACT,ACR2,ACR1)=ACROBJ(ACRACT,ACR2,ACR1)+ACR3
 S ACROBJ(ACRACT,ACR2,ACR1,"I")=ACROBJ(ACRACT,ACR2,ACR1,"I")_$S(ACROBJ(ACRACT,ACR2,ACR1,"I")]"":",",1:"")_I
 S:'$D(ACRCAN(ACRACT,ACR2)) ACRCAN(ACRACT,ACR2)=0
 S ACRCAN(ACRACT,ACR2)=ACRCAN(ACRACT,ACR2)+ACR3
 S ACRTOT=ACRTOT+ACR3
 Q
IR ;EP;TO PRINT INITIATOR RECEIVING INFORMATION
 D IRHEAD
 N D0,DXS,DIP,DC,ACRAPDA,DN
 S ACRAPDA=0
 F  S ACRAPDA=$O(^ACRRR(ACRRRDA,10,ACRAPDA)) Q:'ACRAPDA  D
 .S D0=+$G(^ACRRR(ACRRRDA,10,ACRAPDA,0))
 .D ^ACRPRI:D0
 Q
IRHEAD ;
 W !!,"Certification by Request Initiator(s) of Receipt of Supplies/Servcies."
 W $$DASH^ACRFMENU
 W !!,"Request Initiator/Alternate"
 W ?32,"Status"
 W ?45,"Date Signed"
 W !,"================================================================================"
 Q
NECOP ;EP;TO PRINT NEW EQUIPMENT INFORMATION
 I $E($G(ACROC),1,2)=31,'$D(^ACRSS(ACRSSDA,11)) S ACRNE0="" D N1 Q
 Q:'$D(^ACRSS(ACRSSDA,11))
 N ACRNEDA,ACRNE0
 S ACRNEDA=0
 F  S ACRNEDA=$O(^ACRSS(ACRSSDA,11,ACRNEDA)) Q:'ACRNEDA  I $D(^ACRSS(ACRSSDA,11,ACRNEDA,0)) S ACRNE0=^(0) D N1
 Q
N1 ;EP;
 N X
 W !
 F X=1,2,3,8,7,4,5,6 D
 .W:X=1 !?10,"SERIAL NUMBER: "
 .W:X=2 !?10,"MAKE.........: "
 .W:X=3 !?10,"MODEL........: "
 .W:X=8 !?10,"NOMENCLATURE.: "
 .W:X=7 !?10,"DEPT. CODE...: "
 .W:X=4 !?10,"BAR CODE NO..: "
 .W:X=5 !?10,"INDEX NO.....: "
 .W:X=6 !?10,"VOUCHER NO...: "
 .W $P(ACRNE0,U,X)
 .D W1^ACRFRRPT
 Q
HEAD ;EP;PRINT RR HEADER
 W !?34,"OBJ."
 W ?39,"OR-"
 W ?52,"UNIT"
 W ?63,"TOTAL"
 W ?73,"AC-"
 W !,"NO"
 W ?10,"DESCRIPTION"
 W ?34,"CODE"
 W ?39,"DERED"
 W ?46,"UI"
 W ?52,"COST"
 W ?63,"AMOUNT"
 W ?73,"CEPTED"
 W !,"--"
 W ?3,"------------------------------"
 W ?34,"----"
 W ?39,"------"
 W ?46,"--"
 W ?49,"----------"
 W ?60,"------------"
 W ?73,"------"
 Q
