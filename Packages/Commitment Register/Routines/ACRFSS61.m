ACRFSS61 ;IHS/OIRM/DSD/THL,AEF - EDIT PAY ROLL PERIOD; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;CONTINUATION OF ACRFSS6
ADD ;EP;
 S DIR(0)="YO"
 S DIR("A")="Add a Pay Roll entry"
 S DIR("B")="NO"
 W !
 D DIR^ACRFDIC
 Q:Y'=1
 F ACRI=1,2 D ADD1
 Q
ADD1 S ACRID=ACRDOC_"-"_((ACRJ+1)/2)_"/"_ACRI
 S ACRCANDA=$O(^AUTTCAN("B",ACRFDNCA,0))
 S ACRLBOC=$S(ACRI=1:"1100",1:"1200")
 S ACROBJDA=$O(^AUTTOBJC("C",ACRLBOC,0))
 S X=ACRJ
 S DIC(0)="L"
 S DIC="^ACRSS("
 S DIC("DR")=".02////"_ACRDOCDA_";.03////"_ACRDOCDA_";.05////"_ACRCANDA_";.04////"_ACROBJDA_";.06////"_ACRLBDA_";5////"_ACRID_";13////"_$P(^ATAEMP(ACREMP,"PAY"),U,ACRI)
 D FILE^ACRFDIC
 S ACRPRDA=+Y
 D EDIT1
 Q
EDIT ;EP;
 S DIR(0)="NOA^1:"_((ACRJ-1)\2)
 S DIR("A")="Which Pay Period: "
 W !
 D DIR^ACRFDIC
 I $D(ACRQUIT)!$D(ACROUT)!'$D(ACRPR(Y)) K ACRQUIT Q
 S ACRZ=Y*2-1
 F ACRI=ACRZ+1,ACRZ D
 .S ACRPRDA=$P(ACRPR(ACRI),U)
 .D EDIT1:'$D(ACRDEL)
 .D DEL:$D(ACRDEL)
 Q
DEL S DA=ACRPRDA,DIK="^ACRSS("
 D DIK^ACRFDIC
 Q
EDIT1 N ACRDR
 S ACRLBOC=$S(ACRI#2=1:"1100",1:"1200")
 S DR="S DIE(""NO^"")=""NO"";.05T;.04T;K DIE(""NO^"");13T"
 S DA=ACRPRDA,DIE="^ACRSS("
 W !!,"PAY ROLL............: ",$S(ACRI#2=0:"BASE PAY",1:"BENEFITS")
 D DIE^ACRFDIC
 Q
