ACRFSSD1 ;IHS/OIRM/DSD/THL,AEF - DISPLAY SERVICES/SUPPLIES INFO - CON'T; [ 09/23/2005  4:16 PM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**3,14,19**;NOV 05, 2001
 ;;CONTINUATION OF ACRFSSD
NOTES ;EP;DISPLAY NOTES - CALLED BY FILEMAN TEMPLATE
 Q:'$D(^ACROBL(ACRDOCDA,"NOTES"))&'$D(^ACROBL(ACRDOCDA,"NOTES2"))
 N X,Y
 S X=$G(^ACROBL(ACRDOCDA,"NOTES"))
 S Y=$G(^ACROBL(ACRDOCDA,"NOTES2"))
 D D
 Q
JUST ;EP;DISPLAY JUSTIFICATION - CALLED BY FILEMAN TEMPLATE
 Q:'$D(^ACROBL(ACRDOCDA,"JST"))&'$D(^ACROBL(ACRDOCDA,"JST2"))
 N X,Y
 S X=$G(^ACROBL(ACRDOCDA,"JST"))
 S Y=$G(^ACROBL(ACRDOCDA,"JST2"))
 D D
 Q
INFO ;EP;DISPLAY INFO CHANGED - CALLED BY FILEMAN TEMPLATE
 Q:'$D(^ACROBL(ACRDOCDA,"CNG"))
 N X,Y
 S X=$G(^ACROBL(ACRDOCDA,"CNG"))
 S Y=""
 D D
 Q
REASON ;EP;DISPLAY REASON FOR CHANGE - CALLED BY FILEMAN TEMPLATE
 Q:'$D(^ACROBL(ACRDOCDA,"RSN"))
 N X,Y
 S X=$G(^ACROBL(ACRDOCDA,"RSN"))
 S Y=""
 D D
 Q
ADDM ;EP;PRINT ADDITIONAL DESCRIPTION FOR AN ITEM
 Q:'$D(^ACRSS(ACRSSDA,1,1,0))#2
 N ACR
 S ACR=0
 F  S ACR=$O(^ACRSS(ACRSSDA,1,ACR)) Q:'ACR  S ACRDISP=^(ACR,0) D
 .S ACRDC=$S($G(ACRDC):ACRDC,1:$G(DC))
 .I ACRDISP["|TAB|" F I=1:1 S ACRDISP=$P(ACRDISP,"|TAB|")_"     "_$P(ACRDISP,"|TAB|",2) Q:ACRDISP'["|TAB|"
 .I ACRDISP]"" D
 ..D P
 ..W !?3
 ..W:'$D(ACRORIGF) "|"
 ..W ?4,ACRDISP
 ..D W
 Q
SSNOTES ;EP;PRINT SPECIAL REMARKS FOR AN ITEM
 N I,X
 Q:'$D(^ACRSS(ACRSSDA,"NOTES"))
 S ACRNOTES=^ACRSS(ACRSSDA,"NOTES")
 Q:ACRNOTES=""
 S X=ACRNOTES,ACRDISP=""
 F I=1:1:5 S:$P(X,U,I)]"" ACRDISP=ACRDISP_$P(X,U,I)_" "
 Q:ACRDISP=""
 W !?3
 W:'$D(ACRORIGF) "|"
 D P
DISP N J,X
 Q:'$D(ACRDISP)#2
 F J=1:1:$L(ACRDISP," ") D
 .S X=$P(ACRDISP," ",J)
 .I X]"" D
 ..D P
 ..I $X+$L(X)+1<75 W ?4,X," "
 ..E  D
 ...D W,P
 ...W !?3
 ...W:'$D(ACRORIGF) "|"
 ...W ?4,X
 D W
 K ACRDISP
 Q
P D P^ACRFSSD
 Q
W D W^ACRFSSD
 Q
HEAD ;EP;
 Q:$D(ACROUT)
 I $E(IOST,1,2)="C-" D
 .W @IOF
 .W !?10,@ACRON,"ITEMS",@ACROF," FOR DOCUMENT: ",@ACRON,ACRDOC,@ACROF
 .W "  ",$$EXPDN^ACRFUTL(ACRDOCDA)          ;ACR*2.1*14.01 IM12272
 I ACRREFX=103!(ACRREFX=349)!(ACRREFX=326),$D(DC),DC>1 W $$DASH^ACRFMENU
 I (ACRREFX'=103&(ACRREFX'=349)&(ACRREFX'=326))!'$D(DC) W $$DASH^ACRFMENU
 W !,"ITM|"
 W ?34,"|QUAN"
 W ?41,"|"
 W ?44,"| UNIT"
 W ?55,"| ITEM"
 W ?67,"|"
 I ACRREFX'=103&(ACRREFX'=349)&(ACRREFX'=326)&'$D(ACRPRT)&'$D(ACRPQT) D
 .W ?68,"OBJ"
 .W ?72,"|"
 W:ACRREFX=103&'$D(ACRPQT) ?68,"QUANTITY"
 W !,"NO.|"
 W ?8,"SUPPLIES OR SERVICES"
 W ?34,"|ORD'D"
 W ?41,"|UI"
 W ?44,"| PRICE"
 W ?55,"| TOTAL"
 W ?67,"|"
 I ACRREFX'=103&(ACRREFX'=349)&(ACRREFX'=326)&'$D(ACRPRT)&'$D(ACRPQT) D
 .W ?68,"CODE"
 .W ?72,"|CAN NO."
 W:ACRREFX=103&'$D(ACRPQT) ?68,"ACCEPTED"
 W !,"(A)|"
 W ?4,"             (B)              "
 W ?34,"| (C)"
 W ?41,"|D)"
 W ?44,"|    (E)"
 W ?55,"|    (F)"
 W ?67,"|"
 I ACRREFX'=103&'$D(ACRPRT) D
 .W ?68,"----"
 .W ?72,"|-------"
 W:ACRREFX=103&'$D(ACRPQT) ?68,"  (G)"
 W !,"---|"
 W ?4,"------------------------------"
 W ?34,"|------"
 W ?41,"|--"
 W ?44,"|----------"
 W ?55,"|-----------"
 W ?67,"|"
 I ACRREFX'=103&(ACRREFX'=349)&(ACRREFX'=326)&'$D(ACRPRT)&'$D(ACRPQT) D
 .W ?68,"----"
 .W ?72,"|-------"
 W:ACRREFX=103&'$D(ACRPQT) ?68,"--------"
 Q
HEADSS4 ;EP;
 Q:$D(ACROUT)
 W @IOF
 W !?10,"DHHS/INDIAN HEALTH SERVICE - TRAVEL ",$S(ACRREFX=130:"EXPENSE ESTIMATE",1:"VOUCHER")
 W !?4,"TRAVEL ORDER: ",ACRDOC
 W ?40,"TRAVELER: "
 N ACRX
 S ACRX=$P($G(^ACRDOC(ACRDOCDA,"TO")),U,9) ;ACR*2.1*3.44
 I 'ACRX D  Q
 .W !,"TRAVELER'S NAME MUST BE ENTERED BEFORE YOU PROCEED."
 .D PAUSE^ACRFWARN
 .S (ACRQUIT,ACROUT)=""
 ;E  S ACRX=$P(^VA(200,ACRX,0),U)  ;ACR*2.1*19.02 IM16848
 E  S ACRX=$$NAME2^ACRFUTL1(ACRX)  ;ACR*2.1*19.02 IM16848
 W ACRX
 W !,"DHHS #: ",$$EXPDN^ACRFUTL(ACRDOCDA)       ;ACR*2.1*14.01 IM12272
 W:ACRREFX=600 !,"SCHEDULE OF EXPENSES AND AMOUNTS CLAIMED"
 W $$DASH^ACRFMENU
SS41 ;EP;
 W !,"DAY"
 W ?3,"DATE"
 W ?8,"DEPT"
 W ?13,"ARRV"
 W ?18,"CITY"
 W ?26," M & IE"
 W ?36,"LODGING"
 W ?46,"MILES"
 W ?54,"TAXI"
 W ?63,"CALL HOM"
 W ?72,"OTHER"
 W !,"--"
 W ?3,"----"
 W ?8,"----"
 W ?13,"----"
 W ?18,"-------"
 W ?26,"---------"
 W ?36,"---------"
 W ?46,"-------"
 W ?54,"--------"
 W ?63,"--------"
 W ?72,"-------"
 Q
D ;DISPLAY PROCESSOR
 S ACRX=""
 F Z=X,Y F I=1:1:5 S:$P(Z,U,I)]"" ACRX=ACRX_$P(Z,U,I)_" "
 Q:ACRX=""
 W !
 D D1
 W "-----------------------------"
 W ?(80-$L(ACRY))\2,ACRY
 W ?50,"-----------------------------"
 D D1
 W !
 D D2
 W ?5
 N J
 F J=1:1:$L(ACRX," ") D
 .S X=$P(ACRX," ",J)
 .I X]"" D
 ..I $X+$L(X)+1>75 D
 ...W ?79
 ...D D2
 ...W !
 ...D D2
 ...W ?5
 ..W X
 ..W ?$X+1
 I ACRX]"" W ?79 D D2
 Q
D1 W $S(ACRREFX'=600:"|",1:"-")
 Q
D2 W $S(ACRREFX'=600:"|",1:" ")
 Q
