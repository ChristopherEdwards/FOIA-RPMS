ACRFSTK1 ;IHS/OIRM/DSD/THL,AEF - ACRFSTOK - CON'T; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;CONTINUATION OF ACRFSTOK
ITEMSS ;EP;
 S DIR(0)="YO"
 S DIR("A")="STORES STOCK ITEM...: "
 S DIR("B")="NO"
 W !
 D DIR^ACRFDIC
 S ACRSTOCK=Y
 Q
DISP ;EP;TO DISPLAY STANDARD ITEM INFORMATION
 W @IOF
 W !?15,@ACRON,"ITEM INFORMATION",@ACROF
 S D0=ACRD0
 N DXS,DIP,DC,DN
 I $P(^ACRITEM(D0,0),U)'="XXXXXX" D ^ACRPSTM I 1 Q
 E  D ^ACRPITM
 Q
NEW ;EP;CREATE NEW STANDARD ITEM ENTRY
 S ACRSSITM=""
 S X="XXXXXX"
 S DIC="^ACRITEM("
 S DIC(0)="L"
 S DIC("DR")=DIC("DR")_";26////1"
 D FILE^ACRFDIC
NEW1 S ACRDA=Y
 S DA=+Y
 S DIE="^ACRITEM("
 S DR="15////"_+Y
 D DIE^ACRFDIC
 S DA=+ACRDA
 S DIE="^ACRITEM("
 S DR="[ACR ADD DIRECT ISSUE]"
 D DIE^ACRFDIC
 S ACR(1)=+ACRDA
 K ACRADD
 Q
DELETE ;EP;TO DELETE A SECONDARY OR TERTIARY VENDOR
 S DIR(0)="YO"
 S DIR("A")="Remove this vendor"
 S DIR("B")="NO"
 W !
 D DIR^ACRFDIC
 Q:Y'=1
 S DA=+ACRDEL
 S DIK="^ACRITEM("
 D ^DIK
 W !!,$P(ACR($P(ACRDEL,U,2)),U,3)," removed as a ",$S($P(ACRDEL,U,2)=2:"SECONDARY",1:"TERTIARY")," vendor for this item."
 H 2
 K ACR($P(ACRDEL,U,2))
 I $P(ACRDEL,U,2)=2,$D(ACR(3)) D
 .S DA=+ACR(3)
 .S DIE="^ACRITEM("
 .S DR=".03////2;26////2"
 .D DIE^ACRFDIC
 .S ACR(2)=ACR(3)
 .K ACR(3)
 S ACRQUIT=""
 Q
