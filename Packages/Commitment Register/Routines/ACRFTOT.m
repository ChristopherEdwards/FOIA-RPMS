ACRFTOT ;IHS/OIRM/DSD/THL,AEF - CALCULATE TO/TV TOTAL; [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;CALCULATE TRAVEL ORDER/TRAVEL VOUCHER TOTALS
DISPLAY ;EP;
 N ACRMRR,ACRJ,ACRTVDA,ACRTOT,ACRPD,ACRLDG,ACRMR,ACRTAX,ACRPHN,ACROTH,ACROTHT,ACRRC,ACRTVRC,ACRAIRPT,ACRAIRT,ACR4P,ACRALTOT,ACRADV,ACRTV1,ACRTVDAY,ACRDESC,ACRTVEP,ACRTVLV,ACRTVDAT,ACRTVAR,ACRTVCIT,ACRTVPD,ACRPMRR,ACRTVLDG,ACRTVMLS
 N ACRTVPML,ACRTVMR,ACRTVTAX,ACRTVPHN,ACRTVOTH,ACRTVEXP,ACRTVRC,ACRTVRCC,ACRPHN,ACRPHNX,ACRATM,ACRATMX,ACRATM1,ACRATM2
 D CHK^ACRFSSA1
 K ACRQUIT
 S ACRMRR=$P(^ACRDOC(ACRDOCDA,"TOSA"),U,10)
 S (ACRREIM,ACRJ,ACRTVDA,ACRTOT,ACRPD,ACRLDG,ACRMR,ACRTAX,ACRPHN,ACROTH,ACROTHT,ACRRC,ACRTVRC,ACRAIRPT,ACRAIRT)=0
 S ACR4P=$P(^ACRDOC(ACRDOCDA,"TO"),U,26)
 S ACRDUZ=$P(^ACRDOC(ACRDOCDA,"TO"),U,9)
 S:'$D(ACRADV) ACRADV=0
 S ACRALTOT=$G(ACRALTOT)
 F  S ACRTVDA=$O(^ACRTV("D",ACRDOCDA,ACRTVDA)) Q:'ACRTVDA  D SETS
 I $P(^ACRDOC(ACRDOCDA,"TOAU"),U,6),$P(^("TOAU"),U,6)<ACRMR S ACRMR=$P(^("TOAU"),U,6)
 S ACRDAYS=ACRJ
 S ACRPHNX=ACRJ-1*$P(^ACRSYS(1,"DT"),U,17)
 S ACRPHN=$S(ACRPHN>ACRPHNX:ACRPHNX,1:ACRPHN)
 D TAXI
 K ACRPHNX
 K ACRQUIT
 S ACRTOT=ACRTOT+ACRPD+ACRLDG+ACRMR+ACRTAX+ACRPHN+ACROTH+ACRRC+ACR4P+ACRAIRPT
 S ACROTHT=ACROTHT+ACRMR+ACRTAX+ACRPHN+ACROTH+ACRAIRPT
 D ALTOT^ACRFSSA1
 D ADVANCE^ACRFSSA1
 S ACRTOT=ACRTOT+$G(ACRALTOT)
 I $P($G(^ACRDOC(ACRDOCDA,"TRNG4")),U,16) S ACRTOT=$P(^("TRNG4"),U,16)
 S ACRREIM=ACRREIM+ACRRC+ACRPD+ACRLDG+ACROTHT+ACR4P+$S($P(^ACRDOC(ACRDOCDA,"TOAU"),U,5)=1:$G(ACRALTOT),1:0)
 D TOTAL,UP^ACRFSS3
 S D0=ACRDOCDA
 K ACRHOT
 Q
TAXI ;TO CALCULATE IF AIRPORT PARKING PLUS TRIP TO AIRPORT IS GREATER
 ;THAN TAXI TO AIRPORT
 S ACRAIRPT(1)=ACRAIRPT+ACRAIRT
 I ACRAIRPT(1),$P($G(^ACRAU(ACRDUZ,1)),U,9),ACRAIRPT(1)>($P(^(1),U,9)) S ACRAIRPT=$P(^(1),U,9)
 E  S ACRAIRPT=ACRAIRPT(1)
 Q
TOTAL Q:$P(^ACRDOC(ACRDOCDA,"TO"),U,22)'=1
 S ACRATM=ACRREIM-ACRLDG-ACRRC-ACRPHN-ACR4P-$S($P(^ACRDOC(ACRDOCDA,"TOAU"),U,5)=1:$G(ACRALTOT),1:0)
 S ACRATM=$P(ACRATM,".")
 S ACRATMX=$E(ACRATM,$L(ACRATM))
 S:ACRATMX#10 ACRATM=ACRATM+(10-ACRATMX)
 S:$E(ACRATM,$L(ACRATM)-1)#2 ACRATM=ACRATM+10
 S ACRATM1=$S(ACRREFX=600:$P(^ACRDOC(ACRDOCDA,"TO"),U,23),1:ACRATM)
 S ACRATM2=$P(^ACRDOC(ACRDOCDA,"TOAU"),U,8)
 K ^TMP("ACRTV",$J,"D")
 Q
SETS ;SETS TRAVEL INFO
 I $G(ACRREFX)'=600,$P($G(^ACRTV(ACRTVDA,0)),U,5)=1 Q
 S ^TMP("ACRTV",$J,"D",+$P(^ACRTV(ACRTVDA,"DT"),U),ACRDOCDA,ACRTVDA)=""
 S ACRJ=ACRJ+1
 S ACRTV=ACRTVDA_"^"_^ACRTV(ACRTVDA,0)
 S ACRTV1=^ACRTV(ACRTVDA,"DT")
 S ACRTVDAY=$P(^ACRTV(ACRTVDA,0),U)
 S ACRDESC=$G(^ACRTV(ACRTVDA,"DESC"))
 S ACRTVEP=$G(^ACRTV(ACRTVDA,1))
 I ACRTVDAY'=ACRJ D
 .S DIE="^ACRTV("
 .S DA=ACRTVDA
 .S DR=".01///"_ACRJ
 .D DIE^ACRFDIC
 S ^TMP("ACRTV",$J,ACRJ)=ACRTV
 S ACRTVDAT=$E($P($P(ACRTV1,U),","),4,7)
 S Y=$P(ACRTV1,U,2)
 X ^DD("DD")
 S ACRTVLV=$P(Y,"@",2)
 S ACRTVLV=$P(ACRTVLV,":")_$P(ACRTVLV,":",2)
 S Y=$P(ACRTV1,U,3)
 X ^DD("DD")
 S ACRTVAR=$P(Y,"@",2)
 S ACRTVAR=$P(ACRTVAR,":")_$P(ACRTVAR,":",2)
 S ACRTVCIT=$S('$P(ACRTV1,U,4):"NOT SPECIFIED",'$D(^ACRPD($P(ACRTV1,U,4),0)):"NOT SPECIFIED",1:$E($P(^ACRPD($P(ACRTV1,U,4),0),U),1,7))
 S ACRTVPD=$P(ACRTV1,U,5)
 S:$P(ACRTV1,U,20)>0 ACRMRR=$P(ACRTV1,U,20)
 S ACRPMRR=$P(ACRTV1,U,23)
 S ACRAIRP=$P(ACRTV1,U,22)
 S ACRAIRPT=ACRAIRPT+ACRAIRP
 S ACRTVLDG=$P(ACRTV1,U,6)
 S ACRTVMLS=$P(ACRTV1,U,7)
 S ACRTVPML=$P(ACRTV1,U,21)
 S ACRTVMR=ACRTVPML*ACRPMRR
 S ACRTVTAX=$P(ACRTV1,U,8)
 S ACRAIRT=ACRAIRT+(ACRTVMLS*ACRMRR)
 S ACRTVPHN=$P(ACRTV1,U,9)
 S ACRTVOTH=$P(ACRTV1,U,10)
 S ACRTVEXP=$P(ACRTV1,U,17)
 S ACRTVRC=$P(ACRTV1,U,15)
 S:ACRTVEP]"" ACRTVEXP=ACRTVEXP_U_ACRTVEP
 S ACRTVRCC=$P(ACRTV1,U,13)
 I ACRTVRCC,$D(^ACRRCOMP(ACRTVRCC,0)) S ACRTVRCC=$P(^(0),U)
 S ACROTH=ACROTH+ACRTVOTH
 S ACRPD=ACRPD+ACRTVPD
 S ACRLDG=ACRLDG+ACRTVLDG
 S ACRMR=ACRMR+ACRTVMR
 S ACRTAX=ACRTAX+ACRTVTAX
 S ACRPHN=ACRPHN+ACRTVPHN
 S ACRRC=ACRRC+ACRTVRC
 Q
