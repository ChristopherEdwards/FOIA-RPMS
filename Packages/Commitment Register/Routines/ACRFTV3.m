ACRFTV3 ;IHS/OIRM/DSD/THL,AEF - TRAVEL REPORT;  [ 11/01/2001   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;;NOV 05, 2001
 ;;ROUTINE TO PRINT TRAVEL REPORT
SS4 ;EP;
 S (ACR,ACR4,ACR9,ACR21)=0
 I ACRTVT'="D" S (ACRREQ,ACROBL,ACRSPT)=0
 F  S ACR=$O(^ACRSS(ACRTVT,ACRZDA,ACR)) Q:'ACR  D
 .Q:'$D(^ACRSS(ACR,0))!'$D(^ACRSS(ACR,"DT"))
 .S ACR0=^ACRSS(ACR,0)
 .S ACRDT=^ACRSS(ACR,"DT")
 .D SS5
 Q:ACRTVT="D"!(ACRTVT="TDEPT")!(ACRTVT="PO")!(ACRTVT="C")
 I '$D(ACRQUIT),$D(^TMP("ACRTV",$J)) D SS1^ACRFTV2
 I '$D(ACRQUIT),$E(IOST,1,2)="C-" D PAUSE^ACRFWARN
 W @IOF
 K ^TMP("ACRTV",$J)
 Q
SS5 S ACRDOCDA=$P(ACR0,U,2)
 Q:'ACRDOCDA
 Q:'$D(^ACRDOC(ACRDOCDA,"TO"))
 S ACRDOCTO=^ACRDOC(ACRDOCDA,"TO")
 I $D(ACRBEGIN),ACRBEGIN Q:$P(ACRDOCTO,U,14)<ACRBEGIN
 I $D(ACREND),ACREND,$P(ACRDOCTO,U,14)>ACREND Q
 I $D(ACRINVIT),$P($G(^AUTTLOC(+$P(ACRDOCTO,U,13),0)),U,4)=+$G(^ACRSYS(ACRADA,0)) Q
 K ACRDOCTO
 I ACRTVT["CAN",$P(ACR0,U,5)'=ACRZDA Q
 I ACRTVT["CAN",+^ACRLOCB($P(ACR0,U,6),"DT")'=ACRFY Q
 S ACROBJ=$P(ACR0,U,4)
 S ACROBJ=$P($G(^AUTTOBJC(+ACROBJ,0)),U)
 Q:$E(ACROBJ,1,2)'=21
 I '$G(ACRDTL1) S ACROBJ="ALL"
 S ACRCAN=$P(ACR0,U,5)
 S ACRCAN=$P($G(^AUTTCAN(+ACRCAN,0)),U)
 Q:ACRCAN=""
 I $D(ACRINCMP) D INCOMP I $D(ACRQUIT) K ACRQUIT Q
 S:'$D(^TMP("ACRTV",$J,ACROBJ)) ^TMP("ACRTV",$J,ACROBJ)=""
 F ACRI=4,9,21 S @("ACR"_ACRI)=$P(ACRDT,U,ACRI)
 N X
 S X=^TMP("ACRTV",$J,ACROBJ)
 S $P(X,U)=$P(X,U)+ACR4
 S $P(X,U,2)=$P(X,U,2)+ACR9
 S $P(X,U,3)=$P(X,U,3)+ACR21
 S $P(X,U,4)=$P(X,U,4)+ACR21
 S ^TMP("ACRTV",$J,ACROBJ)=X
 I $D(ACRDTAIL) D
 .S ACRDOC=$P(ACR0,U,3)
 .S ACRPD=$O(^ACRDOC(ACRDOC,9,"B",0))
 .S ACRTO=$G(^ACRDOC(ACRDOC,"TO"))
 .S ACRPURP=$E($P($G(^ACROBL(ACRDOC,"JST")),U),1,27)
 .S ACRREQ2=^ACRDOC(ACRDOC,"REQ2")
 .S ACRDOC=^ACRDOC(ACRDOC,0)
 .S ACRREF=$P(ACRDOC,U,13)
 .S ACRREF=$P(^AUTTDOCR(ACRREF,0),U)
 .S:$P(ACRDOC,U,14)["CANCEL" ACRPURP="*"_ACRPURP
 .I ACRPD,$D(^ACRPD(ACRPD,0)) S ACRPD=$P(^(0),U)
 .I "^103^349^326^210^"'[(U_ACRREF_U) D  I 1
 ..S ACRDOC0=$P(ACRDOC,U)
 ..S ACRDOC2=""
 .E  D
 ..S ACRDOC2=$P(ACRDOC,U)
 ..S ACRDOC0=$S($L($P(ACRDOC,U,2))>3:$E($P(ACRDOC,U,2),4,99),1:$P(ACRDOC,U))
 .I '$D(^TMP("ACRTV",$J,ACROBJ,ACRDOC0)) S X=$P(ACRTO,U,14)_U_ACRDOC0_U_$S($G(ACRPURP)]"":ACRPURP,1:$P(ACRDOC,U,14))_U_$P(ACRREQ2,U,8)
 .E  S X=^TMP("ACRTV",$J,ACROBJ,ACRDOC0)
 .S $P(X,U,5)=$P(X,U,5)+ACR4
 .S $P(X,U,6)=$P(X,U,6)+ACR9
 .S $P(X,U,21)=$P(X,U,21)+ACR21
 .S $P(X,U,11)=$P(ACRTO,U,15)
 .S $P(X,U,12)=ACRPD
 .S $P(X,U,13)=$P(ACRTO,U,9)
 .S $P(X,U,14)=ACRCAN
 .S:ACRDOC0'=ACRDOC2 $P(X,U,10)=ACRDOC2
 .S ^TMP("ACRTV",$J,ACROBJ,ACRDOC0)=X
 .I $D(ACRSIGS) D
 ..D SIGS
 ..S ^TMP("ACRTV",$J,ACROBJ,ACRDOC0,"SIGS")=$G(ACRD1)_U_$G(ACRD2)_U_$G(ACRD3)_U_$G(ACRD4)_U_$G(ACRD5)_U_$G(ACRD6)
 .K ACR4,ACR21,ACR9,ACRDOC2
 Q
INCOMP ;CHECK EACH DOCUMENT IF ONLY INCOMPLETE TV'S ARE BEING REPORTED
 N ACRDOC0,ACRAPV,ACRAPDA,ACRAP0,ACRAPDT,ACRREFDA,ACRREF,ACRD1,ACRD2,ACRD3,ACRTV
 S ACRDOC0=^ACRDOC(ACRDOCDA,0)
 S ACRAPV=$G(^ACROBL(ACRDOCDA,"APV"))
 I $P(ACRAPV,U,8)]"" D Q Q
 S ACRREFDA=$O(^AUTTDOCR("B",600,0))
 I $P(ACRDOC0,U,13)'=ACRREFDA D Q Q
 I '$D(^ACRAPVS("AB",ACRDOCDA)) D Q Q
 Q
SIGS Q:ACRSIGS=ACRDOCDA
 K ACRD1,ACRD2,ACRD3,ACRD4,ACRD5,ACRD6
 S ACRAPDA=0
 S ACRSIGS=ACRDOCDA
 F  S ACRAPDA=$O(^ACRAPVS("AB",ACRDOCDA,ACRAPDA)) Q:'ACRAPDA  S ACRAP0=$G(^ACRAPVS(ACRAPDA,0)),ACRAPDT=$G(^ACRAPVS(ACRAPDA,"DT")) D
 .I $P(ACRAP0,U,3)=40 S ACRD1=$P(ACRAPDT,U,3),ACRD2=$P(ACRAPDT,U,4)
 .I $P(ACRAP0,U,3)=45 S ACRD3=$P(ACRAPDT,U,4)
 .I $P(ACRAP0,U,3)=37 S ACRD4=$P(ACRAPDT,U,4)
 .I $P(ACRAP0,U,3)=39 S ACRD5=$P(ACRAPDT,U,4)
 .I $P(ACRAP0,U,3)=38 S ACRD6=$P(ACRAPDT,U,4)
 Q
Q S ACRQUIT=""
 Q
TDAY ;EP;TO SORT REPORT BY TRAVEL DAY SPECIFIC ITEMS
 K ^TMP("ACRTVD",$J)
 S ACRBEG=ACRBEGIN
 S (ACRREQ,ACROBL,ACRSPT)=0
 F  S ACRBEG=$O(^ACRTV("E",ACRBEG)) Q:'ACRBEG!$D(ACROUT)!$D(ACRQUIT)!(ACRBEG>ACREND)  D
 .S ACRTVDA=0
 .F  S ACRTVDA=$O(^ACRTV("E",ACRBEG,ACRTVDA)) Q:'ACRTVDA!$D(ACROUT)!$D(ACRQUIT)  D
 ..S ACRTV0=$G(^ACRTV(ACRTVDA,0))
 ..S ACRTVDT=$G(^ACRTV(ACRTVDA,"DT"))
 ..S ACRDOCDA=$P(ACRTV0,U,2)
 ..I ACRTVT="RC",$P(ACRTVDT,U,13)]"",'$D(^TMP("ACRTVD",$J,ACRDOCDA)) D
 ...S ^TMP("ACRTVD",$J,ACRDOCDA)=""
 ...D TD1
 ..I ACRTVT="LOC",$P(ACRTVDT,U,4)=ACRLOC,'$D(^TMP("ACRTVD",$J,ACRDOCDA)) D
 ...S ^TMP("ACRTVD",$J,ACRDOCDA)=""
 ...D TD1
 K ^TMP("ACRTVD",$J)
 Q
TD1 S ACRSSDA=0
 F  S ACRSSDA=$O(^ACRSS("J",ACRDOCDA,ACRSSDA)) Q:'ACRSSDA  D
 .S ACR0=$G(^ACRSS(ACRSSDA,0))
 .S ACRDT=$G(^ACRSS(ACRSSDA,"DT"))
 .D SS5
 Q
