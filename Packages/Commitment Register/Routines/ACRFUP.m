ACRFUP ;IHS/OIRM/DSD/THL,AEF - ARMS USER PROFILES; [ 09/26/2005   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**19**;NOV 05, 2001
 ;;ROUTINE USED TO DISPLAY ARMS USER PROFILES
EN F  D EN1 Q:$D(ACRQUIT)!$D(ACROUT)
 K ACR,ACRDUZ,ACRUSER,ACRUP
 Q
EN1 ;EP;TO SELECT ARMS USER(S) FOR DATA DISPLAY
 W @IOF
 W !?10,"PRINT ARMS USER PROFILES"
 S DIR(0)="SO^1:Individual User;2:Department Account"
 S DIR("A")="Which one"
 D DIR^ACRFDIC
 Q:$D(ACRQUIT)!$D(ACROUT)
 I Y=1 D SELECT K ACRQUIT Q
 I Y=2 D
 .S ACRUP=""
 .D OBLAMT^ACRFDTPE
 .D DEPT:ACRUP
 .K ACRQUIT
 Q
SELECT ;EP;TO SELECT ARMS USER VIA LOOKUP TO THE USER FILE
 S DIC="^VA(200,"
 S DIC(0)="AEMQZ"
 S DIC("A")="EMPLOYEE............: "
 S DIC("DR")=""
 W !!?21,"|"
 F ACRI=1:1:30 W "="
 W "|"
 D DIC^ACRFDIC
 I U[$E(X)!(+Y<1) S ACRQUIT="" Q
 N ACRDA
 S ACRDUZ=+Y
 ; S ACRUSER=Y(0,0)                                 ;ACR*2.1*19.02 IM16848
 ; S ACRUSER=$P(ACRUSER,",",2)_" "_$P(ACRUSER,",")  ;ACR*2.1*19.02 IM16848
 S ACRUSER=$$NAME3^ACRFUTL1(+Y)                     ;ACR*2.1*19.02 IM16848
 S ACRDA=ACRDUZ
 D PSUM
 Q
DEPT ;PRINT ARMS USER PROFILE FOR ALL WITH DEPARTMENT ACCOUNT ACCESS
 S ZTDESC="ARMS USER DATA SUMMARY BY DEPARTMENT ACCOUNT"
 S ZTSAVE("ACR*")=""
 S ACRRTN="DEPT1^ACRFUP"
 D ^ACRFZIS
 Q
DEPT1 S ACRZA=0
 F  S ACRZA=$O(^ACRLOCB(ACRUP,"SC",ACRZA)) Q:'ACRZA!$D(ACRQUIT)!$D(ACROUT)  D
 .I $D(^ACRLOCB(ACRUP,"SC",ACRZA,0)) S (ACRDUZ,ACRZDA)=+^(0) D
 ..Q:'$D(^VA(200,+ACRZDA,0))
 ..; S ACRUSER=$P(^VA(200,ACRZDA,0),U)                ;ACR*2.1*19.02 IM16848
 ..; S ACRUSER=$P(ACRUSER,",",2)_" "_$P(ACRUSER,",")  ;ACR*2.1*19.02  IM16848
 ..S ACRUSER=$$NAME3^ACRFUTL1(ACRZDA)                 ;ACR*2.1*19.02 IM16848
 ..D PS1^ACRFUP1
 K ACRZA
 Q
PSUM ;EP;TO DISPLAY ARMS USER DATA
 S ZTDESC="ARMS USER DATA SUMMARY"
 S ZTSAVE("ACR*")=""
 S ACRRTN="PS1^ACRFUP1"
 D ^ACRFZIS
 Q
PS1 ;EP;TO PRINT ARMS USER SUMMARY DATA
 D PS1^ACRFUP1
 Q
