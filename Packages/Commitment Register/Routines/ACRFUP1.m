ACRFUP1 ;IHS/OIRM/DSD/THL,AEF - ARMS USER PROFILES; [ 07/20/2006   9:44 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**19,20**;NOV 05, 2001
 ;;CONTINUATION OF ACRFUP
PS1 ;EP;TO PRINT ARMS USER SUMMARY DATA
 I $E($G(IOST),1,2)="C-" W @IOF
 D NAME
 W !!?5
 I D0,$D(^VA(200,D0,20)) W $S($P(^(20),U,4)="":"NO ",1:"")
 E  W "NO "
 W "Electronic Signature Code has been assigned."
 I $P($G(^ACRAU(D0,1)),U,6) W !?5,"Government ATM card has been authorized."
 N ACRDA
 S ACRDA=0
 K ACRAPVT
 F  S ACRDA=$O(^ACRAPL("B",ACRDUZ,ACRDA)) Q:'ACRDA!$D(ACRQUIT)!$D(ACROUT)  D
 .S ACR=$P(^ACRAPL(ACRDA,0),U,2)
 .I 'ACR D  Q
 ..S DA=ACRDA
 ..S DIK="^ACRAPL("
 ..D DIK^ACRFDIC
 .S ACRCAT=$P(^ACRAPVT(ACR,0),U,6)
 .S ACRORD=$P(^ACRAPVT(ACR,0),U,4)
 .I ACRCAT,ACRORD S ACRAPVT(ACRCAT,ACRORD,ACR)=$G(^ACRAPL(ACRDA,"DT"))
 S ACR1=0
 F  S ACR1=$O(^ACRAPL("ALT",ACR1)) Q:'ACR1!$D(ACRQUIT)!$D(ACROUT)  D
 .S ACR=0
 .F  S ACR=$O(^ACRAPL("ALT",ACR1,ACR)) Q:'ACR  D
 ..S ACRCAT=$P(^ACRAPVT(ACR,0),U,6),ACRORD=$P(^(0),U,4) I $D(^ACRAPL("ALT",ACR1,ACR,ACRDUZ)),ACRCAT,ACRORD S ACRAPVT(ACRCAT,ACRORD,ACR,ACR1)=""
 I '$D(ACRAPVT) W !!?5,"NO Signature Authorities assigned."
 I $D(ACRAPVT) D
 .N ACRCAT,ACRCATX
 .S (ACRCAT,ACRCATX)=$O(ACRAPVT(0))
 .D SIGHEAD
 .S ACRCAT=0
 .F  S ACRCAT=$O(ACRAPVT(ACRCAT)) Q:'ACRCAT!$D(ACRQUIT)!$D(ACROUT)  D
 ..I ACRCAT'=ACRCATX D SIGHEAD S ACRCATX=ACRCAT
 ..S ACRORD=0
 ..F  S ACRORD=$O(ACRAPVT(ACRCAT,ACRORD)) Q:'ACRORD!$D(ACRQUIT)!$D(ACROUT)  D
 ...S ACR=0
 ...F  S ACR=$O(ACRAPVT(ACRCAT,ACRORD,ACR)) Q:'ACR!$D(ACRQUIT)!$D(ACROUT)  D
 ....W !?5,$E($P(^ACRAPVT(ACR,0),U),1,25)
 ....S (ACRALT,ACRII)=0
 ....F ACRI=1:1 S ACRALT=$O(ACRAPVT(ACRCAT,ACRORD,ACR,ACRALT)) Q:'ACRALT&($S($D(ACRAPVT(ACRCAT,ACRORD,ACR))#2:$P(ACRAPVT(ACRCAT,ACRORD,ACR),U,ACRI)="",1:'ACRALT))  D
 .....S:ACRALT="" ACRALT=999999
 .....I $D(ACRAPVT(ACRCAT,ACRORD,ACR))#2,$P(ACRAPVT(ACRCAT,ACRORD,ACR),U,ACRI)]"" S ACRALTDA=$P(ACRAPVT(ACRCAT,ACRORD,ACR),U,ACRI) I ACRALTDA,$D(^VA(200,ACRALTDA,0)) D  I 1
 ......;S ACRII=ACRII+1,ACRALTX=$P(^VA(200,ACRALTDA,0),U),ACRALTX=$P($P(ACRALTX,",",2)," ")_" "_$P(ACRALTX,",") W:ACRI>1 ! W ?31,$E(ACRALTX,1,23)  ;ACR*2.1*19.02 IM16848
 ......S ACRII=ACRII+1 W:ACRI>1 ! W ?31,$E($$NAME3^ACRFUTL1(ACRALTDA),1,23)  ;ACR*2.1*19.02 IM16848
 .....E  I ACRI<5 W:ACRI>1 ! W ?31,"_________________________" S ACRII=ACRII+1
 .....E  W:ACRI>1 !
 .....;I ACRALT]"",$D(^VA(200,ACRALT,0)) S ACRALTX=$P(^(0),U),ACRALTX=$P($P(ACRALTX,",",2)," ")_" "_$P(ACRALTX,",") W ?57,$E(ACRALTX,1,25)  ;ACR*2.1*19.02 IM16848
 .....I ACRALT]"",$D(^VA(200,ACRALT,0))  W ?57,$E($$NAME3^ACRFUTL1(ACRALT),1,25)  ;ACR*2.1*19.02 IM16848
 ....I ACRII<5 F ACRI=ACRII:1:3 W:ACRI>0 ! W ?31,"_________________________"
 ....W !
 ....I $D(IOSL),$Y>(IOSL-10) D PAUSE^ACRFWARN W @IOF D SIGHEAD
 ..I $E($G(IOST),1,2)="P-",$D(IOSL),$Y>(IOSL-10) W @IOF D NAME
 Q:$D(ACRQUIT)!$D(ACROUT)
 K ACR,ACRCAT,ACRALT,ACRALTX,ACRORD,ACRAPVT
 I $D(^ACRUAL("LVL",ACRDUZ)) D  I 1
 .S ACRLVL=$O(^ACRUAL("LVL",ACRDUZ,0))
 .I ACRLVL,$D(^ACRACC(ACRLVL,0)) D
 ..W !!?5,"ARMS SYSTEM ACCESS LEVEL ",$P(^ACRACC(ACRLVL,0),U,2)," (",$P(^(0),U,3),")"
 E  W !!?5,"NO ACCESS LEVEL ASSIGNED."
 I $E($G(IOST),1,2)="P-",$D(IOSL),$Y>(IOSL-4) W @IOF D NAME
 I '$D(^ACRLOCB("SEC",ACRDUZ)) W !!?5,ACRUSER," does not have access to any Department Accounts."
 I $D(^ACRLOCB("SEC",ACRDUZ)) D
 .D ACCHEAD
 .S ACR=999999999
 .F  S ACR=$O(^ACRLOCB("SEC",ACRDUZ,ACR),-1) Q:'ACR!$D(ACRQUIT)!$D(ACROUT)  D
 ..;Q:'$G(^ACRLOCB(ACR,0))            ;ACR*2.1*19.04 IM17697   ; ACR*2.1*20.09 IM18831
 ..Q:$G(^ACRLOCB(ACR,0))=""                                    ; ACR*2.1*20.09 IM18831
 ..S ACRDEPT=$P(^ACRLOCB(ACR,0),U,5)
 ..S ACRSADA=$P(^ACRLOCB(ACR,0),U,4)
 ..S ACRSSADA=$P(^ACRLOCB(ACR,"DT"),U,8)
 ..S ACRFY=$P(^ACRLOCB(ACR,"DT"),U)
 ..Q:'$D(^AUTTPRG(+ACRDEPT,0))
 ..W !,ACRSADA,"-",ACR
 ..W ?15,$P(^AUTTPRG(ACRDEPT,0),U)
 ..W ?50,ACRFY
 ..I ACRSSADA,$D(^AUTTSSA(ACRSSADA,0)) W ?54,$P(^(0),U,4)
 ..I IOSL-4<$Y D
 ...D PAUSE^ACRFWARN
 ...D ACCHEAD:'$D(ACRQUIT)&'$D(ACROUT)
 W !
 D PAUSE^ACRFWARN
 W !
 D ^ACRFCAN
 Q
ACCHEAD W @IOF
 W !!,"Department Account access for: ",ACRUSER
 W !!,"ID NO (SA/DA)"
 W ?15,"ACCOUNT"
 W ?50,"FY"
 W ?54,"SUB-SUB-ACTIVITY"
 W !,"-------------"
 W ?15,"---------------------------------"
 W ?50,"--"
 W ?54,"----------------"
 Q
NAME S D0=ACRDUZ
 I '$D(^ACRAU(D0,0)) D
 .S (X,DINUM)=D0
 .S DIC="^ACRAU("
 .S DIC(0)="L"
 .D FILE^ACRFDIC
 .S D0=ACRDUZ
 N DXS,DIP,DC,DN
 I D0,$D(^VA(200,D0,0)) D
 .S Y=DT
 .X ^DD("DD")
 .W !?10,"ARMS USER PROFILE"
 .W ?53,"DATE: ",Y
 .W !?10,"-----------------"
 .W ?59,"-----------"
 .W !!
 .D ^ACRPER
 Q
SIGHEAD Q:$D(ACRQUIT)!$D(ACROUT)
 W !!?5,"Your ",$P($P($P(^DD(9002190.5,.06,0),U,3),ACRCAT_":",2),";")
 W !?5,"Signature Authorities"
 W ?31,"Your alternates are:"
 W ?57,"You may sign for:"
 W !?5,"-------------------------"
 W ?31,"-------------------------"
 W ?57,"-----------------------"
 Q
