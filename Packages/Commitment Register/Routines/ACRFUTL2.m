ACRFUTL2 ;IHS/OIRM/DSD/AEF - VARIOUS UTILITY SUBROUTINES [ 11/04/2002  8:22 AM ]
 ;;2.1;ADMIN RESOURCE MGT SYSTEM;**5,6,T**;NOV 05, 2001
 ;
 ;*** TESTING - MRS *** NEW ROUTINE ACR*2.1*6.04
 ;
NOTA(X) ;----- ALLOW/DISALLOW TRAVEL ADVANCE ; ACR*2.1*5.11
 ;      USED BY INPUT TRANSFORM ON FMS DOCUMENT FIELD TRAVEL ADVANCE
 ;      ALLOWED
 ;
 ;      INPUT:
 ;      X  =  DOCUMENT IEN
 ;
 ;      OUTPUT:
 ;      0  =  TRAVEL ADVANCE NOT ALLOWED
 ;      1  =  TRAVEL ADVANCE ALLOWED
 ;
 N ACRCAN,Y
 S Y=1
 ;
 ;----- IF ATM AUTHORIZED, NO TRAVEL ADVANCE ALLOWED
 I $P($G(^ACRDOC(X,"TO")),U,22) S Y=0
 ;
 ;----- DON'T ALLOW TRAVEL ADVANCE FOR HEADQUARTERS EAST
 S ACRCAN=$P($G(^ACRDOC(X,"REQ")),U,10)
 I ACRCAN S ACRCAN=$P($G(^AUTTCAN(ACRCAN,0)),U)
 ;I $E(ACRCAN,1,3)="J94" S Y=0 ;COMMENTED OUT TO UNBLOCK TRAVEL ADVANCES AT HQE
 ;
 Q Y
 ;
NAME(X) ;EP -- RETURNS EXTERNAL PERSON FILE 200 NAME ; ACR*2.1*5.15
 ;
 ;      X  =  PERSON FILE 200 IEN
 ;
 N Y
 S Y=""
 I X S Y=$P($G(^VA(200,X,0)),U)
 Q Y
 ;Begin new sub-routine *** TESTING - MRS *** ACR*2.1*6.04
FYFUN(X) ;EP -- RETURNS FISCAL YEAR OF FUNDS
 ;
 ;      X  =  FMS DEPARTMENT ACCOUNT IEN
 ;
 N Y
 S Y=""
 I X S Y=$P($G(^ACRLOCB(X,"DT")),U)
 Q Y
 ;End new sub-routine *** TESTING - MRS *** ACR*2.1*6.04
