BNIGT ; cmi/anch/maw - BNI CPHAD GUI Table Utilties 7/24/2007 1:24:55 PM
 ;;1.0;BNI CPHD ACTIVITY DATASYSTEM;**1**;DEC 20, 2006
 ;
 ;
 ;this routine will contain all table loads for the Visual CPHAD application
 ;
 ;cmi/anch/maw 7/24/2007 error when selecting one provider in PRV patch 1
 ;
 ;
 Q
 ;
PRV(BNIRET,BNISTR) ;-- get all providers
 S X="MERR^BNIGU",@^%ZOSF("TRAP") ; m error trap
 N BNIPRV,BNII,BNIERR,BNIIEN,BNIDA,BNIR,P
 S P="|"
 K ^BNITMP($J)
 S BNIRET="^BNITMP("_$J_")"
 S BNII=0
 S BNIERR=""
 I '$G(BNISTR) S BNISTR=""  ;cmi/anch/maw 7/24/2007 error when pressing SELECT button patch 1
 S BNIR=$P(BNISTR,P)
 S ^BNITMP($J,BNII)="T00007BMXIEN^T00050Provider"_$C(30)
 S BNIDA=0 F  S BNIDA=$O(^VA(200,"B",BNIDA)) Q:BNIDA=""  D
 . S BNIIEN=0 F  S BNIIEN=$O(^VA(200,"B",BNIDA,BNIIEN)) Q:'BNIIEN  D
 .. I '$G(BNIR) Q:'$O(^VA(200,"AK.PROVIDER",BNIDA,0))  ;not a provider
 .. I '$G(BNIR) Q:$P($G(^VA(200,BNIIEN,"PS")),U,4)  ;inactive
 .. S BNIPRV=$P($G(^VA(200,BNIIEN,0)),U)
 .. S BNII=BNII+1
 .. S ^BNITMP($J,BNII)=BNIIEN_U_BNIPRV_$C(30)
 S ^BNITMP($J,BNII+1)=$C(31)_$G(BNIERR)
 Q
 ;
COM(BNIRET) ;-- get all communities
 S X="MERR^BNIGU",@^%ZOSF("TRAP") ; m error trap
 N BNICOM,BNICOMS,BNII,BNIERR,BNIIEN,BNIDA
 K ^BNITMP($J)
 S BNIRET="^BNITMP("_$J_")"
 S BNII=0
 S BNIERR=""
 S ^BNITMP($J,BNII)="T00007BMXIEN^T00050Taxonomy"_$C(30)
 S BNIDA=0 F  S BNIDA=$O(^AUTTCOM("B",BNIDA)) Q:BNIDA=""  D
 . S BNIIEN=0 F  S BNIIEN=$O(^AUTTCOM("B",BNIDA,BNIIEN)) Q:'BNIIEN  D
 .. S BNICOM=$P($G(^AUTTCOM(BNIIEN,0)),U)
 .. S BNICOMS=$S($P($G(^AUTTCOM(BNIIEN,0)),U,3):$P($G(^DIC(5,$P($G(^AUTTCOM(BNIIEN,0)),U,3),0)),U,2),1:"")
 .. S BNII=BNII+1
 .. S ^BNITMP($J,BNII)=BNIIEN_U_BNICOM_"-"_BNICOMS_$C(30)
 S ^BNITMP($J,BNII+1)=$C(31)_$G(BNIERR)
 Q
 ;
TAX(BNIRET,BNISTR) ;-- generic taxonomy table
 S X="MERR^BNIGU",@^%ZOSF("TRAP") ; m error trap
 N P,BNIRFL,BNII,BNIERR,BNIIEN,BNITAXE,BNITAX,BNIDA,BNINONC,BNIXRF
 N BNIGL,BNIGRF,BNIP
 S P="|"
 K ^BNITMP($J)
 S BNIRET="^BNITMP("_$J_")"
 S BNII=0
 S BNIERR=""
 S ^BNITMP($J,BNII)="T00080BMXIEN^T00080Taxonomy"_$C(30)
 F BNIP=3:1 S BNITAXE=$P(BNISTR,P,BNIP) Q:$G(BNITAXE)=""  D
 . Q:$G(BNITAXE)=""
 . ;S BNITAXE=$P(BNISTR,P)
 . S BNITAX=$O(^ATXAX("B",BNITAXE,0))
 . Q:'$G(BNITAX)
 . S BNINONC=$P($G(^ATXAX(BNITAX,0)),U,13)
 . S BNIXRF=$P($G(^ATXAX(BNITAX,0)),U,14)
 . S BNIFL=$P($G(^ATXAX(BNITAX,0)),U,15)
 . ;I $G(BNIXRF)="" S BNIXRF="B"
 . I BNIFL=80 S BNIXRF="BA"  ;icd diagnosis x ref
 . I BNIFL=80.1 S BNIXRF="BA"  ;icd op and proc xref
 . ;I $G(BNIXRF)="" S BNIXRF="B"
 . S BNIGL=$G(^DIC(BNIFL,0,"GL"))
 . S BNIDA=0 F  S BNIDA=$O(^ATXAX(BNITAX,21,BNIDA)) Q:'BNIDA  D
 .. N BNIL,BNIH
 .. S BNII=BNII+1
 .. S BNIL=$P($G(^ATXAX(BNITAX,21,BNIDA,0)),U)
 .. S BNIH=$P($G(^ATXAX(BNITAX,21,BNIDA,0)),U,2)
 .. I (BNIL=BNIH)!($G(BNIH)="") D  Q
 ... I $G(BNIXRF)="" D  Q
 .... S BNIGRF=BNIGL_""""_BNIL_""""_")"
 .... S ^BNITMP($J,BNII)=$P($G(@BNIGRF@(0)),U)_$C(30)
 ... S ^BNITMP($J,BNII)=BNIL_U_$$GET1^DIQ(BNIFL,BNIL,.01)_$C(30)
 .. S BNIGRF=BNIGL_""""_BNIXRF_""")"
 .. N BNIIEN
 .. S BNIIEN=$O(@BNIGRF@(BNIL),-1)
 .. F  S BNIIEN=$O(@BNIGRF@(BNIIEN)) Q:BNIIEN>BNIH  D
 ... S BNII=BNII+1
 ... S ^BNITMP($J,BNII)=BNIIEN_U_$$GET1^DIQ(BNIFL,BNIIEN,.01)_$C(30)
 S ^BNITMP($J,BNII+1)=$C(31)_$G(BNIERR)
 Q
 ;
GRET(BNIRET,BNISTR) ;-- get general retrieval items based on value passed in
 N P,BNII
 S P="|"
 K ^BNITMP($J)
 S BNIRET="^BNITMP("_$J_")"
 S BNII=0
 S ^BNITMP($J,BNII)="T00007BMXIEN^T00200ITEMS^T00001TYPE^T00050CALL^T00010CW"_$C(30)
 N BNIIEN
 S BNIIEN=0 F  S BNIIEN=$O(^BNIGRI("C",BNIIEN)) Q:'BNIIEN  D
 . N BNIDA
 . S BNIDA=0 F  S BNIDA=$O(^BNIGRI("C",BNIIEN,BNIDA)) Q:'BNIDA  D
 .. N BNIDATA,BNITYP,BNIFL,BNICW
 .. S BNIDATA=$G(^BNIGRI(BNIDA,0))
 .. S BNITYP=$P(BNIDATA,U,2)
 .. S BNIFL=$P(BNIDATA,U,14)
 .. S BNICW=$P(BNIDATA,U,7)
 .. Q:$P(^BNIGRI(BNIDA,0),U,5)'[BNISTR
 .. S BNII=BNII+1
 .. S ^BNITMP($J,BNII)=BNIDA_U_$P(BNIDATA,U)_U_BNITYP_U_BNIFL_U_BNICW_$C(30)
 S ^BNITMP($J,BNII+1)=$C(31)
 Q
 ;
REP(BNIRET) ;-- get gen retrieval reports
 K ^BNITMP($J)
 S BNIRET="^BNITMP("_$J_")"
 N BNII
 S BNII=0
 S ^BNITMP($J,BNII)="T00080REPORT"_$C(30)
 N BNIDA
 S BNIDA=0 F  S BNIDA=$O(^BNIRTMP("C",BNIDA)) Q:BNIDA=""  D
 . N BNIIEN
 . S BNIIEN=0 F  S BNIIEN=$O(^BNIRTMP("C",BNIDA,BNIIEN)) Q:'BNIIEN  D
 .. Q:$P($G(^BNIRTMP(BNIIEN,0)),U,13)'=DUZ
 .. S BNII=BNII+1
 .. S ^BNITMP($J,BNII)=$P($G(^BNIRTMP(BNIIEN,0)),U,3)_$C(30)
 S ^BNITMP($J,BNII+1)=$C(31)
 Q
 ;
