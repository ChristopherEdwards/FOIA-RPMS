BNIGVL1 ; IHS/CMI/LAB - process record list ;
 ;;1.0;BNI CPHD ACTIVITY DATASYSTEM;;DEC 20, 2006
 ;
 ;
 ;
START ;
 S (BNIGBT,BNIGBTH)=$H,BNIGJOB=$J,BNIGRCNT=0
 D XTMP^BNIGVLU("BNIGVL","CPHAD GENERAL RETRIEVAL")
 D @BNIGTYPE,END
 Q
 ;
RS ; Run by visit date
 S X1=BNIGBD,X2=-1 D C^%DTC S BNIGSD=X
 S BNIGODAT=BNIGSD_".9999" F  S BNIGODAT=$O(^BNIREC("B",BNIGODAT)) Q:BNIGODAT=""!($E(BNIGODAT,1,5)>$E(BNIGED,1,5))  D V1
 Q
 ;
 ;
END ;
 S BNIGET=$H
 Q
V1 ;
 S BNIGVIEN="" F  S BNIGVIEN=$O(^BNIREC("B",BNIGODAT,BNIGVIEN)) Q:BNIGVIEN'=+BNIGVIEN  I $D(^BNIREC(BNIGVIEN,0)) D PROC
 Q
PROC ;
 K BNIGSPEC,BNIGVREC,BNIGPREC,DFN
 I BNIGPTVS="R" S BNIGVREC=^BNIREC(BNIGVIEN,0)
 D SCREENS
 Q:$D(BNIGSKIP)
 K BNIGSRT,BNIGPRNT S BNIGCRIT=BNIGSORT,BNIGX=0
 X:$D(^BNIGRI(BNIGSORT,4)) ^BNIGRI(BNIGSORT,4) I '$D(BNIGPRNT) D
 . I BNIGPTVS="R" S Y=$P($P(BNIGVREC,U),".") S BNIGPRNT=Y Q
 .Q
 S BNIGSRT=BNIGPRNT
 S ^XTMP("BNIGVL",BNIGJOB,BNIGBTH,"DATA HITS",BNIGSRT,BNIGVIEN)="",BNIGRCNT=BNIGRCNT+1
 Q
SCREENS ;
 K BNIGSKIP
 S BNIGI=0 F  S BNIGI=$O(^BNIRTMP(BNIGRPT,11,BNIGI)) Q:BNIGI'=+BNIGI!($D(BNIGSKIP))  D
 .I '$P(^BNIGRI(BNIGI,0),U,8) D SINGLE Q
 .D MULT
 .Q
 Q
SINGLE ;
 K X,BNIGSPEC S X="",BNIGX=0
 X:$D(^BNIGRI(BNIGI,1)) ^(1)
 I X="" S BNIGSKIP="" Q
 I '$D(BNIGSPEC),'$D(^BNIRTMP(BNIGRPT,11,BNIGI,11,"B",X)) S BNIGSKIP="" Q
 Q
MULT ;
 K BNIGFOUN,BNIGSKIP,BNIGSPEC,X S BNIGX=0,X=""
 X:$D(^BNIGRI(BNIGI,1)) ^(1)
 I $O(X(""))="" S BNIGSKIP="" Q
 I '$D(BNIGSPEC) S Y="" F  S Y=$O(X(Y)) Q:Y=""  I $D(^BNIRTMP(BNIGRPT,11,BNIGI,11,"B",Y)) S BNIGFOUN="" Q
 I $D(BNIGSPEC),$G(X) S BNIGFOUN=1 Q
 S:'$D(BNIGFOUN) BNIGSKIP=""
 Q
XIT ;EP - CALLED FROM BNIGVL
 D EN^XBVK("BNIG"),EN^XBVK("AMQQ")
XIT1 ;EP
 K BNIGANS,BNIGBTH,BNIGC,BNIGCNT,BNIGCRIT,BNIGCUT,BNIGD,BNIGDISP,BNIGDONE,BNIGHIGH,BNIGI,BNIGJOB,BNIGQMAN,BNIGSEL,BNIGTEXT,BNIGVAR,BNIGSKIP,BNIGPRNT,BNIGPRNM,BNIGLINE,BNIGRCNT,BNIGSCNT,BNIGDFET,BNIGY,DFN
 K X,X1,X2,IO("Q"),%,Y,POP,DIRUT,ZTSK,ZTQUEUED,H,S,TS,M,ZTIO,DUOUT,DIR,DTOUT,V,Z,I,DIC,DIK,DIADD,DLAYGO,DA,DR,DIE,DIU,AMQQTAX,DINUM,BNIGPACK,BNIGEP1,BNIGEP2,D,BNIGLENG,BNIGLHDR,BNIGSAVE,AMQQND
 Q
