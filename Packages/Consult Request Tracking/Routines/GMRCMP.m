GMRCMP ;SLC/DCM - List Manager routine: Medical Service and sub-specialty consults ;29-Nov-2005 11:54;MGH
 ;;3.0;CONSULT/REQUEST TRACKING;**1,1001**;DEC 27, 1997
 ;IHS/CIA/MGH Modified to use HRCN instead of SSN
EN ; -- main entry point for GMRC TRK MEDICINE CONSULTS
 K GMRCQUT
 D EN^GMRCMCP I $D(GMRCQUT) D EXIT Q
 D EN^VALM("GMRC TRK MEDICINE CONSULTS")
 D EXIT
 Q
 ;
HDR ; -- header code
 D HDR^GMRCSLM
 Q
 ;
INIT ; -- init variables and list array
 D KILL^VALM10(),CLEAR^VALM1
 K ^TMP("GMRCR",$J,"LIST")
 S DSPLINE=0,VALMAR="^TMP(""GMRCR"",$J,""LIST"")"
 ;IHS/CIA/MGH Modified to use HRCN instead of SSN
 ;S GMRCSN=$P(^DPT(DFN,0),"^",9),GMRCSSN=$E(GMRCSN,1,3)_"-"_$E(GMRCSN,4,5)_"-"_$E(GMRCSN,6,9)
 S GMRCHRCN=$$HRCN^GMRCMP(DFN,+$G(DUZ(2)))
 S GMRCSSN=GMRCHRCN
 F LINE=1:1:LNCT S DSPLINE=$O(^TMP("GMRCR",$J,"CS",DSPLINE)) Q:DSPLINE=""!(DSPLINE?1A.E)  S DATA=^(DSPLINE,0) D SET^VALM10(LINE,DATA)
 S VALMCNT=LNCT,VALMPGE=1,XQORM("A")="Select Action: "
 K DATA,DSPLINE,LINE
 Q
 ;
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
HRCN(DFN,SITE) ;EP  IHS/CIA/MGH  Get the health record number for a patient at this site
 Q $P($G(^AUPNPAT(DFN,41,SITE,0)),U,2)
 ;
EXIT ; -- exit code
 K DFN,BLK,CDT,GMRC,GMRCDT1,GMRCDT2,GMRCAGE,GMRCDT1,GMRCDT2,GMRCND,GMRCNO,GMRCPTN,GMRCDOB,GMRCNM,GMRCNM,GMRCPNM,GMRCPRNM,GMRCSEX,GMRCSN,GMRCHRCN,GMRCSSN,GMRCSSNM,LNCT,SEX,TAB
 K GMRCDT,GMRCSA,GMRCSVCN,GMRCVP,GMRCFL,GMRCWT,GMRCWRD,GMRCVP,GMRCTM,GMRCBM,GMRCFL,GMRCSS,GMRCFLG,GMRCPR,GMRCRB,GMRCTYPE,GMRCWARD,GMRCNM,GMRCSS,GMRCSSNM,GMRCSVCN,GMRCWARD,GMRCWRD
 K GMRCACTM,GMRCSTCK,GMRCOER
 Q
 ;
EXPND ; -- expand code
 Q
 ;
