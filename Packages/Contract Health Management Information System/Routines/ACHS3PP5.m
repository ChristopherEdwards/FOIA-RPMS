ACHS3PP5 ; IHS/ITSC/PMF - QUEUE THIRD PARTY PAYMENT REPORT (ALL PATIENTS) ;   [ 10/16/2001   8:16 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;;JUN 11, 2001
 ;
 ;TPF COPIED FOR RE-WRITE FROM ACHS3PPQ
 ;
 K ^TMP("ACHS3PP",$J)
 S ACHSUSR=$$USR^ACHS
 W !!!!,$$C^XBFUNC("****** 3rd PARTY PAYMENT REPORT FOR "_$$LOC^ACHS_" *****",80)
 S ACHSFAC=DUZ(2)
 ;
FISYR ;Select Fiscal Year
 S ACHSFY=$$FYSEL^ACHS
 I $D(DTOUT)!($D(DUOUT))!($D(DIROUT)) D END Q
 ;
SELSER ;Select type of service
 K DIR,DIC,ACHSSER
 S DIR("A")="Service Type",DIR(0)="S^1:43 (HOSPITAL);2:57 (DENTAL);3:64 (OUTPATIENT);4:ALL" S DIR("B")="ALL"
 W !
 D ^DIR
 I $D(DTOUT)!($D(DIROUT)) D END Q
 G FISYR:$D(DUOUT)
 S ACHSSER=Y
 ;
 ;
SELPAT ;Patient Selection
 K DIR,ACHSPAT
 S DIR(0)="Y",DIR("A")="Include ALL PATIENTS",DIR("B")="YES"
 W !
 D ^DIR
 I $D(DTOUT)!($D(DIROUT)) D END Q
 G SELSER:$D(DUOUT)
 I Y=1 S ACHSPAT(+Y)="" G REPTYP
 W !
DIR2 ;
 W !
 K DIC
 S DIC="^AUPNPAT(",DIC(0)="AEQM"
 D ^DIC
 I +Y<1,'$D(ACHSPAT) D END Q
 I +Y<1,$D(ACHSPAT) G REPTYP   ;PATIENT SELECTED
 S:+Y>0 ACHSPAT(+Y)=""
 G DIR2
 ;
REPTYP ;
 K DIR
 S DIR(0)="S^S:SUMMARY;D:DETAILED",DIR("B")="Summary",DIR("A")="     Report Type "
 S DIR("?",1)="Enter 'S' or <RETURN> for a 'SUMMARY' report which includes"
 S DIR("?",2)="TOTALS AND PERCENTAGES ONLY."
 S DIR("?",3)="Enter 'D' for a 'DETAILED' report which also includes"
 S DIR("?")="LISTING OF INDIVIDUAL DOCUMENTS AND ALL ASSOCIATED DATA."
 D ^DIR
 G SELPAT:$D(DUOUT)
 Q:$D(DTOUT)!$D(DIROUT)
 S ACHSRTYP=Y
DEVICE ;Device Selection
 W *7,!!?20,"This report may take awhile to compile.",!?9," It is recommended that you QUEUE your output to a PRINTER.",!
 K DIR
 S %=$$PB^ACHS
 I %=U!$D(DTOUT)!$D(DUOUT) D END Q
 I %="B" D VIEWR^XBLM("^ACHS3PP3"),EN^XBVK("VALM"),END Q
 S %ZIS="PQ"
 D ^%ZIS
 I POP W !,"NO DEVICE SELECTED - REQUEST ABORTED" D HOME^%ZIS D END:'$$DIR^XBDIR("E") G FISYR
 I '$D(IO("Q")) W:'$D(IO("S")) ! D:'$D(IO("S")) WAIT^DICD D ^ACHS3PP3 Q
ZTLOAD ;Loads Taskman
 S ZTRTN="^ACHS3PP3",ZTIO="",ZTDESC="3RD PARTY PAYMENT REPORT",ACHSQIO=ION_";"_IOST_";"_IOM_";"_IOSL
 F %="ACHSPAT(","ACHSFY","ACHSUSR","ACHSQIO","ACHSSER","ACHSFAC","ACHSCFY","ACHSFYWK","ACHSRTYP" S ZTSAVE(%)=""
 D ^%ZTLOAD
 K IO("Q")
 D HOME^%ZIS
 Q
END ;
 K DIC,DIR,DIROUT,DTOUT,DUOUT,ZTSK
 D EN^XBVK("ACHS"),^ACHSVAR,HOME^%ZIS
 Q
 ;
