ACHS3PPA ; IHS/ITSC/PMF - COMPILE CHS THIRD PARTY PAYMENT REPORT - INDIVIDUAL PAT ;   [ 10/16/2001   8:16 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;;JUN 11, 2001
 ;
 S ACHSFAC=DUZ(2),ACHSPAT=0
GETPAT ;
 F  S ACHSPAT=$O(ACHSPAT(ACHSPAT)) Q:ACHSPAT=""  D
 .;PATIENT X-REF
 .I '$D(^ACHSF("AC",ACHSPAT)) S ^TMP("ACHS3PP",$J,ACHSPAT,0)="" Q
 .D GETDIEN
 ;
 K ACHSDOCR,X,Y,Z
 D ^ACHS3PPB         ;THIRD PARTY PAYMENT REPORT
 Q
 ;
 ;
GETDIEN ;
 S ACHSDIEN=""
 F  S ACHSDIEN=$O(^ACHSF("AC",ACHSPAT,ACHSFAC,ACHSDIEN)) Q:ACHSDIEN=""  D
 .I ACHSDIEN="",'$D(^TMP("ACHS3PP",$J,ACHSPAT)) S ^TMP("ACHS3PP",$J,ACHSPAT,0)="" Q
 .Q:'$D(^ACHSF(ACHSFAC,"D",ACHSDIEN,0))!'$D(^ACHSF(ACHSFAC,"D",ACHSDIEN,"PA"))
 .S ACHSDOCR=$G(^ACHSF(ACHSFAC,"D",ACHSDIEN,0))
 .Q:$P(ACHSDOCR,U,9)'>0
GETFY .;
 .S ACHSFY1=$P(ACHSDOCR,U,14)
 .I ACHSFY1="",'$D(^TMP("ACHS3PP",$J,ACHSPAT)) S ^TMP("ACHS3PP",$J,ACHSPAT,0)=""
 .Q:ACHSFY1=""
 .Q:ACHSFY1'=$E(ACHSFY,4)
 .S ACHSSERV=$P(ACHSDOCR,U,4)
 .Q:'(ACHSSER=4)&(ACHSSERV'=ACHSSER)
 .D GETIDT
 Q
GETIDT ;
 S ACHSIDT=$P(ACHSDOCR,U,2)
 S ACHSOBL=$P(ACHSDOCR,U,9)
 S ACHSHRN=$P(ACHSDOCR,U,21)
 K Z
 F %=0:0 S %=$O(^ACHSF(ACHSFAC,"D",ACHSDIEN,"T",%)) Q:'%  D
 .S X=$G(^ACHSF(ACHSFAC,"D",ACHSDIEN,"T",%,0))
 .S Y=$P(X,U,2)
 .;
 .;IF 'TRANSACTION TYPE' IS NOT "INITIAL" AND NOT "CANCEL"
 .I Y'="I",Y'="C" D
 ..S Z("I")=$G(Z("I"))+$P(X,U,4)      ;'IHS PAYMENT AMOUNT'
 ..S Z("3")=$G(Z("3"))+$P(X,U,8)      ;'THIRD PARTY PAY AMT'
 ;
 ;
 Q:'$D(Z)
 S ACHSDOC=$P(ACHSDOCR,U,14)_"-"_$P($G(^AUTTAREA($P($G(^AUTTLOC(ACHSFAC,0)),U,4),0)),U,3)_$E($P($G(^AUTTLOC(ACHSFAC,0)),U,17),2,3)_"-"_$P(ACHSDOCR,U)
 S ^TMP("ACHS3PP",$J,ACHSPAT,ACHSDOC)=ACHSIDT_U_ACHSOBL_U_Z("3")_U_Z("I")_U_ACHSSERV_U_ACHSHRN
 Q
 ;
