ACHSCPTB ; IHS/ITSC/PMF - COMPILE CHS CPT CODE REPORT-SUMMARY ONLY ;   [ 10/31/2003  11:36 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;**7**;JUNE 11,2001
 ;ITSC/SET/JVK ACHS*3.1*7 8/5/2003 - MODIFIED TO GET CODE NOT IEN
 ;
 S C=0
 S ACHSFAC=DUZ(2),ACHSDATE=""
GETDATE ;
 S ACHSDATE=$O(^ACHSF(ACHSFAC,"PDOS",ACHSDATE))
 I ACHSDATE="",'$D(^TMP("ACHSCPT",$J,ACHSFAC)) S ^TMP("ACHSCPT",$J,ACHSFAC,0)=""
 G:ACHSDATE="" NEXTRTN
 I ACHSDATE<ACHSBEG!(ACHSDATE>ACHSEND) G GETDATE
 S ACHSDIEN=""
GETDIEN ;
 S ACHSDIEN=$O(^ACHSF(ACHSFAC,"PDOS",ACHSDATE,ACHSDIEN))
 G GETDATE:ACHSDIEN=""
 I '$D(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,0)) G GETDIEN
 S ACHSSERV=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,0)),U,4)
GETCODE ;
 S (I,ACHSCPTA,ACHS43,ACHS57,ACHS64)=0
 F  S I=$O(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I)) Q:'I  D
 . S ACHSCODE=$P($P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U),";",1)
 .;IHS/ITSC/JVK ACHS*3.1*7 ADD LINE BELOW TO GET CODE NOT IEN
 . S ACHSCDIC=$P($P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U),";",2)
 . I '$D(ACHSCODE(0)),'$D(ACHSCODE(ACHSCODE)) Q
 . I '$D(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)) S ^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)="0^0^0"_U_ACHS43_U_ACHS57_U_ACHS64
 .;IHS/ITSC/JVK ACHS*3.1*7 ADD LINE BELOW TO GET IEN FOR CORRECT DIC
 . S ^TMP("ACHSINDX",$J,ACHSCODE,ACHSCDIC)=""
 . S ACHSCHB=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U,5)
 .S ACHSCHA=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U,6)
 . S $P(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE),U,1)=$P($G(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)),U)+1
 . S $P(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE),U,2)=$P($G(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)),U,2)+ACHSCHB
 . S $P(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE),U,3)=$P($G(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)),U,3)+ACHSCHA
 . I ACHSSERV=1 S $P(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE),U,4)=$P($G(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)),U,4)+1
 . I ACHSSERV=2 S $P(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE),U,5)=$P($G(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)),U,5)+1
 . I ACHSSERV=3 S $P(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE),U,6)=$P($G(^TMP("ACHSCPT",$J,ACHSFAC,ACHSCODE)),U,6)+1
 .Q
 G GETDIEN
 ;
NEXTRTN ;
 K I
 G ^ACHSCPTC
 ;
