ACHSCPTE ; IHS/ITSC/PMF - COMPILE CHS CPT CODE REPORT-BY VENDOR/SUMMARY ;   [ 10/16/2001   8:16 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;;JUN 11, 2001
 ;
 S C=0,ACHSFAC=DUZ(2),ACHSDAT=ACHSBEG-1
GETDATE ;
 S ACHSDAT=$O(^ACHSF(ACHSFAC,"PDOS",ACHSDAT))
 I (ACHSDAT>ACHSEND)!(ACHSDAT="") D
 . I $D(ACHSVNDR(0)),'$D(^TMP("ACHSCPT",$J)) S ^TMP("ACHSCPT",$J,0)=""
 . I '$D(ACHSVNDR(0)),'$D(^TMP("ACHSCPT",$J)) S ^TMP("ACHSCPT",$J,ACHSVEN,0)=""
 .Q
 G:ACHSDAT="" ^ACHSCPTF
 S ACHSDIEN=""
GETDIEN ;
 S ACHSDIEN=$O(^ACHSF(ACHSFAC,"PDOS",ACHSDAT,ACHSDIEN))
 G GETDATE:ACHSDIEN=""
 I '$D(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,0)) G GETDIEN
GETVNDR ;
 S ACHSVEND=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,0)),U,8)
 I $D(ACHSVNDR(0)) G GETCODE
 I ACHSVEND'=ACHSVEN G GETDIEN
GETCODE ;
 S (I,ACHS43,ACHS57,ACHS64)=0
 F  S I=$O(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I)) Q:'I  D
 .S ACHSCODE=$P($P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U),";",1)
 .I '$D(ACHSCODE(0))&'$D(ACHSCODE(ACHSCODE)) Q
 .I '$D(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)) S ^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)="0^0^0"_U_ACHS43_U_ACHS57_U_ACHS64
 .S ACHSCHB=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U,5)
 .S ACHSCHA=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,11,I,0)),U,6)
 .S $P(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE),U,1)=$P($G(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)),U)+1
 .S $P(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE),U,2)=$P($G(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)),U,2)+ACHSCHB
 .S $P(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE),U,3)=$P($G(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)),U,3)+ACHSCHA
 .S ACHSSERV=$P($G(^ACHSF(ACHSFAC,"D",ACHSDIEN,0)),U,4)
 .I ACHSSERV=1 S $P(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE),U,4)=$P($G(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)),U,4)+1
 .I ACHSSERV=2 S $P(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE),U,5)=$P($G(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)),U,5)+1
 .I ACHSSERV=3 S $P(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE),U,6)=$P($G(^TMP("ACHSCPT",$J,ACHSVEND,ACHSCODE)),U,6)+1
 .S ACHSDEN=ACHSDIEN
 .Q
 G GETDIEN
 ;
