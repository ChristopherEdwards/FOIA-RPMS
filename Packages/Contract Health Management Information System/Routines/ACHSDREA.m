ACHSDREA ; IHS/ITSC/TPF/PMF - LIST ACTIVE DENIAL REASONS ;  
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;**18**;JUN 11, 2001
 ;ACHS*3.1*18 TEST FOR INACTIVE DENIAL OPTIONS
 ;
 W !!!
 S %ZIS="QP"
 D ^%ZIS
 I POP D HOME^%ZIS G END
 I '$D(IO("Q")) G START
 S ZTRTN="START^ACHSDREA",ZTDESC=$P($P($T(ACHSDREA),"-",2)," ",2,5)
 D ^%ZTLOAD
 Q
 ;
START ;EP - From TaskMan.
 N ACHSDFAC,ACHSDHDR,ACHSDO,ACHSDOT,ACHSDROP,ACHSDT,ACHSTIME,ACHSDX,DIW,DIWF,DIWL,DIWR
 D NOW^ACHS
 S ACHSDFAC=$$LOC^ACHS(),ACHSTIME=$P(ACHSTIME,"@",1),ACHSDHDR="CHS "_$P($P($T(ACHSDREA),"-",2)," ",2,5)
 U IO
 S DIWR=IOM-5,DIWF="W",ACHSDX=0
GO ;
 S ACHSDX=$O(^ACHSDENS(ACHSDX))
 G END:+ACHSDX=0,GO:'$D(^ACHSDENS(ACHSDX,0))
 I $D(^ACHSDENS(ACHSDX,10)),$P($G(^ACHSDENS(ACHSDX,10)),U)'="",$P($G(^ACHSDENS(ACHSDX,10)),U)<DT G GO
 D HDR
 G END:$G(ACHSQUIT)
 W !!?5,$P($G(^ACHSDENS(ACHSDX,0)),U),!!
 S ACHSDT=0,X="",DIWL=6
RTXT ;
 F  S ACHSDT=$O(^ACHSDENS(ACHSDX,1,ACHSDT)) Q:+ACHSDT=0  G GO:'$D(^ACHSDENS(ACHSDX,1,ACHSDT,0)) S X=$G(^ACHSDENS(ACHSDX,1,ACHSDT,0)) D ^DIWP
 D ^DIWW
 I $Y>(IOSL-6) D HDR G END:$G(ACHSQUIT)
 I '$D(^ACHSDENS(ACHSDX,20,0)) W !! G GO
 S ACHSDO=0
OPT ;
 S ACHSDO=$O(^ACHSDENS(ACHSDX,20,ACHSDO))
 G GO:+ACHSDO=0
 S ACHSDROP=$P($G(^ACHSDENS(ACHSDX,20,ACHSDO,0)),U)
 I $P(^ACHSDENS(ACHSDX,20,ACHSDO,0),U,2) G OPT     ;ACHS*3.1*18 IHS.OIT.FCJ
 W !?10,"OPTION: ",?10,ACHSDROP
 S ACHSDOT=0,X="",DIWL=11
 F  S ACHSDOT=$O(^ACHSDENS(ACHSDX,20,ACHSDO,1,ACHSDOT)) Q:+ACHSDOT=0  S X=$G(^ACHSDENS(ACHSDX,20,ACHSDO,1,ACHSDOT,0)) D ^DIWP
 D ^DIWW
 I $Y>(IOSL-6) D HDR G END:$G(ACHSQUIT)
 G OPT
 ;
END ;
 D ^%ZISC
 Q
 ;
HDR ;
 D RTRN^ACHS
 Q:$G(ACHSQUIT)
 W @IOF,$$C^ACHS(ACHSDHDR,IOM),!,$$C^ACHS(ACHSDFAC,IOM),!,$$C^ACHS(ACHSTIME,IOM)
 Q
 ;
