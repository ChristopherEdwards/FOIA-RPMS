ACHSEPAT ; IHS/ITSC/PMF - PRINT EOBRS BY PATIENT ;  [ 10/16/2001   8:16 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;;JUN 11, 2001
 ;
 K ACHSPAT,DFN
 W $G(IORVON),!,"THIS REPORT WILL ONLY INCLUDE PROCESSED DOCUMENTS.  ANY UNPROCESSED",!,"DOCUMENT WILL HAVE TO BE PRINTED USING ANOTHER OPTION.",$G(IORVOFF),!!!
 D PTLK^ACHS
 Q:'$D(DFN)
 S ACHSPAT=DFN
 W !
 S (ACHSDIEN,ACHSTIEN,ACHSX)=0
PAT1 ;
 S ACHSDIEN=$O(^ACHSF(DUZ(2),"EOBP",ACHSPAT,ACHSDIEN))
 I ACHSDIEN="" S ACHSPATI=0 G PATSEL
 S ACHSTIEN=0
PAT2 ;
 S ACHSTIEN=$O(^ACHSF(DUZ(2),"EOBP",ACHSPAT,ACHSDIEN,ACHSTIEN))
 G PAT1:ACHSTIEN=""
 S ACHSDAT=0
PAT3 ;
 S ACHSDAT=$O(^ACHSF(DUZ(2),"EOBP",ACHSPAT,ACHSDIEN,ACHSTIEN,ACHSDAT))
 G:ACHSDAT="" PAT2
 S ACHSX=ACHSX+1,ACHSPAT(ACHSX)=ACHSDIEN_"^"_ACHSTIEN_"^"_ACHSDAT
 G PAT3
 ;
PAT4 ;NOT USED KEPT FOR FUTURE ENHANCEMENTS
 ;S ACHSPATI=$O(^ACHSEOBE("EOBP",DFN,ACHSPATI)) G PATSEL:ACHSPATI=""
 ;S ACHSX=ACHSX+1,ACHSPATI(ACHSX)=DFN_"^"_ACHSPATI
 ;G PAT4
 ;
PATSEL ;
 I ACHSX=0 G PATNO
 I ACHSX=1 S ACHSLST=1 G PATPR ;S:$D(ACHSPAT(ACHS)) PATPR
 W !!,"ENTRY #",?10,"DOCUMENT #",?27,"PATIENT NAME",?56,"CHART #",?67,"TRANS DATE",!,$$REPEAT^XLFSTR("-",79)
 S ACHSY=0
 F I=1:1:ACHSX D:$D(ACHSPAT(I)) PATSEL1 S ACHSY=ACHSY+1 I ACHSY#10=0 W !!,"Press <REUTRN> To Continue" D READ^ACHSFU G PATSEL:Y="" G DIR:Y?1N.N ;D:$D(ACHSPATI(I)) PATSEL2
DIR ;
 S ACHSLST=$$DIR^XBDIR("L^1:"_ACHSX,"ENTER NUMBER(S) OF SELECTION(S)","","","","",1)
 G PATEND:$D(DUOUT)!$D(DTOUT),PATPR
 ;
PATSEL1 ;
 S ACHSDIEN=$P(ACHSPAT(I),U,1),ACHSTIEN=$P(ACHSPAT(I),U,2),ACHSDAT=$P(ACHSPAT(I),U,3),Y=$$FMTE^XLFDT(9999999-ACHSDAT)
 S ACHSPAT=$P(^ACHSF(DUZ(2),"D",ACHSDIEN,0),U,22),ACHSCHRT=$P(^AUPNPAT(ACHSPAT,41,DUZ(2),0),U,2),ACHSDOC=$P(^ACHSF(DUZ(2),"D",ACHSDIEN,0),U,14)_"-"_ACHSFC_"-"_$P(^(0),U,1)
 S ACHSPAT=$P(^DPT(ACHSPAT,0),U,1)
 D PATSELPR
 Q
 ;
PATSEL2 ;NOT USED KEPT FOR FUTURE ENHANCEMENTS
 ;S ACHSPATI=ACHSPATI(I)
 ;S DFN=$P(ACHSPATI,U,1),ACHSEPAT=$P(ACHSPATI,U,2)
 ;S ACHSPAT=$P(^DPT(DFN,0),U,1),ACHSDOC=$P(^ACHSEOBE(ACHSEPAT,0),U,1),ACHSCHRT=$P(^AUPNPAT(DFN,41,DUZ(2),0),U,2),ACHSDAT=$P(^ACHSEOBE(ACHSEPAT,0),U,8)
 ;S Y=$$FMTE^XLFDT($S($E(ACHSDAT,1,2)>50:2,$E(ACHSDAT,1,2)<50:3)_ACHSDAT)
 ;D PATSELPR
 Q
 ;
PATSELPR ;
 W !,I,".",?10,ACHSDOC,?27,ACHSPAT,?57,ACHSCHRT,?67,Y
 Q
 ;
PATPR ;
 D DEV,BRPT^ACHSFU
 G:Y=0!($D(DUOUT))!($D(DTOUT))!($D(DIRUT)) PATEND
 S ACHSXX=0
PATPR1 ;
 S ACHSXX=ACHSXX+1
 S I=$P(ACHSLST,",",ACHSXX)
 G:I="" PATEND
 D:$D(ACHSPAT(I)) PATPR2
 ;D:$D(ACHSPATI(I)) PATPR3
 G PATPR1
 ;
PATPR2 ;
 S ACHSDIEN=$P(ACHSPAT(I),U,1),ACHSTIEN=$P(ACHSPAT(I),U,2)
 D ^ACHSEOBA,^ACHSEOB2
 Q
 ;
PATPR3 ;NOT USED KEPT FOR FUTURE ENHANCEMENTS
 ;S ACHSNON=ACHSEPAT
 ;D ^ACHSEOBE,^ACHSEOB2
 Q
 ;
PATNO ;
 W !!,"NO EOBRS PROCESSED FOR THIS PATIENT",!!
 I $$DIR^XBDIR("E","Press RETURN...")
 G PATEND
 ;
DEV ;
 W !!
 S %ZIS="P"
 D ^%ZIS
 S ACHSEOIO=IO
 Q
 ;
PATEND ;
 D ^%ZISC,EN^XBVK("ACHS"),^ACHSVAR
 Q
 ;
