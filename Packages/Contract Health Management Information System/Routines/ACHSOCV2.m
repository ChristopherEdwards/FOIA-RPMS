ACHSOCV2 ; IHS/ITSC/PMF - COMPILE CHS SERVICE CLASS CODES BY VENDOR - DETAILED ;  [ 10/16/2001   8:16 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;;JUN 11, 2001
 ;
 S ACHSFAC=DUZ(2),ACHSFY1=""
GETFY ;
 S ACHSFY1=$O(^ACHSF(ACHSFAC,"D","B",ACHSFY1))
 I ACHSFY1="",'$D(^TMP("ACHSOCV",$J,ACHSFAC)) S ^TMP("ACHSOCV",$J,ACHSFAC,0)=""
 G:ACHSFY1="" ^ACHSOCVD
 S ACHSFYA=$E(ACHSFY1,2),ACHSFYB=$E(ACHSFY,4) I ACHSFYA'=ACHSFYB G GETFY
 S ACHSDIEN=""
GETDEN ;
 S ACHSDIEN=$O(^ACHSF(ACHSFAC,"D","B",ACHSFY1,ACHSDIEN))
 G:ACHSDIEN="" GETFY
 I '$D(^ACHSF(ACHSFAC,"D",ACHSDIEN,0)) G GETDEN
 I $P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,9)'>0 G GETDEN
GETCODE ;
 S ACHSOC=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,7)
 I '$D(^ACHS(3,ACHSFAC,1,ACHSOC,0)) G GETDEN
GETDOC ;
 S D=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,1),ACHSDOC=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,14)_"-"_$P(^AUTTAREA($P(^AUTTLOC(ACHSFAC,0),U,4),0),U,3)_$E($P(^AUTTLOC(ACHSFAC,0),U,17),2,3)_"-"_D
 S ACHSHRN=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,21),ACHSPAT=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,22)
GETVNR ;
 S ACHSVEN=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,8),ACHSVNDR=$S($D(^AUTTVNDR(ACHSVEN,0)):$P(^(0),U,1),1:"NOT ON FILE")
GETOBL ;
 S ACHSOBL=$P(^ACHSF(ACHSFAC,"D",ACHSDIEN,0),U,9)
GETPMT ;
 S ACHSPMT=$S($D(^ACHSF(ACHSFAC,"D",ACHSDIEN,"PA")):$P(^("PA"),U,1),1:0)
 I $D(^ACHSF(ACHSFAC,"D",ACHSDIEN,"ZA")) S ACHSADJ=$S($P(^ACHSF(ACHSFAC,"D",ACHSDIEN,"ZA"),U,2):$P(^("ZA"),U,2),1:0) S ACHSPMT=ACHSPMT+ACHSADJ
 S ^TMP("ACHSOCV",$J,ACHSFAC,ACHSVNDR,ACHSOC,ACHSDOC)=ACHSPAT_U_ACHSHRN_U_ACHSOBL_U_ACHSPMT_U_ACHSVEN
 G GETDEN
 ;
