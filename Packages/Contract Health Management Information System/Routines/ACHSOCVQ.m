ACHSOCVQ ; IHS/ITSC/PMF - CHS OBJ CLASS CODE BY VENDOR ;   [ 10/16/2001   8:16 AM ]
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;;JUN 11, 2001
 ;
 S ACHSUSR=$$USR^ACHS,ACHSFAC=$$LOC^ACHS
 W !!!!?10,"***** SERVICE CLASS CODE by VENDOR for "_ACHSFAC_" *****"
FISYR ;Select Fiscal Year
 S ACHSFY=$$FYSEL^ACHS
 G END:$D(DTOUT)!$D(DUOUT)!$D(DIROUT)
REPTYP ;Choose Report Type
 K DIR
 S DIR(0)="S^S:SUMMARY;D:DETAILED",DIR("B")="Summary",DIR("A")="     Report Type "
 S DIR("?",1)="Enter 'S' or <RETURN> for a 'SUMMARY' report"
 S DIR("?",2)="which includes TOTALS and PERCENTAGES ONLY."
 S DIR("?",3)="Enter 'D' for a 'DETAILED' report"
 S DIR("?")="which includes PATIENT NAME, HRN, and VENDOR DATA."
 D ^DIR
 G FISYR:$D(DUOUT),END:$D(DTOUT)!$D(DIROUT)
 S ACHSRTYP=Y
DEVICE ;Select Device
 W *7,!!?20,"This report may take awhile to compile.",!?15,"It is recommended that you QUEUE to a PRINTER.",!
 K DIR
 S %=$$PB^ACHS
 I %=U!$D(DTOUT)!$D(DUOUT) D END Q
 I %="B" D VIEWR^XBLM($S(ACHSRTYP="S":"^ACHSOCV1",1:"^ACHSOCV2")),EN^XBVK("VALM"),END Q
 S %ZIS="PQ"
 D ^%ZIS
 I POP W !,"NO DEVICE SELECTED - REQUEST ABORTED" S DIR(0)="E" D ^DIR D HOME^%ZIS G END:Y=0,REPTYP:Y=1
 I '$D(IO("Q")) W:'$D(IO("S")) ! D:'$D(IO("S")) WAIT^DICD G ^ACHSOCV1:ACHSRTYP="S",^ACHSOCV2:ACHSRTYP="D"
 I $D(IO("S"))!($E(IOST)'="P") G DEVICE
ZTLOAD ;Loads Taskman
 S ZTRTN=$S(ACHSRTYP="S":"^ACHSOCV1",ACHSRTYP="D":"^ACHSOCV2"),ZTIO="",ZTDESC="OBJ CLASS by VENDOR,"_$S(ACHSRTYP="S":"SUMMARY",1:"DETAIL"),ACHSQIO=ION_";"_IOST_";"_IOM_";"_IOSL
 F %="ACHSUSR","ACHSQIO","ACHSFY","ACHSFAC" S ZTSAVE(%)=""
 D ^%ZTLOAD
 K IO("Q")
 D HOME^%ZIS
 K IOP
END ;
 K DIR,DIC,DIROUT,DTOUT,DUOUT
 D EN^XBVK("ACHS"),^ACHSVAR,HOME^%ZIS
 Q
 ;
