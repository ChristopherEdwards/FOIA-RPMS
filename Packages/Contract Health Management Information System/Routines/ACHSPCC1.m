ACHSPCC1 ; IHS/ITSC/TPF/PMF - CHS AREA SPLITOUT (1/5) ; JUL 10, 2008
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;**14**;JUN 11,2001
 ;ACHS*3.1*14 IHS/OIT/FCJ call new routine for printing UFMS and FI totals
 ;
 D HOME^%ZIS
 S ACHSIO=IO
 I $D(^ACHSPCC("ODF-POST")) D  Q
 . U IO(0)
 . W !?10,"CHS FACILITY FILES ALREADY PROCESSED ON ",$$FMTE^XLFDT($P(^ACHSPCC("ODF-POST"),U,1)),!!?10,"JOB CANCELLED"
 . D JOBABEND^ACHSPCC4
 .Q
 S X=$P(^AUTTLOC(DUZ(2),0),U,4),ACHSPFX=$P(^AUTTAREA(X,0),U,4),ACHSAREA=$P(^(0),U)
 S ACHSAPN=$P(^AUTTSITE(1,0),U,2)
 I ACHSAPN']"" D  Q
 . W *7,!,"ACCOUNTING POINT NUMBER is missing from RPMS SITE file...",!
 . D JOBABEND^ACHSPCC4
 .Q
 S X=$O(^AUTTTEL(DUZ(2),1,"B","PCC",""))
 I +X<1 D ERR("PCC") Q
 S ACHSPRN=$P(^AUTTTEL(DUZ(2),1,X,0),U,2)
 I +ACHSPRN<1 D ERR("RJEPCC") Q
 I '$D(^AUTTTEL(DUZ(2),1,"B","BCS")) D ERR("BCS") Q
 S X=$O(^AUTTTEL(DUZ(2),1,"B","BCS",""))
 I '$D(^AUTTTEL(DUZ(2),1,X,0)) D ERR("BCS") Q
 S ACHSFIRN=$P(^AUTTTEL(DUZ(2),1,X,0),U,2)
 I +ACHSFIRN<1 D ERR("RJEBCS") Q
 I +ACHSPFX>0,(+ACHSPFX<300!(+ACHSPFX>399)) D CPFXERR Q
 I +ACHSPFX=0,$E(ACHSPFX)'="J" D CPFXERR Q
 I $L(ACHSPFX)'=3 D CPFXERR Q
 W !,$$C^XBFUNC("AREA PREFIX="_$E(ACHSPFX,2,3)),!
 U IO(0)
 W !,"Your CHS FACILITY DHR Transactions Should be TRANSMITTED to:",!?10,"(1) HAS and/or CORE",!?10,$S($$AOP^ACHS(2,8)="Y":"(2) Fiscal Intermediary",1:" ")
 U IO(0)
 I '$$DIR^XBDIR("E","","","","","",2) D JOBABEND^ACHSPCC4 Q
 I $D(DUOUT)!$D(DTOUT) D JOBABEND^ACHSPCC4 Q
EFFD ;
 S ACHSEFDT=$$DIR^XBDIR("D","Enter Effective Transaction Date ",$$FMTE^XLFDT(DT),"","The effective date will be placed in all DHR records","^S Y=""??"",%DT=""E"" D ^%DT",2)
 I $D(DTOUT)!$D(DUOUT)!(ACHSEFDT<1) D JOBABEND^ACHSPCC4 Q
 I ACHSEFDT>DT W *7," CAN'T BE FUTURE DATE" G EFFD
 S ACHSEFDT=$E(Y,4,7)_$E(Y,2,3)
 W !
 S %ZIS("A")="ENTER DEVICE # FOR SUMMARY REPORT ",%ZIS="P"
 D ^%ZIS
 K %ZIS
 I POP D HOME^%ZIS D JOBABEND^ACHSPCC4 Q
 S ACHSPTRD=IO,ACHSCCTR="PCC"
 ;ACHS*3.1*14 11/15/2007 IHS/OIT/FCJ REMOVED CALL TO ACHSPCC2 AND NOW CALLING ACHSPCCR
 ;D ^ACHSPCC2
 S ACHSCT2=0,ACHSHASH=0 D ^ACHSPCCR
 Q:$D(ACHSFLG)
 D ^ACHSPCC3
 Q
 ;
CPFXERR ;
 W *7,!!?12," CAN NUMBER PREFIX NOT CORRECTLY DEFINED FOR THIS FACILITY",!,"PREFIX = '",ACHSPFX,"'",!!?35,"JOB CANCELLED",!!
 D ENTRETRN^ACHSPCC4
 Q
 ;
PCC ;;COMPUTER CENTER entry Missing for ITS (Parklawn)
RJEPCC ;;Default RJE # for ITS (Parklawn) Missing
BCS ;;COMPUTER CENTER entry Missing for the Fiscal Intemediary
RJEBCS ;;Default RJE # for the Fiscal Intemediary Missing
ERR(X) ;
 W *7,!!?10,$P($T(@X),";",3)
 W !?10,"In IHS COMMUNICATIONS PARAMETERS file for ",$$LOC^ACHS
 D JOBABEND^ACHSPCC4
 Q
 ;
