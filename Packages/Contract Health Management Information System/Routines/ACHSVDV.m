ACHSVDV ; IHS/ITSC/PMF - 1/2 VIEW/EDIT PROVIDER-VENDOR FILE ;
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;**6,11,13,19**;JUN 11, 2001
 ;ITSC/SET/JVK ACHS*3.1*6 MODIFIED TO DISPLAY ACTUAL CONTENT OF FIELD
 ;ITSC/SET/JVK ACHS*3.1*11 MODIFIED TO DISPLAY MEDICARE PROVIDER
 ;ACHS*3.1*13 11/29/06 IHS/OIT/FCJ FORMAT CHANGES AND ADDED DUNS NUMBER OPTION 11
 ;
A1 ;EP
 D LINES^ACHSFU,HDR
 S DIC(0)="AEQMZ"_$S($D(^XUSEC("ACHSZMGR",DUZ)):"L",1:""),DIC="^AUTTVNDR(",DIC("A")="Enter Provider/Vendor:  "
 S:DIC(0)["L" DLAYGO=9999999.11
 D ^DIC
 K DIC,DLAYGO
 G END:Y=-1
 S ACHSPROV=+Y
 G ^ACHSVDVD
 ;
A1A ;EP
 S:'$D(^AUTTVNDR(ACHSPROV,11))#2 ^AUTTVNDR(ACHSPROV,11)=""
 S:'$D(^AUTTVNDR(ACHSPROV,13))#2 ^AUTTVNDR(ACHSPROV,13)=""
A2 ;EP
 Q:'$D(ACHSPROV)
 S ACHS0=$G(^AUTTVNDR(ACHSPROV,0)),ACHS11=$S($D(^AUTTVNDR(ACHSPROV,11)):^(11),1:""),ACHS13=$S($D(^AUTTVNDR(ACHSPROV,13)):^(13),1:""),ACHS17=$S($D(^AUTTVNDR(ACHSPROV,17)):^(17),1:"")
 I $D(^AUTTVNDR(ACHSPROV,11)),$P($G(^AUTTVNDR(ACHSPROV,11)),U)="" G MESSAGE^ACHSVDVD
 D HDR
 S ACHSVT=$P(ACHS11,U,3),(ACHSDAP,ACHSLAD,ACHSDISP)=""
 S %=$O(^ACHSVPMT(DUZ(2),1,"B",ACHSPROV,0))
 I %,$D(^ACHSVPMT(DUZ(2),1,%,1,ACHSCFY,0)) S ACHSDAP=$P(^(0),U,2),ACHSLAD=$P(^(0),U,3)
 I ACHSVT]"",ACHSVT?1N.N S ACHSVT=$P(^AUTTVTYP(+ACHSVT,0),U,2)
 S ACHSEDOS=DT
 K ACHSRT,ACHSCTNA
 D A1^ACHSURT
 S ACHSLAD=$$FMTE^XLFDT(ACHSLAD),ACHSACO="",P=ACHSPROV
 D ^ACHSUCN
 K ACHSRT,ACHSCTNA
 D A1^ACHSURT
 S ACHSMS=$P(ACHS13,U,3)
 I ACHSMS?1N.N S ACHSMS=$P(^DIC(5,ACHSMS,0),U)
 S ACHSBS=$P(ACHS13,U,8)
 I ACHSBS?1N.N S ACHSBS=$P(^DIC(5,ACHSBS,0),U)
 ;ACHS*3.1*13 11/29/06 IHS/OIT/FCJ FORMAT CHANGES AND ADDED DUNS NUMBER OPTION 11
 W !," 1) ",$P(^AUTTVNDR(ACHSPROV,0),U),?46," 2) EIN No: ",$P(ACHS11,U),$S($P(ACHS11,U,2)]"":"-"_$P(ACHS11,U,2),1:"")
 W !," 3) Status: ",$S(+$P(ACHS0,U,5):"IN",1:""),"ACTIVE"
 W ?46," 4) Contracts: ",$S(ACHSACO="N":"NONE ACTIVE",+C>0:"ACTIVE="_+C,1:"NONE")
 I '$D(ACHSRT("RQ")) S ACHSRT("RQ")=-1
 W !," 5) UPIN: ",$P(ACHS17,U),?46," 6) Rate Quotation: ",$S('$D(ACHSRT("RQ")):"NONE",'$D(ACHSRT("RQ","ACTIVE")):"NONE ACTIVE",ACHSRT("RQ","ACTIVE")>0:"ACTIVE="_+$P(ACHSRT("RQ","ACTIVE"),U,1),1:"")
 W !," 7) Type of Business: "
 S X=$P(ACHS11,U,26)
 I X W $P($G(^AUTTTOB(X,0)),U)
 W ?46," 8) Agreement: ",$S('$D(ACHSRT("PA")):"NONE",'$D(ACHSRT("PA","ACTIVE")):"NONE ACTIVE",ACHSRT("PA","ACTIVE")>0:"ACTIVE="_+$P(ACHSRT("PA","ACTIVE"),U,1),1:"")
 ;IHS/SET/JVK ACHS*3.1*11 ADD MEDICARE PROVIDER FIELD NUMBER 9
 I $D(^AUTTVNDR(ACHSPROV,23)) S ACHSMP=$P($G(^(23)),U) S ACHMP=$$EXTSET^XBFUNC(9999999.11,2301,ACHSMP)
 I '$D(^AUTTVNDR(ACHSPROV,23)) S ACHSMP=""
 W !," 9) Medicare Provider: ",$S(ACHSMP="":"No entry",ACHSMP'="":ACHSMP,1:"")
 W ?46,"10) BPA: ",$S('$D(ACHSRT("BPA")):"NONE",'$D(ACHSRT("BPA","ACTIVE")):"NONE ACTIVE",ACHSRT("BPA","ACTIVE")>0:"ACTIVE="_+$P(ACHSRT("BPA","ACTIVE"),U,1),1:"")
 W !,"11) E-Mail: ",$$VAL^XBDIQ1(9999999.11,ACHSPROV,2103)  ;ACHS*3.1*19
 W ?46,"12) DUNS: ",$$VAL^XBDIQ1(9999999.11,ACHSPROV,.07)
 W !!,"**** MAILING/BILLING ADDRESS ****",?44,"**** PROVIDER LOCATION ADDRESS ****"
 W !,"13) Street: ",$P(ACHS13,U),?46,"14) Street: ",$E($P(ACHS13,U,6),1,17)
 ;ACHS*3.1*19 CHANGES TO NXT 2 LINES TO DISPLAY FAX NUM
 W !?6,"City: ",$P(ACHS13,U,2),?52,"City: ",$P(ACHS13,U,7),!?5,"State: ",ACHSMS,?26,"Zip: ",$P(ACHS13,U,4),?51,"State: ",ACHSBS
 W !?5,"Phone: ",$P(ACHS11,U,9),?26,"Fax: ",$P(ACHS11,U,14),?48,"Zip Code: ",$P(ACHS13,U,9),!?6,"Attn: ",$P(ACHS13,U,5)
 W !,"15) Vendor Type: ",ACHSVT
 ;IHS/SET/JVK ACHS*3.1*6 MODIFIED TO DISPLAY ACTUAL CONTENT OF FIELD
 ;W ?48,"(13).  Fed/Non-Fed: ",$S($P(ACHS11,U,10)=1:"NON-",$P(ACHS11,U,10)=2:"",1:"NON-"),"FED"
 W ?46,"16) Fed/Non-Fed: ",$S($P(ACHS11,U,10)=1:"NON-FED",$P(ACHS11,U,10)=2:"FED",$P(ACHS11,U,10)="":"")
 W !,"17) Specialty: ",$S($P(ACHS11,U,4):$P(^DIC(7,$P(ACHS11,U,4),0),U),1:"")
 ;IHS/SET/JVK ACHS*3.1*6 ADD GEOGRAPHICAL LOCATION 4/15/2003     
 W ?46,"18) Geographic Loc: ",$S($P(ACHS11,U,25)="":"",$P(ACHS11,U,25):$P(^AUTTGL($P(ACHS11,U,25),0),U))
 W !?2,"Last Payment Date: ",ACHSLAD,?45,"Current FYTD Paid: "
 I ACHSDAP]"" S X=ACHSDAP,X2="2$" D FMT^ACHS
 W !,$$REPEAT^XLFSTR("*",79)
 G:'$D(^XUSEC("ACHSZMGR",DUZ)) A4^ACHSVDV1
 ;ITSC/SET/JVK ACHS*3.1*11 NOW IS ENTRY POINT 13 ;ACHS*3.1*13 IHS/OIT/FCJ IS NOW 14
 ;I ACHSVT="" W *7,!!,"MUST HAVE VENDOR TYPE." D 12^ACHSVDVA G A2:'$D(Y),A1
 I ACHSVT="" W *7,!!,"MUST HAVE VENDOR TYPE." D 15^ACHSVDVA G A2:'$D(Y),A1
A3 ;
 K ACHSCTFL,ACHSRQFL,ACHSPAFL,ACHSBPFL
 S Y=$$DIR^XBDIR("Y","Want to Edit","NO","","","",2)
 G END:$D(DTOUT),A1:$D(DUOUT),A4^ACHSVDV1:('Y),EDIT^ACHSVDVA:Y
 G A3
 ;
END ;EP
 K X,P,S,C,L,D,D0,DA,DI,DIC,DR
 D EN^XBVK("ACHS"),^ACHSVAR
 Q
 ;
HDR ;
 S X="PROVIDER/VENDOR UPDATE",Y="ACHS"
 D SHDR^ACHS
 W $$REPEAT^XLFSTR("*",79)  ;ACHS 3.1*13 11/29/06 IHS/OIT/FCJ REMOVED !
 Q
 ;
