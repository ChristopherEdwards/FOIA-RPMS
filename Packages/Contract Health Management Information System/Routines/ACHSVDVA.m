ACHSVDVA ; IHS/ITSC/PMF - 2/2 EDIT VENDOR INFORMATION - CALLED FROM ACHSVDV ;
 ;;3.1;CONTRACT HEALTH MGMT SYSTEM;**6,11,13,19**;JUN 11, 2001
 ;IHS/SET/JVK ACHS*3.1*11 MODIFIED FOR MEDICARE PROVIDER ENTRY 8/10/2004
 ;IHS/SET/JVK ACHS*3.1*6 MODIFIED FOR ADDITIONAL ENTRY 4/15/2003
 ;ACHS*3.1*13 11/29/06 IHS/OIT/FCJ ADDED DUNS NUMBER OPTION
 ;
EDIT ;EP
 ;ACHS*3.1*13 11/29/06 IHS/OIT/FCJ MODIFIED FOR ADDITIONAL ENTRY
 ;S Y=+$$DIR^XBDIR("NO^1:14","Change Which Item","","","Enter the number corresponding to the field you need to edit","",2)
 S Y=+$$DIR^XBDIR("NO^1:18","Change Which Item","","","Enter the number corresponding to the field you need to edit","",2)
 G END^ACHSVDV:$D(DTOUT),A1^ACHSVDV:$D(DUOUT)!(Y<1)
 S $P(^AUTTVNDR(ACHSPROV,11),U,11)=DT
 W !
 D @(Y)
 G A2^ACHSVDV
 ;
1 ;
 D EDITVNDR(ACHSPROV,.01)
 G A1^ACHSVDV
 ;
2 ;
 D EDITVNDR(ACHSPROV,"1101;1102")
 D ^ACHSVDVD
 Q
 ;
3 ;
 D EDITVNDR(ACHSPROV,.05)
 Q
 ;
4 ;
 S ACHSACO="L",P=ACHSPROV,A("DISPLAY")=0
 D L^ACHSVDV1
 S:'$D(DA) DA=""
 Q:'$D(DA)!(DA="")
 S DIE("NO^")="",DIE="^AUTTVNDR("_ACHSPROV_",""CN"",",DA(1)=ACHSPROV,DR="1;2;4;3"
 D ^DIE
 K DIE,DA
 Q
 ;
5 ;
 D EDITVNDR(ACHSPROV,1701)
 Q
 ;
6 ;
 S ACHSAGTP="RQ"
 D AGRDSP^ACHSVDV2
 Q:'$G(DA)
 S DIE="^AUTTVNDR("_ACHSPROV_",18,",DA(1)=ACHSPROV,DR=".02;.04;.03;.05;.07;.06;.08;.09"
 D ^DIE
 K DIE
 Q
 ;
7 ;
 D EDITVNDR(ACHSPROV,1126)
 Q
 ;
8 ;
 S ACHSAGTP="PA"
 D AGRDSP^ACHSVDV2
 Q:'$G(DA)
 S DIE="^AUTTVNDR("_ACHSPROV_",18,",DA(1)=ACHSPROV,DR=".02;.04;.03;.05;.07;.06;.08;.09"
 D ^DIE
 K DIE
 Q
 ;
9 ;IHS/SET/JVK ACHS*3.1*11 ADD MEDICARE PROVIDER
 I $P($G(^AUTTVNDR(ACHSPROV,23)),U)'="" D MP^ACHSVDV1,EDITVNDR(ACHSPROV,"2301;2302")
 I $P($G(^AUTTVNDR(ACHSPROV,23)),U)="" D EDITVNDR(ACHSPROV,"2301;2302"),MP^ACHSVDV1
 Q:'$G(DA)
 ;D EDITVNDR(ACHSPROV,"2301;2302")
 Q
 ;
10 ;IHS/SET/JVK ACHS*3.1*11 RE-NUMBER 10-16
 ;
 S ACHSAGTP="BPA"
 D AGRDSP^ACHSVDV2
 Q:'$G(DA)
 S DIE="^AUTTVNDR("_ACHSPROV_",18,",DA(1)=ACHSPROV,DR=".02;.04;.03;.05;.07;.06;.08;.09"
 D ^DIE
 K DIE
 Q
 ;
11 ;ACHS*3.1*19 3.30.11 IHS/OIT/FCJ ADDED E-MAIL OPTION 11 AND RE-NUMBER 12-18
 D EDITVNDR(ACHSPROV,"2103")
 Q
12 ;ACHS*3.1*13 11/29/06 IHS/OIT/FCJ ADDED DUNS NUMBER AND RE-NUMBER 12-17
 D EDITVNDR(ACHSPROV,".07;.08")
 Q
13 ;
 D EDITVNDR(ACHSPROV,"1301:1302;1303;1304;1305;1109;1114") ;ACHS*3.1*19 ADDED FIELD 1114 TO LINE 
 Q
 ;
14 ;
 D EDITVNDR(ACHSPROV,"1306;1307;1308;1309")
 Q
 ;
15 ;EP
 D EDITVNDR(ACHSPROV,1103)
 Q
 ;
16 ;
 D EDITVNDR(ACHSPROV,1110)
 Q
 ;
17 ;
 D EDITVNDR(ACHSPROV,1103.01)
 Q
 ;
 ;IHS/SET/JVK ACHS*3.1*6 ADDED GEOGRAPHICAL LOC 4/15/2003
18 ;
 D EDITVNDR(ACHSPROV,1125)
 Q
 ;
EDITVNDR(DA,DR) ;EP - Edit fields in VENDOR.
 N DIE
 S DIE="^AUTTVNDR("
 D ^DIE
 Q
 ;
