ACGSDIC ;IHS/OIRM/DSD/THL,AEF - FILEMAN INTERFACE UTILITY; [ 03/27/2000   2:22 PM ]
 ;;2.0t1;CONTRACT INFORMATION SYSTEM;;FEB 16, 2000
 ;;FILEMAN INTERFACE UTILITY WHICH IS THE ONLY PLACE WITHIN THE
 ;;PACKAGE FROM WHICH FILEMAN IS CALLED
DIC ;EP;
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 D ^DIC
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACGQUIT,ACGOUT)=""
 K DIC,DA,DD,DR,DINUM,D
 Q
MIX ;EP;
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 D MIX^DIC1
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACGQUIT,ACGOUT)=""
 K DIC,DA,DD,DR,DINUM,D
 Q
IX ;EP;
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 D IX^DIC
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACGQUIT,ACGOUT)=""
 K DIC,DA,DD,DR,DINUM,D
 Q
DIE ;EP;
 Q:'DA
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 L +@(DIE_DA_")"):4
 I '$T S ACGQUIT="" I $D(IOST),$E(IOST,1,2)="C-" W !!,"Entry being edited by another user.  Please try Later.",!
 Q:$D(ACGQUIT)
 S ACGDIEDA=DA
 I DIE="^ACGS(",DR'["1099///" S DR="1099////"_DT_";"_DR
 D ^DIE
 L -@(DIE_ACGDIEDA_")"):0
 I $D(DTOUT)!$D(DUOUT) S (ACGQUIT,ACGOUT)=""
 K DIE,DIC,DA,DR
 Q
FILE ;EP;
 K DD,DO,DTOUT,DUOUT,ACGQUIT,ACGOUT
 D FILE^DICN
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACGQUIT,ACGOUT)=""
 K DIC,DA,DD,DR,DINUM
 Q
DIR ;EP;
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 D ^DIR
 S ACGY=Y
 S:$D(DIRUT)!$D(DIROUT) ACGQUIT=""
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (ACGQUIT,ACGOUT)=""
 K DIR,DIRUT,DIROUT,DUOUT,DTOUT
 Q
DIK ;EP;
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 D ^DIK
 K DIK,DA
 Q
IX1 ;EP;
 K DTOUT,DUOUT,ACGQUIT,ACGOUT
 D IX1^DIK
 K DIK,DA
 Q
