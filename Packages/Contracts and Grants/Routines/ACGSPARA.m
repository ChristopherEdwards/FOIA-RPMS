ACGSPARA ;IHS/OIRM/DSD/THL,AEF - SITE PARAMETER EDIT; [ 03/27/2000  4:37 PM ]
 ;;2.0t1;CONTRACT INFORMATION SYSTEM;;FEB 16, 2000
 ;;ROUTINE TO EDIT SITE PARAMETERS
EN S ACGZ=""
 K ACGPODA
 D SITE Q:ACGPARA=""
 Q:$D(ACGQUIT)
 I '$D(^ACGPARA(ACGPODA,0)) S (X,DINUM)=ACGPODA,DIC="^ACGPARA(",DIC(0)="L" D FILE^ACGSDIC D EN1
 D HEAD^ACGSMENU
 W !!?30,"EDIT SITE PARAMETERS"
 I $D(^ACGPARA(ACGPODA,0)) S DIR(0)="YO",DIR("A")="Edit Site Parameters",DIR("B")="NO" W !! D DIR^ACGSDIC I $D(ACGQUIT)!(Y'=1) K ACGQUIT Q
EN1 ;EP;TO CHECK AND SET SITE PARAMETERS
 I '$D(ACGPARA) W !!,"Site Parameters must be set before you can proceed.",!,"Select the Contracting Office and System Configuration for your system.",!
 S DIE="^ACGPARA(",DA=ACGPODA,DR=".02T;1T"
 D DIE^ACGSDIC
 I '$D(^ACGPARA(ACGPODA,0)) G EN1
 F X=1,2,3 I '$P(^ACGPARA(ACGPODA,0),U,X) G EN1
 S ACGPARA=^ACGPARA(ACGPODA,0),ACG4=$P(ACGPARA,U,3),ACGY="MAIN MENU"
 Q
GEOLOC ;EP
 F  D G1 Q:$D(ACGQUIT)
 K ACGQUIT
 Q
G1 D HEAD^ACGSMENU
G11 ;EP
 S ACGX="GEOGRAPHICAL LOCATION" W !!?80-$L(ACGX)\2,ACGX K ACGX
 S DIC="^AUTTGL(",DIC(0)="AELMQZ",DIC("A")="Geographical Location: "
 W !!
 D DIC^ACGSDIC
 I +Y<1 S ACGQUIT="" Q
G2 ;EP
 S DA=+Y,DIE="^AUTTGL(",DR="[ACG GEOGRAPHICAL LOCATION]"
 D DIE^ACGSDIC
 Q
PERS ;EP
 F  D P1 Q:$D(ACGQUIT)
 K ACGQUIT
 Q
P1 D HEAD^ACGSMENU
 S ACGX="ADD/EDIT CONTRACT PERSONNEL" W !!?80-$L(ACGX)\2,ACGX K ACGX
 S DIC="^ACGCP(",DIC(0)="AELMQZ",DIC("A")="Contract personnel..: "
 W !!
 D DIC^ACGSDIC
 I +Y<1 S ACGQUIT=""
 Q
SITE ;EP;TO SELECT THE PROCUREMENT OFFICE
 N ACGY
 S DIC="^ACGPO("
 S DIC(0)="AEMQ"
 S DIC("A")="Which PROCUREMENT OFFICE: "
 D ^DIC
 I $G(Y)<1 S ACGQUIT=""
 Q:$D(ACGQUIT)
 S ACGPODA=+Y,ACGPARA=$G(^ACGPARA(+Y,0)),ACG4=$P(ACGPARA,U,3)
 I ACGPARA="" W !,"SITE PARAMETERS NOT SET FOR PROCUREMENT OFFICE ",$P(Y,U,2) Q
 Q
