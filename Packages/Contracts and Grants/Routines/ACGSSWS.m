ACGSSWS ;IHS/OIRM/DSD/THL,AEF - HANDICAPPED REPORT; [ 03/27/2000   2:22 PM ]
 ;;2.0t1;CONTRACT INFORMATION SYSTEM;;FEB 16, 2000
 ;;PRINT HANDICAPPED REPORT
EN D EN1
EXIT K ACGQUIT,ACG,ACG2,ACG4,ACG4X,ACG13,ACG26,ACG36,ACGBEGIN,ACGEND,ACG5,ACGBEG,ACGFY,ACGQT,ACGION,ACGX,ACGT
 Q
EN1 K ACGQUIT
 S:'$D(ACGPARA) ACGPARA=^ACGPARA(ACGPODA,0),ACG4=$P(ACGPARA,U,3)
 D QT^ACGSCPAR
 I $D(ACGQUIT) K ACGQUIT Q
 I ACG4=236 D CO^ACGSCPAR Q:$D(ACGQUIT)  I 1
 E  S (ACG4,ACG4X)=ACG4
ZIS S ZTRTN="EN2^ACGSSWS",ZTDESC="CIS QT HANDICAPPED REPORT",ZTSAVE("ACG*")=""
 D ^ACGSZIS
 Q:$D(ACGQUIT)
EN2 I '$D(ZTQUEUED) S (ACGIOP,IOP)=ION D ^%ZIS I POP S ACGQUIT="" Q
 U IO
 I ACG4X=88 F ACG4=102,121,161,235,239,241:1:249,284,285 K ACGQUIT D R1
 G:ACG4X=88 DONE
R1 G:$D(ACGQUIT) DONE
 D ENX
 W:$D(IOF) @IOF
 D:ACG4X'=88 DONE
 Q
DONE D DONE^ACGSZIS
 Q
ENX K ACG
 S ACGBEG=ACGBEGIN,(ACGT,ACGT2)=0
 F  S ACGBEG=$O(^ACGS("T",ACGBEG)) Q:'ACGBEG!(ACGBEG>ACGEND)  S ACG=0 F  S ACG=$O(^ACGS("T",ACGBEG,ACG)) Q:'ACG  D
 .S ACG13=$P(^ACGS(ACG,"DT"),U,13),ACG1=+^("DT"),ACG2=$P(^("DT"),U,2),ACG23=$E($P(^("DT1"),U,2),4,7),ACG26=$P(^("DT1"),U,5),ACG68=$S($D(^("DT3")):$P(^("DT3"),U,13),1:"")
 .Q:'ACG13!'ACG1
 .Q:ACG1=15!(ACG1=17)
 .Q:'$D(^AUTTTOB(ACG13,0))  S ACG13=$P(^(0),U)
 .Q:ACG13'="C5"
 .I ACG4X=88,ACG4'=$E(ACG2,1,3) Q
 .I ACG4'=99,ACG4'=236,ACG4'=$E(ACG2,1,3) Q
 .S ACG(ACG2)=ACG26_U_ACG23_U_ACG26_U_ACG68,ACGT=ACGT+ACG26,ACGT2=ACGT2+ACG68
 D EN3
 I $D(IOST),$E(IOST,1,2)="C-" D HOLD^ACGSMENU
 Q
EN3 S ACGX="MONTHLY SHELTERED WORKSHOPS REPORT"
 D RDATE^ACGSICR
 W !!?50,"NEXT HIGHER FOR-",!?22,"AWARD",?30,"HANDICAPPED",?50,"PROFIT SMALL",!?5,"CONTRACT NUMBER",?22,"DATE",?30,"CONTRACT AMT",?50,"BUSINESS BID",!?5,"---------------",?22,"----",?30,"----------------",?50,"---------------"
 S ACG=""
 F  S ACG=$O(ACG(ACG)) Q:ACG=""  S ACGX=ACG(ACG) D
 .W !?5,ACG,?22,$P(ACGX,U,2),?30,$J($FN($P(ACGX,U,3),"P,",0),15),?50,$J($FN($P(ACGX,U,4),"P,",0),15)
 W !?50,"---------------------",!?30,"TOTALS",?50,$J($FN(ACGT,"P,",0),15)
 Q
