ACGSZIS ;IHS/OIRM/DSD/THL,AEF - %ZIS AND ZTLOAD UTILITY; [ 03/27/2000   2:22 PM ]
 ;;2.0t1;CONTRACT INFORMATION SYSTEM;;FEB 16, 2000
 ;;UTILITY TO CONTROL ALL %ZIS AND %ZTLOAD CALLS FOR CIS
ZIS S %ZIS="NPMQ",ZIBH=$TR($H,",","")_$R(1000)
 W !
 K IOP,ACGQUIT
 D ^%ZIS
 G:POP DONE
 S:ION["HOST" %ZIS("IOPAR")=IOPAR,ZTSAVE("%IS*")=""
 I IO=IO(0),$D(IO("Q")) W !,"Cannot Queue to Screen or Slave Printer!",! K IO("Q") G ZIS
 S (ACGION,ACGIOP)=ION
 I ION["HOST" S ACGIOPAR=IOPAR S:$D(IO("HFSIO")) ACGIO("HFSIO")=IO("HFSIO")
 I $D(IO("Q")) D ZTLOAD S ACGQUIT=""
 Q
ZTLOAD F X="ACG*","DIOEND","FLDS","DIC","BY","FR","TO","DIS*" S ZTSAVE(X)=""
 S ZTIO=ION
 D ^%ZTLOAD
 W !,"REQUEST QUEUED!"
 K ZTSK
DONE ;EP;TO CLOSE DEVICE
 D:'$D(ZTQUEUED) ^%ZISC
 S ACGQUIT=""
 Q
