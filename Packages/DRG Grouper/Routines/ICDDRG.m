ICDDRG ;ALB/GRR/EG - ASSIGNS DRG CODES ; NOV 11 1988 [ 03/09/92  3:18 PM ]
 ;;8.0
TOP S (ICDDRG,ICDMDC,ICDRTC)=""
 ;*********************************************************
 ;FOLLOWING LINES CHECK FOR INVALID INPUT VARIABLES
 ;
 I '$D(ICDDX(1)) S ICDRTC=1 G ERR
 I ICDEXP'=0&(ICDEXP'=1)&(ICDEXP'="") S ICDRTC=5 G ERR
 I ICDTRS'=0&(ICDTRS'=1)&(ICDTRS'="") S ICDRTC=6 G ERR
 I ICDDMS'=0&(ICDDMS'=1)&(ICDDMS'="") S ICDRTC=7 G ERR
 I SEX'="M"&(SEX'="F")&(SEX'="") S ICDRTC=4 G ERR
 ;********************************************************
 ;FOLLOWING ESTABLISHES PRIMARY DIAGNOSIS RELATED VARIABLES
 ;
 D KILL S ICDSEX($S(SEX="M":1,SEX="F":2,1:0))=""
 I '$D(^ICD9(ICDDX(1),0)) S ICDRTC=1 G ERR
 S ICDY(0)=^ICD9(ICDDX(1),0) I $P(ICDY(0),"^",4)=1!($P(ICDY(0),"^",9)=1) S ICDRTC=1 G ERR
 S ICDMDC=$P(ICDY(0),"^",5),ICDPD=$P(ICDY(0),"^",2),ICDRG=0 I 'ICDMDC S ICDDRG=469,ICDRTC=1 G ERR
 I ICDMDC=12 S ICDMDC=$S(SEX="F":13,1:12) I SEX="" S ICDRTC=4 G ERR
 ; NEXT LINE, REMOVED SPACE AT END OF LINE        ;IHS/ANMC/MWR 03/09/92
 I $D(^ICD9(ICDDX(1),"DRG")) S ICDPDRG=^("DRG") F ICDI=1:1 Q:$P(ICDPDRG,"^",ICDI)']""  S ICDPDRG($P(ICDPDRG,"^",ICDI))="",ICDRG($P(ICDPDRG,"^",ICDI))=""
 S ICD104=0,ICDP24=$P(ICDY(0),"^",12),ICDP25=$P(ICDY(0),"^",13) D SEX9
 ;********************************************************
 ;FOLLOWING ESTABLISHES SECONDARY DIAGNOSIS VARIABLES
 ;
 S (ICDCCT,ICDSD)="",ICDCC=0,ICDI=1
 ; NEXT LINE, REMOVED SPACE AT END OF LINE        ;IHS/ANMC/MWR 03/09/92
 F ICDIZ=0:0 S ICDI=$O(ICDDX(ICDI)) Q:ICDI'>0  X "I '$D(^ICD9(ICDDX(ICDI),0)) S ICDRTC=8 Q" I ICDRTC="" S ICDY(0)=^ICD9(ICDDX(ICDI),0),ICDDXT($P(ICDY(0),"^",1))="",ICDP15($S($P(ICDY(0),"^",2)["J":1,1:0))="" D SEC,SEX9 G:ICDRTC]"" ERR
 S:$D(ICDCCT(1)) ICDCC=1 K ICDCCT
 ;********************************************************
 ;FOLLOWING ESTABLISHES OPERATION/PROCEDURE VARIABLES
 ;
 S (ICDORNI,ICDOP,ICDOR)="",(ICDOCNT,ICDONR,ICDORNR,ICDNOR,ICDOPCT)=0
 I $D(ICDPRC) F ICDI=1:1 Q:'$D(ICDPRC(ICDI))  X "I '$D(^ICD0(ICDPRC(ICDI),0)) S ICDRTC=2 Q" I ICDRTC="" S ICDY(0)=^(0),ICDNOR=ICDNOR+1,ICDY=ICDPRC(ICDI),ICDO24($S($P(ICDY(0),"^",3)'="":$P(ICDY(0),"^",3),1:"N"))="" D OPSTUF,SEX9
 K ICDO24("N") G:ICDRTC]"" ERR
 G ^ICDDRG0
SEC S ICDCC=$S($D(^ICD9("ACC",ICDDX(ICDI),ICDDX(1))):0,$P(ICDY(0),"^",7)=1:1,1:ICDCC),ICDCCT(ICDCC)=""
 S ICDSD=ICDSD_$P(ICDY(0),"^",2)
 I (($P(ICDY(0),"^",7)=1)!(ICDPD["h")!(ICDPD["J")),$D(^ICD9(ICDDX(ICDI),"DRG")) S ICDSDRG=^("DRG") F ICDK=1:1 Q:$P(ICDSDRG,"^",ICDK)']""  S ICDSDRG($P(ICDSDRG,"^",ICDK))=""
 S ICDS24($S($P(ICDY(0),"^",12)'="":$P(ICDY(0),"^",12),1:"N"))="",ICDS25($S($P(ICDY(0),"^",13)'="":$P(ICDY(0),"^",13),1:0))=""
 K ICDS24("N"),ICDS25(0) Q
OPSTUF I '$D(ICDOP(" "_$P(ICDY(0),"^",1))) S ICDOP(" "_$P(ICDY(0),"^",1))="",ICDOCNT=ICDOCNT+1
 I '$D(^ICD0(ICDY,"MDC",ICDMDC,0)) S ICDONR=ICDONR+1,ICDORNI=ICDORNI_$P(ICDY(0),"^",2),ICDORNI($S($P(ICDY(0),"^",2)'="":$P(ICDY(0),"^",2),1:0))="" S:ICDORNR'=0 ICDORNR=1
 S ICDOR=ICDOR_$P(ICDY(0),"^",2) I $P(ICDY(0),"^",2)["O" S ICDOPCT=ICDOPCT+1
 I $D(^ICD0(ICDY,"MDC",ICDMDC,"DRG")) S ICDF=^("DRG") F ICDFX=1:1:6 Q:$P(ICDF,"^",ICDFX)']""  S ICDODRG($P(ICDF,"^",ICDFX))=$P(ICDF,"^",ICDFX)
 S ICD104=$S($P(ICDY(0),"^",2)["P":1,1:0),ICDNMDC($S($P(ICDY(0),"^",2)["l":1,$P(ICDY(0),"^",2)["B":1,$P(ICDY(0),"^",2)["t":1,1:0))="" Q
ERR S ICDDRG=470
SEX9 ;get sex for dx or proc
 S ICDSEX($S($P(ICDY(0),"^",10)="M":1,$P(ICDY(0),"^",10)="F":2,1:0))=""
 Q
KILL ;EP                                             ;IHS/ANMC/MWR 03/09/92 
 K ICD104,ICDJ,ICDJJ,ICDOCNT,ICDOR,ICDNOR,ICDP15,ICDPDRG,ICDRG,ICDSEX,ICDSDRG,ICDODRG,ICDCC,ICDOP,ICDORNR,ICDORNI,ICDP24,ICDP25,ICDPD,ICDSD,ICDI,ICDK,ICDF,ICDFX,ICDFK,ICDY,ICDDXT,ICDIZ,ICDONR,ICDOPCT Q
 Q
