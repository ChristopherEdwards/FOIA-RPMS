ICDDRGX ;ALB/EG - GROUPER PROCESS ; MAR 7 1991 [ 03/09/92  3:23 PM ]
 ;;8.0
CKHIV ;EP-MDC25 grouping                               ;IHS/ANMC/MWR 03/09/92
 Q:ICDP25=""  S ICDGH=$S("488^489^490"[ICDRG:1,1:0),ICDORNI=$S(ICDOCNT>0:ICDORNI,1:0),ICDORNA=$F(ICDORNI,"O",$F(ICDORNI,"O"))
 S ICDRG=$S(ICDP25=1&(ICDORNA>0):488,1:ICDRG) I 'ICDGH&(ICDRG=488) Q
 S:(ICDOCNT>0) ICDRG=$S(ICDP25>1&(ICDORNA>0)&($D(ICDS25(1))):488,1:ICDRG) I 'ICDGH&(ICDRG=488) Q
 S ICDRG=$S(ICDP25=1&('$D(ICDS25))&('$D(^ICD9(ICDDX(1),"R"))):490,1:ICDRG) I 'ICDGH&(ICDRG=490) Q
 S ICDRG=$S(ICDP25=1&($D(ICDS25(2))):489,ICDP25=1&($D(ICDS25(3))):490,1:ICDRG) I 'ICDGH&((ICDRG=489)!(ICDRG=490)) Q
 S ICDRG=$S(ICDP25=2&($D(ICDS25(1))):489,ICDP25=3&($D(ICDS25(1))):490,1:ICDRG) I 'ICDGH&((ICDRG=489)!(ICDRG=490)) Q
 S ICDRG=$S((ICDP25&(ICDOCNT=0)&('$D(ICDS25))):490,1:ICDRG) I 'ICDGH&(ICDRG=490) Q
 S:(ICDRG=488)!(ICDRG=489)!(ICDRG=490) ICDRTC=0
 K ICDGH,ICDP25,ICDS25,ICDORNA Q
CKMST ;EP-MDC24 grouping                               ;IHS/ANMC/MWR 03/09/92
 S ICDAJ=0 F ICDS24K=1:1 S ICDAJ=$O(ICDS24(ICDAJ)) Q:ICDAJ=""
 S ICDS24K=ICDS24K-1,ICDS24L=0 F ICDI=1:1:8 S:$D(ICDS24(ICDI))&(ICDI'=ICDP24) ICDS24L=$S($D(ICDS24(ICDI)):1,1:0)
 G:((ICDP24=0)&(ICDS24K<2))!((ICDP24>0)&('ICDS24L)) CKMSTE
 S ICDRG=$S(ICDP24=0&(ICDS24K>1)&($D(ICDO24(1))):484,ICDP24>0&($D(ICDO24(1)))&(ICDS24L):484,1:ICDRG) I ICDRG=484 D CKMSTE Q
 S:ICDRG'=484 ICDRG=$S(ICDP24=0&(ICDS24K>1)&($D(ICDO24(2))):485,ICDP24>0&($D(ICDO24(2)))&(ICDS24L):485,1:ICDRG) I ICDRG=485 D CKMSTE Q
 S:ICDRG'=485 ICDRG=$S(ICDP24=0&(ICDS24K>1)&($D(ICDO24(3))):486,ICDP24>0&($D(ICDO24(3)))&(ICDS24L):486,1:ICDRG) I ICDRG=486 D CKMSTE Q
 S ICDRG=$S(ICDP24=0&(ICDS24K>1):487,ICDP24>0&ICDS24L:487,1:ICDRG)
 S:(ICDRG>483)&(ICDRG<488) ICDRTC=0
CKMSTE K ICDAJ,ICDP24,ICDS24,ICDO24,ICDS24K,ICDO24,ICDS24L
 Q
CKNMDC ;EP-non MDC drg's                                ;IHS/ANMC/MWR 03/09/92
 S:(ICDRG>479)&(ICDRG<484) ICDRG=470
 S ICDRG=$S(ICDOR["l":480,1:ICDRG) I ICDRG=480 S ICDRTC=0 Q
 S ICDRG=$S(ICDOR["B":481,1:ICDRG) I ICDRG=481 S ICDRTC=0 Q
 S ICDRG=$S($D(ICDOP(" 30.3"))!$D(ICDOP(" 30.4")):482,1:ICDRG) I ICDRG=482 S ICDRTC=0 Q
 S ICDRG=$S(ICDOR["t"&($P(^ICD9(ICDDX(1),0),"^",2)["Y"):482,1:ICDRG) I ICDRG=482 S ICDRTC=0 Q
 S ICDRG=$S(ICDOR["t"&($P(^ICD9(ICDDX(1),0),"^",2)'["Y"):483,1:ICDRG) I ICDRG=483 S ICDRTC=0 Q
 Q
