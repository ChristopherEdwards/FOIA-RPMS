BDMD110 ; IHS/CMI/LAB -IHS -GETS DATA FOR DIABETES QA REPORT ;
 ;;2.0;DIABETES MANAGEMENT SYSTEM;**4**;JUN 14, 2007
 ;
 ;
EN ; - ENTRY POINT - from ^BDMASK
 K ^BDMDATA("BDMEPI",$J)
 S ^XTMP("BDMDM11",0)=$$FMADD^XLFDT(DT,14)_"^"_DT_"^DM AUDIT 2011"
 S BDMEPIN=0
 S BDMPD=0 F  S BDMPD=$O(^XTMP("BDMDM11",BDMJOB,BDMBTH,"PATS",BDMPD)) Q:'BDMPD  D
 .I BDMTYPE'="P" Q:$$DEMO^BDMUTL(BDMPD,$G(BDMDEMO))
 .I BDMPREP=2 D EPIREC Q
 .D GATHER
 I BDMPREP=2 D WRITEF^BDMD11 Q
 I BDMPREP=3!(BDMPREP=4) D CUML^BDMD115
 Q
S(P,I,V) ;
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",P,I)=V
 Q
REC(DFN,BDMRTYP,BDMRBD,BDMRED,BDMED,BDMDMRG,BDMBDAT) ;EP - called to send back a visit record as
 NEW BDMX,BDMREC
 S BDMREC=""
 S BDMRTYP("IEN")=$O(^BDMRECD("B",BDMRTYP,0))
 I 'BDMRTYP("IEN") Q BDMREC
PROC ;
 S BDMEPIX=0
 F  S BDMEPIX=$O(^BDMRECD(BDMRTYP("IEN"),11,"AC",BDMEPIX)) Q:BDMEPIX'=+BDMEPIX!(BDMREC=-1)  S BDMTTT=$O(^BDMRECD(BDMRTYP("IEN"),11,"AC",BDMEPIX,0))  D
 .S X="" X:$D(^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,11)) ^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,11)
 .I X="" S X=" "
 .S $P(BDMREC,U,$P(^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,0),U,2))=X  ;W !,BDMTTT,?5,$P(^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,0),U),?40,X 
 ;W !!,BDMREC
 Q BDMREC
EPIREC ;create epi info record in ^BDMDATA("BDMEPI",$J,n)
 ;skip this patient if dodx is greater than the audit date
 S X=$$DODX^BDMD116(BDMPD,BDMDMRG,"I")
 I X>BDMADAT Q
 S BDMEPIR="",BDMTHER=""
 S BDMEPIR=$$REC(BDMPD,"DM AUDIT 2011 EXPORT RECORD",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 ;S BDMTHER="",BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2011 EPI REC 2",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 ;S BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2011 EPI REC 3",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 ;S BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2011 EPI REC 4",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 Q
GATHER ;gather data for 1 patient
 S BDMER=0
HEADER ; Set node with report header info
 ;set report dates
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,1)=$S($G(BDMFISC)]"":BDMFISC,1:BDMRBD_" - "_BDMRED)
 ;set audit date to DT
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,2)=$$FMTE^XLFDT(DT)
 ;reviewer
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,14)=$P(^VA(200,DUZ,0),U,2)  ;reviewer initials
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,122)=$$COMM(BDMPD)
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,120)=$$TRIBE(BDMPD)  ;tribal enrollment code
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,121)=$S($P($G(^DPT(BDMPD,.11)),U,5):$P($G(^DIC(5,$P(^DPT(BDMPD,.11),U,5),0)),U,2),1:"")  ;state of residence
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,15)=$$VAL^XBDIQ1(9000001,BDMPD,.14)  ;primary care provider
DEMO ;pat demographics
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,16)=$$HRN^AUPNPAT(BDMPD,$S($G(BDMDUZ2):BDMDUZ2,1:DUZ(2)))  ;hrn
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,18)=$$DOB^AUPNPAT(BDMPD,"E")  ;dob
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,20)=$$VAL^XBDIQ1(2,BDMPD,.02)  ;sex
DXDT ;dates of and dm dxs
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,22)=$S(BDMDMRG:$$CMSFDX^BDMD113(BDMPD,BDMDMRG,"D"),1:"")
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)=$S(BDMDMRG:$$CMSFDX^BDMD113(BDMPD,BDMDMRG,"DX"),1:"")
 S ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,23)=$$PLDMDOO^BDMD113(BDMPD)
 D S(BDMPD,25,$$PLDMDXS^BDMD113(BDMPD))
 D S(BDMPD,21,$$FRSTDMDX^BDMD113(BDMPD))
 D S(BDMPD,26,$$LASTDMDX^BDMD113(BDMPD,BDMRED))
 I $$DODX^BDMD116(BDMPD,BDMDMRG,"I")>BDMADAT D S(BDMPD,26.5,"*** Patient's Date of Onset is after the audit date.") D
 .D S(BDMPD,26.6,"**** This patient will not be included in the cumulative audit.")
 S BDMTYDM="" D TYPEDM,S(BDMPD,29,BDMTYDM)
 D S(BDMPD,27,$$TOBACCO^BDMD11T(BDMPD,$$DOB^AUPNPAT(BDMPD),BDMADAT))
 D S(BDMPD,28,$$CESS^BDMD111(BDMPD,BDMBDAT,BDMADAT))
 ;
VITAL ;
 D S(BDMPD,30,$$LASTHT^BDMD113(BDMPD,BDMRED))
 D S(BDMPD,32,$$LASTWT^BDMD113(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,112,$$BMI^BDMD118(BDMPD,BDMRBD,BDMRED))
 ;htn dx
 D S(BDMPD,34,$$HTNDX^BDMD113(BDMPD,BDMRED))
 ;last 3 BPs
 D S(BDMPD,36,$$BPS^BDMD113(BDMPD,BDMRBD,BDMRED))
EXAMS ;
 D S(BDMPD,38,$$DFE^BDMD117(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,40,$$EYE^BDMD117(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,42,$$DENTAL^BDMD117(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,44,$$DIETEDUC^BDMD117(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,46,$$EXEDUC^BDMD117(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,48,$$OTHEDUC^BDMD117(BDMPD,BDMRBD,BDMRED))
MH ;
 D S(BDMPD,200,$$DEPDX^BDMD112(BDMPD,BDMBDAT,BDMADAT))
 S BDMDEP=$E($G(^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,200)))
 D S(BDMPD,210,$S(BDMDEP="1":"",1:$$DEPSCR^BDMD112(BDMPD,BDMBDAT,BDMADAT)))
THERAPY ;
 S BDM6MBD=$$FMADD^XLFDT(BDMADAT,-(6*31)),BDM6MBD=$$FMTE^XLFDT(BDM6MBD)
 D S(BDMPD,52,$$INSULIN^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,53,$$SULF^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,98,$$SULFLIKE^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,54,$$MET^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,55,$$ACAR^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,56,$$TROG^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,58,$$INCR^BDMD112(BDMPD,BDM6MBD,BDMRED))  ;cmi/maw 12/18/2007 DM2011
 D S(BDMPD,59,$$DPP4^BDMD112(BDMPD,BDM6MBD,BDMRED))  ;cmi/maw 12/18/2011 DM2011
 D S(BDMPD,99,$$AMYLIN^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,100,$$GLP1^BDMD112(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,101,$$BROM^BDMD112(BDMPD,BDM6MBD,BDMRED))
 S Z="" F X=52:1:56,58,59,98,99,100,101 I $E($G(^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,X)))="X" S Z=1  ;ANY MED AT ALL?
 S Y=$$REFMED^BDMD112(BDMPD,BDMRBD,BDMRED) S Y=$S(Z:"",Y="":"",1:"X"_U_$P(Y,U,2)) D S(BDMPD,57,Y)  ;REFUSAL OF A MED
 S Y="" F X=52:1:59,98,99,100,101 I $E($G(^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,X)))="X" S Y=1  ;cmi/maw 6/17/2011 per gary
 D S(BDMPD,51,$S(Y:"",1:"X"))
 D S(BDMPD,60,$$ACE^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 ;
IMM ;
 D S(BDMPD,62,$$ASPIRIN^BDMD116(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,300,$$STATIN^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 D S(BDMPD,301,$$FIBRATE^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 D S(BDMPD,302,$$NIACIN^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 D S(BDMPD,303,$$BILE^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 D S(BDMPD,304,$$EZET^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 D S(BDMPD,305,$$FISHOIL^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 D S(BDMPD,306,$$LOVAZA^BDMD116(BDMPD,BDM6MBD,BDMADAT))
 S Z=0 F X=300:1:306 I $E($G(^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,X)))="X" S Z=1  ;has one so skip refusal and none
 I Z G PPD
 S Y=$$REFMEDL^BDMD116(BDMPD,BDMBDAT,BDMRED) S Y=$S(Y="":"X",1:"X"_U_$P(Y,U,2)) D S(BDMPD,307,Y)
PPD ;
 D S(BDMPD,70,$$PPD^BDMD118(BDMPD,BDMADAT))
 D S(BDMPD,114,$$LASTNP^BDMD118(BDMPD,BDMADAT))
 D S(BDMPD,72,$$TBTX^BDMD112(BDMPD))
 D S(BDMPD,76,$$EKG^BDMD112(BDMPD,BDMADAT))
FLU ;
 D S(BDMPD,64,$$FLU^BDMD113(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,66,$$PNEU^BDMD113(BDMPD,BDMADAT))
 D S(BDMPD,68,$$TD^BDMD11B(BDMPD,BDMADAT))
LABS ;
 D S(BDMPD,78,$$HGBA1C^BDMD118(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,79,$$GFR^BDMD11C(BDMPD,BDMBDAT,BDMADAT))  ;cmi/maw 12/17/2007 DM2011
 D S(BDMPD,84,$$CREAT^BDMD118(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,86,$$CHOL^BDMD118(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,88,$$LDL^BDMD118(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,89,$$HDL^BDMD118(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,90,$$TRIG^BDMD118(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,92,$$URIN^BDMD118(BDMPD,BDMBDAT,BDMADAT))  ;cmi/maw 12/17/2007 DM2011
 ;
 Q
TYPEDM ;return type of DM in BDMTYDM
 NEW X
 I ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)="NIDDM" S BDMTYDM="2  Type 2" Q
 I ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)="TYPE II" S BDMTYDM="2  Type 2" Q
 I ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)[2 S BDMTYDM="2  Type 2" Q
 I ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)="IDDM" S BDMTYDM="1  Type 1" Q
 I ^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)[1 S BDMTYDM="1  Type 1" Q
 S X=^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,25) ;get problem list dxs
 F I=1:1 S C=$P(X,";",I) Q:C=""!(BDMTYDM]"")  I $E(C,6)=0!($E(C,6)=2) S BDMTYDM="2  Type 2"
 Q:BDMTYDM]""
 F I=1:1 S C=$P(X,";",I) Q:C=""!(BDMTYDM]"")  I $E(C,6)=1!($E(C,6)=3) S BDMTYDM="1  Type 1"
 Q:BDMTYDM]""
 S X=^XTMP("BDMDM11",BDMJOB,BDMBTH,"AUDIT",BDMPD,26) ;get pov list dxs
 I X[2 S BDMTYDM="2  Type 2" Q
 I X[1 S BDMTYDM="1  Type 1" Q
 Q
DATE(D) ;EP
 I $G(D)="" Q ""
 Q $E(D,4,5)_"/"_$E(D,6,7)_"/"_(1700+($E(D,1,3)))
TRIBE(P) ;EP
 I '$G(P) Q ""
 I '$D(^AUPNPAT(P,11)) Q ""
 Q $$TRIBE^AUPNPAT(P,"C")_"^"_$$TRIBE^AUPNPAT(P,"E")
COMM(P) ;EP
 I '$G(P) Q ""
 I '$D(^AUPNPAT(P,11)) Q ""
 Q $$COMMRES^AUPNPAT(P,"E")
EAUDIT ;EP
 K ^BDMDATA("BDMEPI",$J)
 S ^XTMP("BDMDM11",0)=$$FMADD^XLFDT(DT,14)_"^"_DT_"^DM AUDIT 2011"
 S BDMEPIN=0
 S BDMBEGD=$$FMADD^XLFDT(BDMADAT,-365)
 S BDM3YE=$$FMADD^XLFDT(BDMADAT,-1096)
 I BDMACTI=1 D
 .S BDMEAUT=0 F  S BDMEAUT=$O(^ACM(41,"B",BDMDMRG,BDMEAUT)) Q:BDMEAUT'=+BDMEAUT  D
 ..I $P($G(^ACM(41,BDMEAUT,"DT")),U,1)'="A" Q
 ..S BDMPD=$P(^ACM(41,BDMEAUT,0),U,2)
 ..Q:$$DEMO^BDMUTL(BDMPD,$G(BDMDEMO))
 ..I BDMPD=1717 S ^LORIEAUD(1717)="I made it passed demo with "_$G(BDMDEMO)
 ..S BDMX=$$ACTDMPT^BDMD11G(BDMPD,BDMBEGD,BDMADAT,BDMTAXI,BDMBEN,BDM3YE)
 ..I 'BDMX Q  ;not active diabetic
 ..I BDMPREP=2 D EPIREC Q
 ..D GATHER
 .Q
 I BDMACTI=0 S BDMPD=0 F  S BDMPD=$O(^AUPNPAT(BDMPD)) Q:'BDMPD  D
 .S ^LORIEAUD(1)=BDMBEGD_U_BDMADAT_U_BDMTAXI_U_BDMBEN_U_BDM3YE
 .Q:$$DEMO^BDMUTL(BDMPD,$G(BDMDEMO))
 .S BDMX=$$ACTDMPT^BDMD11G(BDMPD,BDMBEGD,BDMADAT,BDMTAXI,BDMBEN,BDM3YE)
 .I 'BDMX Q  ;not active diabetic
 .I BDMACTI,'$$ACTONREG(BDMPD,BDMDMRG) Q
 .I BDMPREP=2 D EPIREC Q
 .D GATHER
 I BDMPREP=2 D WRITEF^BDMD11 Q
 I BDMPREP=3!(BDMPREP=4) D CUML^BDMD115
 Q
ACTONREG(P,R) ;
 I $G(R)="" Q ""
 I $G(P)="" Q ""
 I '$D(^ACM(41,"AC",P,R)) Q 0
 S X=^ACM(41,"AC",P,R)
 I $P($G(^ACM(41,X,"DT")),U,1)'="A" Q 0
 Q 1
 ;
LOCN(P,R) ;EP
 I $G(R)="" Q ""
 I $G(P)="" Q ""
 I '$D(^ACM(41,"AC",P,R)) Q 0
 S X=^ACM(41,"AC",P,R)
 I 'X Q ""
 Q $$VAL^XBDIQ1(9002241,X,1101)
 ;
LOCT(P,R) ;EP
 I $G(R)="" Q ""
 I $G(P)="" Q ""
 I '$D(^ACM(41,"AC",P,R)) Q 0
 S X=^ACM(41,"AC",P,R)
 I 'X Q ""
 Q $$VAL^XBDIQ1(9002241,X,1102)
 ;
