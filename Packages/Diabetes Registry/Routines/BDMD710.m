BDMD710 ; IHS/CMI/LAB -IHS -GETS DATA FOR DIABETES QA REPORT ;
 ;;2.0;DIABETES MANAGEMENT SYSTEM;**2**;JUN 14, 2007
 ;
 ;
EN ; - ENTRY POINT - from ^BDMASK
 K ^BDMDATA("BDMEPI",$J)
 S ^XTMP("BDMDM71",0)=$$FMADD^XLFDT(DT,14)_"^"_DT_"^DM AUDIT 2007"
 S BDMEPIN=0
 S BDMPD=0 F  S BDMPD=$O(^XTMP("BDMDM71",BDMJOB,BDMBTH,"PATS",BDMPD)) Q:'BDMPD  D
 .I BDMTYPE'="P",BDMTYPE'="S" Q:$$DEMO^BDMUTL(BDMPD,$G(BDMDEMO))
 .I BDMPREP=2 D EPIREC Q
 .D GATHER
 I BDMPREP=2 D WRITEF^BDMD71 Q
 I BDMPREP=3!(BDMPREP=4) D CUML^BDMD715
 Q
S(P,I,V) ;
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",P,I)=V
 Q
REC(DFN,BDMRTYP,BDMRBD,BDMRED,BDMED,BDMDMRG,BDMBDAT) ;EP - called to send back a visit record as
 NEW BDMX,BDMREC
 S BDMREC=""
 S BDMRTYP("IEN")=$O(^BDMRECD("B",BDMRTYP,0))
 I 'BDMRTYP("IEN") Q BDMREC
PROC ;
 S BDMEPIX=0
 F  S BDMEPIX=$O(^BDMRECD(BDMRTYP("IEN"),11,"AC",BDMEPIX)) Q:BDMEPIX'=+BDMEPIX!(BDMREC=-1)  S BDMTTT=$O(^BDMRECD(BDMRTYP("IEN"),11,"AC",BDMEPIX,0))  D
 .S X="" X:$D(^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,11)) ^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,11)
 .S $E(BDMREC,$P(^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,0),U,2))=X   ;W !,BDMTTT,?5,$P(^BDMRECD(BDMRTYP("IEN"),11,BDMTTT,0),U),?40,X
 Q BDMREC
EPIREC ;create epi info record in ^BDMDATA("BDMEPI",$J,n)
 S BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2007 EPI REC 1",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 S BDMTHER="",BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2007 EPI REC 2",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 S BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2007 EPI REC 3",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 ;S BDMEPIR="",BDMEPIR=$$REC(BDMPD,"DM AUDIT 2007 EPI REC 4",BDMRBD,BDMRED,BDMADAT,BDMDMRG,BDMBDAT),BDMEPIN=BDMEPIN+1,^BDMDATA("BDMEPI",$J,BDMEPIN)=BDMEPIR
 Q
GATHER ;gather data for 1 patient
 S BDMER=0
HEADER ; Set node with report header info
 ;set report dates
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,1)=$S($G(BDMFISC)]"":BDMFISC,1:BDMRBD_" - "_BDMRED)
 ;set audit date to DT
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,2)=$$FMTE^XLFDT(DT)
 ;set area, su, facility code and name
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,4)=$P(^DIC(4,DUZ(2),0),U)
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,6)=$E($P(^AUTTLOC(DUZ(2),0),U,10),1,2)
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,8)=$E($P(^AUTTLOC(DUZ(2),0),U,10),3,4)
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,10)=$E($P(^AUTTLOC(DUZ(2),0),U,10),5,6)
 ;# pats in register
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,12)=$S(BDMDMRG:$$RSTAT^BDMDM6(BDMDMRG,"A"),1:"")
 ;reviewer
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,14)=$P(^VA(200,DUZ,0),U,2)
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,15)=$$VAL^XBDIQ1(9000001,BDMPD,.14)
DEMO ;pat demographics
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,16)=$$HRN^AUPNPAT(BDMPD,DUZ(2))
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,18)=$$DOB^AUPNPAT(BDMPD,"E")
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,20)=$$VAL^XBDIQ1(2,BDMPD,.02)
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,120)=$$TRIBE(BDMPD)
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,121)=$$COMM(BDMPD)
DXDT ;dates of and dm dxs
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,22)=$S(BDMDMRG:$$CMSFDX^BDMD713(BDMPD,BDMDMRG,"D"),1:"")
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)=$S(BDMDMRG:$$CMSFDX^BDMD713(BDMPD,BDMDMRG,"DX"),1:"")
 S ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,23)=$$PLDMDOO^BDMD713(BDMPD)
 D S(BDMPD,25,$$PLDMDXS^BDMD713(BDMPD))
 D S(BDMPD,21,$$FRSTDMDX^BDMD713(BDMPD))
 D S(BDMPD,26,$$LASTDMDX^BDMD713(BDMPD,BDMRED))
 D S(BDMPD,27,$$TOBACCO^BDMD716(BDMPD,BDMRED))
 S BDMTYDM="" D TYPEDM,S(BDMPD,29,BDMTYDM)
 D S(BDMPD,28,$$CESS^BDMD711(BDMPD,BDMRBD,BDMRED))
VITAL ;
 D S(BDMPD,30,$$LASTHT^BDMD713(BDMPD,BDMRED))
 D S(BDMPD,32,$$LASTWT^BDMD713(BDMPD,BDMRED))
 ;htn dx
 D S(BDMPD,34,$$HTNDX^BDMD713(BDMPD,BDMRED))
 ;last 3 BPs
 D S(BDMPD,36,$$BPS^BDMD713(BDMPD,BDMRBD,BDMRED))
EXAMS ;
 D S(BDMPD,38,$$DFE^BDMD717(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,40,$$EYE^BDMD717(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,42,$$DENTAL^BDMD717(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,44,$$DIETEDUC^BDMD717(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,46,$$EXEDUC^BDMD717(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,48,$$OTHEDUC^BDMD717(BDMPD,BDMRBD,BDMRED))
THERAPY ;
 S BDM6MBD=$$FMADD^XLFDT(BDMADAT,-(6*31)),BDM6MBD=$$FMTE^XLFDT(BDM6MBD)
 D S(BDMPD,52,$$INSULIN^BDMD712(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,53,$$SULF^BDMD712(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,54,$$MET^BDMD712(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,55,$$ACAR^BDMD712(BDMPD,BDM6MBD,BDMRED))
 D S(BDMPD,56,$$TROG^BDMD712(BDMPD,BDM6MBD,BDMRED))
 S Y=$$REFMED^BDMD712(BDMPD,BDMRBD,BDMRED) S Y=$S(Y="":"",1:"X"_U_$P(Y,U,2)) D S(BDMPD,57,Y)
 S Y=0 F X=52:1:57 I $E(^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,X))="X" S Y=1
 D S(BDMPD,51,$S(Y:"",1:"X"))
 D S(BDMPD,60,$$ACE^BDMD716(BDMPD,BDM6MBD,BDMRED))
IMM ;
 D S(BDMPD,62,$$ASPIRIN^BDMD716(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,61,$$LIPID^BDMD716(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,64,$$FLU^BDMD713(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,66,$$PNEU^BDMD713(BDMPD,BDMRED))
 D S(BDMPD,68,$$TD^BDMD71B(BDMPD,BDMRED))
PPD ;
 D S(BDMPD,70,$$PPD^BDMD718(BDMPD,BDMRED))
 D S(BDMPD,114,$$LASTNP^BDMD718(BDMPD,BDMRED))
 D S(BDMPD,72,$$TBTX^BDMD712(BDMPD))
 D S(BDMPD,76,$$EKG^BDMD712(BDMPD,BDMRED))
LABS ;
 D S(BDMPD,78,$$HGBA1C^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 ;D S(BDMPD,82,$$BS^BDMD718(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,84,$$CREAT^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,86,$$CHOL^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,88,$$LDL^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,89,$$HDL^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,90,$$TRIG^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,92,$$URIN^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,94,$$PROTEIN^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 D S(BDMPD,96,$$MICRO^BDMD718(BDMPD,BDMBDAT,BDMADAT))
 ;D S(BDMPD,98,$$SELF^BDMD716(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,112,$$BMI^BDMD718(BDMPD,BDMRBD,BDMRED))
 D S(BDMPD,200,$$DEPDX^BDMD712(BDMPD,BDMBDAT,BDMADAT))
 S BDMDEP=$E($G(^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,200)))
 D S(BDMPD,210,$S(BDMDEP="Y":"",1:$$DEPSCR^BDMD712(BDMPD,BDMBDAT,BDMADAT)))
 ;
 Q
TYPEDM ;return type of DM in BDMTYDM
 NEW X
 I ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)="NIDDM" S BDMTYDM="2  Type 2" Q
 I ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)="TYPE II" S BDMTYDM="2  Type 2" Q
 I ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)[2 S BDMTYDM="2  Type 2" Q
 I ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)="IDDM" S BDMTYDM="1  Type 1" Q
 I ^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,24)[1 S BDMTYDM="1  Type 1" Q
 S X=^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,25) ;get problem list dxs
 F I=1:1 S C=$P(X,";",I) Q:C=""!(BDMTYDM]"")  I $E(C,6)=0!($E(C,6)=2) S BDMTYDM="2  Type 2"
 Q:BDMTYDM]""
 F I=1:1 S C=$P(X,";",I) Q:C=""!(BDMTYDM]"")  I $E(C,6)=1!($E(C,6)=3) S BDMTYDM="1  Type 1"
 Q:BDMTYDM]""
 S X=^XTMP("BDMDM71",BDMJOB,BDMBTH,"AUDIT",BDMPD,26) ;get pov list dxs
 I X[2 S BDMTYDM="2  Type 2" Q
 I X[1 S BDMTYDM="1  Type 1" Q
 Q
DATE(D) ;EP
 I $G(D)="" Q ""
 Q $E(D,4,5)_"/"_$E(D,6,7)_"/"_(1700+($E(D,1,3)))
TRIBE(P) ;EP
 I '$G(P) Q ""
 I '$D(^AUPNPAT(P,11)) Q ""
 Q $$TRIBE^AUPNPAT(P,"C")_"^"_$$TRIBE^AUPNPAT(P,"E")
COMM(P) ;EP
 I '$G(P) Q ""
 I '$D(^AUPNPAT(P,11)) Q ""
 Q $$COMMRES^AUPNPAT(P,"C")_"^"_$$COMMRES^AUPNPAT(P,"E")
EAUDIT ;EP
 K ^BDMDATA("BDMEPI",$J)
 S ^XTMP("BDMDM71",0)=$$FMADD^XLFDT(DT,14)_"^"_DT_"^DM AUDIT 2007"
 S BDMEPIN=0
 S BDMBEGD=$$FMADD^XLFDT(BDMADAT,-365)
 S BDM3YE=$$FMADD^XLFDT(BDMADAT,-1096)
 I BDMACTI=1 D
 .S BDMEAUT=0 F  S BDMEAUT=$O(^ACM(41,"B",BDMDMRG,BDMEAUT)) Q:BDMEAUT'=+BDMEAUT  D
 ..I $P($G(^ACM(41,BDMEAUT,"DT")),U,1)'="A" Q
 ..S BDMPD=$P(^ACM(41,BDMEAUT,0),U,2)
 ..Q:$$DEMO^BDMUTL(BDMPD,$G(BDMDEMO))
 ..S BDMX=$$ACTDMPT^BDMD71G(BDMPD,BDMBEGD,BDMADAT,BDMTAXI,BDMBEN,BDM3YE)
 ..I 'BDMX Q  ;not active diabetic
 ..I BDMPREP=2 D EPIREC Q
 ..D GATHER
 .Q
 I BDMACTI=0 S BDMPD=0 F  S BDMPD=$O(^AUPNPAT(BDMPD)) Q:'BDMPD  D
 .S BDMX=$$ACTDMPT^BDMD71G(BDMPD,BDMBEGD,BDMADAT,BDMTAXI,BDMBEN,BDM3YE)
 .I 'BDMX Q  ;not active diabetic
 .I BDMACTI,'$$ACTONREG(BDMPD,BDMDMRG) Q
 .I BDMPREP=2 D EPIREC Q
 .D GATHER
 I BDMPREP=2 D WRITEF^BDMD71 Q
 I BDMPREP=3!(BDMPREP=4) D CUML^BDMD715
 Q
ACTONREG(P,R) ;
 I '$D(^ACM(41,"AC",P,R)) Q 0
 S X=^ACM(41,"AC",P,R)
 I $P($G(^ACM(41,X,"DT")),U,1)'="A" Q 0
 Q 1
