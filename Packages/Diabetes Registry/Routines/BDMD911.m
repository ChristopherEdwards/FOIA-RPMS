BDMD911 ; IHS/CMI/LAB -IHS -CUMULATIVE REPORT ; [ 03/16/2009 4:23 AM ]
 ;;2.0;DIABETES MANAGEMENT SYSTEM;**2**;JUN 14, 2007
 ;
 ;
IMM ;
 S:'$D(BDMCUML(140)) BDMCUML(140)="IMMUNIZATIONS"
 S $P(BDMCUML(140),U,2)=$P(BDMCUML(140),U,2)+1
 S V=$G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,64))
 I $E(V)="1" S $P(BDMCUML(140),U,3)=$P(BDMCUML(140),U,3)+1
 I $E(V)="3" S $P(BDMCUML(140),U,6)=$P(BDMCUML(140),U,6)+1
 S V=$G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,66))
 I $E(V)="1" S $P(BDMCUML(140),U,4)=$P(BDMCUML(140),U,4)+1
 I $E(V)="3" S $P(BDMCUML(140),U,7)=$P(BDMCUML(140),U,7)+1
 S V=$G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,68))
 I $E(V)="1" S $P(BDMCUML(140),U,5)=$P(BDMCUML(140),U,5)+1
 I $E(V)="3" S $P(BDMCUML(140),U,8)=$P(BDMCUML(140),U,8)+1
 ;
QUAN ;
 ;145
 ;title^total YES^NO^REF^UACR^SEMI^PCR^OTHER QUANT^PROTEIN 1+^OTHER^NORMAL^MICRO^OVERT
 K BDMOFLG
 S BDMOFLG=0
 S:'$D(BDMCUML(145)) BDMCUML(145)="LABORATORY EXAMS"
 S $P(BDMCUML(145),U,2)=$P(BDMCUML(145),U,2)  ;TOTAL # of patients
 S Q=$G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,92))
 S V=$E($G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,92)))  ;test done?
 S T=$P($G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,92)),U,5)  ;type of test
 S R=$S($P(Q,U,6)]"":$P(Q,U,6),1:$P(Q,U,2))  ;value/result
 I V=1 D
 .S $P(BDMCUML(145),U,3)=$P(BDMCUML(145),U,3)+1
 .S P=$S(T=1:6,T=2:7,T=3:8,T=4:9,T=5:10,T=6:11,1:"")
 .S $P(BDMCUML(145),U,P)=$P(BDMCUML(145),U,P)+1
 .I T=1!(T=2)!(T=5) D
 ..I T=5 D
 ...S $P(BDMCUML(145),U,14)=$P(BDMCUML(145),U,14)+1
 ..I T=1 D
 ...I R[">" S $P(BDMCUML(145),U,14)=$P(BDMCUML(145),U,14)+1 Q
 ...;I $$UP^XLFSTR(R)["COMMENT" S $P(BDMCUML(145),U,12)=$P(BDMCUML(145),U,12)+1 Q
 ...S R=$$STV^BDMD918(R,8)
 ...I R="" S $P(BDMCUML(145),U,15)=$P(BDMCUML(145),U,15)+1 Q
 ...S R=+R
 ...I R<30 S $P(BDMCUML(145),U,12)=$P(BDMCUML(145),U,12)+1 Q
 ...I R<300.9999 S $P(BDMCUML(145),U,13)=$P(BDMCUML(145),U,13)+1 Q
 ...S $P(BDMCUML(145),U,14)=$P(BDMCUML(145),U,14)+1
 ..I T=2 D
 ...I R["-" S $P(BDMCUML(145),U,13)=$P(BDMCUML(145),U,13)+1 Q
 ...I R["300" S $P(BDMCUML(145),U,14)=$P(BDMCUML(145),U,14)+1 Q
 ...I R[">" S $P(BDMCUML(145),U,14)=$P(BDMCUML(145),U,14)+1 Q
 ...S R=$$STV^BDMD918(R,5,1) I R>300 S $P(BDMCUML(145),U,14)=$P(BDMCUML(145),U,14)+1 Q
 ...S $P(BDMCUML(145),U,12)=$P(BDMCUML(145),U,12)+1
 I V=2 S $P(BDMCUML(145),U,4)=$P(BDMCUML(145),U,4)+1  ;no urine testing
 I V=3 S $P(BDMCUML(145),U,5)=$P(BDMCUML(145),U,5)+1  ;refused urine testing
EKG ;need date of last ekg
 S:'$D(BDMCUML(150)) BDMCUML(150)="EKG (Age 30 and above)"
 G:$$AGE^AUPNPAT(BDMPD,BDMADAT)<30 CREAT
 S $P(BDMCUML(150),U,2)=$P(BDMCUML(150),U,2)+1
 S V=$$EKG^BDMD912(BDMPD,BDMRED,"I")
 I V]"" D
 .S E=$$FMDIFF^XLFDT(BDMADAT,V)
 .I E<(365.25*3) S $P(BDMCUML(150),U,3)=$P(BDMCUML(150),U,3)+1
 .I E<(365.25*5) S $P(BDMCUML(150),U,4)=$P(BDMCUML(150),U,4)+1
 .S $P(BDMCUML(150),U,5)=$P(BDMCUML(150),U,5)+1
CREAT ;
 S:'$D(BDMCUML(170)) BDMCUML(170)="Creatinine obtained in the past 12 months"
 S $P(BDMCUML(170),U,2)=$P(BDMCUML(170),U,2)+1
 S V=$$CREAT^BDMD918(BDMPD,BDMBDAT,BDMADAT,"I")
 I V="" S $P(BDMCUML(170),U,5)=$P(BDMCUML(170),U,5)+1 G GFR
 S V=$P(V,U)
 S V=$$STV^BDMD918(V,5,1) I $E(V)'=+$E(V),$E(V)'="." S $P(BDMCUML(170),U,6)=$P(BDMCUML(170),U,6)+1 G GFR ;unable to determine result, not a number
 I V>1.9 S $P(BDMCUML(170),U,3)=$P(BDMCUML(170),U,3)+1
 I V<2.0 S $P(BDMCUML(170),U,4)=$P(BDMCUML(170),U,4)+1
 ;
GFR ;
 S:'$D(BDMCUML(175)) BDMCUML(175)="Estimated GFR documented during audit period"
 S $P(BDMCUML(175),U,2)=$P(BDMCUML(175),U,2)+1
 S V=$E($G(^XTMP("BDMDM91",BDMJOB,BDMBTH,"AUDIT",BDMPD,79)))
 I V=1 S $P(BDMCUML(175),U,5)=$P(BDMCUML(175),U,5)+1
 ;
TCHOL ;
 S:'$D(BDMCUML(180)) BDMCUML(180)="Total Cholesterol obtained in past 12 months"
 S $P(BDMCUML(180),U,2)=$P(BDMCUML(180),U,2)+1
 S V=$$CHOL^BDMD918(BDMPD,BDMBDAT,BDMADAT,"I")
 I V="" S $P(BDMCUML(180),U,6)=$P(BDMCUML(180),U,6)+1 G LDL
 S V=$P(V,U)
 S V=$$STV^BDMD918(V,5,1)
 I V="" S $P(BDMCUML(180),U,7)=$P(BDMCUML(180),U,7)+1 G LDL
 I $E(V)'=+$E(V)!(+V=0) S $P(BDMCUML(180),U,7)=$P(BDMCUML(180),U,7)+1 G LDL ;unable to determine result, not a number or is blank
 I V<200 S $P(BDMCUML(180),U,3)=$P(BDMCUML(180),U,3)+1 G LDL
 I V<240 S $P(BDMCUML(180),U,4)=$P(BDMCUML(180),U,4)+1 G LDL
 S $P(BDMCUML(180),U,5)=$P(BDMCUML(180),U,5)+1
LDL ;
 S:'$D(BDMCUML(190)) BDMCUML(190)="LDL Cholesterol obtained in the past 12 months"
 S $P(BDMCUML(190),U,2)=$P(BDMCUML(190),U,2)+1
 S V=$$LDL^BDMD918(BDMPD,BDMBDAT,BDMADAT,"I")
 I V="" S $P(BDMCUML(190),U,7)=$P(BDMCUML(190),U,7)+1 G HDL
 S V=$P(V,U)
 ;S V=$$STV^BDMD918(V,4)
 S V=$$STV^BDMD918(V,5,1) I $E(V)'=+$E(V)!(+V=0) S $P(BDMCUML(190),U,8)=$P(BDMCUML(190),U,8)+1 G HDL ;unable to determine result, not a number or blank
 I V<100 S $P(BDMCUML(190),U,3)=$P(BDMCUML(190),U,3)+1 G HDL
 I V<130 S $P(BDMCUML(190),U,4)=$P(BDMCUML(190),U,4)+1 G HDL
 I V<160.1 S $P(BDMCUML(190),U,5)=$P(BDMCUML(190),U,5)+1 G HDL
 S $P(BDMCUML(190),U,6)=$P(BDMCUML(190),U,6)+1
HDL ;
 S:'$D(BDMCUML(195)) BDMCUML(195)="HDL Cholesterol obtained in the past 12 months"
 S $P(BDMCUML(195),U,2)=$P(BDMCUML(195),U,2)+1
 S V=$$HDL^BDMD918(BDMPD,BDMBDAT,BDMADAT,"I")
 I V="" S $P(BDMCUML(195),U,7)=$P(BDMCUML(195),U,7)+1 G TRIG
 S V=$P(V,U)
 S V=$$STV^BDMD918(V,5,1) I $E(V)'=+$E(V)!(+V=0) S $P(BDMCUML(195),U,8)=$P(BDMCUML(195),U,8)+1 G TRIG ;unable to determine result, not a number
 S V=$P(V,"."),V=$$STV^BDMD918(V,5,1)
 I $E(V)'=+$E(V)!(+V=0)!(V="") S $P(BDMCUML(195),U,8)=$P(BDMCUML(195),U,8)+1 G TRIG ;unable to determine result, not a number
 I V<35 S $P(BDMCUML(195),U,3)=$P(BDMCUML(195),U,3)+1 G TRIG
 I V<46 S $P(BDMCUML(195),U,4)=$P(BDMCUML(195),U,4)+1 G TRIG
 I V<55.1 S $P(BDMCUML(195),U,5)=$P(BDMCUML(195),U,5)+1 G TRIG
 S $P(BDMCUML(195),U,6)=$P(BDMCUML(195),U,6)+1
TRIG ;
 S:'$D(BDMCUML(200)) BDMCUML(200)="Triglycerides obtained in past 12 months"
 S $P(BDMCUML(200),U,2)=$P(BDMCUML(200),U,2)+1
 S V=$$TRIG^BDMD918(BDMPD,BDMBDAT,BDMADAT,"I")
 I V="" S $P(BDMCUML(200),U,7)=$P(BDMCUML(200),U,7)+1 G SELF
 S V=$P(V,U)
 S V=$$STV^BDMD918(V,5,1) I $E(V)'=+$E(V) S $P(BDMCUML(200),U,8)=$P(BDMCUML(200),U,8)+1 G SELF ;unable to determine result, not a number
 I V<150 S $P(BDMCUML(200),U,3)=$P(BDMCUML(200),U,3)+1 G SELF
 I V<200 S $P(BDMCUML(200),U,4)=$P(BDMCUML(200),U,4)+1 G SELF
 I V<401 S $P(BDMCUML(200),U,5)=$P(BDMCUML(200),U,5)+1 G SELF
 S $P(BDMCUML(200),U,6)=$P(BDMCUML(200),U,6)+1
SELF ;
 Q
PED(P,BDATE,EDATE) ;EP
 K BDMALED
 NEW Y,B,D,T,A,G,Z,V,X
 S Y="BDMALED("
 S X=P_"^ALL EDUC;DURING "_BDATE_"-"_EDATE S E=$$START1^APCLDF(X,Y)
 I $D(BDMALED(1)) S %="" D  I %]"" Q %
 .S (X,D)=0,%="",T="" F  S X=$O(BDMALED(X)) Q:X'=+X!(%]"")  D
 ..S T=$P(^AUPNVPED(+$P(BDMALED(X),U,4),0),U)
 ..Q:'T
 ..Q:'$D(^AUTTEDT(T,0))
 ..S T=$P(^AUTTEDT(T,0),U,2)
 ..I $E(T,1,4)="TO-Q" S %="1  Yes-"_T_" "_$$FMTE^XLFDT($P(BDMALED(X),U),5) Q
 ..I T="TO-LA" S %="1  Yes-"_T_" "_$$FMTE^XLFDT($P(BDMALED(X),U),5) Q
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S X=0,G="" F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G]"")  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .S B=$$CLINIC^APCLV(V,"C")
 .I B=94 S G="1  Yes-"_$$FMTE^XLFDT($P($P(^AUPNVSIT(V,0),U),"."),5)_" CL 94" Q
 .S Z=0 F  S Z=$O(^AUPNVDEN("AD",V,Z)) Q:Z'=+Z!(G]"")  S B=$P($G(^AUPNVDEN(Z,0)),U) I B S B=$P($G(^AUTTADA(B,0)),U) I B=1320 S G="1  Yes-"_$$FMTE^XLFDT($P($P(^AUPNVSIT(V,0),U),"."),5)_" ADA 1320" Q
 .Q
 Q G
CESS(P,BDATE,EDATE) ;EP - find any cessation hf in 12 months before E
 I '$G(P) Q ""
 I $E($$TOBACCO^BDMD91T(P,BDATE,EDATE))'=1 Q ""
 NEW BDM,E,X,G,T,O,D,H,C
 K BDM
 S T=$O(^ATXAX("B","DM AUDIT CESSATION HLTH FACTOR",0))
 S C=$O(^AUTTHF("B","TOBACCO",0)) ;ien of category passed
 I '$G(C) Q ""
 S (H,D)=0 S O=""
 S H=0 F  S H=$O(^AUPNVHF("AA",P,H)) Q:H'=+H!(O]"")  D
 .S G=0
 .I $D(^ATXAX(T,21,"AA",H)) S G=1
 .I $P(^AUTTHF(H,0),U,1)["CESSATION",$$VAL^XBDIQ1(9999999.64,H,.03)="TOBACCO" S G=1
 .S D="" F  S D=$O(^AUPNVHF("AA",P,H,D)) Q:D'=+D!(O]"")  D
 ..Q:(9999999-D)>EDATE  ;after time frame
 ..Q:(9999999-D)<BDATE  ;before time frame
 ..S O="1  Yes- "_$$FMTE^XLFDT(9999999-D)_"  "_$P(^AUTTHF(H,0),U)
 .Q
 S X=P_"^EDUC [DM AUDIT SMOKING CESS EDUC;DURING "_BDATE_"-"_EDATE S E=$$START1^APCLDF(X,"BDM(")
 I $D(BDM(1)) Q "1  Yes-"_$$FMTE^XLFDT($P(BDM(1),U),5)_" "_$P(BDM(1),U,3)
 S BDM=$$PED(P,BDATE,EDATE)
 I BDM]"" Q BDM
 NEW T S T=$O(^ATXAX("B","DM AUDIT SMOKING CESS EDUC",0))
 ;I 'T Q "No"
 S (X,G)=0
 I T F  S X=$O(^ATXAX(T,21,X)) Q:X'=+X!(G)  S G=$$REFUSAL^BDMD917(P,9999999.09,$P(^ATXAX(T,21,X,0),U),BDATE,EDATE)
 I G Q "3  Refused "_$P(G,U,6)_" "_$P(G,U,4)_" "_$P(G,U,3)_" "_$P(G,U,5)
 S X=$O(^AUTTEDT("C","TO-Q",0))
 I X S Y=$$REFUSAL^BDMD917(P,9999999.09,X,BDATE,EDATE) I $P(Y,U,1)=1 Q "3  Refused "_$P(Y,U,6)_" "_$P(Y,U,4)_" "_$P(Y,U,3)_" "_$P(Y,U,5)
 S X=$O(^AUTTEDT("C","TO-LA",0))
 I X S Y=$$REFUSAL^BDMD917(P,9999999.09,X,BDATE,EDATE) I $P(Y,U,1)=1 Q "3  Refused "_$P(Y,U,6)_" "_$P(Y,U,4)_" "_$P(Y,U,3)_" "_$P(Y,U,5)
 S X=$O(^AUTTEDT("C","TO-QT",0))
 I X S Y=$$REFUSAL^BDMD917(P,9999999.09,X,BDATE,EDATE) I $P(Y,U,1)=1 Q "3  Refused "_$P(Y,U,6)_" "_$P(Y,U,4)_" "_$P(Y,U,3)_" "_$P(Y,U,5)
 S X=$O(^AUTTEDT("C","TO-QU",0))
 I X S Y=$$REFUSAL^BDMD917(P,9999999.09,X,BDATE,EDATE) I $P(Y,U,1)=1 Q "3  Refused "_$P(Y,U,6)_" "_$P(Y,U,4)_" "_$P(Y,U,3)_" "_$P(Y,U,5)
 Q "2  No"
 ;
