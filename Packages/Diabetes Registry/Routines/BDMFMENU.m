BDMFMENU ; IHS/CMI/LAB - CONTROL HEADINGS AND MENUS ;
 ;;2.0;DIABETES MANAGEMENT SYSTEM;**2,3,4**;JUN 14, 2007
 ;;ROUTINE TO CONTROL HEADINGS AND MENUS
TITLE ;EP;TO DISPLAY TITLE
 S BDMFINSS=""
 S IOP="HOME"
 D ^%ZIS
 D HOME
 D HEAD
EXIT K BDMX,BDMZ,BDMTRM
 I $D(BDMY),BDMY]"" S BDMY=BDMY_$S($G(BDMREGNM)]"":" - "_BDMREGNM,1:"") W !!?80-$L(BDMY)/2,BDMY
 D ^BDMFEXIT
 D REG^BDMFUTIL
 Q
HEAD ;EP;TO DISPLAY HEADER
 W @IOF
HEAD1 F BDMJ=0:1:4 D H1
 D HDVS
 I DUZ D
 .S BDMX=$P(^VA(200,DUZ,0),U)
 .S BDMX=$P($P(BDMX,",",2)," ")_" "_$P(BDMX,",")
 .S BDMX="CURRENT USER: "_BDMX
 .W !?80-$L(BDMX)\2,BDMX
 D:BDMON=""!(BDMOF="") RV
 Q
H1 S BDMX=$T(TEXT+BDMJ),BDMX=$P(BDMX,";;",2)
 W !?80-$L(BDMX)\2,BDMX
 Q
HDVS I $D(DUZ(2))#2,DUZ(2) S:$D(^DIC(4,DUZ(2),0)) BDMX=$P(^(0),U)
 I $D(^DIC(9.4,"C","BDM")) D
 .S BDMZ=$O(^DIC(9.4,"C","BDM",0))
 .S BDMZ=$P(^DIC(9.4,BDMZ,"VERSION"),U)
 .S BDMZ="VERSION 2.0 (Patch 4)"
 I $E(IOST,1,2)="C-" W:$D(BDMZ) !?80-$L(BDMZ)\2,BDMZ
 W:$D(BDMX) !?80-$L(BDMX)\2,BDMX
 Q
RM ;EP
 S BDMY="REGISTER MAINTENANCE"
 D TITLE
 Q
MM ;EP
 S BDMY="MAIN MENU"
 D TITLE
 Q
RP ;EP
 S BDMY="REPORTS MENU"
 D TITLE
 Q
DA ;EP
 S BDMY="IHS DIABETES QA AUDIT MENU"
 D TITLE
 Q
LM ;EP
 S BDMY="LETTER MANAGEMENT"
 D TITLE
 Q
HOME ;EP;TO SET TERMINAL CONTROL VARIABLES
 S BDMTRM=^%ZIS(2,IOST(0),5)
 S BDMXY=$G(^%ZIS(2,IOST(0),"XY"))
 S BDMON=$P(BDMTRM,U,4)
 S BDMOF=$P(BDMTRM,U,5)
 D:BDMON=""!(BDMOF="") RV
 D ENS^%ZISS
 Q
CLEAN ;EP;TO KILL SELECTD VARIABLES
 K BDMON,BDMOF,BDMZ,BDMY,BDMTRM,BDMFINSS,BDMPPO,BDMPTX,BDMREV,BDMRR,BDMSOF,BDMTRN,BDMBOX
 Q
RV ;WARNING MESSAGE WHEN REVERSE VIDEO NOT DEFINED
 W @IOF
 W !?10,"WARNING     WARNING     WARNING     WARNING     WARNING"
 W *7,*7
 W !!,"Your DEVICE and/or TERMINAL TYPE are not properly defined to work with the ARMS"
 W !,"system.  The REVERSE VIDEO ON and/or REVERSE VIDEO OFF are not properly defined."
 W !,"This is the responsibility of the AREA OFFICE computer site manager."
 W !!,"Please contact this individual for assistance.  Let him/her know that you"
 W !,"are logged in on device NO.: ",$I
 D P1
 Q
B1 ;EP;TO TURN BLINK ON
 W:$D(IOBON)&$D(IOBOFF) IOBON
 Q
B2 ;EP;TO TURN BLINK OFF
 W:$D(IOBOFF) IOBOFF
 Q
U1 ;EP;TO TURN UNDERLINE ON
 W:$D(IOUON)&$D(IOUOFF) IOUON
 Q
U2 ;EP;TO TURN UNDERLINE OFF
 W:$D(IOUOFF) IOUOFF
 Q
DASH(Z) ;EP;TO WRITE LINE OF DASHES
 S:'$G(Z) Z=80
 W !
 W $$DASH1(Z)
 Q ""
DASH1(Z) ;EP;TO WRITE DASHES WITHOUT LINE FEED
 S:'$G(Z) Z=80
 N I
 F I=1:1:Z W "-"
 Q ""
WARNING ;EP;TO DISPLAY WARNING MESSAGE
 D B1
 N ACRI
 W !!
 F ACRI=1:1:6 W ?$X+5,"WARNING"
 D B2
 Q
PAUSE ;EP;
 Q:$E(IOST,1,2)'="C-"
 K DIR
P1 ;EP;
 N X,Y,Z
 W !
 S DIR(0)="EOA"
 S DIR("A")="Press RETURN to continue or '^' to exit. "
 D DIR^BDMFDIC
 S:$G(X)["^" DN=0,BDMOUT=""
 Q
TEXT ;;THIS SYSTEM CONTAINS CONFIDENTIAL PATIENT INFORMATION COVERED
 ;;BY THE PRIVACY ACT.  UNAUTHORIZED USE OF THIS DATA IS ILLEGAL
 ;;****************************************************
 ;;**           DIABETES MANAGEMENT SYSTEM           **
 ;;****************************************************
