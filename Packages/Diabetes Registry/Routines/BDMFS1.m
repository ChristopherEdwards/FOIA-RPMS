BDMFS1 ; IHS/CMI/LAB - DMS FLOW SHEET MANAGEMENT UTILITY ;
 ;;2.0;DIABETES MANAGEMENT SYSTEM;**2**;JUN 14, 2007
 ;UTILITY PROGRAM TO MANAGE FLOW SHEET CREATION AND EDITING
CSADD ;EP;ADD CLINIC STOP
 W @IOF
 W !?5,"Select CLINIC to add to the"
 W !?5,BDMSNAM,?25,"Flow Sheet"
 S DIC="^DIC(40.7,"
 S DIC(0)="AEMQZ"
 S DIC("A")="Which CLINIC: "
 W !
 D DIC^BDMDIC
 Q:+Y<1
 S (DINUM,X)=+Y
 S DA(1)=BDMSDA
 S DIC="^APCHSFLC("_BDMSDA_",3,"
 S DIC(0)="L"
 D FILE^BDMDIC
 D CSBACK
 Q
CSDEL ;EP;DELETE CLINIC STOP
 N BDMY
 D CSSEL
 I $D(BDMQUIT) K BDMQUIT D FSCBACK^BDMFS Q
 F BDMI=1:1 S BDMX=$P(BDMY,",",BDMI) Q:BDMX=""  D
 .Q:'$D(BDMCS(BDMSDA,BDMX))
 .S DA=$P(BDMCS(BDMSDA,BDMX),U,2)
 .S DA(1)=BDMSDA
 .S DIK="^APCHSFLC("_DA(1)_",3,"
 .D DIK^BDMDIC
CSBACK S BDMGO="FSC"
 D BACK^BDMFS
 Q
CSSEL ;SELECT CLINIC STOP
 S DIR(0)="LO^1:"_BDMCS
 S DIR("A")="Whick Clinic Stop(s)"
 W !
 D DIR^BDMDIC
 I Y<1 S BDMQUIT="" Q
 S BDMY=Y
 Q
