BDMLLTZ ; IHS/CMI/LAB - DEVICE CALLS AND QUEUING ;
 ;;2.0;DIABETES MANAGEMENT SYSTEM;**3**;JUN 14, 2007
 ;;ROUTINE USED AS CENTRAL POINT FOR ALL DEVICE HANDLING AND QUEUING
ZIS ;call to XBDBQUE
 W ! S DIR(0)="S^P:PRINT Output;B:BROWSE Output on Screen",DIR("A")="Do you wish to",DIR("B")="P" K DA D ^DIR K DIR
 I $D(DIRUT) D XIT Q
 S BDMOPT=Y
 I Y="B" D BROWSE,XIT Q
 S XBRP="CPRINT^BDMLLT",XBRC="",XBRX="EXIT^BDMLLT",XBNS="BDM"
 D ^XBDBQUE
 D XIT
 Q
BROWSE ;
 S XBRP="VIEWR^XBLM(""CPRINT^BDMLLT"")"
 S XBRC="",XBRX="EXIT^BDMLLT",XBIOP=0 D ^XBDBQUE
 Q
ZISX ;EP;TO CALL DEVICE
 D  Q
 .S DIR(0)="SO^P:PRINT Output;B:BROWSE Output on Screen"
 .S DIR("A")="Do you want to "
 .S DIR("B")="PRINT"
 .W !
 .D DIR^BDMLLTD
 .Q:$D(BDMLLTQT)!$D(BDMLLTOT)
 .I $E($G(X))="P" D ZIS1 Q
 .I $E($G(Y))="B" D BROWSE Q
ZIS1 ;EP;
 K DN
 S %ZIS="AEMNPQ"
 S ZIBH=$TR($H,",","")_$R(1000)
 W !
 D ^%ZIS
 I POP>0 D CLOSE Q
 S:$G(IOPAR)]"" %ZIS("IOPAR")=IOPAR
 S ZTSAVE("%ZIS*")=""
 S ZTSAVE("ZIBH")=""
 S ZTRTN="OPEN^BDMLLTZ"
 I $D(IO("Q")),IO=IO(0)!$D(IO("S")) D  G ZIS
 .W *7,*7
 .W !!,"CANNOT QUEUE TO HOME OR SLAVE DEVICE."
 I '$D(IO("Q")) D  D CLOSE Q
 .I $E(IOST,1,2)="P-" D
 ..W !!,"...One moment please, while I complete your print request..."
 ..W !
 .D:$D(BDMLRTN) @ZTRTN
 E  D ZTLOAD
 Q
CLOSE ;EP;TO CLOSE DEVICE
 D ^%ZISC
 K IOP,IOPAR,%ZIS,ZTSK,ZTQUEUED,ZTREQ
 Q
ZTLOAD ;EP;TO CALL %ZTLOAD
 K BDMLLTDR
 S ZTIO=ION
 S ZTSAVE("ACM*")=""
 D ^%ZTLOAD
 W !!,$S($G(ZTSK)]"":"Request queued!",1:"Request cancelled.")
 D CLOSE
 H 2
 Q
OPEN ;EP;TO OPEN DEVICE AND PRINT SELECTED REPORT
 I '$D(ZTQUEUED)!(ION["HOST") S IOP=ION D ^%ZIS I POP S BDMLLTQT="" Q
 U IO
 D @BDMLRTN
 S:$D(ZTQUEUED) ZTREQ="@"
 D:'$D(ZTQUEUED) CLOSE
 Q
HOST ;EP;TO OPEN HOST FILE
 ;%FN - FILE NAME REQUIRED
 ;BDMLLTOP - 'R' FOR READ, 'W' FOR WRITE REQUIRED, 'M' FOR READ/WRITE
 Q:'$D(%FN)!'$D(BDMLLTOP)
 S POP=1
 F BDMLI=51:1:54 Q:'POP  D
 .S (IOP,ION)=BDMLI
 .S %ZIS("IOPAR")="("""_%FN_""":"""_BDMLLTOP_""")"
 .D ^%ZIS
 I POP D  G HOST:$G(BDMLLTX)<2 S BDMLLTQT="" Q
 .W !!,"Waiting for HOST FILE SERVER."
 .S BDMLLTX=$G(BDMLLTX)+1
 K IOP,POP
 Q
BROWSE1 ;EP;TO BROWSE
 Q:$G(BDMLRTN)=""
 S BDMLFLD("BROWSE")=1
 D VIEWR^XBLM(BDMLRTN)
 I $D(BDMLLTQT) D  Q
 .K BDMLLTQT
 .W !!,"BROWSE function temporarily unavailable."
 .D ZIS1
 D CLEAR^VALM1
 Q
XIT ;
 D EN^XBVK("BDML")
 Q
