AMER ; IHS/ANMC/GIS - PRIMARY ROUTINE FOR ER ADMISSION ;
 ;;3.0;ER VISIT SYSTEM;;FEB 23, 2009
 ;
 N AMERLINE,AMERPCC,AMERTIME
 I $D(AMERBFLG) K AMERBFLG S AMERSTRT=10,AMERFIN=14,AMERQSEQ=AMERSSEQ G RUN
VAR D EXIT1 S AMERSTRT=1,AMERFIN=19
VAR1 S %="",$P(%,"~",80)="",AMERLINE=%
 S AMERVER=$$VERSION^XPDUTL("AMER")
 S AMERSVER=$$VERSION^XPDUTL("PIMS")
 S IOP=0 D ^%ZIS W @IOF,"ER SYSTEM Ver ",AMERVER,": ",$S('$D(AMERDOA):"ADMISSION TO EMERGENCY ROOM",1:"   DOA ADMISSION"),"      ^ = back up     ^^ = quit",!,"Questions preceded by a '*' are MANDATORY.  Enter '??' to see choices."
EDIT ; ENTRY POINT FROM AMER4
 I '$D(AMERTFLG) S AMERQSEQ=""
RUN F AMERRUN=AMERSTRT:1 Q:AMERRUN>AMERFIN  Q:$D(AMERQUIT)  D
 . I '$D(^AMER(2.3,"B",("QA"_AMERRUN))) Q
 . S AMERQNO=AMERRUN
 . I '$D(AMERMAND) W $$LINE("QA"_AMERRUN)
 . D OPT("QA"_AMERRUN)
 . D @("QA"_AMERRUN_"^AMER"_$S(AMERRUN<6:"1",AMERRUN<10:"1B",1:"1"))
 . D SET
 . Q
 I $D(AMEREFLG) S AMERNOHS=""
 I $D(AMERBCH) Q  ;NO TEMP FILES SO NO "SAVE" FROM TEMP FILES
 I $D(AMEREFLG) D  Q
 .S AMERNOHS=""
 .S AMERPCC=$$EXISTING^AMERPCC(AMERDFN)
 .D:AMERPCC>0 VISITIN^AMERPCC(AMERDFN,AMERPCC) ;update VISIT file if it exists
 .Q
 I $D(AMERBCH) Q
 I $D(AMERQUIT) G EXIT
 I '$D(AMERNOHS) D PRINT I $D(AMERQUIT) G EXIT
 D ^AMER0 I $D(AMERQUIT) G EXIT
 W *7,!!,"ER admission data collection is now complete.  Thank you.",!!
EXIT ; ENTRY POINT FROM AMERD
 H 2 I $D(IOF) W @IOF
EXIT1 ; ENTRY POINT FROM AMERD
 K AMERPRV,L,AGMVDF,AMERDA,AMERSSEQ
 K AMERDR,AGE,DFN,DOB,SEX,SSN
 K AMERQUIT,AMERRUN,AMERLINE,AMERDFN,AMERQNO,AMERQSEQ,AMERSTG,X,Y,Z,F,I,A,%,AUPNPAT,AUPNDOD,AUPNDOB,AUPNSEX,AMERI,AMERNOHS,DIROUT,DH,AMQQSSEQ
 K D,D0,DA,DD,DI,DIADD,DIC,DICR,DIG,DIH,DIK,DISYS,DIU,DIV,DIW,DO,DQ,DIE,DR,DX,%T,S,DUOUT,DTOUT,DPP,DINUM,DIPGM
 K AUPNDAYS,DIRUT,AMERSTRT,AMERFIN,AMEREFLG,AMER,AMERNV,AMERRV,AMERDEST,AMERTAB,B,C,E,G,H,J,K,N,POP,Q,T,V,SDSEX,%Y,%DT
 K ^TMP("AMER",$J,1),^(2),^(3)
 Q
 ;
SET K AMERMAND I $D(AMERQUIT) Q
 I AMERRUN=98 Q
 I AMERRUN="REG" D REG Q
 I AMERRUN="PRINT" D PRINT S AMERRUN=98 Q
 I AMERRUN="EDIT" S AMERNOHS="" D RESTORE Q
 I X?2."^" W !,*7,"Session terminated..." S AMERQUIT="" Q
 I $E(X)=U S X=U
 I X=U,$D(AMEREFLG),(AMERRUN>AMERFIN) Q
 I X=U,AMERQNO<3 W !,"Session terminated...",*7 S AMERQUIT="" Q
 I Y=""!(Y=-1),'$D(AMEROPT),AMERQNO>2,X'=U D MAND Q
 K AMEROPT
 S AMERQSEQ=AMERQSEQ_AMERQNO_";"
 I X=U S X="^B" D BACK Q
 I Y=""!(Y=-1) Q
 S ^TMP("AMER",$J,1,AMERQNO)=Y
 Q
 ;
OUT ; ENTRY POINT FROM MULTIPLE ROUTINES
 I '$D(POP) S POP=0
 I ($D(DTOUT))!($D(DUOUT))!(POP)!($D(DIROUT)) K DTOUT,DUOUT,POP,DIRUT S AMERQUIT=""
 I $G(X)=U K AMERQUIT
 I $G(X)?2."^" S AMERQUIT=""
 I $D(AMERQUIT) W *7,!,"Session terminated.  No data has been entered!"
 Q
 ;
BACK ; ENTRY POINT FROM AMERD
 S A=$L(AMERQSEQ,";"),AMERRUN=$P(AMERQSEQ,";",A-2)-1,AMERQSEQ=$P(AMERQSEQ,";",1,A-3)_";"
 I AMERRUN=-1 S AMERRUN=999
 Q
 ;
RESTORE ;
 S AMERDFN=+Y,AMERRUN=1
 W *7,!!,"This patient is currently admitted to the ER....",!,"Want to edit his/her ER admission data"
 S %=1 D YN^DICN D OUT I $D(AMERQUIT) Q
 I "Yy"'[$E(%Y) S AMERRUN=0 Q
 D UTL^AMER0(AMERDFN)
 Q
 ;
PRINT ; PRINT HEALTH SUMMARY
 Q
 ;
 W ! S DIC="^APCHSCTL(",DIC(0)="AEQ"
 S DIC("B")="ADULT REGULAR"
 D ^DIC K DIC D OUT I $D(AMERQUIT) Q
 S APCHSTYP=+Y,APCHSPAT=AMERDFN
 D HSOUT^APCHS
 Q
 ;
REG ;
 I $D(AMERDEMO) W !!,"Patient registration not available in demo mode",!! H 2 W *7 Q
 S DOG="" K DFN D ^AGVAR,DOG^AG0
 I '$D(DFN) S AMERQUIT="" G REGEXIT
 S ^TMP("AMER",$J,1,1)=DFN,AMERDFN=DFN,AMERRUN=1
REGEXIT K XY,XYER,AG,AUPDIC,AUPDICW,DIC,DIE,DO,D0,DIU,DIV,DIG,DA,D1,DFN,DIPGM,BS,F,FF,RM,SL,SUB,D,DI,DIH,DOG,DQ,%W,%Y,%,AGOPT,AUPL,AUPQF,AUPX,DIW,DR,%DT
 Q
 ;
MAND ; ENTRY POINT FROM AMERD ; MANDATORY ANSWER
 W *7,!,"This answer is mandatory. If you need help, type '??'."
 S AMERRUN=AMERRUN-1,AMERMAND=""
 Q
 ;
BATCH ; ENTRY POINT FOR BATCH MODE
 S AMERBCH=""
B1 D AMER I $D(AMERQUIT) D EXIT1 K AMERBCH Q
 S AMERSSEQ=$P(AMERQSEQ,";",1,$L(AMERQSEQ,";")-2)_";"
 D VAR^AMERD I $D(AMERQUIT) D EXIT1 K AMERBCH,AMERSSEQ Q
 I $D(AMERDOA) K AMERDOA
 G B1
 ;
DOA ; ENTRY POINT FOR DOA
 S AMERDOA=""
 D BATCH,EXIT1 K AMERDOA
 Q
 ;
SCAN ; ENTRY POINT FROM SCAN PT NAME OPTION ;SCAN PT NAMES
 D MSG^AMER1A
SC1 W !! S DIR(0)="9000001,.01",DIR("A")="Enter patient NAME, DOB, or LOCAL CHART NUMBER" D ^DIR K DIR
 I '$T,'$D(DUOUT)!'$D(DTOUT) W !! S DIR(0)="E",DIR("A")="Press 'Return to continue" D ^DIR K DIR
 I $D(IOF) W @IOF
 D EXIT1^AMER K DIRUT
 Q
 ;
EN1 ; ENTRY POINT FOR MINI REGISTRATION
 D REG
 K A1BETAG,AGDOG,AGDTS
 D EXIT1 I $D(IOF) W @IOF
 Q
 ;
TRG ; ENTRY POINT FOR EDITING TRIAGE NURSE'S FIELDS
 N AMERLINE
 D EXIT1
 S %="",$P(%,"~",80)="",AMERLINE=% K %
 ;S AMERSTRT=1,AMERFIN=27,AMERQSEQ=""   ;IHS/OIT/SCR 01/06/09
 S AMERSTRT=1,AMERFIN=25,AMERQSEQ=""
 S AMERTRG=1
 D EDIT^AMERD
 K AMERTRG,AMERLINE
 Q
 ;
LINE(X) ; QUERY SEPERATOR LINE
 N Y,AMERLINE
 S %="",$P(%,"~",80)="",AMERLINE=% K %
 I $G(X)="" S X="Q"
 S Y=$O(^AMER(2.3,"B",X,"")),Y=$P($G(^AMER(2.3,+Y,0)),U,11)
 I 'Y Q ($C(10)_$C(13)_$C(10)_$C(13)_AMERLINE_$C(10)_$C(13))
 Q ""
 ;
DEMO ; ENTRY POINT FOR DEMO MODE.  NO VISIT CREATED
 S AMERDEMO=1
 D AMER
 K AMERDEMO
 Q
 ;
OPT(X) ; ENTRY POINT FROM AMERD ;OPTIONAL
 K AMEROPT
 N %,Y,Z
 S Y=$O(^AMER(2.3,"B",X,"")) I 'Y Q
 S %=$P($G(^AMER(2.3,Y,0)),U,12)
 I % S AMEROPT=""
 Q
 ;
R1 ;
 Q
