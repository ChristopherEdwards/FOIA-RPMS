AMEREDTI ; IHS/OIT/SCR - SUB-ROUTINE FOR ER VISIT EDIT of INJURY INFORMATION
 ;;3.0;ER VISIT SYSTEM;;FEB 23, 2009
 ;
 ;  
EDINJRY(AMERDA,AMERAIEN)   ;EP CALLED BY AMEREDIT when "INJURY" is selected for editing
 N AMERCAUS,AMERMV,AMEROLD,AMERNEW,AMEREDTS,AMERSTRG,DR,AMERQUIT,DIR
 N AMERTEMP,AMERC,AMERI,AMEROPTN
 S (AMEROLD,AMERNEW,AMEREDTS,AMERSTRG,DR)=""
 S AMERCAUS="",(AMERMV,AMERQUIT)=0
 D EN^DDIOL("**Changing CAUSED BY INJURY can cause injury data to be deleted**","","!!?3")
 S AMEROLD=$P($G(^AMERVSIT(AMERDA,3)),U,1)
 S DIR("B")=$S(AMEROLD=1:"YES",1:"NO")
 S DIR(0)="YO",DIR("A")="Was this ER visit caused by an injury"
 D ^DIR K DIR
 I $D(DUOUT)!$D(DTOUT) K DIR,DUOUT,DTOUT Q 0
 S AMERNEW=Y
 I (AMEROLD'=AMERNEW) D
 .;If answer used to be "YES" and now its "NO" kill injury related information
 .I AMEROLD=1 S:AMERNEW=0 DR=$S(DR'="":DR_";",1:""),DR=DR_"3.2////@;3.3////@;3.4////@;3.5////@;3.6////@"
 .S DR=$S(DR'="":DR_";",1:""),DR=DR_"3.1////"_AMERNEW
 .S AMERSTRG=$$EDAUDIT^AMEREDAU("3.1",$$EDDISPL^AMEREDAU(AMEROLD,"B"),$$EDDISPL^AMEREDAU(AMERNEW,"B"),"INJURED")
 .I AMERSTRG="^" Q
 .D DIE^AMEREDIT(AMERDA,DR)
 .D DIEREC^AMEREDAU(AMERAIEN,AMERSTRG)
 .Q
 I $D(DUOUT)!$D(DTOUT) K DUOUT,DTOUT Q 0
 I AMERNEW=0 Q 1  ;If not injury related we are done
 S DR=""
 ;
 D EN^DDIOL("","","!")
 S DIC("A")="Cause of injury: " K DIC("B")
 S AMERI=0,AMERCAUS=$P($G(^AMERVSIT(AMERDA,3)),U,2)  ;AMERCAUS IS A POINTER TO ICD9 FILE
 I AMERCAUS'="" D
 .I $P($$ICDDX^ICDCODE(AMERCAUS,,,1),U,1)'=-1 D
 ..S AMEROLD=AMERCAUS
 ..S AMEROPTN=0  ;Setting up to $O through option file looking for ICD code
 ..F  Q:AMEROPTN="AC"  D
 ...S AMEROPTN=$O(^AMER(3,AMEROPTN))
 ...S AMERTEMP=$G(^AMER(3,AMEROPTN,"ICD"))
 ...Q:AMERTEMP=""
 ...I AMERTEMP=AMERCAUS D
 ....S DIC("B")=$P($G(^AMER(3,AMEROPTN,0)),U,1)
 ....I DIC("B")="MOTOR VEHICLE" S AMERMV=1
 ....E  S AMERMV=0
 ....S AMEROPTN="AC"  ;To get out of  for loop
 ....Q
 ...Q
 ..Q
 .E  S AMERMV=0 K DIC("B")
 .Q
 E  S AMEROLD=""
 S DIC="^AMER(3,"
 S DIC("S")="I $P(^(0),U,2)="_$$CAT^AMER0("CAUSE OF INJURY"),DIC(0)="AEQ"
 D ^DIC K DIC
 I $D(DUOUT)!$D(DTOUT)!(Y<0) K DUOUT,DTOUT,Y Q 0
 S AMERNEW=$G(^AMER(3,+Y,"ICD"))
 I AMERNEW="" D
 .D EN^DDIOL("No ICD9 code associated to Cause of Injury!","","!!")
 .D EN^DDIOL("Cause of injury will be lost","","!!")
 .Q
 I (AMERNEW'=AMEROLD) D
 .S AMERSTRG=$$EDAUDIT^AMEREDAU("3.2",$$EDDISPL^AMEREDAU(AMEROLD,"C"),$$EDDISPL^AMEREDAU(AMERNEW,"C"),"CAUSE OF INJURY")
 .I AMERSTRG="^" Q
 .S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 .S DR=$S(DR'="":DR_";",1:""),DR=DR_"3.2////"_AMERNEW,AMERCAUS=AMERNEW
 .;IF the original cause was Motor Vehicle, and we have changed it, set up DR to delete any 
 .;Motor Vehicle related data
 .I AMERMV S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.1////@;13.2////@;13.3////@;13.4////@;13.5////@;13.6////@"
 .S AMERC=0,AMERC=$O(^AMER(3,"B","MOTOR VEHICLE",AMERC))
 .I +Y=AMERC S AMERMV=1
 .E  S AMERMV=0
 .Q
 S DIC("A")="*Setting of accident/injury: " K DIC("B")
 S AMEROLD=$P($G(^AMERVSIT(AMERDA,3)),U,3)
 I AMEROLD'="" S DIC("B")=$P($G(^AMER(3,AMEROLD,0)),U,1)
 S DIC="^AMER(3,",DIC("S")="I $P(^(0),U,2)="_$$CAT^AMER0("SCENE OF INJURY"),DIC(0)="AEQ"
 D ^DIC K DIC
 I $D(DUOUT)!$D(DTOUT) K DIC,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 I Y<0 S AMERNEW=""
 E  S AMERNEW=+Y
 I AMERNEW'=AMEROLD D
 .S AMERSTRG=$$EDAUDIT^AMEREDAU("3.3",$$EDDISPL^AMEREDAU(AMEROLD,"S"),$$EDDISPL^AMEREDAU(AMERNEW,"S"),"SCENE OF INJURY")
 .I AMERSTRG="^" Q
 .S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 .S DR=$S(DR'="":DR_";",1:""),DR=DR_"3.3////"_AMERNEW
 .Q
 ;Determine if "Safety Equipment" questions apply and collect info if it is
 S Z=U,%="MOTOR VEHICLE^BICYCLE^TOOLS JOB EQUIP.^INHALATION (GAS/SMOKE)^CHEMICALS (CAUSTIC OR TOXIC)^ELECTRICITY^FIRE FLAME^FALL (ACCIDENTAL)"
 F I=1:1:$L(%,U) S A=$P(%,U,I),Z=Z_+$O(^AMER(3,"B",A,0))_U D
 .I Z[(U_AMERCAUS_U) D    ;SAFETY EQUIP RELEVANT
 ..S DIC("A")="*Safety equipement used: " K DIC("B")
 ..S AMEROLD=$P($G(^AMERVSIT(AMERDA,3)),U,5)
 ..I AMEROLD'="" S DIC("B")=AMEROLD
 ..S DIC="^AMER(3,",DIC("S")="I $P(^(0),U,2)="_$$CAT^AMER0("SAFETY EQUIPMENT"),DIC(0)="AEQ"
 ..D ^DIC K DIC
 ..I $D(DUOUT)!$D(DTOUT) K DIC,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 ..S AMERNEW=+Y
 ..I AMERNEW>0&(AMERNEW'=AMEROLD) D
 ...S DR=$S(DR'="":DR_";",1:""),DR=DR_"3.5////"_AMERNEW
 ...S AMERSTRG=$$EDAUDIT^AMEREDAU("3.5",$$EDDISPL^AMEREDAU(AMERNEW,"Q"),$$EDDISPL^AMEREDAU(AMERNEW,"Q"),"SAFETY EQUIPMENT")
 ...I AMERSTRG="^" Q
 ...S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 ...Q
 ..Q
 .Q
 I AMERQUIT Q 0
 ;If the cause of injury is MOTOR VEHICLE collect add'l info
 I AMERMV D
 .;QD41 - DESCIPTION OF MVA LOCATION
 .K DIR("B")
 .S AMEROLD=$P($G(^AMERVSIT(AMERDA,13)),U,1)
 .I AMEROLD'="" S DIR("B")=AMEROLD
 .S DIR(0)="FO^1:100",DIR("A")="*Location of MVC"
 .S DIR("?")="Enter free text location description (100 characters max.)"
 .D ^DIR K DIR
 .I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .S AMERNEW=Y
 .I AMERNEW<0 S AMERQUIT=1 Q
 .I AMERNEW'=AMEROLD D
 ..I AMERNEW="" S AMERNEW="@"
 ..S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.1////"_AMERNEW
 ..S AMERSTRG=$$EDAUDIT^AMEREDAU("13.1",AMEROLD,AMERNEW,"EXACT MVC LOCATION")
 ..I AMERSTRG="^" Q
 ..S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 ..Q
 .Q:AMERQUIT
 .;QD42 - DRIVER INSURANCE COMPANY
 .K DIR("B")
 .S AMEROLD=$P($G(^AMERVSIT(AMERDA,13)),U,2)
 .I AMEROLD'="" S DIR("B")=AMEROLD
 .S DIR(0)="FO^1:100",DIR("A")="*Driver's insurance company"
 .S DIR("?")="Enter free text description"
 .D ^DIR K DIR
 .I $D(DUOUT)!$D(DTOUT) K DIC,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .S AMERNEW=Y
 .S:AMERNEW<0 AMERNEW=""
 .I AMERNEW'=AMEROLD D
 ..I AMERNEW="" S AMERNEW="@"
 ..S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.2////"_AMERNEW
 ..S AMERSTRG=$$EDAUDIT^AMEREDAU("13.2",AMEROLD,AMERNEW,"DRIVER INSURANCE COMPANY")
 ..I AMERSTRG="^" Q
 ..S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 ..Q
 .;QD43 - DRIVER POLICY NUMBER
 .K DIR("B")
 .S (AMEROLD,DIR("B"))=$P($G(^AMERVSIT(AMERDA,13)),U,3)
 .S DIR(0)="FO^1:100",DIR("A")="*Driver's insurance policy number"
 .S DIR("?")="Enter free text description"
 .D ^DIR K DIR
 .I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .S AMERNEW=Y
 .S:AMERNEW<0 AMERNEW=""
 .I AMERNEW'=AMEROLD D
 ..I AMERNEW="" S AMERNEW="@"
 ..S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.3////"_AMERNEW
 ..S AMERSTRG=$$EDAUDIT^AMEREDAU("13.3",AMEROLD,AMERNEW,"DRIVER POLICY #")
 ..I AMERSTRG="^" Q
 ..S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 .;QD44 - OWNER'S NAME
 .K DIR("B")
 .S AMEROLD=$P($G(^AMERVSIT(AMERDA,13)),U,4)
 .I AMEROLD'="" S DIR("B")=AMEROLD
 .S DIR(0)="FO^1:100",DIR("A")="*Owner of vehicle (if different than driver)"
 .S DIR("?")="Enter free text description"
 .D ^DIR K DIR
 .I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .S AMERNEW=Y
 .I AMERNEW'=AMEROLD D
 ..I AMERNEW="" S AMERNEW="@"
 ..S AMERSTRG=$$EDAUDIT^AMEREDAU("13.4",AMEROLD,AMERNEW,"OWNER NAME")
 ..I AMERSTRG="^" Q
 ..S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 ..S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.4////"_AMERNEW
 ..Q
 .;QD45 - OWNER'S INSURANCE COMPANY
 .Q:AMERQUIT
 .K DIR("B")
 .S AMEROLD=$P($G(^AMERVSIT(AMERDA,13)),U,5)
 .I AMEROLD'="" S DIR("B")=AMEROLD
 .S DIR(0)="FO^1:100",DIR("A")="*Owner's insurance company"
 .S DIR("?")="Enter free text description"
 .D ^DIR K DIR
 .I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .S AMERNEW=Y
 .S:AMERNEW<0 AMERNEW=""
 .I AMERNEW'=AMEROLD D
 ..I AMERNEW="" S AMERNEW="@"
 ..S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.5////"_Y
 ..S AMERSTRG=$$EDAUDIT^AMEREDAU("13.5",AMEROLD,AMERNEW,"OWNER INSURANCE CO.")
 ..I AMERSTRG="^" Q
 ..S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 .;QD46 - OWNER'S POLICY NUMBER
 .Q:AMERQUIT
 .K DIR("B")
 .S AMEROLD=$P($G(^AMERVSIT(AMERDA,13)),U,6)
 .I AMEROLD'="" S DIR("B")=AMEROLD
 .S DIR(0)="FO^1:100",DIR("A")="*Owner's insurance policy number"
 .S DIR("?")="Enter free text description"
 .D ^DIR K DIR
 .I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .S AMERNEW=Y
 .S:AMERNEW<0 AMERNEW=""
 .I AMERNEW'=AMEROLD D
 ..I AMERNEW="" S AMERNEW="@"
 ..S AMERSTRG=$$EDAUDIT^AMEREDAU("13.6",AMEROLD,AMERNEW,"OWNER POLICY #")
 ..I AMERSTRG="^" Q
 ..S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 ..S DR=$S(DR'="":DR_";",1:""),DR=DR_"13.6////"_AMERNEW
 .Q
 I AMERQUIT=1 Q 0
 ;TIME OF INJURY
 S AMEROLD=$P($G(^AMERVSIT(AMERDA,3)),U,4)
 I AMEROLD'="" S Y=AMEROLD X ^DD("DD") S DIR("B")=Y
 S DIR(0)="DO^::ER",DIR("A")="*Enter the exact time and date of injury"
 S DIR("?")="Enter a time and date in the usual FileMan format (e.g., 1/3/90@1PM)."
 F  Q:Y="^"  D
 .D ^DIR
 .I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y S AMERQUIT=1 Q
 .I $$TCK^AMER2A($P($G(^AMERVSIT(AMERDA,0)),U,1),Y,0,"admission")=0  D
 ..S AMERNEW=Y
 ..S:AMERNEW<0 AMERNEW=""
 ..I AMERNEW'=AMEROLD D
 ...S AMERSTRG=$$EDAUDIT^AMEREDAU("3.4",$$EDDISPL^AMEREDAU(AMEROLD,"D"),$$EDDISPL^AMEREDAU(AMERNEW,"D"),"TIME OF INJURY")
 ...I AMERSTRG="^" Q
 ...S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 ...S DR=$S(DR'="":DR_";",1:""),DR=DR_"3.4////"_AMERNEW
 ...S Y="^"
 ..E  S Y="^"
 .Q
 K DIR
 I AMERQUIT=1 Q 0
 S AMEROLD=$P($G(^AMERVSIT(AMERDA,3)),U,6)
 I AMEROLD'="" S DIR("B")=AMEROLD
 S DIR(0)="FO^1:30",DIR("A")="*Town/village where injury occurred"
 D ^DIR K DIR
 I $D(DUOUT)!$D(DTOUT)!(Y<0) K DIR,DUOUT,DTOUT,Y Q 0
 S AMERNEW=Y
 S:AMERNEW<0 AMERNEW=""
 I AMERNEW'=AMEROLD D
 .S AMERSTRG=$$EDAUDIT^AMEREDAU("3.6",AMEROLD,AMERNEW,"TOWN OF INJURY")
 .I AMERSTRG="^" Q
 .S DR=$S(DR'="":DR_";",1:""),DR=DR_"3.6////"_Y
 .S AMEREDTS=$S(AMEREDTS="":AMERSTRG,1:AMEREDTS_"^"_AMERSTRG)
 I DR'="" D
 .D DIE^AMEREDIT(AMERDA,DR)
 .D MULTAUDT^AMEREDAU(AMEREDTS,AMERAIEN)
 .Q
 Q 1
