BGUPOST ; IHS/OIT/MJL - BGU TRACE FILE CLEANUP ; [ 04/21/2006  9:15 AM ]
 ;;1.5;BGU;**2**;MAY 26, 2005
SETUP ;SET THE SITE PARAMETER FILE
 S DIR(0)="Y",DIR("A")="Should I set the BGU SITE PARAMETERS File?",DIR("B")="Y",DIR("?")="Will set site parameters for the GUI" K DA D ^DIR K DIR
 I 'Y G CLEANUP
 S BGUFILE=90062,BGUIENST="1,",BGUFDA(BGUFILE,BGUIENST,.01)=1,BGUFDA(BGUFILE,BGUIENST,.02)="N",BGUFDA(BGUFILE,BGUIENST,.04)=8000,BGUFDA(BGUFILE,BGUIENST,.05)="N",BGUFDA(BGUFILE,BGUIENST,.06)=3600,BGUFDA(BGUFILE,BGUIENST,.07)=10 D FILE
 ;SCHEDULE THE FOLLOWING TASKS IF THEY AREN'T ALREADY
CLEANUP ;SET THE TASK FOR BGUTRACE CLEANUP
 S DIR(0)="Y",DIR("A")="Should I create the CLEANUP Task for the GUI Trace?",DIR("B")="Y",DIR("?")="Most Sites including Regional Setups having multiple UCI's should answer Yes." K DA D ^DIR K DIR
 I 'Y G TSKLISTN
 S DIC=19.2,DIC(0)="XM",X="BGUTASK TRACE ENTRY CLEANUP" D ^DIC
 I Y<0 S DIC=19 D ^DIC I Y>0 S BGUOPT=+Y D
 .S %H=$H+1 D YMD^%DTC S BGUDT=X
 .S BGUFILE=19.2,BGUIENST="?+1,",BGUFDA(BGUFILE,BGUIENST,.01)=BGUOPT,BGUFDA(BGUFILE,BGUIENST,2)=BGUDT+.02,BGUFDA(BGUFILE,BGUIENST,5)=^%ZOSF("VOL"),BGUFDA(BGUFILE,BGUIENST,6)="1D"
 .D FILE
TSKLISTN ;
 ;SET THE STARTUP TASK FOR THE LISTENER
 S DIR(0)="Y",DIR("A")="Should I create the STARTUP Task for the GUI Listener?",DIR("B")="Y",DIR("?")="Most Sites including Regional Setups having multiple UCI's should answer Yes." K DA D ^DIR K DIR
 I 'Y G STLISTEN
 S DIC=19.2,DIC(0)="XM",X="BGU START LISTENER" D ^DIC
 I Y<0 S DIC=19 D ^DIC I Y>0 S BGUOPT=+Y D
 .S %H=$H+1 D YMD^%DTC S BGUDT=X
 .S BGUFILE=19.2,BGUIENST="?+1,",BGUFDA(BGUFILE,BGUIENST,.01)=BGUOPT,BGUFDA(BGUFILE,BGUIENST,2)=BGUDT+.02,BGUFDA(BGUFILE,BGUIENST,5)=^%ZOSF("VOL"),BGUFDA(BGUFILE,BGUIENST,9)="S"
 .D FILE
 K BGUDT,BGUFILE,BGUIENST,BGUOPT
STLISTEN ;START THE LISTENER
 S DIR(0)="Y",DIR("A")="Should I start the listener on port 8000?",DIR("B")="Y",DIR("?")="Most Sites including Regional Setups having multiple UCI's should answer Yes." K DA D ^DIR K DIR
 I 'Y G EOJ
 W !! D STRT^BGUTCP(8000)
EOJ ;
 S BGUN="" F  S BGUN=$O(^BGUMRU(BGUN)) Q:BGUN=""  K ^(BGUN)
 K BGUN
 Q
 ;
FILE ;
 D UPDATE^DIE(,"BGUFDA",,"BGUEMSG")
 K BGUFDA,BGUEMSG
 Q
