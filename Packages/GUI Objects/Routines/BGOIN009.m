BGOIN009 ; IHS/MSC/PLS - BGO*1.1*9 ;04-Nov-2011 13:38;PLS
 ;;1.1;BGO COMPONENTS;**9**;Mar 20, 2007
EC Q
 ; Preinit
PRE Q
 ; Postinit
POST ;
 ; Clean up menu items
 D CLNMNU
 ; Register RPCs
 D REGNMSP^CIAURPC("BGO","CIAV VUECENTRIC")
 ; Update BGO component versions
 N VER,FDA,PID,IEN,X
 D BMES^XPDUTL("Updating version numbers...")
 F VER=0:1 S X=$P($T(VER+VER),";;",2) Q:'$L(X)  D
 .S PID=$$PRGID^CIAVMCFG($P(X,";"))
 .S:PID FDA(19930.2,PID_",",2)=$P(X,";",2),FDA(19930.2,PID_",",7)=$P(X,";",3)
 D:$D(FDA) FILE^DIE(,"FDA")
 Q
 ;
CLNMNU ;
 ; Remove option from menu
 N OPTION,MENU,DA,DIK,PAR,ERR,X
 S (OPTION,MENU)=""
 S OPTION="BGO IMM STOP ADDING CPT CODES"
 S MENU="BGOIMM MAIN"
 S X=$$DELETE^XPDMENU(MENU,OPTION)
 Q:'+X
 ;Inactivate the option
 D OUT^XPDMENU(OPTION,"No longer used")
 ;Clean out the parameter
 S PAR=""
 S PAR=$O(^XTV(8989.51,"B","BGO IMM STOP ADDING CPT CODES",PAR))
 Q:'+PAR
 S ERR=0
 D NDEL^XPAR("USR",PAR,.ERR)
 Q:ERR>0
 D NDEL^XPAR("DIV",PAR,.ERR)
 Q:ERR>0
 D NDEL^XPAR("PKG",PAR,.ERR)
 Q:ERR>0
 ;Delete the parameter
 S DA=PAR,DIK="^XTV(8989.51," D ^DIK
 Q
VER ;;IHSBGOPATIENTED.BGOPATED;1.2.0.119;22C7E9AC84C5D5E4D5C77EDD4DD5A6DF
 ;;IHSBGOREPHISTORY.IHSBGOREPHISTCTRL;1.2.0.1;3C8875285B094701A508F150CF271E5F
 ;;IHSBGOVCPT.BGOVCPT;1.2.0.62;B7ACDAD96A4C9FA25D4C7489DCBB2DAB
 ;;IHSBGOPOVHISTORY.BGOPOVHISTORY;1.2.0.14;1E9362AA4899C1D5C5537D75E34C0F0C
 ;;IHSBGOVPOV.BGOVPOV;1.2.0.31;1D7CA1175A5A1DFD6B1B659D5369A95E
 ;;
