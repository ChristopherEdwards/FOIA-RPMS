BGOIN010 ; IHS/MSC/PLS - BGO*1.1*10 ;16-May-2012 10:06;PLS
 ;;1.1;BGO COMPONENTS;**10**;Mar 20, 2007
EC Q
 ; Preinit
PRE ;
 N USES,FDA,PID,IEN,X,MSOCX
 S MSOCX=$$FIND1^DIC(19930.2,,,"FILE:MSCOMCT2.OCX")
 Q:'MSOCX
 S FDA(19930.2,MSOCX_",",13)=1
 F USES=0:1 S X=$P($T(USES+USES),";;",2) Q:'$L(X)  D
 .S PID=$$PRGID^CIAVMCFG($P(X,";"))
 .D:PID
 ..; Find IEN of entry in USES multiple
 ..S IEN=$O(^CIAVOBJ(19930.2,PID,9,"B",MSOCX,0)) Q:'IEN  D
 ...S FDA(19930.221,IEN_","_PID_",",.01)="@"
 D:$D(FDA) FILE^DIE(,"FDA")
 Q
 ; Postinit
POST ;
 ; Clean up menu items
 ;D CLNMNU
 ; Register RPCs
 D REGNMSP^CIAURPC("BGO","CIAV VUECENTRIC")
 ; Update BGO component versions
 N VER,FDA,PID,IEN,X
 D BMES^XPDUTL("Updating version numbers...")
 F VER=0:1 S X=$P($T(VER+VER),";;",2) Q:'$L(X)  D
 .S PID=$$PRGID^CIAVMCFG($P(X,";"))
 .S:PID FDA(19930.2,PID_",",2)=$P(X,";",2),FDA(19930.2,PID_",",7)=$P(X,";",3)
 D:$D(FDA) FILE^DIE(,"FDA")
 Q
 ;
CLNMNU ;
 ; Remove option from menu
 N OPTION,MENU,DA,DIK,PAR,ERR,X
 S (OPTION,MENU)=""
 S OPTION="BGO IMM STOP ADDING CPT CODES"
 S MENU="BGOIMM MAIN"
 S X=$$DELETE^XPDMENU(MENU,OPTION)
 Q:'+X
 ;Inactivate the option
 D OUT^XPDMENU(OPTION,"No longer used")
 ;Clean out the parameter
 S PAR=""
 S PAR=$O(^XTV(8989.51,"B","BGO IMM STOP ADDING CPT CODES",PAR))
 Q:'+PAR
 S ERR=0
 D NDEL^XPAR("USR",PAR,.ERR)
 Q:ERR>0
 D NDEL^XPAR("DIV",PAR,.ERR)
 Q:ERR>0
 D NDEL^XPAR("PKG",PAR,.ERR)
 Q:ERR>0
 ;Delete the parameter
 S DA=PAR,DIK="^XTV(8989.51," D ^DIK
 Q
VER ;;IHSBGOVPOV.BGOVPOV;1.2.0.32;8F13AB842DAB0DABBD93FE009E6BEC86
 ;;IHSBGOPOVHISTORY.BGOPOVHISTORY;1.2.0.23;C20681C08B99AE2BA8B463702583B45F
 ;;IHSBGOREPFACTORS.IHSBGOREPFACTORSCTRL;1.2.0.158;01182500AA617782CD10E11767929809
 ;;IHSBGOIMMUNIZATION.BGOIMM;1.2.0.103;50C1D50591111C46A3C8DA45B74DEC5A
 ;;IHSBGOREPHISTORY.IHSBGOREPHISTCTRL;1.2.0.23;5FFA21C3D3A5DA555197B67661543AE5
 ;;FILE:IHSBGOVCDATE.OCX;1.1.0.61;7BA1841F813D1E3952C0ADE171AED32C
 ;;IHSBGOVCPT.BGOVCPT;1.2.0.62;B7ACDAD96A4C9FA25D4C7489DCBB2DAB
 ;;
USES ;;IHSBGOIMMUNIZATION.BGOIMM
 ;;IHSBGOREPHISTORY.IHSBGOREPHISTCTRL
 ;;IHSBGOVCPT.BGOVCPT
 ;;IHSBGOFAMHX.BGOFAMHX
 ;;IHSBGOPROBLEM.BGOPROBLEM
 ;;IHSBGOACTIVITYTIME.IHSBGOACTTIMECTRL
 ;;IHSBGOITEMS.BGOITEMS
 ;;IHSBGOSKINTEST.IHSBGOSK
 ;;IHSBGOICDPICKLIST.ICDPICKLIST
 ;;
