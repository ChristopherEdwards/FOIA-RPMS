BGOUTL3 ; IHS/BAO/TMD - Utilities (continued)  ;09-Apr-2012 14:06;DU
 ;;1.1;BGO COMPONENTS;**11**;Mar 20, 2007;Build 3
GETSET(RET,BGOFILE,BGOFLD,BGOCHK) ;gets set of codes
 ; RET(n)=code^text for code
 N BGOPCC,BGOPCCL,BGOPCCC,BGOLO,BGOHI,BGOPCCD,BGOPCCT
 S BGOLO="abcdefghijklmnopqrstuvwxyz"
 S BGOHI="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
 D FIELD^DID(BGOFILE,BGOFLD,"","POINTER","BGOPCC","BGOPCC")
 S BGOPCCL=$L(BGOPCC("POINTER"),";")-1
 F BGOPCCC=1:1:BGOPCCL D
 . S BGOPCCD=$P($P(BGOPCC("POINTER"),";",BGOPCCC),":",1)
 . S BGOPCCT=$P($P(BGOPCC("POINTER"),";",BGOPCCC),":",2)
 . I BGOCHK'="" D CHK(BGOFILE,BGOPCCD,BGOCHK)
 . I BGOCHK="" S RET(BGOPCCC)=BGOPCCD_"^"_$E(BGOPCCT)_$TR($E(BGOPCCT,2,99),BGOHI,BGOLO)
 Q
CHK(BGOFILE,BGOPCCD,BGOCHK) ;See which codes are acceptable for this file
 N IEN,C
 I BGOFILE=9000010.13 D  Q         ;EXAM FILE
 .S IEN="" S IEN=$O(^AUTTEXAM("B",BGOCHK,IEN))
 .I IEN="" S BGOCHK="" Q
 .S C=$P($G(^AUTTEXAM(IEN,0)),U,2)
 .S X=BGOPCCD
 .D EXAM
 .I $D(X) S RET(BGOPCCC)=BGOPCCD_"^"_$E(BGOPCCT)_$TR($E(BGOPCCT,2,99),BGOHI,BGOLO)
 Q
EXAM ;Check exam codes
 I X="PA",C'=34 K X Q
 I X="PR",C'=34 K X Q
 I X="PAP",C'=34 K X Q
 I X="A",C=34 K X Q
 I X="A",C=35 K X Q
 I X="A",C=36 K X Q
 I X="PO",(C'=35&(C'=36)) K X Q
 Q
