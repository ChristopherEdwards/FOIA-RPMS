BGOVPOV ; IHS/BAO/TMD - Visit POV maintenance ;02-Nov-2010 00:27;PLS
 ;;1.1;BGO COMPONENTS;**1,3,4,5,6,7**;Mar 20, 2007
 ; Check for note signed by provider
 ; Patch 6 added check for asthma DX
 ; Patch 7 added bulletin for first time diagnosis
CKSIGNBY(RET,VIEN) ;EP
 N X
 S VIEN=+VIEN
 Q:'VIEN
 S RET=$$PRIPRV^BGOUTL(VIEN)
 Q:RET<0
 S RET=+RET
 I RET'=DUZ S RET=$$ERR^BGOUTL(1089) Q
 S RET="",X=0
 F  S X=$O(^TIU(8925,"V",VIEN,X)) Q:'X  D  Q:RET
 .S:$P($G(^TIU(8925,X,15)),U,2)=DUZ RET=X
 S:'RET RET=$$ERR^BGOUTL(1090)
 Q
 ; Lookup ICD code
 ;  INP = ICD code ^ Reference Date
 ;  RET = Null if not found or ICD IEN^ICD Text
GETICD(RET,INP) ;EP
 N IEN,ICD,CDT
 S RET="",ICD=$P(INP,U),CDT=$P(INP,U,2)
 I $$CSVACT^BGOUTL2("ICDCODE") D
 .S IEN=$$ICDDX^ICDCODE(ICD,CDT)
 .S:IEN>0 RET=+IEN_U_$P(IEN,U,4)
 E  D
 .S IEN=$O(^ICD9("AB",ICD,0))
 .S:'IEN IEN=$O(^ICD9("AB",ICD_" ",0))
 .S:IEN RET=IEN_U_$P(^ICD9(IEN,0),U,3)
 Q
 ; Return ICD code given ICD IEN
 ; INP = ICD IEN ^ Reference Date
 ; RET = Null if not found or ICD Code^ICD Text
GETCODE(RET,INP) ;EP
 N ICDIEN,CDT
 S ICDIEN=$P(INP,U),CDT=$P(INP,U,2)
 I $$CSVACT^BGOUTL2("ICDCODE") D
 .S RET=$$ICDDX^ICDCODE(ICDIEN,CDT)
 .S RET=$S(RET<0:"",1:$P(RET,U,2)_U_$P(RET,U,4))
 E  I $D(^ICD9(+ICDIEN,0)) S RET=$P(^(0),U)_U_$P(^(0),U,3)
 E  S RET=""
 Q
 ; Set primary/secondary for a POV
 ;  INP = VPOV IEN ^ Primary/Secondary (P/S)
SETPRI(RET,INP,NOEVT) ;EP
 N PRI,VFIEN,VIEN,FDA,X
 S VFIEN=+INP
 I 'VFIEN S RET=$$ERR^BGOUTL(1008) Q
 S PRI=$P(INP,U,2)
 I '$D(^AUPNVPOV(VFIEN,0)) S RET=$$ERR^BGOUTL(1091) Q
 S VIEN=$P(^AUPNVPOV(VFIEN,0),U,3)
 S FDA($$FNUM,VFIEN_",",.12)=PRI
 I PRI="P" D
 .S X=0
 .F  S X=$O(^AUPNVPOV("AD",VIEN,X)) Q:'X  D:X'=VFIEN
 ..S:$P($G(^AUPNVPOV(X,0)),U,12)="P" FDA($$FNUM,X_",",.12)="S"
 S RET=$$UPDATE^BGOUTL(.FDA)
 Q:RET
 I $$FIXVPOVS^BGOVPOV1(VIEN,.VFIEN)  ; Fix VPOV sequencing
 D:'$G(NOEVT) VFEVT^BGOUTL2($$FNUM,VFIEN,1)
 S RET=VFIEN
 Q
 ; Returns POV for current visit context
TIUSTR() N X,Y
 S X=$$GETVAR^CIANBUTL("ENCOUNTER.ID.ALTERNATEVISITID",,"CONTEXT.ENCOUNTER")
 I X="" Q " "
 S X=$$VSTR2VIS^BEHOENCX(DFN,X)
 I X<1 Q " "
 D GET(.X,X_"^^1")
 S Y=$G(@X@(1))
 K @X
 Q $S(Y<0:"",1:Y)
 ; Returns POV for current visit context in multi-line format
TIUML(RET) ;EP
 N X,I,CNT
 S X=$$GETVAR^CIANBUTL("ENCOUNTER.ID.ALTERNATEVISITID",,"CONTEXT.ENCOUNTER")
 I X="" Q " "
 S X=$$VSTR2VIS^BEHOENCX(DFN,X)
 I X<1 Q " "
 D GET(.X,X_"^^2")
 K @RET
 S (I,CNT)=0
 F  S I=$O(@X@(I)) Q:'I  D
 .S CNT=CNT+1
 .S @RET@(CNT,0)=@X@(I)
 S:'CNT @RET@(1,0)="No Diagnosis."
 K @X
 Q "~@"_$NA(@RET)
 ; Get VPOVs associated with a visit
 ;  INP=Visit IEN ^ VPOV IEN (optional) ^ Format (0-detailed,1-tiu string,2-multi-line)
GET(RET,INP) ;EP
 N CNT,REC,VIEN,VPOVIEN,FORMAT,IEN,PRV,PNAR,POV,ICD,ICDNAME,VDATE,DFN
 N STAGE,MOD,CAUSE,REVISIT,PRIM,ONSET,IDT,IPL,ICAU,FNUM,OFF,ASTHMA,CONTROL
 ;MSC/MGH - 07/08/09 - Offset created to support VistA and RPMS
 S OFF=$S($G(DUZ("AG"))="I":0,1:9999999)
 S RET=$$TMPGBL^BGOUTL
 S FNUM=$$FNUM
 S VIEN=+INP
 S VPOVIEN=$P(INP,U,2)
 S FORMAT=+$P(INP,U,3)
 S (CNT,IEN)=0
 F  S IEN=$O(^AUPNVPOV("AD",VIEN,IEN)) Q:'IEN  D
 .I VPOVIEN,VPOVIEN'=IEN Q
 .S REC=$G(^AUPNVPOV(IEN,0))
 .Q:REC=""
 .S PRV=$P($G(^AUPNVPOV(IEN,12)),U,4)
 .S:PRV PRV=$P($G(^VA(200,PRV,0)),U)
 .S PNAR=$P(REC,U,4)
 .S:PNAR PNAR=$P($G(^AUTNPOV(PNAR,0)),U)
 .I FORMAT=1 D
 ..S:$L(PNAR) CNT=CNT+1,@RET@(1)=$S(CNT=1:"",1:@RET@(1)_"; ")_PNAR
 .E  I FORMAT=2 D
 ..S:$L(PNAR) CNT=CNT+1,@RET@(CNT)=CNT_") "_PNAR_":"
 .E  D
 ..S POV=+REC
 ..S ICD=$P($G(^ICD9(POV,0)),U),ICDNAME=$P($G(^(0)),U,3)
 ..Q:ICD=""
 ..S F=$P($G(^AUPNVSIT(VIEN,0)),U,6),DFN=$P($G(^AUPNVSIT(VIEN,0)),U,5)
 ..I F S FAC=$P($G(^AUTTLOC(F,0)),U,10),FACNAM=$P($G(^(0)),U)
 ..E  S (FAC,FACNAM)=""
 ..S:FACNAM FACNAM=$P($G(^DIC(4,FACNAM,0)),U)
 ..S VDATE=$$FMTDATE^BGOUTL($P($G(^AUPNVSIT(VIEN,0)),U))
 ..S STAGE=$P(REC,U,5)
 ..S MOD=$$EXTERNAL^DILFD(FNUM,.06,,$P(REC,U,6))
 ..S CAUSE=$$EXTERNAL^DILFD(FNUM,.07,,$P(REC,U,7))
 ..S REVISIT=$$EXTERNAL^DILFD(FNUM,.08,,$P(REC,U,8))
 ..S PRIM=$P(REC,U,12)
 ..S PRIM=$$EXTERNAL^DILFD(FNUM,.12,,$S($L(PRIM):PRIM,1:"S"))
 ..I DUZ("AG")="I" S ONSET=$$FMTDATE^BGOUTL($P(REC,U,17))
 ..E  S ONSET=$$FMTDATE^BGOUTL($P($G(^AUPNVPOV(IEN,9999999)),U,17))
 ..S IDT=$$FMTDATE^BGOUTL($P(REC,U,13))
 ..S IPL=$P(REC,U,11)
 ..S IPL=$$EXTERNAL^DILFD(FNUM,.11,,IPL)_"~"_IPL
 ..S ICAU=$P(REC,U,9)
 ..S:ICAU ICAU=$P($G(^ICD9(ICAU,0)),U,3)_"~"_ICAU
 ..;CHECK FOR ASTHMA DX
 ..S CONTROL=""
 ..I DUZ("AG")="I" D
 ...S ASTHMA=$$CHECK^BGOASLK(ICD)
 ...I ASTHMA=1 S CONTROL=$$ACONTROL^BGOASLK(DFN) I CONTROL="" S CONTROL="NONE RECORDED"
 ..S CNT=CNT+1,@RET@(CNT)=IEN_U_VDATE_U_FAC_U_FACNAM_U_ICD_U_ICDNAME_U_PNAR_U_MOD_U_ONSET_U_STAGE_U_REVISIT_U_CAUSE_U_IDT_U_ICAU_U_IPL_U_PRIM_U_POV_U_PRV_U_VIEN_U_$$ISLOCKED^BEHOENCX(VIEN)_U_CONTROL
 Q
 ; Delete a VPOV entry
DEL(RET,VPOV) ;EP
 D VFDEL^BGOUTL2(.RET,$$FNUM,VPOV)
 Q
 ; Checks validity of ICD code
 ;  ICDIEN = ICD IEN
 ;  ACTDT  = Active Date
 ;  Returns null if valid or -n^error text if not
CHKICD(ICDIEN,ACTDT) ;EP
 N RET,X
 S RET=""
 S ACTDT=$G(ACTDT,DT)
 I $$CSVACT^BGOUTL2("ICDCODE") D
 .S X=$$ICDDX^ICDCODE(ICDIEN,ACTDT)
 .I X<0 S RET=$$ERR^BGOUTL(1092)
 .E  I '$P(X,U,10) S RET=$$ERR^BGOUTL(1093)
 E  D
 .N DELDT
 .S X=$G(^ICD9(ICDIEN,0))
 .I '$L(X) S RET=$$ERR^BGOUTL(1092) Q
 .I $P(X,U,9) S RET=$$ERR^BGOUTL(1093) Q
 .S DELDT=$P(X,U,11)
 .I DELDT D
 ..S:'ACTDT ACTDT=DT
 ..S:$$FMDIFF^XLFDT(ACTDT,DELDT)>-1 RET=$$ERR^BGOUTL(1093)
 Q RET
 ;
CHRTREVW(RET,VIEN) ;EP
 N VCODE
 S VCODE=$$GET^XPAR("ALL","BGO POV DEFAULT CHART",1,"E")  ;P6
 I VCODE="" S VCODE="V68.9"
 ;D TELECHRT(.RET,VIEN,"C","V68.9","CHART REVIEW")
 D TELECHRT(.RET,VIEN,"C",VCODE,"CHART REVIEW")
 Q
 ;
TELEPHON(RET,VIEN) ;EP
 N VCODE
 S VCODE=$$GET^XPAR("ALL","BGO POV DEFAULT TELEPHONE",1,"E")  ;P6
 I VCODE="" S VCODE="V65.9"
 ;D TELECHRT(.RET,VIEN,"T","V65.9","TELEPHONE CALL")
 D TELECHRT(.RET,VIEN,"T",VCODE,"TELEPHONE CALL")
 Q
TELECHRT(RET,VIEN,VCAT,VCODE,DESC) ;
 N DFN,X
 Q:$$ISLOCKED^BEHOENCX(VIEN)
 S X=$G(^AUPNVSIT(+VIEN,0))
 S DFN=$P(X,U,5)
 Q:'DFN
 Q:$P(X,U,7)'=VCAT
 D:'$D(^AUPNVPOV("AD",VIEN)) SET(.RET,U_VIEN_U_VCODE_U_DFN_U_DESC)
 Q
 ; Add/Edit VPOV data
 ;  INP = VPOV IEN [1] ^ Visit IEN [2] ^ ICD Code IEN [3] ^ Patient IEN [4] ^ Narrative [5] ^ Stage [6] ^
 ;        Modifier [7] ^ Cause Dx [8] ^ First/Revisit [9] ^ Injury E-Code [10] ^ Injury Place [11] ^
 ;        Primary/Secondary [12] ^ Injury Date [13] ^ Onset Date [14] ^ Provider IEN [15] ^ asthma control [16]
SET(RET,INP) ;EP
 N VFIEN,VIEN,DFN,TYPE,NARR,STAGE,MOD,CAUSEDX,REVISIT,ECODE,PLACE,CONTROL
 N PRIM,INJDT,ONSET,FDA,FNUM,VFNEW,PRV,CANDUP,OFF,APCDVSIT,PXCEVIEN
 S RET="",FNUM=$$FNUM
 ;MSC/MGH - 07/08/09 - Offset to support VistA and RPMS
 S OFF=$S($G(DUZ("AG"))="I":0,1:9999999)
 S VFIEN=+INP
 S VFNEW='VFIEN
 S VIEN=$P(INP,U,2)
 I 'VIEN S RET=$$ERR^BGOUTL(1008) Q
 I $G(DUZ("AG"))="I" S APCDVSIT=VIEN
 E  S PXCEVIEN=VIEN
 S TYPE=$P(INP,U,3)
 S TYPE=$S($E(TYPE)="`":$E(TYPE,2,99),1:$O(^ICD9("AB",TYPE_$S($$CSVACT^BGOUTL2():" ",1:""),0)))
 I 'TYPE S RET=$$ERR^BGOUTL(1094) Q
 S CANDUP=+$G(^ICD9(TYPE,0))=.9999
 S DFN=$P(INP,U,4)
 S RET=$$CHKVISIT^BGOUTL(VIEN,DFN)
 Q:RET
 D CHECK(.RET,TYPE_U_DFN_U_+$G(^AUPNVSIT(VIEN,0)))
 Q:RET
 S RET=$$FNDNARR^BGOUTL2($P(INP,U,5))
 Q:RET<0
 S NARR=$S(RET:"`"_RET,1:""),RET=""
 S STAGE=$P(INP,U,6)
 S MOD=$P(INP,U,7)
 S CAUSEDX=$P(INP,U,8)
 S REVISIT=$P(INP,U,9)
 S ECODE=$P(INP,U,10)
 S PLACE=$P(INP,U,11)
 S PRIM=$P(INP,U,12)
 S CONTROL=$P(INP,U,16)
 S PRIM=$S($L(PRIM):PRIM,$$FNDPRI^BGOVPOV1(VIEN):"S",1:"P")
 S INJDT=$$CVTDATE^BGOUTL($P(INP,U,13))
 S ONSET=$$CVTDATE^BGOUTL($P(INP,U,14))
 S PRV=$P(INP,U,15)
 S RET=$$CHKVISIT^BGOUTL(VIEN,DFN)
 Q:RET
 I 'VFIEN D  Q:'VFIEN
 .D VFNEW^BGOUTL2(.RET,FNUM,TYPE,VIEN,$S(CANDUP:"",1:"POV"))
 .S:RET>0 VFIEN=RET,RET=""
 S FDA=$NA(FDA(FNUM,VFIEN_","))
 S @FDA@(.01)="`"_TYPE
 S @FDA@(.04)=NARR
 S @FDA@(.05)=STAGE
 S @FDA@(.06)=MOD
 S @FDA@(.07)=CAUSEDX
 S @FDA@(.08)=REVISIT
 S @FDA@(.09)=$S(ECODE:"`"_ECODE,1:"")
 S @FDA@(.11)=PLACE
 S @FDA@(.12)=PRIM
 S @FDA@(.13)=INJDT
 S @FDA@(.17+OFF)=ONSET  ;P6
 S @FDA@(1201)="N"
 ;S:VFNEW @FDA@(1204)="`"_$S(PRV:PRV,1:DUZ)  ; PATCH 5
 S @FDA@(1204)="`"_DUZ  ; PATCH 5
 S RET=$$UPDATE^BGOUTL(.FDA,"E@")
 I RET,VFNEW,$$DELETE^BGOUTL(FNUM,VFIEN)
 Q:RET
 I PRIM="P" D  Q:RET
 .D SETPRI(.RET,VFIEN_U_PRIM,1)
 .S:RET>0 VFIEN=RET,RET=""
 E  I $$FIXVPOVS^BGOVPOV1(VIEN,.VFIEN)  ; Fix VPOV sequencing
 D VFEVT^BGOUTL2(FNUM,VFIEN,'VFNEW)
 S RET=VFIEN
 ;Patch 6 check for asthma diagnoses
 I DUZ("AG")="I" D
 . N ASTHMA
 . S ASTHMA=$$CHECK^BGOASLK(TYPE)
 . I ASTHMA=1 S RET=RET_U_ASTHMA
 . I CONTROL'="" D ASTHMA(DFN,CONTROL)
 .;Patch 7 check for bulletin for new dx
 .N DA,X,APCDDATE,APCDVSIT,ATXAD,APCDPAT,AUPNPAT
 .S DA=VFIEN,X=TYPE
 .S APCDDATE=$P($G(^AUPNVSIT(VIEN,0)),U,1)
 .S APCDVSIT=VIEN,ATXAD="",(APCDPAT,AUPNPAT)=DFN
 .D ^ATXPOV
 Q
 ; Check validity of an ICD9 code
 ;  INP = ICD IEN ^ Patient IEN ^ Visit Date
 ; .RET = Returned as -1^Reason if not valid or null if valid
CHECK(RET,INP) ;EP
 N DFN,ICDIEN,VDT,X
 S RET=""
 S ICDIEN=+INP
 Q:'ICDIEN
 S DFN=$P(INP,U,2)
 S VDT=$P(INP,U,3)
 S RET=$$CHKICD(ICDIEN,VDT)
 Q:RET<0
 S X=$G(^ICD9(ICDIEN,0))
 I $P(X,U,10)'="",$P(X,U,10)'=$P(^DPT(DFN,0),U,2) S RET=$$ERR^BGOUTL(1095)
 E  S RET=""
 Q
 ; Return recent POV's by patient or by visit
 ;  INP = Patient IEN ^ Max Records ^ Visit IEN
 ;  RET returned as a list of records in the format:
 ;    Visit Date [1] ^ Facility ID [2] ^ Facility Name [3] ^ ICD Code [4] ^ ICD Text [5] ^
 ;    Provider Narrative [6] ^ V POV IEN [7] ^ Visit IEN [8] ^ ICD IEN [9] ^ Visit Locked [10] ^
 ;    Onset Date [11]
RECENT(RET,INP) ;EP
 N DFN,CNT,VIEN,MAX,VPOV,VDT
 S RET=$$TMPGBL^BGOUTL
 S DFN=$P(INP,U)
 S MAX=$P(INP,U,2)
 S:'MAX MAX=9999
 S VIEN=$P(INP,U,3)
 S CNT=0
 D RECBYVIS:VIEN,RECBYPAT:'VIEN
 Q
 ; VPOV's by patient
RECBYPAT S VDT=0
 F  S VDT=$O(^AUPNVPOV("AA",DFN,VDT)) Q:'VDT  D  Q:CNT'<MAX
 .S VPOV=0
 .F  S VPOV=$O(^AUPNVPOV("AA",DFN,VDT,VPOV)) Q:'VPOV  D RECBUILD Q:CNT'<MAX
 Q
 ; VPOV's by visit
RECBYVIS S VPOV=0
 F  S VPOV=$O(^AUPNVPOV("AD",VIEN,VPOV)) Q:'VPOV  D RECBUILD
 Q
RECBUILD N REC,POV,ICD,ICDNAME,VIEN,F,FAC,FACNAM,VSIT,VDATE,PNAR,ONSET
 S REC=$G(^AUPNVPOV(VPOV,0))
 S POV=+REC
 Q:'POV
 S ICD=$P($G(^ICD9(POV,0)),U),ICDNAME=$P($G(^(0)),U,3)
 Q:ICD=""
 S VIEN=$P(REC,U,3)
 Q:'VIEN
 S VSIT=$G(^AUPNVSIT(VIEN,0))
 S F=$P(VSIT,U,6)
 I F S FAC=$P($G(^AUTTLOC(F,0)),U,10),FACNAM=$P($G(^(0)),U)
 E  S (FAC,FACNAM)=""
 S:FACNAM FACNAM=$P($G(^DIC(4,FACNAM,0)),U)
 S VDATE=$$FMTDATE^BGOUTL(+VSIT)
 S PNAR=$P(REC,U,4)
 S:PNAR PNAR=$P($G(^AUTNPOV(PNAR,0)),U)
 S ONSET=$$FMTDATE^BGOUTL($P(REC,U,17))
 S CNT=CNT+1
 S @RET@(CNT)=VDATE_U_FAC_U_FACNAM_U_ICD_U_ICDNAME_U_PNAR_U_VPOV_U_VIEN_U_POV_U_$$ISLOCKED^BEHOENCX(VIEN)_U_ONSET
 Q
ASTHMA(DFN,CONTROL) ;Find last control, if it has changed store the change
 N LEVEL,INP,RET
 Q:CONTROL="@"
 S LEVEL=$$ACONTROL^BGOASLK(DFN)
 I LEVEL'=CONTROL D
 .S INP=U_VIEN_U_CONTROL
 .D SET^BGOVAST(.RET,INP)
 Q
 ; Return V File #
FNUM() Q 9000010.07
