HLCSTERM ;ALB/RJS - SET UP VIDEO ATTRIBUTES - 8/1/94 ;07/28/98  09:43 [ 04/02/2003   8:38 AM ]
 ;;1.6;HEALTH LEVEL SEVEN;**1004**;APR 1, 2003
 ;;1.6;HEALTH LEVEL SEVEN;**40,49**;Oct 13, 1995
TERM ; -- set up term characteristics
 N X
 I '$D(IOST(0)) D HOME^%ZIS
 S X=$$IO D ENDR^%ZISS
 S (HLCON,HLCOFF)=""
 I $E(IOST,1,4)="C-VT" S HLCOFF=$C(13,27,91)_"?25l"_$C(13),HLCON=$C(27,91)_"?25h"
 Q
HEADER ;
 ;Write out Header
 ;
 N HLMIDDLE,HLLNGTH,HLJUST
 W @IOF,!
 S HLPARAM=$$PARAM^HLCS2
 S HLSITE=$P(HLPARAM,U,5),HLPROD=$P(HLPARAM,U,3)
 S HLHDR="SYSTEM LINK MONITOR for "_HLSITE_" ("_HLPROD_" System)"
 S HLMIDDLE=IOM\2,HLLNGTH=$L(HLHDR)\2,HLJUST=HLMIDDLE-HLLNGTH
 D WDATA^HLCSMON1(HLJUST,1,IORVON,IORVOFF,HLHDR,75)
 D WDATA^HLCSMON1(5,4,IOUON,IOUOFF,"NODE",8)
 D WDATA^HLCSMON1(16,3,"","","MESSAGES",8),WDATA^HLCSMON1(16,4,IOUON,IOUOFF,"RECEIVED",8)
 D WDATA^HLCSMON1(26,3,"","","MESSAGES",8),WDATA^HLCSMON1(26,4,IOUON,IOUOFF,"PROCESSED",9)
 D WDATA^HLCSMON1(37,3,"","","MESSAGES",8),WDATA^HLCSMON1(37,4,IOUON,IOUOFF,"TO SEND ",8)
 D WDATA^HLCSMON1(47,3,"","","MESSAGES",8),WDATA^HLCSMON1(47,4,IOUON,IOUOFF,"SENT    ",8)
 D WDATA^HLCSMON1(57,3,"","","DEVICE",8),WDATA^HLCSMON1(57,4,IOUON,IOUOFF,"TYPE   ",8)
 D WDATA^HLCSMON1(66,4,IOUON,IOUOFF,"STATE",8)
 Q
KVAR ;
 D KILL^%ZISS
 K HLCON,HLCOFF
 Q
IO() ; -- what device params
 Q "IOELALL;IOELEOL;IORESET;IORVON;IORVOFF;IOIL;IOSTBM;IOSC;IORC;IOEDEOP;IOINHI;IOINORM;IOUON;IOUOFF;IOBOFF;IOBON"
 Q
