HLOCLNT3 ;ALB/CJM- Updates messages missing application acks - 10/4/94 1pm
 ;;1.6;HEALTH LEVEL SEVEN;**126**;Oct 13, 1995
 ;
DOWORK(WORK) ;
 ;
 N CUTOFF,MSGIEN,QUIT
 S QUIT=0
 ;
 ;7 day wait for an application ack is more than reasonable
 S CUTOFF=$$FMADD^XLFDT($$NOW^XLFDT,-3)
 ;
 S MSGIEN=+$G(^HLTMP("LAST IEN CHECKED FOR MISSING APPLICATION ACK"))
 F  S MSGIEN=$O(^HLB(MSGIEN)) Q:'MSGIEN  Q:MSGIEN>99999999999  D  Q:QUIT
 .N MSG,HDR
 .Q:'$$GETMSG^HLOMSG(MSGIEN,.MSG)
 .Q:'MSG("DT/TM")
 .Q:'MSG("BODY")
 .I MSG("DT/TM")>CUTOFF S:MSG("DT/TM CREATED")>CUTOFF QUIT=1,MSGIEN=MSGIEN-1 Q
 .Q:MSG("STATUS")'=""
 .Q:MSG("DIRECTION")'="OUT"
 .Q:MSG("BATCH")
 .Q:MSG("STATUS","APP ACK'D")
 .;Q:MSG("STATUS","APP ACK RESPONSE")=""
 .;message has been in a non-complete status for a longtime, pending an application ack - set status to error
 .S $P(^HLB(MSGIEN,0),"^",20)="AE"
 .S $P(^HLB(MSGIEN,0),"^",21)="MISSING APPLICATION ACKNOWLEDGMENT"
 .M HDR=MSG("HDR")
 .Q:'$$PARSEHDR^HLOPRS(.HDR)
 .S ^HLB("ERRORS","AE",$S($L(HDR("RECEIVING APPLICATION")):HDR("RECEIVING APPLICATION"),1:"UNKNOWN"),MSG("DT/TM CREATED"),MSGIEN)=""
 S:MSGIEN>99999999999 MSGIEN=0
 S ^HLTMP("LAST IEN CHECKED FOR MISSING APPLICATION ACK")=MSGIEN
 Q
