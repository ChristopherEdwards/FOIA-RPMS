HLZP4ENV ;IHS/OIRM/DSD/AEF - PATCH HL*1.6*1004 ENVIRONMENT CHECK ROUTINE [ 04/02/2003   8:37 AM ]
 ;;1.6;HEALTH LEVEL SEVEN;**1004**;APR 1, 2003
 ;
EN ;EP -- MAIN ENTRY POINT      
 ;
 D ^XBKVAR
 D NOQUE
 D VERSION
 D NOASK
 D OK
 Q
NOQUE ;----- PREVENT QUEUEING OF INSTALL   
 ;
 S XPDNOQUE=1
 Q
VERSION ;----- CHECK VERSION
 ;
 I "1.6"'=$$VERSION^XPDUTL("HEALTH LEVEL SEVEN") D
 . S XPDQUIT=1
 . D BMES^XPDUTL("CURRENT VERSION IS NOT 1.6, CANNOT CONTINUE WITH INSTALL")
 Q
NOASK ;----- SUPPRESS ASK 'DISABLE OPTS','MOVE ROUTINES' PROMPT
 ;
 S XPDDIQ("XPDZ1")=0 ;'DISABLE OPTIONS' SET DEFAULT TO 'NO' & NOT ASK
 S XPDDIQ("XPDZ2")=0 ;'MOVE ROUTINES' SET DEFAULT TO 'NO' & NOT ASK
 Q
OK ;----- OK TO INSTALL?
 ;
 I $G(XPDQUIT) D
 . D BMES^XPDUTL("INSTALL ABORTED!")
 I '$G(XPDQUIT) D
 . D BMES^XPDUTL("EVERYTHING LOOKS OK")
 Q
