BHSLAB ;IHS/CIA/MGH - Health Summary for V lab file ;14-Aug-2009 12:55;MGH
 ;;1.0;HEALTH SUMMARY COMPONENTS;**2,3**;March 17, 2006
 ;===================================================================
 ;VA Health Summary for IHS V lab file
 ;Take from APCHS3
 ; IHS/TUCSON/LAB - PART 3 OF APCHS -- SUMMARY PRODUCTION COMPONENTS
 ;;2.0;IHS RPMS/PCC Health Summary;;JUN 24, 1997
 ;Patch 3 added result date
LAB ; ******************** LAB DATA * 9000010.09 *******
 N BHSPAT,BHSQ,X
 S BHSPAT=DFN,BHSELX=""
 I '$D(^AUPNVLAB("AE",BHSPAT)) D EKGLAB^BHSLAB1 S BHST="LAB",BHSFN=60 D DISPREF^BHSRAD G LABX
 D CKP^GMTSUP Q:$D(GMTSQIT)
 ; <SETUP>
 K ^TMP($J,"BHSLRT"),^("BHSLDT"),^("BHSLD2")
 ; <PROCESS>
 D LBLD,LPRT
 W ! D EKGLAB^BHSLAB1
 ; <CLEANUP>
 ;Patch 2, lab refusals
 S BHST="LAB",BHSFN=60 D DISPREF^BHSRAD
LABX K BHSLT,BHSLR,BHSLTX,BHSDFN,BHSNDT,BHSLRT,BHSLDT,BHST,BHSFN,BHSLD2,BHSNA,BHSIVD,BHSDTL,BHSI,BHSJ,BHSL,BHSLL,BHSDSN,BHSIDN,BHSNMX,BHSLW,BHSMXL,BHSLTO,BHSLTN,BHSELX,Y
 K ^TMP($J,"BHSLRT"),^("BHSLDT"),^("BHSLD2")
 Q
 ; <BUILD>
LBLD K BHSLRT,BHSLDT,BHSLD2
 S (BHSNDT,BHSMXL,BHSLTN)=0
 S BHSIVD="" F BHSQ=0:0 S BHSIVD=$O(^AUPNVLAB("AE",BHSPAT,BHSIVD)) Q:'BHSIVD!(BHSIVD>GMTSDLM)  D
 .D LDATE S:$D(^TMP($J,"BHSLDT",BHSIVD)) GMTSSNDM=GMTSNDM-1 Q:'GMTSNDM
 S BHSIVD="" F BHSI=1:1 S BHSIVD=$O(^TMP($J,"BHSLDT",BHSIVD)) Q:BHSIVD=""  D
 .S ^TMP($J,"BHSLD2",BHSI)=BHSIVD
 Q
LDATE S BHSLT="" F BHSQ=0:0 S BHSLT=$O(^AUPNVLAB("AE",BHSPAT,BHSIVD,BHSLT)) Q:'BHSLT  D
 .S BHSDFN=$O(^AUPNVLAB("AE",BHSPAT,BHSIVD,BHSLT,""))
 .D LSET
 Q
LSET ;
 S BHSLR=$P(^AUPNVLAB(BHSDFN,0),U,4) Q:BHSLR=""
 S (BHSLTO,BHSLTN)=BHSLT
 S BHSLTO=10000+BHSLTO_"-"_BHSLT
 S Y=$$RDT(BHSDFN)
 S ^TMP($J,"BHSLRT",BHSLTO,BHSIVD)=BHSLR_$S(Y]"":" (",1:"")_$$RDT(BHSDFN)_$S(Y]"":")",1:"") S BHSLTX=$P(^LAB(60,BHSLT,0),U,1) S:$L(BHSLTX)>BHSMXL BHSMXL=$L(BHSLTX)
 S:'$D(^TMP($J,"BHSLDT",BHSIVD)) BHSNDT=BHSNDT+1
 S ^TMP($J,"BHSLDT",BHSIVD)=""
 Q
 ; <PRINT>
LPRT S BHSLW=BHSMXL+1,BHSLL=25,BHSNMX=(80-1-BHSLW)\BHSLL
 F BHSDSN=1:BHSNMX:BHSNDT D LPRT2
 Q
LPRT2 ;
 S BHSDTL="" F BHSI=1:1:BHSNMX S BHSJ=BHSDSN+BHSI-1 Q:BHSJ>BHSNDT  D
 .S X=-^TMP($J,"BHSLD2",BHSJ)\1+9999999 D REGDT4^GMTSU S BHSDTL=BHSDTL_$J(X,BHSLL)
 D CKP^GMTSUP Q:$D(GMTSQIT)  W ! D CKP^GMTSUP Q:$D(GMTSQIT)  W ?BHSLW,BHSDTL
 D CKP^GMTSUP Q:$D(GMTSQIT)  W !
 S BHSLT="" F BHSQ=0:0 S BHSLT=$O(^TMP($J,"BHSLRT",BHSLT)) Q:BHSLT=""  D
 .S BHSLTX=$P(^LAB(60,$P(BHSLT,"-",2),0),U,1) D LPRT3 I BHSNA D CKP^GMTSUP Q:$D(GMTSQIT)  W:GMTSNPG ?BHSLW,BHSDTL,! W BHSLTX,?BHSLW,BHSL,!
 Q
LPRT3 S BHSNA=0 S BHSL="" F BHSIDN=1:1:BHSNMX S BHSJ=BHSDSN+BHSIDN-1 Q:BHSJ>BHSNDT  D
 .S BHSIVD=^TMP($J,"BHSLD2",BHSJ) D LADD
 Q
LADD I $D(^TMP($J,"BHSLRT",BHSLT,BHSIVD)) S BHSNA=BHSNA+1 S BHSL=BHSL_$J(^TMP($J,"BHSLRT",BHSLT,BHSIVD),BHSLL)
 E  S BHSL=BHSL_$J(" ",BHSLL)
 Q
RDT(R) ;
 I $G(R)="" Q ""
 NEW X
 S X=$P($G(^AUPNVLAB(R,12)),U,12)
 I X="" Q ""
 Q $$DATE^APCHSMU($P(X,"."))_"@"_$P($P($$FMTE^XLFDT(X),"@",2),":",1,2)
