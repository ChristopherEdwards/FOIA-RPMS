AUMDO1A ; IHS/OIRM/DSD/JCM,AEF - UPDATE ICD0 AND ICD9 GLOBALS ;  [ 12/03/1998   2:35 PM ]
 ;;99.1;ICD UPDATE;;DEC 03, 1998
 W !!,"ENTRY NOT PERMITTED HERE (^AUMDO1A)",! Q
SPECNOTE ; SPECIAL NOTE FOR PROGRAMMERS
 ; ***NOTE - ALL VARIABLES ARE IN THE AUMDO("variable name') ARRAY
 ;
EN ; ENTRY POINT FROM PROCICD+5^AUMDO1
 F AUMDO("DO LABEL")="CKREQD","ADDCHG","CKSTAT","EN^AUMDO1B" D:'AUMDO("QUIT") @AUMDO("DO LABEL")
 Q  ; RETURN TO AUMDO1
CKREQD ; CHECK UPDATE FILE FOR BOTH 0 AND 2100000 NODES - BOTH REQUIRED
 I '$D(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",0)")) S AUMDO("QUIT")=1 D:$Y>55 HDR^AUMDO W !,?27,"Invalid update - Missing node 0"
 I '$D(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",2100000)")) S AUMDO("QUIT")=1 D:$Y>55 HDR^AUMDO W !,?27,"Invalid update - Missing node 2100000"
 Q
ADDCHG ; CHECK IF ADD OR CHANGE
 D @$S($P(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",2100000)"),U,2)="C":"CHG",$P(^(2100000),U,2)="A":"ADD",1:"INV")
 W !,?27,$E(AUMDO("UPD GL REF"),1,9)_"("_AUMDO("UPD DFN")_"  "_$E(AUMDO("ICD GL REF"),1,5)_"("_$S(AUMDO("ICD DFN")'="":AUMDO("ICD DFN"),1:"NULL")
 Q
CHG ; CHG ICD FILE DFN - IF NULL, THEN ERROR (DOES NOT EXIST)
 W ?27,"Changing old ICD entry"
 S AUMDO("ICD DFN")=$O(@(AUMDO("ICD GL REF")_"""BA"","""_AUMDO("ICD CODE")_""","""")"))
 I '+AUMDO("ICD DFN") S AUMDO("QUIT")=1 W " - ICD code not on file" Q
 S AUMDO("CHANGE")=1
 Q
ADD ; CHECK FOR NODE (DFN,1) IN UPDATE FILE
 W ?27,"Adding new ICD entry"
 S AUMDO("ADD!A/R")=1
 I '$D(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",1)")) S AUMDO("QUIT")=1 W " - Missing required node (DFN,1)" Q
 I $D(@(AUMDO("ICD GL REF")_"""BA"","""_AUMDO("ICD CODE")_""")")) S AUMDO("ICD DFN")=$O(^(AUMDO("ICD CODE"),"")) D ADDREP Q
 S AUMDO("ADD")=1
 S X=$P(AUMDO("ICD CODE")," "),DIC=AUMDO("ICD GL REF"),DIC(0)="LF",DLAYGO=AUMDO("DLAYGO ICD"),DIADD=1
 D ^DIC K DIC,DLAYGO,DIADD,DR
 I Y<1 S AUMDO("QUIT")=1 W " - Add operation failed. (Call to ^DIC)" Q
 S AUMDO("ICD DFN")=+Y
 Q
ADDREP ; ADD/REPLACE ACTION
 W !,?27,"This ICD code already exists,",!,?30,"replacing old data with new data"
 S AUMDO("ADD/REPLACE")=1
 S AUMDO("OLD STATUS")=$P(@(AUMDO("ICD GL REF")_AUMDO("ICD DFN")_",0)"),U,9)
 Q
CKSTAT ; SET ICD CODE STATUS FLAG
 I AUMDO("CHANGE"),$P(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",0)"),U,9)="",$P(@(AUMDO("ICD GL REF")_AUMDO("ICD DFN")_",0)"),U,9)="" Q  ; DON'T CHECK
 I AUMDO("CHANGE"),$P(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",0)"),U,9)="",$P(@(AUMDO("ICD GL REF")_AUMDO("ICD DFN")_",0)"),U,9)=1 D CHGSTAT ; change status  vjm  11/1/94
 ;I AUMDO("CHANGE"),$P(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",0)"),U,9)="" Q  ; DON'T CHECK
 F AUMDO("STATUS")="AUMDO(""ACTIVE"")","AUMDO(""SUPERCEDED"")","AUMDO(""VA EXPANDED"")","AUMDO(""INACTIVE"")" D SETSTAT
 I 'AUMDO("ACTIVE"),'AUMDO("SUPERCEDED"),'AUMDO("INACTIVE"),'AUMDO("VA EXPANDED") S AUMDO("QUIT")=1 W !,?27,"Invalid ICD status code"
 Q
SETSTAT ; SET ICD STATUS FLAG
 S @AUMDO("STATUS")=$S(AUMDO("STATUS")["""A"&($P(@(AUMDO("UPD GL REF")_AUMDO("UPD DFN")_",0)"),U,9)=""):1,AUMDO("STATUS")["""I"&($P(^(0),U,9)=1):1,AUMDO("STATUS")["""S"&($P(^(0),U,9)=2):1,AUMDO("STATUS")["""V"&($P(^(0),U,9)=3):1,1:0)
 Q
INV ; INVALID UPDATE CODE IN UPDATE ENTRY (DFN,2100000)
 S AUMDO("QUIT")=1 W ?27,"Invalid update action code (Not an ""A"" or ""C"")"
 Q
CHGSTAT ;delete flag to reactivate code - field INACTIVE FLAG
 W !?23,"....INACTIVE CODE - reactivating"
 N DIE
 S DIE=AUMDO("ICD GL REF"),DA=AUMDO("ICD DFN"),DR="100///@"
 D ^DIE
 S AUMDO("TOTAL CHANGES")=AUMDO("TOTAL CHANGES")+1
 Q
