BAR50DET ;IHS/SD/LSL - DISPLAY MATCH REASONS AND CLAIMS REPORT;
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**,23**;OCT 26, 2005
 ;CALLED FROM BARED* AND BAR50*
 ;P.OTT NEW ROUTINE JAN 2013
 Q
INS(BARTXT,BARMODE) ;
 NEW BARPTR,%
 S BARPTR=$G(^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L"))+1
 I BARPTR=1 D
 . D NOW^%DTC
 . S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L")=BARPTR
 . S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",BARPTR)=%
 S BARMODE=$G(BARMODE)
 I 'BARMODE D  Q  ;NEW ENTRY
 . S BARPTR=$G(^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L"))+1
 . S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L")=BARPTR
 . S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",BARPTR)=BARTXT
 I BARMODE D  Q  ;APPEND TO LAST ENTRY
 . S BARPTR=$G(^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L"))
 . S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L")=BARPTR
 . S BARTXT=^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",BARPTR)_BARTXT
 . S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",BARPTR)=BARTXT
 Q
NOMATCH ; 
 S ^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"NOMATCH")=1
 Q
PRT ;EP
 ;I '$D(^XTMP("BAR-LIST_DETAIL",$J)) Q
 I '$D(^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"NOMATCH")) D  Q
 ;. W !!!,"*** NO UNMATCHED DATA TO PRINT ***"
 ;. D EOP^BARUTL(0)
 I $G(^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"NOMATCH")) D
 . W !
 . W !,"***THE MATCHING PROCESS FOUND SEVERAL UNMATCHED CLAIMS ***"
 . W !,"      YOU CAN PRINT NOW A REPORT WITH MORE DETAILS"
 . W !
 S Y=$$DIR^XBDIR("Y","Do you wish to print report of the matching process","Y") ;DEFAULT = Y
 ;S Y=$$DIR^XBDIR("Y","Do you wish to print report of the matching process","N") ;DEFAULT = N
 Q:+Y<1
 D ^%ZIS
 Q:$D(DUOUT)
 Q:POP
 U IO
 D PRINT
 D ^%ZISC
 Q
PRINT ;
 NEW BARLINE
 D HDR
 W !,"Created: ",^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",1)
 S BARLINE=1 F  S BARLINE=$O(^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",BARLINE)) Q:BARLINE=""  D
 . W !,^XTMP("BAR-LIST_DETAIL",$J,DUZ(2),"L",BARLINE)
 . QUIT
 D FOOT
 QUIT
HDR ;
 D UNDL
 NEW BARFHDR,BARTHDR,BARTMP,BARCHDR
 W !?10,"** Detailed report of the matching process ** "
 S BARFHDR="File name : "_$P($G(^BAREDI("I",DUZ(2),"L",IMPDA,0)),"^")
 S BARTHDR="Transport : "_$P($G(^BAREDI("1T",TRDA,0)),"^")
 S BARTMP=$J(BARTHDR,45)_BARFHDR
 I TRNAME["HIPAA" D
 . S BARTHDR=TRNAME
 . S BARFHDR="File: "_$P($G(^BAREDI("I",DUZ(2),"L",IMPDA,0)),U)
 . S BARCHDR="Chk/EFT#: "_$S($G(BARCHK)'="":BARCHK,1:$P($G(^BARECHK(BARCKIEN,0)),U))
 . S BARTMP=$J(BARTHDR,18)_"  "_$J(BARFHDR,28)_"  "_BARCHDR
 W !,BARTMP
 D UNDL
 Q
FOOT ;
 W !,"--- END OF REPORT ---"
 D UNDL
 Q
UNDL NEW I
 W ! F I=1:1:78 W "="
 Q  ;EOR
