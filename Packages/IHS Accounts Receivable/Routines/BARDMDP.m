BARDMDP ;IHS/OIT/FCJ - DEBT MANAGEMENT DISPLAY PARAMETERS
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**22**;OCT 26, 2005;Build 38
 ;New routine 5-12-2011 for Debt Letter Management
 ;
ST ;
 ;REPORT HEADER VARS
 D PAR^BARDMU
 D RRDT^BARDMU
 ;
SEL ;SELECT LOCATION
 S DIC="^BAR(90052.06,"_BARPIEN_",",DIC(0)="AEQZ"
 W ! D ^DIC
 K BARQ,DIE,DIC,DA,DR
 G:+Y<0 XIT
 S BARLOC=+Y
 D PAR^BARDMU
 ;
ZIS ;
 D ZIS^BARDMU
 G:$G(BARQUIT) XIT
 S XBRC="COMP^BARDMDP",XBRX="XIT^BARDMDP",XBNS="BAR"
 I $G(BAROPT)="B" S XBRP="VIEWR^XBLM(""PRINT^BARDMDP"")",XBIOP=0  ;bar*1.8*22 SDR
 E  S XBRP="PRINT^BARDMDP"
 D ^XBDBQUE
 D XIT
 Q
XIT ;
 ;
 ;K PG,L  ;bar*1.8*22 SDR
 K BARPG,BARL  ;bar*1.8*22 SDR
 I $D(IO("S")) S IOP="`"_IOS D ^%ZIS Q
 D ^%ZISC
 Q
PRINT ;
 S:'$D(BARPSDX) BARPSDX=""
 D HDR,COMP,HDR2
 S I1=0
 F I=1:1:4 W !!?2,"CYCLE ",I,?13,"LETTER: ",BARPCL(I),?60,"CYCLE PERIOD: ",BARPCD(I)  ;bar*1.8*22 SDR
 W ! F I=1:1:80 W "-"  ;bar*1.8*22 SDR
INS ;DISPLAY INSURER TYP
 W !,"INSURER TYPE: "  ;bar*1.8*22 SDR
 I $D(^BAR(90052.06,BARPIEN,BARLOC,19)) D
 .;start old code bar*1.8*22 SDR
 .;S L=0
 .;F  S L=$O(^BAR(90052.06,BARPIEN,BARLOC,19,L)) Q:L'?1N.N  D
 .;.S BARINSP=$P(^BAR(90052.06,BARPIEN,BARLOC,19,L,0),U)
 .;.I $Y>(IOSL-5) D:$G(BAROPT)'="B" RTRN^BARDMU Q:$G(BARDLQ)  D HDR,HDR2
 .;end old code start new code
 .S BARL=0
 .F  S BARL=$O(^BAR(90052.06,BARPIEN,BARLOC,19,BARL)) Q:BARL'?1N.N  D
 ..S BARINSP=$P(^BAR(90052.06,BARPIEN,BARLOC,19,BARL,0),U)
 ..I $D(^BAR(90053.03,BARINSP)) W !?5,$P(^(BARINSP,0),U)
 ..I $Y>(IOSL-5) D:$G(BAROPT)'="B" RTRN^BARDMU Q:$G(BARDLQ)  D HDR,HDR2
 .;end new code
 ;
ACC ;DISPLAY ACCOUNTS
 W !,"ACCOUNT: ",?50,"LETTERS"  ;bar*1.8*22 SDR
 I $D(^BARAC(BARLOC,"D")) D
 .S BARACS=""
 .F  S BARACS=$O(^BARAC(BARLOC,"D",BARACS)) Q:BARACS=""  D
 ..S BARAC=""
 ..F  S BARAC=$O(^BARAC(BARLOC,"D",BARACS,BARAC)) Q:BARAC'?1N.N  D
 ...W !?5,$$VAL^XBDIQ1(90050.02,BARAC,.01),?52,$$VAL^XBDIQ1(90050.02,BARAC,7)  ;bar*1.8*22 SDR
 ...I $Y>(IOSL-5) D:$G(BAROPT)'="B" RTRN^BARDMU Q:$G(BARDLQ)  D HDR,HDR2
 Q:$G(BARDLQ)
 D:$G(BAROPT)'="B" RTRN^BARDMU
 Q
HDR ;
 ;S PG=PG+1  ;bar*1.8*22 SDR
 S BARPG=BARPG+1  ;bar*1.8*22 SDR
 ;W @IOF,!,BARRDT,?27,"DEBT MANAGEMENT PARAMETERS",?70,"PAGE ",PG  ;bar*1.8*22 SDR
 I $E(IOST)="C",IOT["TRM",'$D(ZTQUEUED),$Y+20>IOSL W $$EN^BARVDF("IOF")  ;bar*1.8*22 SDR
 W !,"A/R PARENT LOCATION: ",BARPSAT(BARLOC,.01),!  ;bar*1.8*22 SDR
 F I=1:1:80 W "="  ;bar*1.8*22 SDR
 Q
HDR2 ;
 ;W !,?2,"EFFECTIVE DATE: ",BARPSDX,?35,"NPI: ",BARPNPI,?55,"DOB: ",BARPDOB  ;bar*1.8*22 SDR
 W !,?2,"EFFECTIVE DATE: ",BARPSDX,?35,"NPI: ",BARPNPI,?70,"DOB: ",BARPDOB  ;bar*1.8*22 SDR
 W !?2,"PRINT MAX#: ",$P(BARPAR1,U,8),?35,"TOP MARGIN: "_+$P(BARPAR1,U,9),?65,"LEFT MARGIN: "_+$P(BARPAR1,U,10)  ;bar*1.8*22 SDR
 W !?2,"WRITE OFF AMOUNT: ",+BARPWO  ;bar*1.8*22 SDR
 W ! F I=1:1:80 W "-"  ;bar*1.8*22 SDR
 Q
COMP ;
 ;
 Q
