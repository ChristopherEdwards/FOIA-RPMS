BARDMDP ;IHS/OIT/FCJ - DEBT MANAGEMENT DISPLAY PARAMETERS
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**22,23**;OCT 26, 2005;Build 38
 ;New routine 5-12-2011 for Debt Letter Management
 ;JAN 2013 P.OTT DISPLAY NEW PARAMS (SORT DEBT LETTERS ...)
 ;12/02/2013 P.OTT BETA TESTING: ADDED SRT BY CYCLE
ST ;
 ;REPORT HEADER VARS
 D PAR^BARDMU
 D RRDT^BARDMU
 ;
SEL ;SELECT LOCATION
 S DIC="^BAR(90052.06,"_BARPIEN_",",DIC(0)="AEQZ"
 W ! D ^DIC
 K BARQ,DIE,DIC,DA,DR
 G:+Y<0 XIT
 S BARLOC=+Y
 D PAR^BARDMU
 ;
ZIS ;
 D ZIS^BARDMU
 G:$G(BARQUIT) XIT
 S XBRC="COMP^BARDMDP",XBRX="XIT^BARDMDP",XBNS="BAR"
 I $G(BAROPT)="B" S XBRP="VIEWR^XBLM(""PRINT^BARDMDP"")",XBIOP=0
 E  S XBRP="PRINT^BARDMDP"
 D ^XBDBQUE
 D XIT
 Q
XIT ;
 ;
 K BARPG,BARL
 I $D(IO("S")) S IOP="`"_IOS D ^%ZIS Q
 D ^%ZISC
 Q
PRINT ;
 S:'$D(BARPSDX) BARPSDX=""
 D HDR,COMP,HDR2
 S I1=0
 F I=1:1:4 W !!?2,"CYCLE ",I,?13,"LETTER: ",BARPCL(I),?60,"CYCLE PERIOD: ",BARPCD(I)
 W ! F I=1:1:80 W "-"
INS ;DISPLAY INSURER TYP
 W !,"INSURER TYPE: "
 I $D(^BAR(90052.06,BARPIEN,BARLOC,19)) D
 .S BARL=0
 .F  S BARL=$O(^BAR(90052.06,BARPIEN,BARLOC,19,BARL)) Q:BARL'?1N.N  D
 ..S BARINSP=$P(^BAR(90052.06,BARPIEN,BARLOC,19,BARL,0),U)
 ..I $D(^BAR(90053.03,BARINSP)) W !?5,$P(^(BARINSP,0),U)
 ..I $Y>(IOSL-5) D:$G(BAROPT)'="B" RTRN^BARDMU Q:$G(BARDLQ)  D HDR,HDR2
 ;
ACC ;DISPLAY ACCOUNTS
 W !,"ACCOUNT: ",?50,"LETTERS"
 I $D(^BARAC(BARLOC,"D")) D
 .S BARACS=""
 .F  S BARACS=$O(^BARAC(BARLOC,"D",BARACS)) Q:BARACS=""  D
 ..S BARAC=""
 ..F  S BARAC=$O(^BARAC(BARLOC,"D",BARACS,BARAC)) Q:BARAC'?1N.N  D
 ...W !?5,$$VAL^XBDIQ1(90050.02,BARAC,.01),?52,$$VAL^XBDIQ1(90050.02,BARAC,7)
 ...I $Y>(IOSL-5) D:$G(BAROPT)'="B" RTRN^BARDMU Q:$G(BARDLQ)  D HDR,HDR2
 Q:$G(BARDLQ)
 D:$G(BAROPT)'="B" RTRN^BARDMU
 Q
HDR ;
 S BARPG=BARPG+1
 I $E(IOST)="C",IOT["TRM",'$D(ZTQUEUED),$Y+20>IOSL W $$EN^BARVDF("IOF")
 W !,"A/R PARENT LOCATION: ",BARPSAT(BARLOC,.01),!
 F I=1:1:80 W "="
 Q
HDR2 ;
 W !,?2,"EFFECTIVE DATE: ",BARPSDX,?35,"NPI: ",BARPNPI
 W !,?2,"PRINT DOB: ",$P("NO,YES",",",BARPDOB+1),?35,"MAX # OF LETTERS TO PRINT: ",$P(BARPAR1,U,8)
 W !,?2,"TOP MARGIN: "_+$P(BARPAR1,U,9),?35,"LEFT MARGIN: "_+$P(BARPAR1,U,10)
 W !,?2,"WRITE OFF AMOUNT: ",+BARPWO,?35,"PRINT BILL DATE: ",$P("NO,YES",",",BARPBDT+1) ;P.OTT
 IF BARPSRT]"" D   ;P.OTT
 . W !?2,"SORT DEBT LETTERS: "
 . I BARPSRT="P" W "BY POLICY HOLDER'S NAME"
 . I BARPSRT="I" W "BY INSURANCE NAME"
 . I BARPSRT="C" W "BY CYCLE" ;12/02/2013
 W ! F I=1:1:80 W "-"
 Q
COMP ;
 ;
 Q
