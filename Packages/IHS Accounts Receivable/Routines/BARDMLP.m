BARDMLP ;IHS/OIT/FCJ - 1 OF 2 ;DEBT MANAGEMENT PRINT LETTERS
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**22**;OCT 26, 2005;Build 38
 ;New routine 5-12-2011 for Debt Letter Management
 ;Routine to print letters
 ;AUG 2012 added summary of leters to be printed ;P.OTT
 ;
ST ;
 S BARRPT="L"
 W @IOF
 D TSTPRT
 G:$D(DUOUT) XIT
 D RRDT^BARDMU
 ;
 K ^TMP("BARDME",$J)
 K ^TMP("BARDM",$J)  ;bar*1.8*22 SDR
 K ^TMP("BARDMQN",$J)
 ;
 D ETST
 G:$G(BARQ) XIT2
 G:$D(DUOUT) XIT
 ;D ^%ZIS
 ;Q:POP  ;bar*1.8*22 SDR
 ;U IO  ;bar*1.8*22 SDR
 D VAR
 D LET
 D XIT
 Q
XIT ;
 I $D(IO("S")) S IOP="`"_IOS D ^%ZIS
 E  D ^%ZISC
XIT2 ;
 K ^TMP("BARDME",$J)
 K ^TMP("BARDM",$J)  ;bar*1.8*22 SDR
 K ^TMP("BARDMQN",$J)  ;bar*1.8*22 P.OTT
 K ERRT,ERRCT,AGE
 Q
ETST ;ERROR TEST
 ;S ERRT=0  ;bar*1.8*22 SDR
 S BARERRT=0  ;bar*1.8*22 SDR
 D CALC^BARDMRE(2) ;P.OTT : COUNT ONLY DUE PRINT ERR LETTERS
 ;D:ERRCT>0 PRINT^BARDMRE  ;bar*1.8*22 SDR
 D:BARERRCT>0 PRINT^BARDMRE  ;bar*1.8*22 SDR
 Q:$G(BARQ)
 ;Q:ERRCT=0  ;bar*1.8*22 SDR
 Q:BARERRCT=0  ;bar*1.8*22 SDR
 W !
 S DIR(0)="Y"
 S DIR("A")="There are letters with Errors, print Letters with and without Errors"
 S DIR("B")="No"
 S DIR("?")="Enter 'NO' to skip Letters with errors"
 D ^DIR
 ;S ERRT=+Y  ;bar*1.8*22 SDR
 S BARERRT=+Y  ;bar*1.8*22 SDR
 Q
TSTPRT ;EP
 D PAR^BARDMU
 S Y=$$DIR^XBDIR("Y","Do you wish to print a test letter","N")
 Q:+Y<1
 D ^%ZIS
 Q:$D(DUOUT)
 Q:POP  ;bar*1.8*22 SDR
 U IO  ;bar*1.8*22 SDR
 D VAR,VARSET
 ;S CY=1,BARDACG="AUTNINS(",L="CYCLE 1"  ;bar*1.8*22 SDR
 S CY=1,BARDACG="AUTNINS(",BARL="CYCLE 1"  ;bar*1.8*22 SDR
 S BARDM("INS_NM")="TEST INSURANCE"
 S BARDM("INS_STR")="1234 STREET"
 S BARDM("INS_CTY")="Portland"
 S BARDM("INS_ST")=38
 S BARDM("INS_ZP")=97204
 S BARPAT="TEST PATIENT",BARBILN=1234,BARAMTO=0
 S BARDM("DOS")=""
 S BARRPT="L"  ;bar*1.8*22 SDR
 S BARDTP=DT  ;bar*1.8*22 SDR
 S BARDOB="",BARNPIF="1234567890",BARNPIP="0987654321"  ;bar*1.8*22 SDR
 D PRINT^BARDMLP1
 D ^%ZISC  ;bar*1.8*22 SDR
 G TSTPRT
 Q
VAR ;SET LET VARIABLES
 ;S LEN="" F I=1:1:30 S LEN=LEN_" "  ;bar*1.8*22 SDR
 S BARLEN="" F I=1:1:30 S BARLEN=BARLEN_" "  ;bar*1.8*22 SDR
 S BARPAR1=^BAR(90052.06,BARPIEN,DUZ(2),18)
 S BARAD1=$P(BARPAR1,U),BARAD2=$P(BARPAR1,U,2),BARCTY=$P(BARPAR1,U,3),BARST=$P(BARPAR1,U,4),BARZP=$P(BARPAR1,U,5),BARPH=$P(BARPAR1,U,6)
 S BARPMX=$P(BARPAR1,U,8),BARMRGT=$P(BARPAR1,U,9),BARMRGL=$P(BARPAR1,U,10)
 S BARSG=$P(BARPAR,U,13),BARSG1=$P(BARPAR,U,14),BARSG2=$P(BARPAR,U,15)
 S C=1 F I=5:2:11 S BARPCP(C)=$P(BARPAR,U,I),C=C+1
 S BARNPI=$P(BARPAR,3),BARLDOB=$P(BARPAR,4)
 S:BARMRGT="" BARMRGT=5
 S:BARMRGL="" BARMRGL=3
 S BARFAC=$$VAL^XBDIQ1(9999999.06,DUZ(2),.01)
 K BARPAR,BARPAR1
 Q
VARSET ;RESET VARS
 S BARMIN=0
 S (BARDM("INS"),BARDM("MEMBER"),BARDM("POL_HOLDER_IEN"),BARDM("POL_HOLDER"),BARDM("POL_NUM"),BARDM("POL_DOB"),BARDM("INS_TX"))=""
 Q
LET ;
 ;renamed vars in this TAG to be namespaced bar*1.8*22 SDR
 S CY=0,BARCT=0,BARQ=0
 S BARTOT=0  ;bar*1.8*22 P.OTT
 F BARCYCLE="CYCLE 1","CYCLE 2","CYCLE 3","CYCLE 4" D  Q:BARQ
 .S BARDM=0,CY=CY+1
 .F  S BARDM=$O(^BARDM(DUZ(2),"S","Q",BARCYCLE,BARDM)) Q:BARDM'?1N.N  D  Q:BARQ
 ..Q:$P(^BARDM(DUZ(2),BARDM,0),U,2)'="A"
 ..I BARERRT=0 Q:$D(^TMP("BARDME",$J,BARDM))
 ..S BARDMC=0,BARDM("DOS")=""
 ..F  S BARDMC=$O(^BARDM(DUZ(2),"S","Q",BARCYCLE,BARDM,BARDMC)) Q:BARDMC'?1N.N  D  Q:BARQ
 ...I CY'=1 D CYDAY^BARDMRU I BARLQ=0 QUIT
 ...S X=$P(^BARDM(DUZ(2),BARDM,100,BARDMC,0),U,6),X2="2$" D COMMA^%DTC S BARAMTO="$"_$P(X,"$",2)
 ...S BARBIEN=$P(^BARDM(DUZ(2),BARDM,0),U)
 ...S BARBILN=$$VAL^XBDIQ1(90053.05,BARDM,.01)
 ...S BARDAC=$P(^BARBL(DUZ(2),BARBIEN,0),U,3),BARDBDT=$P(^(0),U,7),BARD3P=$P(^(0),U,17)
 ...S BARD3PD=$P($G(^BARBL(DUZ(2),BARBIEN,0)),U,22)
 ...I '$D(^ABMDBILL(BARD3PD,BARD3P)) QUIT  ;DUPLICATE BILL / NONEX CLAIM #
 ...S BARDMINS=$P($G(^ABMDBILL(BARD3PD,BARD3P,0)),U,8)
 ...S BARDM("DOS")=$$VAL^XBDIQ1(90050.01,BARBIEN,102)
 ...S BARDOB=$$GET1^DIQ(2,BARDM("PAT_IEN"),".03","E")
 ...S BARNPIF=$P($$NPI^XUSNPI("Organization_ID",DUZ(2)),U)
 ...S BARNPIP=$S(+$$GET1^DIQ(90050.01,BARBIEN,113,"I")'=0:$P($$NPI^XUSNPI("Individual_ID",$$GET1^DIQ(90050.01,BARBIEN,113,"I")),U),1:"")
 ...D INSTYP^BARDMU
 ...S BARDI=$$VAL^XBDIQ1(90050.02,BARDAC,.01)
 ...I '$D(BARDINS(BARDI))&'$D(BARDINS(BARDITY)) Q  ;not insurer type or insurer we are looking for  ;bar*1.8*22 SDR
 ...I $P(^BARBL(DUZ(2),BARBIEN,0),U,15)'>0 Q
 ...D POLCHK
 ...S BARTOT=BARTOT+1
 ...I BARCT<BARPMX D
 ....S BARCT=BARCT+1
 ....S ^TMP("BARDM",$J,"PRT",BARCYCLE,BARDMINS,BARDM,BARDMC)=""
 ....S ^TMP("BARDMQN",$J,BARCYCLE,BARDM,BARDMC)=BARDMINS ;for BARDMRQN report (after printing)  ;bar*1.8*22 P.OTT
 D SUMMARY I BARQ QUIT  ;DISPLAY SUMMARY OF LETTERS TO BE PRINTED ;bar*1.8*22 P.OTT
 D ^%ZIS
 Q:POP  ;bar*1.8*22 SDR
 U IO  ;bar*1.8*22 SDR
 S CY=0
 F BARCYCLE="CYCLE 1","CYCLE 2","CYCLE 3","CYCLE 4" D
 .S CY=CY+1
 .S BARDMINS=""
 .F  S BARDMINS=$O(^TMP("BARDM",$J,"PRT",BARCYCLE,BARDMINS)) Q:$G(BARDMINS)=""  D
 ..S BARDM=0
 ..F  S BARDM=$O(^TMP("BARDM",$J,"PRT",BARCYCLE,BARDMINS,BARDM)) Q:'BARDM  D
 ...S BARDMC=0
 ...F  S BARDMC=$O(^TMP("BARDM",$J,"PRT",BARCYCLE,BARDMINS,BARDM,BARDMC)) Q:'BARDMC  D
 ....S BARDM("DOS")=""
 ....S X=$P(^BARDM(DUZ(2),BARDM,100,BARDMC,0),U,6),X2="2$" D COMMA^%DTC S BARAMTO="$"_$P(X,"$",2)
 ....S BARBIEN=$P(^BARDM(DUZ(2),BARDM,0),U)
 ....S BARBILN=$$VAL^XBDIQ1(90053.05,BARDM,.01)
 ....S BARDAC=$P(^BARBL(DUZ(2),BARBIEN,0),U,3),BARDBDT=$P(^(0),U,7),BARD3P=$P(^(0),U,17)
 ....S BARD3PD=$P($G(^BARBL(DUZ(2),BARBIEN,0)),U,22)
 ....S BARDM("DOS")=$$VAL^XBDIQ1(90050.01,BARBIEN,102)
 ....S BARDOB=$$GET1^DIQ(2,BARDM("PAT_IEN"),".03","E")
 ....S BARNPIF=$P($$NPI^XUSNPI("Organization_ID",DUZ(2)),U)
 ....S BARNPIP=$S(+$$GET1^DIQ(90050.01,BARBIEN,113,"I")'=0:$P($$NPI^XUSNPI("Individual_ID",$$GET1^DIQ(90050.01,BARBIEN,113,"I")),U),1:"")
 ....D INSTYP^BARDMU
 ....S BARDI=$$VAL^XBDIQ1(90050.02,BARDAC,.01)
 ....I $P(^BARBL(DUZ(2),BARBIEN,0),U,15)'>0 Q
 ....D POLCHK
 ....D PRINT^BARDMLP1
 ....D CYUP
 ....D LGUP
 ;start new code bar*1.8*22 P.OTT
 D ^%ZISC  ;CLSE PRINTER
 S Y=$$DIR^XBDIR("Y","Do you wish to display the print report","Y")
 I +Y<1 Q
 D ^BARDMRQN ;DISPLAY PRINT REPORT
 K DIR S (X,Y)=""
 S DIR(0)="E"
 S DIR("A")="Hit ENTER to continue"
 D ^DIR
 K DIR
 ;end new code bar*1.8*22 P.OTT
 Q
POLCHK ;TEST FOR POLICY NO, POLICY HOLDER AND POLICY HOLDER DOB
 S BARMIN=0
 I BARDACG'="VA(" D
 .S BARDM("PAT_IEN")=$P(^BARBL(DUZ(2),BARBIEN,1),U)
 .;S BARHRN=$P(^AUPNPAT(BARDM("PAT_IEN"),41,DUZ(2),0),U,2)  ;bar*1.8*22 SDR
 .S BARHRN=$P($G(^AUPNPAT(BARDM("PAT_IEN"),41,DUZ(2),0)),U,2)  ;bar*1.8*22 SDR
 .S BARPAT=$P(^DPT(BARDM("PAT_IEN"),0),U)
 D VARSET
 I BARDACG="AUTNINS(" D INSCHK
 I BARDACG="AUPNPAT(" D PATCHK
 I BARDACG="VA(" D PERCHK
 Q
INSCHK ;
 S BARDM("INS")=^AUTNINS(BARDACI,0)
 S BARDM("INS_NM")=$P(BARDM("INS"),U),BARDM("INS_STR")=$P(BARDM("INS"),U,2)
 S BARDM("INS_CTY")=$P(BARDM("INS"),U,3),BARDM("INS_ST")=$P(BARDM("INS"),U,4)
 S BARDM("INS_ZP")=$P(BARDM("INS"),U,5)
 S BARDM("INS_TX")=$P(BARDM("INS"),U,11)
 ;start old code bar*1.8*22 SDR
 ;S TST=0
 ;I $D(^ABMDBILL(DUZ(2),BARD3P,13,"B",BARDACI)) D
 ;.S L="",L=$O(^ABMDBILL(DUZ(2),BARD3P,13,"B",BARDACI,L))
 ;.D INSCHK1
 ;Q:TST=1
 ;S L=0 F  S L=$O(^ABMDBILL(DUZ(2),BARD3P,13,L)) Q:L'?1N.N  D  Q:TST=1
 ;.I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,11)=BARDACI D INSCHK1
 ;end old code start new code
 S BARTST=0
 I $D(^ABMDBILL(BARD3PD,BARD3P,13,"B",BARDACI)) D
 .;S L="",L=$O(^ABMDBILL(BARD3PD,BARD3P,13,"B",BARDACI,L))  ;bar*1.8*22 SDR
 .S BARL="",BARL=$O(^ABMDBILL(BARD3PD,BARD3P,13,"B",BARDACI,BARL))  ;bar*1.8*22 SDR
 .D INSCHK1
 Q:BARTST=1
 S BARL=0 F  S BARL=$O(^ABMDBILL(BARD3PD,BARD3P,13,BARL)) Q:BARL'?1N.N  D  Q:BARTST=1
 .I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,11)=BARDACI D INSCHK1
 ;end new code
 Q
INSCHK1 ;
 ;start old code bar*1.8*22 SDR
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,4)'="" D MCR^BARDMRE Q
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,5)'="" D RR^BARDMRE Q
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,7)'="" D MCD^BARDMRE Q
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,8)'="" D PRVT^BARDMRE Q
 ;end old code start new code
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,4)'="" D MCR^BARDMRE Q
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,5)'="" D RR^BARDMRE Q
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,7)'="" D MCD^BARDMRE Q
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,8)'="" D PRVT^BARDMRE Q
 ;end new code
 Q
PATCHK ;
 S BARDM("INS_NM")=$P(BARPAT,",",2)_" "_$P(BARPAT,",",1)_" "_$P(BARPAT,",",3)
 S BARDM("INS_STR")=$$VAL^XBDIQ1(2,BARDM("PAT_IEN"),.111)
 S BARDM("INS_CTY")=$$VAL^XBDIQ1(2,BARDM("PAT_IEN"),.114)
 ;S BARDM("INS_ST")=$P(^DPT(BARDM("PAT_IEN"),.11),U,5)  ;bar*1.8*22 SDR
 S BARDM("INS_ST")=$P($G(^DPT(BARDM("PAT_IEN"),.11)),U,5)  ;bar*1.8*22 SDR
 S BARDM("INS_ZP")=$$VAL^XBDIQ1(2,BARDM("PAT_IEN"),.116)
 ;**CHECK AND SET BARMIN FOR MINOR OR NOT
 S X1=$$VALI^XBDIQ1(90050.01,BARBIEN,102)
 S X2=$$VALI^XBDIQ1(2,BARDM("PAT_IEN"),.03)
 D ^%DTC S AGE=X\365.25 S:AGE>17 BARMIN=1
 Q
PERCHK ;
 S BARPAT=$$VAL^XBDIQ1(200,BARDACI,.01)
 S BARDM("INS_NM")=$P(BARPAT,",",2)_" "_$P(BARPAT,",",1)_" "_$P(BARPAT,",",3)
 S BARDM("INS_STR")=$$VAL^XBDIQ1(200,BARDACI,.111)
 S BARDM("INS_CTY")=$$VAL^XBDIQ1(200,BARDACI,.114)
 S BARDM("INS_ST")=$$VAL^XBDIQ1(200,BARDACI,.115)
 S BARDM("INS_ZP")=$$VAL^XBDIQ1(200,BARDACI,.116)
 Q
CYUP ;CYCLE UPDATE - SET LETTER STATUS AND QUE
 S (DIC,DIE)="^BARDM(DUZ(2),"_BARDM_",100,",DA(1)=BARDM,DA=BARDMC
 S DR=".03///P;.04///"_DUZ_";.05///"_DT
 D ^DIE
 I CY=4 K DIE,DIC,DA,DR Q
 ;SET NEW LETTER STATUS AND QUE
 S DIC(0)="L",X="CYCLE "_(CY+1)
 D ^DIC
 Q:+Y<0
 S DA=+Y
 S DR=".02///"_BARPCP(CY+1)_";.03///Q"_";.06///"_$P(^BARBL(DUZ(2),BARBIEN,0),U,15)
 D ^DIE
 K DIE,DIC,DA,DR
 Q
LGADD ;ENTRY TO LOG FILE
 D NOW^%DTC
 S DIC(0)="L",DIC="^BARDMLG("_DUZ(2)_","
 S X=%
 D ^DIC
 S BARLG=+Y
 K DA
 Q
LGCYL ;ADD CYCLE TO LOG FILE
 S DA(1)=BARLG
 S DIC(0)="L",DIC="^BARDMLG("_DUZ(2)_","_BARLG_",100,"
 S DIC("P")=$P(^DD(90053.08,100,0),U,2)
 ;S X=CYCLE  ;bar*1.8*22 SDR
 S X=BARCYCLE  ;bar*1.8*22 SDR
 D ^DIC
 S BARLGC=+Y
 K DA
 Q
LGUP ;ADD BILL TO LOG FILE
 D:'$D(BARLG) LGADD
 ;D:'$D(^BARDMLG(DUZ(2),BARLG,100,"B",CYCLE)) LGCYL  ;bar*1.8*22 SDR
 D:'$D(^BARDMLG(DUZ(2),BARLG,100,"B",BARCYCLE)) LGCYL  ;bar*1.8*22 SDR
 K DIC,DIE,DIR,X,Y,DA,DR  ;bar*1.8*22 SDR
 S DA(2)=BARLG  ;bar*1.8*22 SDR
 S DA(1)=BARLGC  ;bar*1.8*22 SDR
 S DIC(0)="L"
 S DIC="^BARDMLG("_DUZ(2)_","_BARLG_",100,"_BARLGC_",10,"
 S DIC("P")=$P(^DD(90053.09,10,0),U,2)
 S X=BARBILN
 D ^DIC
 S $P(^BARDMLG(DUZ(2),BARLG,100,BARLGC,0),U,2)=$P(^BARDMLG(DUZ(2),BARLG,100,BARLGC,0),U,2)+1
 Q
 ;start new code bar*1.8*22 P.OTT
SUMMARY ;
 S BARQ=0
 D UNDL
 I BARTOT=0 I BARERRT S BARCT=$G(BARERRCT)
 W !,?15,"SUMMARY OF DEBT LETTERS SELECTED FOR PRINTING" D UNDL
 W !,"            # of letters in queue : ",$J(BARTOT,6)
 W !,"# of letters that will be printed : ",$J(BARCT,6)
 I BARERRT I $G(BARERRCT) W " incl. ",BARERRCT," letter(s) with errors."
 I BARTOT>BARCT W !,"NOTE: According to the parameter setup only ",BARPMX," letters will be printed."
 D UNDL
 ;
 K DIR S (X,Y)=""
 S DIR(0)="E"
 I 'BARCT S DIR("A")="There is nothing to print. Enter ^ to quit"
 I BARCT S DIR("A")="Hit ENTER to continue printing, or ^ to quit (no printing)"
 D ^DIR
 K DIR
 I X="^" S BARQ=1
 QUIT
UNDL ;
 NEW BARTMP
 W ! F BARTMP=1:1:78 W "-"
 Q
 ;end new code bar*1.8*22 P.OTT
