BARDMLPR ;IHS/OIT/FCJ - REPRINT DEBT MANAGEMENT PRINT LETTERS
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**22**;OCT 26, 2005;Build 38
 ;New routine 5-12-2011 for Debt Letter Management
 ;
 ;Routine to RE PRINT BATCHES OR letters
ST ;
 S BARRPT="R"
 ;W @IOF  ;bar*1.8*22 SDR
 D TSTPRT
 G:$D(DUOUT) XIT
 D RRDT^BARDMU
 S BARLTY=$$DIR^XBDIR("S^L:LETTER;B:BATCH")
 G:$D(DUOUT) XIT
 D @BARLTY
 G:BARQ XIT
 W !
 D ^%ZIS
 U IO  ;bar*1.8*22 SDR
 D VAR
 D LET
 D XIT
 Q
XIT ;
 I $D(IO("S")) S IOP="`"_IOS D ^%ZIS
 E  D ^%ZISC
XIT2 ;
 ;start old code bar*1.8*22 SDR
 ;K CYL,CY,CYCLE,CT,DA,X,Z,DIC,L,I,I1
 ;K ERRT,ERRCT,AGE
 ;end old code start new code
 K BARCYL,CY,BARCYCLE,BARCT,DA,X,Z,DIC,BARL,I,I1
 K BARERRT,BARERRCT,AGE
 ;end new code
 Q
TSTPRT ;EP
 D PAR^BARDMU
 S Y=$$DIR^XBDIR("Y","Do you wish to print a test letter","N")
 Q:+Y<1
 D ^%ZIS
 U IO  ;bar*1.8*22 SDR
 Q:$D(DUOUT)
 D VAR,VARSET
 D DD^%DT S BARDTP=Y
 ;S CY=1,BARDACG="AUTNINS(",L="CYCLE 1"  ;bar*1.8*22 SDR
 S CY=1,BARDACG="AUTNINS(",BARL="CYCLE 1"  ;bar*1.8*22 SDR
 S BARDM("INS_NM")="TEST INSURANCE"
 S BARDM("INS_STR")="1234 STREET"
 S BARDM("INS_CTY")="Portland"
 S BARDM("INS_ST")=38
 S BARDM("INS_ZP")=97204
 S BARPAT="TEST PATIENT",BARBILN=1234,BARAMTO=0
 S BARDM("DOS")=""
 S BARDOB="",BARNPIF="1234567890",BARNPIP="0987654321"  ;bar*1.8*22 SDR
 D PRINT^BARDMLP1
 D ^%ZISC  ;bar*1.8*22 SDR
 G TSTPRT
 Q
VAR ;SET LET VARIABLES
 ;S LEN="" F I=1:1:30 S LEN=LEN_" "  ;bar*1.8*22 SDR
 S BARLEN="" F I=1:1:30 S BARLEN=BARLEN_" "  ;bar*1.8*22 SDR
 S BARPAR1=^BAR(90052.06,BARPIEN,DUZ(2),18)
 S BARAD1=$P(BARPAR1,U),BARAD2=$P(BARPAR1,U,2),BARCTY=$P(BARPAR1,U,3),BARST=$P(BARPAR1,U,4),BARZP=$P(BARPAR1,U,5),BARPH=$P(BARPAR1,U,6)
 S BARPMX=$P(BARPAR1,U,8),BARMRGT=$P(BARPAR1,U,9),BARMRGL=$P(BARPAR1,U,10)
 S BARSG=$P(BARPAR,U,13),BARSG1=$P(BARPAR,U,14),BARSG2=$P(BARPAR,U,15)
 S C=1 F I=5:2:11 S BARPCP(C)=$P(BARPAR,U,I),C=C+1
 S BARNPI=$P(BARPAR,3),BARLDOB=$P(BARPAR,4)
 S:BARMRGT="" BARMRGT=5
 S:BARMRGL="" BARMRGL=3
 S BARFAC=$$VAL^XBDIQ1(9999999.06,DUZ(2),.01)
 K BARPAR,BARPAR1
 Q
VARSET ;RESET VARS
 S BARMIN=0
 S (BARDM("INS"),BARDM("MEMBER"),BARDM("POL_HOLDER_IEN"),BARDM("POL_HOLDER"),BARDM("POL_NUM"),BARDM("POL_DOB"),BARDM("INS_TX"))=""
 Q
L ;SELECT LETTER
 D SEL^BARDMU
 Q:BARQ=1
 ;TEST FOR CYCLE...
 I '$D(^BARDM(DUZ(2),BARDM,100)) D LERR Q
 ;start old code bar*1.8*22 SDR
 ;S CYL=0,CT=0
 ;F  S CYL=$O(^BARDM(DUZ(2),BARDM,100,CYL)) Q:CYL'?1N.N  D
 ;.I $P(^BARDM(DUZ(2),BARDM,100,CYL,0),U,3)="P" S CT=CT+1,BARDM(CT)=BARDM_U_CYL_U_^BARDM(DUZ(2),BARDM,100,CYL,0)
 ;I CT=0 D LERR Q
 ;W !?5,"Select Letter for Bill : "
 ;F I=1:1:CT W !?10,I_". ",$P(BARDM(I),U,3)," Letter"
 ;I CT>1 S I=CT+1,BARDM(I)="A" W !?10,I,". All Letters"
 ;I CT>3 S I=I+1,BARDM(I)=3 W !?10,I,". Letters 1-3"
 ;W !
 ;S CYL=$$DIR^XBDIR("N^1:"_I)
 ;end old code start new code
 S BARCYL=0,BARCT=0
 F  S BARCYL=$O(^BARDM(DUZ(2),BARDM,100,BARCYL)) Q:BARCYL'?1N.N  D
 .I $P(^BARDM(DUZ(2),BARDM,100,BARCYL,0),U,3)="P" S BARCT=BARCT+1,BARDM(BARCT)=BARDM_U_BARCYL_U_^BARDM(DUZ(2),BARDM,100,BARCYL,0)
 I BARCT=0 D LERR Q
 W !?5,"Select Letter for Bill : "
 F I=1:1:BARCT W !?10,I_". ",$P(BARDM(I),U,3)," Letter"
 I BARCT>1 S I=BARCT+1,BARDM(I)="A" W !?10,I,". All Letters"
 I BARCT>3 S I=I+1,BARDM(I)=3 W !?10,I,". Letters 1-3"
 W !
 S BARCYL=$$DIR^XBDIR("N^1:"_I)
 ;end new code
 I $D(DUOUT) S BARQ=1 Q
 Q
B ;SELECT BATCH
 S (BARQ,BARREQ)=0
 S DIC="^BARDMLG("_DUZ(2)_","
 S DIC("A")="Enter the Debt Management Batch Date: "
 S DIC(0)="AEQ"
 D ^DIC
 I +Y<1 S BARQ=1 Q
 S BARBAT=+Y
 ;start old code bar*1.8*22 SDR
 ;S CT=0,CYL=0
 ;F  S CYL=$O(^BARDMLG(DUZ(2),BARBAT,100,CYL)) Q:CYL'?1N.N  D
 ;.S CT=CT+1,BARDM(CT)=CYL_U_^BARDMLG(DUZ(2),BARBAT,100,CYL,0)
 ;I CT=0 D LERR Q
 ;W !?5,"Select Cycle for Batch:"
 ;F I=1:1:CT W !?10,I_". ",$P(BARDM(I),U,2)," - Total Letters = ",$P(BARDM(I),U,3)
 ;I CT>1 S I=CT+1,BARDM(I)="A" W !?10,I,". All Letters"
 ;S CYL=$$DIR^XBDIR("N^1:"_I)
 ;end old code start new code
 S BARCT=0,BARCYL=0
 F  S BARCYL=$O(^BARDMLG(DUZ(2),BARBAT,100,BARCYL)) Q:BARCYL'?1N.N  D
 .S BARCT=BARCT+1,BARDM(BARCT)=BARCYL_U_^BARDMLG(DUZ(2),BARBAT,100,BARCYL,0)
 I BARCT=0 D LERR Q
 W !?5,"Select Cycle for Batch:"
 F I=1:1:BARCT W !?10,I_". ",$P(BARDM(I),U,2)," - Total Letters = ",$P(BARDM(I),U,3)
 I BARCT>1 S I=BARCT+1,BARDM(I)="A" W !?10,I,". All Letters"
 S BARCYL=$$DIR^XBDIR("N^1:"_I)
 ;end new code
 I $D(DUOUT) S BARQ=1 Q
 Q
LET ;BEGIN PRINT LETTERS
 S BARQ=0
 ;start old code bar*1.8*22 SDR
 ;I BARLTY="L" D  Q
 ;.I BARDM(CYL)="A" F CY=1:1:CYL-1 D LETS
 ;.E  I BARDM(CYL)=3 F CY=1:1:3 D LETS
 ;.Q:BARQ
 ;.S CY=CYL D LETS
 ;I BARLTY="B" D
 ;.S CY=0
 ;.I BARDM(CYL)="A" F CY=1:1:CYL-1 D LETB
 ;.E  S CY=CYL D LETB
 ;end old code start new code
 I BARLTY="L" D  Q
 .I BARDM(BARCYL)="A" F CY=1:1:BARCYL-1 D LETS
 .E  I BARDM(BARCYL)=3 F CY=1:1:3 D LETS
 .Q:BARQ
 .S CY=BARCYL D LETS
 I BARLTY="B" D
 .S CY=0
 .I BARDM(BARCYL)="A" F CY=1:1:BARCYL-1 D LETB
 .E  S CY=BARCYL D LETB
 ;end new code
 Q
LETS ;SET CYCLES TO SINGLE LETTERS
 S BARQ=1
 ;S CYCLE=$P(BARDM(CY),U,3),BARDMC=$P(BARDM(CY),U,2),BARDM=$P(BARDM(CY),U) D LET2 ;bar*1.8*22 SDR
 S BARCYCLE=$P(BARDM(CY),U,3),BARDMC=$P(BARDM(CY),U,2),BARDM=$P(BARDM(CY),U) D LET2 ;bar*1.8*22 SDR
 Q
LETB ;SET CYCLES TO PRINT BATCHES
 ;start old code bar*1.8*22 SDR
 ;S B=0,CT=0
 ;F  S B=$O(^BARDMLG(DUZ(2),BARBAT,100,$P(BARDM(CY),U),10,B)) Q:B'?1N.N  D
 ;.S BARDM=$P(^BARDMLG(DUZ(2),BARBAT,100,$P(BARDM(CY),U),10,B,0),U)
 ;.S CYCLE=$P(BARDM(CY),U,2)
 ;.S BARDMC=0,BARDMC=$O(^BARDM(DUZ(2),BARDM,100,"B",CYCLE,BARDMC))
 ;.D LET2
 ;end old code start new code
 S BARB=0,BARCT=0
 F  S BARB=$O(^BARDMLG(DUZ(2),BARBAT,100,$P(BARDM(CY),U),10,BARB)) Q:BARB'?1N.N  D
 .S BARDM=$P(^BARDMLG(DUZ(2),BARBAT,100,$P(BARDM(CY),U),10,BARB,0),U)
 .S BARCYCLE=$P(BARDM(CY),U,2)
 .S BARDMC=0,BARDMC=$O(^BARDM(DUZ(2),BARDM,100,"B",BARCYCLE,BARDMC))
 .D LET2
 ;end new code
 Q
 ;
LET2 ;
 S Y=$P(^BARDM(DUZ(2),BARDM,100,BARDMC,0),U,5) D DD^%DT S BARDTP=Y
 S BARDM("DOS")=""
 S X=$P(^BARDM(DUZ(2),BARDM,100,BARDMC,0),U,6),X2="2$" D COMMA^%DTC S BARAMTO="$"_$P(X,"$",2)
 S BARBIEN=$P(^BARDM(DUZ(2),BARDM,0),U)
 S BARBILN=$$VAL^XBDIQ1(90053.05,BARDM,.01)
 S BARDAC=$P(^BARBL(DUZ(2),BARBIEN,0),U,3),BARDBDT=$P(^(0),U,7),BARD3P=$P(^(0),U,17)
 S BARD3PD=$P($G(^BARBL(DUZ(2),BARBIEN,0)),U,22)  ;bar*1.8*22 SDR
 S BARDM("DOS")=$$VAL^XBDIQ1(90050.01,BARBIEN,102)
 D INSTYP^BARDMU
 S BARDI=$$VAL^XBDIQ1(90050.02,BARDAC,.01)
 I $P(^BARBL(DUZ(2),BARBIEN,0),U,15)'>0 Q
 D POLCHK
 D PRINT^BARDMLP1
 Q
POLCHK ;TEST FOR POLICY NO, POLICY HOLDER AND POLICY HOLDER DOB
 S BARMIN=0
 I BARDACG'="VA(" D
 .S BARDM("PAT_IEN")=$P(^BARBL(DUZ(2),BARBIEN,1),U)
 .;S BARHRN=$P(^AUPNPAT(BARDM("PAT_IEN"),41,DUZ(2),0),U,2)  ;bar*1.8*22 SDR
 .;start new code bar*1.8*22 SDR
 .S BARVLOC=$P($G(^ABMDBILL(BARD3PD,BARD3P,0)),U,3)
 .S BARHRN=$P($G(^AUPNPAT(BARDM("PAT_IEN"),41,BARVLOC,0)),U,2)
 .I BARHRN="" S BARHRN=$P($G(^AUPNPAT(BARDM("PAT_IEN"),41,DUZ(2),0)),U,2)
 .;end new code bar*1.8*22 SDR
 .S BARPAT=$P(^DPT(BARDM("PAT_IEN"),0),U)
 .S BARDOB=$$GET1^DIQ(2,BARDM("PAT_IEN"),".03","E")  ;bar*1.8*22 SDR
 .S BARNPIF=$P($$NPI^XUSNPI("Organization_ID",DUZ(2)),U)  ;bar*1.8*22
 .S BARNPIP=$S(+$$GET1^DIQ(90050.01,BARBIEN,113,"I")'=0:$P($$NPI^XUSNPI("Individual_ID",$$GET1^DIQ(90050.01,BARBIEN,113,"I")),U),1:"")  ;bar*1.8*22 SDR
 D VARSET
 I BARDACG="AUTNINS(" D INSCHK
 I BARDACG="AUPNPAT(" D PATCHK
 I BARDACG="VA(" D PERCHK
 Q
INSCHK ;
 S BARDM("INS")=^AUTNINS(BARDACI,0)
 S BARDM("INS_NM")=$P(BARDM("INS"),U),BARDM("INS_STR")=$P(BARDM("INS"),U,2)
 S BARDM("INS_CTY")=$P(BARDM("INS"),U,3),BARDM("INS_ST")=$P(BARDM("INS"),U,4)
 S BARDM("INS_ZP")=$P(BARDM("INS"),U,5)
 S BARDM("INS_TX")=$P(BARDM("INS"),U,11)
 ;start old code bar*1.8*22
 ;S TST=0
 ;I $D(^ABMDBILL(DUZ(2),BARD3P,13,"B",BARDACI)) D
 ;.S L="",L=$O(^ABMDBILL(DUZ(2),BARD3P,13,"B",BARDACI,L))
 ;.D INSCHK1
 ;Q:TST=1
 ;S L=0 F  S L=$O(^ABMDBILL(DUZ(2),BARD3P,13,L)) Q:L'?1N.N  I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,2)=BARDACI D INSCHK1 Q
 ;end old code start new code
 S BARTST=0
 I $D(^ABMDBILL(BARD3PD,BARD3P,13,"B",BARDACI)) D
 .S BARL="",BARL=$O(^ABMDBILL(BARD3PD,BARD3P,13,"B",BARDACI,BARL))
 .D INSCHK1
 Q:BARTST=1
 S BARL=0 F  S BARL=$O(^ABMDBILL(BARD3PD,BARD3P,13,BARL)) Q:BARL'?1N.N  I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,2)=BARDACI D INSCHK1 Q
  ;end new code
 Q
INSCHK1 ;
 ;start old code bar*1.8*2
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,4)'="" D MCR^BARDMRE Q
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,5)'="" D RR^BARDMRE Q
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,7)'="" D MCD^BARDMRE Q
 ;I $P(^ABMDBILL(DUZ(2),BARD3P,13,L,0),U,8)'="" D PRVT^BARDMRE Q
 ;end old code start new code
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,4)'="" D MCR^BARDMRE Q
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,5)'="" D RR^BARDMRE Q
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,7)'="" D MCD^BARDMRE Q
 I $P(^ABMDBILL(BARD3PD,BARD3P,13,BARL,0),U,8)'="" D PRVT^BARDMRE Q
 ;end new code
 Q
PATCHK ;
 S BARDM("INS_NM")=$P(BARPAT,",",2)_" "_$P(BARPAT,",",1)_" "_$P(BARPAT,",",3)
 S BARDM("INS_STR")=$$VAL^XBDIQ1(2,BARDM("PAT_IEN"),.111)
 S BARDM("INS_CTY")=$$VAL^XBDIQ1(2,BARDM("PAT_IEN"),.114)
 S BARDM("INS_ST")=$P(^DPT(BARDM("PAT_IEN"),.11),U,5)
 S BARDM("INS_ZP")=$$VAL^XBDIQ1(2,BARDM("PAT_IEN"),.116)
 ;**CHECK AND SET BARMIN FOR MINOR OR NOT
 S X1=$$VALI^XBDIQ1(90050.01,BARBIEN,102)
 S X2=$$VALI^XBDIQ1(2,BARDM("PAT_IEN"),.03)
 D ^%DTC S AGE=X\365.25 S:AGE>17 BARMIN=1
 Q
PERCHK ;
 S BARPAT=$$VAL^XBDIQ1(200,BARDACI,.01)
 S BARDM("INS_NM")=$P(BARPAT,",",2)_" "_$P(BARPAT,",",1)_" "_$P(BARPAT,",",3)
 S BARDM("INS_STR")=$$VAL^XBDIQ1(200,BARDACI,.111)
 S BARDM("INS_CTY")=$$VAL^XBDIQ1(200,BARDACI,.114)
 S BARDM("INS_ST")=$$VAL^XBDIQ1(200,BARDACI,.115)
 S BARDM("INS_ZP")=$$VAL^XBDIQ1(200,BARDACI,.116)
 Q
LERR ;
 W !,"Letters have not been printed for this Bill" S BARQ=1
 Q
