BAREDEP1 ; IHS/SD/TPF - AR ERA NONPAYMENT CHECKER ; 01/30/2009
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**5,6,10**;JUN 22,2008
 Q
NONPAYCH(IMPDA) ;EP - CHECK PAYMENTS NOT MATCHED WITH A REVERSAL
 N CLMDA,ERACHECK,CLSTATUS,POSTAS,CNT,CLIENS
 N BPR02  ;BAR*1.8*6 SCR119
 I $G(DEBUG) W !!!,"LOOKING FOR NON-MATCHED PAYMENTS AGAINST NONPAYMENT BATCHES"
 S CLMDA=0
 F CNT=1:1 S CLMDA=$O(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA)) Q:'CLMDA  D
 .W:'(CNT#1000) "."
 .Q:$P($G(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,0)),U,2)="P"  ;DON'T CHECK POSTED CLAIMS
 .;Q:$D(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,4,"B",44))  ;DON'T PROCESS USER STATUS OVVERRIDE BAR*1.8*6 SCR120
 .Q:$$OVERIDE^BAREDEP1(CLMDA)                         ;MRS:BAR*1.8*10 D159-1 AND 2
 .S BPR02=+$P($G(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,2)),U,5)  ;BPR MONETARY AMOUNT BAR*1.8*6 SCR119 IHS/SD/TPF
 .S CLIENS=CLMDA_","_IMPDA_","
 .S ERACHECK=$$GET1^DIQ(90056.0205,CLIENS,201,"E")  ;CHECK/EFT TRACE
 .S CLSTATUS=$P($$GET1^DIQ(90056.0205,CLIENS,.11,"E")," ")  ;E-CLAIM STATUS CODE (CLP02)
 .I CLSTATUS="" S CLSTATUS=$P($G(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,2)),U,4)  ;MRS:BAR*1.8*10 H2555
 .Q:(U_"1"_U_"2"_U_"3"_U_"19"_U_"20"_U_"21"_U)'[(U_CLSTATUS_U)  ;PRIMARY,SECONDARY,TERTIARY AS WELL AS THOSE 19,20,21 FORWARDED TO ADDITIONAL PAYER BAR*1.8*6 IM29637
 .S POSTAS=$$GET1^DIQ(90056.0205,CLIENS,501,"I")
 .Q:POSTAS=138!(POSTAS=139)
 .;D NONPAY(ERACHECK,IMPDA,CLMDA)
 .D NONPAY(ERACHECK,IMPDA,CLMDA,BPR02)  ;BAR*1.8*6 SCR119 
 Q
 ;NONPAY(ERACHECK,IMPDA,CLMDA)
NONPAY(ERACHECK,IMPDA,CLMDA,BPR02) ;EP - SEE IF TDN/IPAC = NONPAYMENT  ;BAR*1.8*6 SCR119 IHS/SD/TPF
 N TDNIPAC,BARCOLDA,ERRORS,BARITMDA,NONPAY,NOITEM,NOHEAD
 S (NOITEM,NOHEAD,NONPAY)=0
 S BARCOLDA=$O(^BARCOL(DUZ(2),"D",ERACHECK,""))
 I 'BARCOLDA,(BPR02=0) Q  ;BAR*1.8*6 SCR119
 I 'BARCOLDA D  Q
 .S ERRORS("NB")=""  ;NO BATCH FOUND FOR ERA CHECK
 .D ADDREAS^BAREDP04(IMPDA,CLMDA,.ERRORS)
 .K ERRORS
 ;CHECK ALL ERACHECK BATCHES/ITEMS FOR A NONPAYMENT
 S BARCOLDA=""
 F  S BARCOLDA=$O(^BARCOL(DUZ(2),"D",ERACHECK,BARCOLDA)) Q:BARCOLDA=""  D
 .S TDNIPAC=$$GET1^DIQ(90051.01,BARCOLDA_",",28,"E")
 .I TDNIPAC="" S NOHEAD=1
 .Q:TDNIPAC'=""&(TDNIPAC'="NONPAYMENT")
 .I TDNIPAC="NONPAYMENT" S ERRORS("P NP")="" Q
 .S BARITMDA=""
 .F  S BARITMDA=$O(^BARCOL(DUZ(2),"D",ERACHECK,BARCOLDA,BARITMDA)) Q:BARITMDA=""  D
 ..S TDNIPAC=$$GET1^DIQ(90051.1101,BARITMDA_","_BARCOLDA_",",20,"E")
 ..I TDNIPAC="" S NOITEM=1
 ..I TDNIPAC="NONPAYMENT" S ERRORS("P NP")=""
 .;IF THERE ARE NO TDN FOR BOTH TOP LEVEL AND ITEM LEVEL THEN THIS IS ONE WITH MISSING TDN
 .;ONE OR THE OTHER HAS TO BE POPULATED
 .I NOITEM,NOHEAD D
 ..S ERRORS("NIPAC")=""     ;TDN/IPAC MISSING IN RPMS BATCH AND ITEM
 .S (NOITEM,NOHEAD)=0
 I $D(ERRORS) D
 .I $G(DEBUG) W !!?5,"PROBLEM WITH A NONPAYMENT BATCH FOR ERA CHECK: ",ERACHECK
 .D ADDREAS^BAREDP04(IMPDA,CLMDA,.ERRORS)
 .K ERRORS
 Q
 ;
OVERIDE(X) ;EP; EXTRINSIC FUNCTION TO CHECK FOR OVERRIDES MRS:BAR*1.8*10 D159-2
 ; ENTERS WITH X = CLAIM IEN
 N BAROR,I
 S BAROR=0           ;NOT AN OVERRIDE
 ;NODE 44 = USER STATUS OVVERRIDE
 ;     45 = PLB CHECK
 ;     46 = REVERSAL
 ;     47 = NEGATIVE PAYMENT
 F I=44:1:47 D
 .I $D(^BAREDI("I",DUZ(2),IMPDA,30,X,4,"B",I)) S BAROR=1 Q
 Q BAROR
