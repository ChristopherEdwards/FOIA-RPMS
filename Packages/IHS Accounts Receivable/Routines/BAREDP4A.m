BAREDP4A ; IHS/SD/SDR - MATCH REASONS AND CLAIMS - 2 ; 01/09/2009
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**20,21**;OCT 26, 2005
 ;CLM/CLM2 split from BAREDP04 due to size
 ;;
PAYER ;EP
 S IENS=BARCKIEN_","_IMPDA_","
 S EPAYER=$$GET1^DIQ(90056.02011,IENS,".06")
 D ^XBFMK
 S DIC=$$DIC^XBDIQ1(90056.24)
 S DIC(0)="SMQ"
 S X=EPAYER
 D ^DIC
 Q:+Y<0  ;stop here if no match in A/R EDI BILL LOGIC file
 W !!,"A matching ""override"" has been set up for this A/R Account",!
 S BARIEN=+Y
 D DISPLAY^BAREDMCH
 D ^XBFMK
 K DIR
 S DIR(0)="Y"
 S DIR("A")="Do you wish to continue with the A/R Bill Matching using this criteria?"
 S DIR("B")="N"
 D ^DIR
 I Y=0 S QFLG=1 Q
 Q
CLM ;EP
 S BARX="B"
 K BILMATCH S BILMATCH=0  ;bar*1.8*20
 I $$GET1^DIQ(90056.0205,IENS,1.01)'="" D  Q
 .S BARBIEN=$$GET1^DIQ(90056.0205,IENS,1.01,"I")
 .D CLM3
 .;S (BARCNT,BILMATCH)=1,BILMATCH(BARBIEN)="",BARTMP(BARBIEN)="" I $G(DEBUG) W "  MATCHED"  ;bar*1.8*20
 ;I '$D(^BARBL(DUZ(2),"B",BARBILL)) S BARX="G"  ;Pharmacy POS  ;bar*1.8*20 REQ4
 I $O(^BARBL(DUZ(2),"B",$S(($L(+BARBILL)=$L(BARBILL)):BARBILL_"-",1:BARBILL)))'[BARBILL S BARX="G"  ;Pharmacy POS  ;bar*1.8*20 REQ4
 ;K BILMATCH S BILMATCH=0  ;bar*1.8*20
 S (BARBIEN,BARCNT)=0
 ;start new code bar*1.8*20 REQ4
 I BARX="G" D CLM2 Q
 S BARSAVE=BARBILL
 F  S BARBILL=$O(^BARBL(DUZ(2),"B",$S(($L(+BARBILL)=$L(BARBILL)):BARBILL_"-",1:BARBILL))) Q:(BARBILL'[BARSAVE)  D CLM2
 S BARBILL=BARSAVE
 ;
 Q
 ;end new code REQ4
CLM2 ;
 F  S BARBIEN=$O(^BARBL(DUZ(2),BARX,BARBILL,BARBIEN)) Q:'+BARBIEN  D
 .D CLM3
 .I (ERABILL'=BARAMT)!(ERADOS'=BARDOS) Q
 .S BARCNT=BARCNT+1
 .S BARTMP(BARBIEN)=""
 Q
CLM3 ;
 S ERABILL=$P($G(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,0)),U,5)  ;E-BILLED
 S ERADOS=$P($G(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,0)),U,8)  ;E-DOS BEGIN
 S BARDOS=$$GET1^DIQ(90050.01,BARBIEN,102,"I")  ;DOS BEGIN
 S ERATYPE=$P($G(^BAREDI("I",DUZ(2),IMPDA,30,CLMDA,0)),U,11)  ;E-CLM STATUS CD
 K %DT  ;IHS/SD/TPF 8/5/2011 bar*1.8*21 HEAT42678
 S X=ERADOS D ^%DT S ERADOS=Y
 S BARAMT=$$GET1^DIQ(90050.01,BARBIEN,13,"I")  ;AMT BILLED
 ;I $G(DEBUG) W !,"ERA BILL : ",BARBILL  ;bar*1.8*20 REQ4
 I $G(DEBUG) W !,"ERA BILL : ",$$GET1^DIQ(90056.0205,BAREIENS,.01)  ;bar*1.8*20 REQ4
 I $G(DEBUG) W ?35,"ERA BILL TYPE: ",ERATYPE
 I $G(DEBUG) W !,"ERA BILLED: ",ERABILL,?25,"ERA DOS: ",ERADOS
 I $G(DEBUG) W !,"A/R BILLED: ",BARAMT,?25,"A/R DOS: ",BARDOS
 I ERABILL<0 S ERABILL=-ERABILL I $G(DEBUG) W ?50,"ERA BILL IS A NEGATIVE AMOUNT"
 I (ERABILL=BARAMT),(ERADOS=BARDOS) S BILMATCH=BILMATCH+1,BILMATCH(BARBIEN)="" I $G(DEBUG) W "  MATCHED"
 Q
TRANSCK ;EP
 Q:$G(BARBIEN)=""  ;no bill number matched
 S BARTFLG=0
 S BARBTCH=$$GET1^DIQ(90056.02011,BARCKIEN,.07)
 S BARITEM=$$GET1^DIQ(90056.02011,BARCKIEN,.08)
 S BARTR=0,BARTFLG=0
 F  S BARTR=$O(^BARTR(DUZ(2),"AC",BARBIEN,BARTR)) Q:'BARTR  D  Q:(BARTFLG=1)
 .S BARTBTCH=$$GET1^DIQ(90050.03,BARTR,14)
 .S BARTITEM=$$GET1^DIQ(90050.03,BARTR,15)
 .I BARTBTCH'="",BARITEM'="",BARBTCH=BARTBTCH,BARITEM=BARTITEM S BARTFLG=1
 I BARTFLG S ERRORS("HTRN")=""
 Q
