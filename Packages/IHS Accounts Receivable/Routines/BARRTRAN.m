BARRTRAN ; IHS/SD/LSL - BATCH POSTED PAYMENTS REPORT ;
 ;;1.8;IHS ACCOUNTS RECEIVABLE;;OCT 26, 2005
 ;
START ; EP
 ; Collections report using FM print
 ;
LOOKUP ;
 ; Collection Register name lookup
 K DUOUT,DTOUT,BAREFLG
 S DIC="90051.01"
 S DIC(0)="AEMQZ"
 D ^DIC
 K DIC
 S:Y<0 BAREFLG=1
 S:$D(DUOUT) BAREFLG=1
 S:$D(DTOUT) BAREFLG=1
 I $D(BAREFLG) Q
 I Y>0 D
 . S BARBATCH=+Y
 . S BARBEX=$P(Y(0),U)
 G:$D(BAREFLG) END
 ;
DIPVAR ;
 ; Set up DIP variables and Header routine
 S BAR("SITE")=$P(^DIC(4,DUZ(2),0),U)
 S DIC="90050.03"
 S L=0
 S FLDS="15;W3;N;""IT"",4;L15;N,&3.5;""PMT"",NUMDATE4(#12);""DT"",4:3;L15;""BILLED TO"",4:101;L15;""PATIENT"""  ;Y2000
 S BY="@14,+15;S1,6,4,@101"
 S FR=BARBEX_",,,,PAYMENT"
 S TO=BARBEX_",,,,PAYMENT"
 ;
PRINT ;
 ; Print
 ; GET DEVICE (QUEUEING ALLOWED)
 S Y=$$DIR^XBDIR("S^P:PRINT Output;B:BROWSE Output on Screen","Do you wish to ","P","","","",1)
 K DA
 Q:$D(DIRUT)
 S DHD=BARBEX_"POSTINGS ***CONFIDENTIAL PATIENT INFORMATION***"
 I Y="B" D  Q
 . S XBFLD("BROWSE")=1
 . S BARIOSL=IOSL
 . S IOSL=600
 . D VIEWD^XBLM("EN1^DIP")
 . D FULL^VALM1
 . W $$EN^BARVDF("IOF")
 .D CLEAR^VALM1 ;clears out all list man stuff
 .K XQORNEST,VALMKEY,VALM,VALMAR,VALMBCK,VALMBG,VALMCAP,VALMCNT
 .K VALMOFF,VALMCON,VALMDN,VALMEVL,VALMIOXY,VALMKEY,VALMLFT,VALMLST
 .K VALMMENU,VALMSGR,VALMUP,VALMWD,VALMY,XQORS,XQORSPEW,VALMCOFF
DEVE .;
 .S IOSL=BARIOSL
 .K BARIOSL
 D EN1^DIP
 ;
DSP ; EP for VALM
 D ^%ZISC,HOME^%ZIS
 D EOP^BARUTL(1)
 ;
END ;
 Q
