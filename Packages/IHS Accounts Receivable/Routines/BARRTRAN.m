BARRTRAN ; IHS/SD/LSL - BATCH POSTED PAYMENTS REPORT ;
 ;;1.8;IHS ACCOUNTS RECEIVABLE;*24*;OCT 26, 2005;Build 69
 ; IHS/SD/POTT HEAT133069 BAR*1.8*24
 ; IHS/SD/POTT HEAT124730 ADDING DOS TO REPORT BAR*1.8*24
START ; EP
 ; Collections report using FM print
 ;
LOOKUP ;
 ; Collection Register name lookup
 K DUOUT,DTOUT,BAREFLG
 S DIC="90051.01"
 S DIC(0)="AEMQZ"
 D ^DIC
 K DIC
 S:Y<0 BAREFLG=1
 S:$D(DUOUT) BAREFLG=1
 S:$D(DTOUT) BAREFLG=1
 I $D(BAREFLG) Q
 I Y>0 D
 . S BARBATCH=+Y
 . S BARBEX=$P(Y(0),U)
 G:$D(BAREFLG) END
 ;
DIPVAR ;
 ; Set up DIP variables and Header routine
 S BAR("SITE")=$P(^DIC(4,DUZ(2),0),U)
 S DIC="90050.03"
 S L=0
 ;S FLDS="15;W3;N;""IT"",4;L15;N,&3.5;""PMT"",NUMDATE4(#12);""DT"",4:3;L15;""BILLED TO"",602;L15;""SESSION"",601;C10;""UFMS EXPORT FILE"""  ;Y2000
 S FLDS="15;W3;N;""IT"",4;L15;N,&3.5;""PMT"",NUMDATE4(#12);""DT / DOS"",4:3;L15;""BILLED TO"",4:101;L15;""PATIENT"""  ;Y2000
 S BY="@14,+15;S1,6,4,@101"
 S DHIT="W ?34,$$GETDOS^BARRTRAN(DITTO(2))" ;P.OTT HEAT#124730 BAR*1.8*24
 ;A string of M code which will be executed for every entry after all the fields specified in FLDS have been printed. 
 S FR=BARBEX_",,,,PAYMENT"
 S TO=BARBEX_",,,,PAYMENT"
 ;
PRINT ;
 ; Print
 ; GET DEVICE (QUEUEING ALLOWED)
 S Y=$$DIR^XBDIR("S^P:PRINT Output;B:BROWSE Output on Screen","Do you wish to ","P","","","",1)
 K DA
 Q:$D(DIRUT)
 S DHD=BARBEX_"POSTINGS ***CONFIDENTIAL PATIENT INFORMATION***"
 I Y="B" D  Q
 . S XBFLD("BROWSE")=1
 . S BARIOSL=IOSL
 . S IOSL=600
 . D VIEWD^XBLM("EN1^DIP")
 . D FULL^VALM1
 . W $$EN^BARVDF("IOF")
 .D CLEAR^VALM1 ;clears out all list man stuff
 .K XQORNEST,VALMKEY,VALM,VALMAR,VALMBCK,VALMBG,VALMCAP,VALMCNT
 .K VALMOFF,VALMCON,VALMDN,VALMEVL,VALMIOXY,VALMKEY,VALMLFT,VALMLST
 .K VALMMENU,VALMSGR,VALMUP,VALMWD,VALMY,XQORS,XQORSPEW,VALMCOFF
DEVE .;
 .S IOSL=BARIOSL
 .K BARIOSL
 D EN1^DIP
 ;
DSP ; EP for VALM
 D ^%ZISC,HOME^%ZIS
 D EOP^BARUTL(1)
 ;
END ;
 Q
GETDOS(BARBILL) ;HEAT#124730
 NEW X
 S X=$$GET1^DIQ(90050.01,BARBILL,102,"I")
 Q $$SDT^BARDUTL(X)
 ;---EOR 
