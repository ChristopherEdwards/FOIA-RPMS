BARUFER1 ; IHS/SD/TPF - UFMS ERROR RESOLUTION ; 03/28/2008
 ;;1.8;IHS ACCOUNTS RECEIVABLE;**4**;JAN 24, 2008
 ;NEW ROUTINE; BAR*1.8*4
 Q
 ;
ERRINQ ;EP - INQUIRE INTO 'A/R UFMS ERRORS' FILE
 K DIE,DIC,DIR,DA,DR
 S DIC="^BARUFERR("
 S DIC(0)="AEMQ"
 D ^DIC
 Q:Y<0
 K DIE,DIC,DIR,DA,DR
 D DISPLAY(+Y)
 Q
 ;
DISPLAY(CODE) ;EP - DISPLAY CAPTIONED FIELDS
 S IENS=CODE_","
 S SHORT=$$GET1^DIQ(90057.1,IENS,.01,"E")
 S SYNONYM=$$GET1^DIQ(90057.1,IENS,.02,"E")
 S ERRTYPE=$$GET1^DIQ(90057.1,IENS,.03,"E")
 S DISMSG=$$GET1^DIQ(90057.1,IENS,.04,"E")
 W !!!,"SHORT DESCRIPTION:"
 W !,SHORT
 W !,"SYNONYM: ",SYNONYM
 W ?25,"TYPE OF ERROR: ",ERRTYPE
 W !,"DISPLAY MESSAGE:"
 W !,DISMSG
 W !!
 ;
 S DIWL=12,DIWR=75
 S DIWF="WC65|"
 N LN,X
 S LN=0
 F  S LN=$O(^BARUFERR(CODE,12,LN)) Q:'LN  D
 .S X=$G(^BARUFERR(CODE,12,LN,0))
 .D ^DIWP
 D ^DIWW
 K DIR S DIR(0)="E" D ^DIR
 Q
