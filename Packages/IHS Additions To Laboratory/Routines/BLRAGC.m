BLRAGC ; IHS/MSC/SAT - LABORATORY ACCESSION GUI PRE/POST ROUTINES ;
 ;;5.2;IHS LABORATORY;**1031**;NOV 01, 1997;Build 185
 ;
PRE ;Pre-Install routine
 Q
 ;
POST ;Post-Install routine
 D AFMSC
 D RPC
 Q
 ;
RPC ;register RPCs into the option CIAV VUECENTRIC and OR CPRS GUI CHART
 ;
 N BLRJ,BLRK,BLROPT,BLRRPC
 N MSCIEN,MSCRPC,MSCFDA,MSCERR
 F BLRK=1:1 D  Q:BLROPT=""
 .S BLROPT=$P($T(OPTL+BLRK),";;",2)
 .Q:BLROPT=""
 .S MSCIEN=$O(^DIC(19,"B",BLROPT,0))
 .I 'MSCIEN Q  W !!,"**** COULD NOT FIND THE OPTION, CIAV VUECENTRIC! ****",!!! Q
 .;
 .F BLRJ=1:1 D  Q:BLRRPC=""
 ..S BLRRPC=$P($T(RPCL+BLRJ),";;",2)
 ..Q:BLRRPC=""
 ..S MSCRPC=$O(^XWB(8994,"B",BLRRPC,0))
 ..I 'MSCRPC W !!,"**** COULD NOT FIND RPC, "_BLRRPC_"! ****",!!! Q
 ..I $D(^DIC(19,MSCIEN,"RPC","B",MSCRPC)) Q  W !!,"*** RPC, """_BLRRPC_""" ALREADY REGISTERED TO CPRS/CV! ****",!!! Q
 ..S MSCRPC=$P(^XWB(8994,MSCRPC,0),U)
 ..S MSCFDA(19.05,"+1,"_MSCIEN_",",.01)=MSCRPC
 ..D UPDATE^DIE("E","MSCFDA","","MSCERR")
 ..I $D(MSCERR) W !!,"ERROR IN ASSIGNING "_BLRRPC_" RPC TO CPRS GUI",!!!
 Q
 ;
OPTL ;
 ;;CIAV VUECENTRIC
 ;;OR CPRS GUI CHART
 ;
RPCL ;
 ;;BLR REF LAB USING LEDI
 ;;BLR ICD LOOKUP
 ;;BLR ORDER REASON LKUP
 ;;BLR PATIENT LOOKUP
 ;;BLR PRINTERS AVAILABLE
 ;;BLR USER LOOKUP
 ;;BLR ACCESSION
 ;;BLR ACCESSION PRINT
 ;;BLR ALL NON-ACCESSIONED
 ;;BLR ALL-ACCESSIONED
 ;;BLR COLLECTION INFO
 ;;BLR DELETE TEST
 ;;BLR ORDER/TEST STATUS
 ;;BLR SHIP CONFIG
 ;;BLR MANIFEST BUILD
 ;;BLR MANIFEST CLOSE/SHIP
 ;;BLR MANIFEST DISPLAY
 ;;BLR MANIFEST START
 ;;BLR MANIFEST TEST ADD
 ;;BLR MANIFEST TEST REMOVE
 ;;BLR MANIFEST TESTS TO ADD
 ;;BLR XPAR DAYS TO ACCESSION
 ;;BLRAG05G AOE
 ;
AFMSC ;build AFMSC xref from file 69
 N BLRRES,BLRXREF
 S BLRXREF("FILE")=69
 S BLRXREF("TYPE")="MU"
 S BLRXREF("NAME")="AFMSC"
 S BLRXREF("ROOT FILE")=69.03
 S BLRXREF("SHORT DESCR")="ACCESSION DATE"
 S BLRXREF("DESCR,1")="This Cross Reference is an index of ACCESSION DATEs from the TEST"
 S BLRXREF("DESCR,2")="multiple."
 S BLRXREF("DESCR,3")="For every test that has an ACCESSION DATE defined, the index will"
 S BLRXREF("DESCR,4")="be the date in FM format."
 S BLRXREF("USE")="S"
 S BLRXREF("EXECUTION")="R"
 S BLRXREF("ACTIVITY")="IR"
 S BLRXREF("SET CONDITION")=""
 S BLRXREF("KILL CONDITION")=""
 S BLRXREF("SET")="S MSCDT=+$P($G(^LRO(69,DA(2),1,DA(1),2,DA,0)),U,3) S:MSCDT'=0 ^LRO(69,""AFMSC"",MSCDT,DA(2)_""|""_DA(1)_""|""_DA)="""""
 S BLRXREF("KILL")="S MSCDT=+$P($G(^LRO(69,DA(2),1,DA(1),2,DA,0)),U,3) K ^LRO(69,""AFMSC"",MSCDT,DA(2)_""|""_DA(1)_""|""_DA)"
 S BLRXREF("WHOLE KILL")="K ^LRO(69,""AFMSC"")"
 S BLRXREF("VAL",1)=2
 S BLRXREF("VAL",1,"SUBSCRIPT")=1
 ;
 S BLRRES=""
 D CREIXN^DDMOD(.BLRXREF,"SW",.BLRRES)
 Q
