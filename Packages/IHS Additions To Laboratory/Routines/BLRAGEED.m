BLRAGEED ; IHS/MSC/MKK - BLR AGE DETAIL Parameter EDit  ; 17-Dec-2015 15:37 ; MKK
 ;;5.2;IHS LABORATORY;**1038**;NOV 01, 1997;Build 6
 ;
EEP ; Ersatz EP
 D EEP^BLRGMENU
 Q
 ;
PEP ; EP
CHANGE ; EP
 NEW (DILOCKTM,DISYS,DT,DTIME,DUZ,IO,IOBS,IOF,IOM,ION,IOS,IOSL,IOST,IOT,IOXY,U,XPARSYS,XQXFLG)
 ;
 S HEADER(1)="IHS Laboratory"
 S HEADER(2)="BLR AGE DETAIL Parameter"
 S HEADER(3)=$$CJ^XLFSTR("Modify Value",IOM)
 ;
 S RESULT=$$GET^XPAR("PKG","BLR AGE DETAIL",1,"Q")
 S RESULT=$S(RESULT:"YES",RESULT=0:"NO",1:"")
 ;
 D HEADERDT^BLRGMENU
 ;
 D ^XBFMK
 S DIR(0)="YO"
 S DIR("A")="BLR AGE DETAIL (YES/NO)"
 S:$L(RESULT) DIR("B")=RESULT
 D ^DIR
 ;
 I +$G(DIRUT)!($G(Y)="") D  Q
 . W !!,?4,"Invalid/No Entry/Quit. Routine Ends."
 . D PRESSKEY^BLRGMENU(9)
 ;
 S ANSWER=$S($E($$UP^XLFSTR(X))="Y":"YES",1:"NO")
 ;
 D EN^XPAR("PKG","BLR AGE DETAIL",,ANSWER,.ERRS)
 ;
 I +$G(ERRS)<1 D
 . S RESULT=$$GET^XPAR("PKG","BLR AGE DETAIL",1,"Q")
 . W !!,?4,"BLR AGE DETAIL Parameter is currently ",$S(RESULT:"YES",RESULT=0:"NO",1:"")
 . D PRESSKEY^BLRGMENU(9)
 ;
 D:+$G(ERRS)>0 RPTERR(.ERRS)
 ;
 Q
 ;
RPTERR(ERROR) ; EP
 NEW ERRDESC,ERRNUM,NOWH
 ;
 S ERRNUM=$P(ERROR,"^")
 S ERRDESC=$P(ERROR,"^",2)
 ;
 W !!,"*** ERROR Modifying BLR AGE DETAIL paramter. ***",!!
 W ?4,"Error Number:",ERRNUM,!
 W:$L(ERRDESC)<50 ?9,"Description:",ERRDESC
 I $L(ERRDESC)>49 D LINEWRAP^BLRGMENU(9,ERRDESC,60)
 W !
 ;
 D PRESSKEY^BLRGMENU(9)
 ;
 S BLRVERN=$P($P($T(+1),";")," ")
 S NOWH=$H
 S:$D(^XTMP(BLRVERN))<1 XTMP(BLRVERN)="^"_$$DT^XLFDT_"^Errors Modifying BLR AGE DETAIL Parameter"
 S $P(^XTMP(BLRVERN),"^")=$$HTFM^XLFDT(+$H+30)
 ;
 M ^XTMP(BLRVERN,NOWH,"01","DUZ")=DUZ
 S ^XTMP(BLRVERN,NOWH,"02","ERROR")=ERROR
 ;
 Q
