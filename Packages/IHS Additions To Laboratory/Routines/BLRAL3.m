BLRAL3 ;DAOU/ALA-ListMan program for delinquent labs 
 ;;5.2T9;LR;**1018**;Nov 17, 2004
 ;;5.2t6;LR;**1013,1015**;Nov 18, 2002
 ;; ;
EN ;EP
 ; -- main entry point for BLRA LAB RESULT DELINQ LIST
 D EN^VALM("BLRA LAB RESULT DELINQ LIST")
 Q
 ;
HDR ; -- header code
 S VALMHDR(1)=" "
 S VALMHDR(2)=" "
 Q
 ;
INIT ; -- init variables and list array
 D CLEAN^VALM10    ;Clears screen before display of list
 ;
 ;  Get the reverse video
 S X="IORVON;IORVOFF;IOBON;IOBOFF;IOIL;IOSTBM;IOSC;IORC;IOEDEOP;IOINHI;IOINORM;IOUON;IOUOFF" D ENDR^%ZISS
 ;
 S BLRALINE=0,BLRAPHY=""
 F  S BLRAPHY=$O(^TMP("BLRADELQ",$J,BLRAPHY)) Q:BLRAPHY=""  D
 . S TYP=""
 . F  S TYP=$O(^TMP("BLRADELQ",$J,BLRAPHY,TYP)) Q:TYP=""  D
 .. S BLRADT=""
 .. F  S BLRADT=$O(^TMP("BLRADELQ",$J,BLRAPHY,TYP,BLRADT)) Q:'BLRADT  D
 ... S BLRAP=""
 ... F  S BLRAP=$O(^TMP("BLRADELQ",$J,BLRAPHY,TYP,BLRADT,BLRAP)) Q:'BLRAP  D
 .... ;----- BEGIN IHS MODIFICATIONS LR*5.2*1018
 .... S BLRSS=""
 .... F  S BLRSS=$O(^TMP("BLRADELQ",$J,BLRAPHY,TYP,BLRADT,BLRAP,BLRSS)) Q:BLRSS=""  D
 ..... ;----- END IHS MODIFICATIONS
 ..... S BLRALINE=BLRALINE+1,BLRALVAR=""
 ..... S BLRADATA=$G(^TMP("BLRADELQ",$J,BLRAPHY,TYP,BLRADT,BLRAP,BLRSS))
 ..... S DATE=$P($G(^TMP("BLRADELQ",$J,BLRAPHY,TYP,BLRADT,BLRAP,BLRSS)),U,3)
 ..... S DATE=$$FMTE^XLFDT(DATE,2)
 ..... S BLRALVAR=$$SETFLD^VALM1(BLRALINE,BLRALVAR,"LINENUMBER")
 ..... S BLRALVAR=$$SETFLD^VALM1($P(BLRADATA,U,1),BLRALVAR,"ACCESSION")
 ..... S BLRALVAR=$$SETFLD^VALM1($P(BLRADATA,U,2),BLRALVAR,"PATIENTNAME")
 ..... S BLRALVAR=$$SETFLD^VALM1(DATE,BLRALVAR,"LABDATE")
 ..... S BLRALVAR=$$SETFLD^VALM1($P(BLRADATA,U,4),BLRALVAR,"PROVIDER")
 ..... S BLRALVAR=$$SETFLD^VALM1(" "_$P(BLRADATA,U,5),BLRALVAR,"STATUS")
 ..... S BLRALVAR=$$SETFLD^VALM1($J($P(BLRADATA,U,6),4),BLRALVAR,"DAYSDELINQ")
 ..... D SET^VALM10(BLRALINE,BLRALVAR)
 ..... ;
 ..... I $P(BLRADATA,U,5)="ABN" D CNTRL^VALM10(BLRALINE,65,3,IORVON,IORVOFF)
 ..... I $P(BLRADATA,U,5)="CRIT" D CNTRL^VALM10(BLRALINE,65,4,IORVON_IOBON,IORVOFF_IOBOFF)
 ;
 S VALMCNT=BLRALINE   ;IF LINE CNTER IS NULL THEN THERE WERE NO RESULTS
 I BLRALINE=0 S VALMSG="No Overdue Lab Results to Review at this time"
 Q
 ;
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 Q
 ;
EXPND ; -- expand code
 Q
 ;
