BLRALBM1 ;DAOU/ALA-Build Micro Results for Bacteria [ 11/18/2002  1:33 PM ]
 ;;5.2;LR;**1013,1015**;NOV 18, 2002
 ;
 ;
ANTI ;EP
 I $P($G(^LR(LRDFN,"MI",LRIDT,14,0)),U,4)>0 D
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=$E(BLRABLKS,1,28)_"Antibiotic Level(s):"
 . S BLRAZ="ANTIBIOTIC",BLRAZ1=20 D Z1
 . S BLRAZ=BLRAZ_"CONC RANGE (ug/ml)",BLRAZ1=42 D Z1
 . S BLRAZ=BLRAZ_"DRAW TIME"
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 . S B=0 F  S B=$O(^LR(LRDFN,"MI",LRIDT,14,B)) Q:B<1  D
 .. S BLRALAL=$G(^LR(LRDFN,"MI",LRIDT,14,B,0))
 .. S BLRAZ=$P(BLRALAL,U),BLRAZ1=20 D Z1
 .. S BLRAZ=BLRAZ_$P(BLRALAL,U,3),BLRAZ1=42 D Z1
 .. S BLRAZ=BLRAZ_$S($P(BLRALAL,U,2)="P":"PEAK",$P(BLRALAL,U,2)="T":"TROUGH",1:"")
 .. S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 Q
BACT ;EP
 I '$L($P($G(^LR(LRDFN,"MI",LRIDT,1)),U)) Q:'$D(LRWRDVEW)  Q:LRSB'=1
 D BUG
 I $D(^LR(LRDFN,"MI",LRIDT,2,0)) D GRAM
 I $D(^LR(LRDFN,"MI",LRIDT,25,0)) D BSMEAR
 I $D(^LR(LRDFN,"MI",LRIDT,3,0)) D BRMK,BACT^BLRALBM4
 I $D(^LR(LRDFN,"MI",LRIDT,4,0)),$P($G(^(0)),U,4)>0 D
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="Bacteriology Remark(s):"
 . S B=0 F  S B=+$O(^LR(LRDFN,"MI",LRIDT,4,B)) Q:B<1  D
  .. S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="   "_$G(^LR(LRDFN,"MI",LRIDT,4,B,0))
 Q
 ;
BUG S BLRABUG=$G(^LR(LRDFN,"MI",LRIDT,1))
 S LRTUS=$P(BLRABUG,U,2),DZ=$P(BLRABUG,U,3),LRUS=$P(BLRABUG,U,6)
 S LRNS=$P(BLRABUG,U,5),Y=$P(BLRABUG,U) D D^LRU
 S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="* BACTERIOLOGY "_$S(LRTUS="F":"FINAL",LRTUS="P":"PRELIMINARY",1:"")_" REPORT => "_Y_"   TECH CODE: "_DZ  ;IHS/ANMC/CLS 08/18/96
 S LRPRE=19 D PRE
 I $L(LRUS) S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="URINE SCREEN: "_$S(LRUS="N":"Negative",LRUS="P":"Positive",1:LRUS)
 I $L(LRNS) S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="SPUTUM SCREEN:  "_LRNS
 Q
 ;
GRAM S BLRAZ="GRAM STAIN:",BLRAZ1=14 D Z1
 S LRGRM=0 F  S LRGRM=+$O(^LR(LRDFN,"MI",LRIDT,2,LRGRM)) Q:LRGRM<1  D
 . S BLRAZ=BLRAZ_$G(^LR(LRDFN,"MI",LRIDT,2,LRGRM,0))
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 . S BLRAZ=$E(BLRABLKS,1,14)
 Q
 ;
BSMEAR S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="BACTERIOLOGY SMEAR/PREP:"
 S LRMYC=0 F  S LRMYC=+$O(^LR(LRDFN,"MI",LRIDT,25,LRMYC)) Q:LRMYC<1  D
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=$E(BLRABLKS,1,5)_$G(^LR(LRDFN,"MI",LRIDT,25,LRMYC,0))
 Q
 ;
BRMK S (LRBUG,LR2ORMOR)=0
 F LRAX=1,2 S LRBUG=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG)) Q:LRBUG<1  S:LRAX=2 LR2ORMOR=1
 I LRAX'=1 S (LRBUG,LRTSTS)=0 F LRAX=1:1 S LRBUG=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG)) Q:LRBUG<1  D LST
 Q
 ;
LST S (LRBUG(LRAX),LRORG)=$P($G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,0)),U)
 S BLRAORG=$G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,0))
 S LRQU=$P(BLRAORG,U,2),LRSSD=$P(BLRAORG,U,3,8)
 S LRORG=$P($G(^LAB(61.2,LRORG,0)),U)
 I LRSSD'?.U D
 . S LRSIC1=$P(LRSSD,U),LRSBC1=$P(LRSSD,U,2),LRDRTM1=$P(LRSSD,U,3)
 . S LRSIC2=$P(LRSSD,U,4),LRSBC2=$P(LRSSD,U,5),LRDRTM2=$P(LRSSD,U,6),LRSSD=1
 S BLRAZ=$E(BLRABLKS,1,17)
 I LRAX=1 D LIN S BLRAZ="CULTURE RESULTS:",BLRAZ1=17 D Z1
 S BLRAZ=BLRAZ_$S(LR2ORMOR:LRBUG_", ",1:"")_LRQU_LRORG
 S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 I LRSSD D SSD
 S:$D(^LR(LRDFN,"MI",LRIDT,3,LRBUG,2)) LRTSTS=LRTSTS+1 I $D(^LR(LRDFN,"MI",LRIDT,3,LRBUG,3,0)),$P($G(^(0)),U,4)>0 D MIC
 I $D(^LR(LRDFN,"MI",LRIDT,3,LRBUG,1,0)),$P($G(^(0)),U,4)>0 D CMNT
 Q
 ;
SSD S LRDRTM1=$S(LRDRTM1="P":"PEAK",LRDRTM1="T":"TROUGH",1:LRDRTM1),LRDRTM2=$S(LRDRTM2="P":"PEAK",LRDRTM2="T":"TROUGH",1:LRDRTM2)
 S BLRAZ=$E(BLRABLKS,1,20)
 I $L(LRSIC1) S BLRAZ=BLRAZ_"SIT " S:$L(LRDRTM1) BLRAZ=BLRAZ_"("_LRDRTM1_"): "_LRSIC1
 I $L(LRSBC1) S BLRAZ=BLRAZ_"SBT " S:$L(LRDRTM1) BLRAZ=BLRAZ_"("_LRDRTM1_"): "_LRSBC1
 I $L(LRSIC2) S BLRAZ=BLRAZ_"SIT " S:$L(LRDRTM2) BLRAZ=BLRAZ_"("_LRDRTM2_"): "_LRSIC2
 I $L(LRSBC2) S BLRAZ=BLRAZ_"SBT " S:$L(LRDRTM2) BLRAZ=BLRAZ_"("_LRDRTM2_"): "_LRSBC2
 Q
 ;
MIC S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=$E(BLRABLKS,1,18)="Antibiotic"
 S B=0 F  S B=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG,3,B)) Q:B<1  D
 . I $L($P($G(^(B,0)),U,2,3))>0 D
 .. S BLRAZ=$G(^TMP($J,"BLRA",BLRADSP,0)),BLRAZ1=35 D Z1
 .. S BLRAZ=BLRAZ_"MIC (ug/ml)",BLRAZ1=50 D Z1
 .. S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ_"MBC (ug/ml)"
 S B=0 F  S B=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG,3,B)) Q:B<1  D
 . S BLRAMIC=$G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,3,B,0))
 . S BLRAZ=$E(BLRABLKS,1,18)_$P(BLRAMIC,U),BLRAZ1=35 D Z1
 . S BLRAZ=BLRAZ_$J($P(BLRAMIC,U,2),7),BLRAZ1=50 D Z1
 . S BLRAZ=BLRAZ_$J($P(BLRAMIC,U,3),7)
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 Q
 ;
CMNT S LRPC=0
 F A=0:1 S LRPC=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG,1,LRPC)) Q:LRPC<1  D
 . I A=0 D  Q
 .. S BLRAZ="Comment: ",BLRAZ1=20 D Z1
 .. S BLRAZ=BLRAZ_$G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,1,LRPC,0))
 .. S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=$E(BLRABLKS,1,20)_BLRAZ
 . S BLRAZ=$G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,1,LRPC,0))
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=$E(BLRABLKS,1,29)_BLRAZ
 Q
 ;
PRE ;EP
 Q:LRTUS["F"&('$D(^XUSEC("LRLAB",DUZ))!$D(LRWRDVEW))
 I +$O(^LR(LRDFN,"MI",LRIDT,LRPRE,0)) D
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)="Preliminary Comments: "
 . S J=0 F  S J=+$O(^LR(LRDFN,"MI",LRIDT,LRPRE,J)) Q:J<1  D
 .. S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=$E(BLRABLKS,1,3)_$G(^LR(LRDFN,"MI",LRIDT,LRPRE,J,0))
 D LIN
 Q
 ;
Z1 ;  Pad with trailing spaces
 F BLRAI=1:1:(BLRAZ1-$L(BLRAZ)) S BLRAZ=BLRAZ_" "
 Q
 ;
LIN ;EP
 ; Set a Blank Line
 S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=" "
 Q
