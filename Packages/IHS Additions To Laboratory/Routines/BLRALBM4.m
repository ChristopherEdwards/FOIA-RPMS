BLRALBM4 ;DAOU/ALA-Build Micro Results - BACTERIA, ANTIBIOTICS [ 11/18/2002  1:34 PM ]
 ;;5.2;LR;**1013,1015**;NOV 18, 2002
 ;
 ;
 ;
BACT ;EP
 Q:+$O(^LR(LRDFN,"MI",LRIDT,3,0))<1
 S LRFMT=$P($G(^LAB(69.9,1,0)),U,11),LRFMT=$S(LRFMT="":"I",1:LRFMT)
 K LRRES,LRINT
 S LRBUG=0 F A=1:1 S LRBUG=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG)) Q:LRBUG<1  S:+$O(^(LRBUG,2))'["2." A=A-1 I +$O(^(2))["2." D CHECK
 S (LRABCNT,LRBN)=0 F  S LRBN=+$O(LRRES(LRBN)) Q:LRBN<1  S LRABCNT=LRABCNT+1
 Q:'LRABCNT!($G(LREND))
 D LIN^BLRALBA
 S BLRAZ="ANTIBIOTIC SUSCEPTIBILITY TEST RESULTS:"
 I $D(^XUSEC("LRLAB",DUZ))&'$D(LRWRDVEW) D
 . S BLRAZ=BLRAZ_"  ('*' indicates display is suppressed)"
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 . D BUGHDR
 S LRCOMTAB=$S(LRFMT="B":A*13+17,1:A*5+17)
 S (LRAO,LRACNT)=0
 F  S LRAO=$O(^LAB(62.06,"AO",LRAO)) Q:LRAO<.001  D
 . S B=$O(^LAB(62.06,"AO",LRAO,0)) I B>0,$D(^LAB(62.06,B,0)) D AB
 K LR1PASS,LRRES,LRINT,LRBN  ;IHS/ANMC/CLS 08/18/96
 Q
CHECK S LRFLAG=0,LRBN=2 K LR1PASS
 F  S LRBN=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG,LRBN)) Q:LRBN'["2."  D
 . S B=$G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,LRBN))
 . S B1=$P(B,U),B2=$P(B,U,2)
 . I $L(B1),$D(^LAB(62.06,"AI",LRBN,B1)) S BLRAB1=$G(^LAB(62.06,"AI",LRBN,B1)) D FIRST
 S LRBN=2 F  S LRBN=+$O(LR1PASS(LRBN)) Q:LRBN<1  S B=LR1PASS(LRBN),B1=$P(B,U),B2=$P(B,U,2),B3=$P(B,U,3) D LAB
 K LRBN,LR1PASS,LRFLAG,B,B1,B2,B3,BLRAB1
 Q
FIRST S B2=$S(B2]"":B2,1:BLRAB1),B3=$P(B,U,3) S:$E(B2)'="R"&("A"[B3) LRFLAG=1 S LR1PASS(LRBN)=B1_U_B2_U_B3,^LR(LRDFN,"MI",LRIDT,3,LRBUG,LRBN)=LR1PASS(LRBN)
 Q
LAB I $D(^XUSEC("LRLAB",DUZ)),'$D(LRWRDVEW) S $P(LRRES(LRBN),U,A)=$S(B3="N"!(B3="R"&LRFLAG):B1_"*",1:B1),$P(LRINT(LRBN),U,A)=$S(B3="N"!(B3="R"&LRFLAG):B2_"*",1:B2) Q
 I B3=""!(B3="A")!(B3="R"&'LRFLAG) S $P(LRRES(LRBN),U,A)=B1,$P(LRINT(LRBN),U,A)=B2
 Q
AB ;
 S J=$P($G(^LAB(62.06,B,0)),U,2),BLRAAB=$G(^LAB(62.06,B,0))
 I $D(LRINT(J)),LRINT(J)'?."^" D
 . S BLRAZ=$E($P(BLRAAB,U),1,14),LRDCOM=$P(BLRAAB,U,3),LRACNT=LRACNT+1
 . D SIR
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 Q
BUGHDR S LRBUG=0 F A=0:1 S LRBUG=+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG)) Q:LRBUG<1  D
 . S BLRABUG=$G(^LR(LRDFN,"MI",LRIDT,3,LRBUG,0))
 . S LRORG=$P(BLRABUG,U),LRORG=$P($G(^LAB(61.2,LRORG,0)),U)
 . S:+$O(^LR(LRDFN,"MI",LRIDT,3,LRBUG,2))'["2." A=A-1 I +$O(^(2))["2." D ORG
 . I LRFMT="B" D
 .. ;D LIN^BLRALBA
 .. S BLRAZ="" F J=1:1:A S BLRAZ1=J-1*13+15 D Z1 S BLRAZ=BLRAZ_":"
 . D LIN^BLRALBA F J=1:1:A D
 .. I LRFMT'="B" S BLRAZ1=(J*5+10) D Z1 S BLRAZ=BLRAZ_":"
 .. I LRFMT="B" S BLRAZ1=J-1*13+15 D Z1 S BLRAZ=BLRAZ_"SUSC  INTP"
 Q
 ;
ORG D LIN^BLRALBA
 S BLRAZ=""
 I A>0 F J=1:1:A S BLRAZ=$E(BLRABLKS,1,$S(LRFMT="B":J-1*13+15,1:J*5+10))_":"
 S BLRAZ=$E(BLRABLKS,1,$S(LRFMT="B":A*13+15,1:A*5+15))_$S(LR2ORMOR:LRBUG_". ",1:"")_LRORG
 S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 Q
SIR F II=1:1:10 D:$P(LRINT(J),U,II,10)="" DCOM Q:$P(LRINT(J),U,II,10)=""  D
 . I LRFMT'="B" D
 .. S BLRAZ1=(II*5+10) D Z1
 .. S BLRAZ=BLRAZ_$S(LRFMT="I":$P(LRINT(J),U,II),1:$P(LRRES(J),U,II))
 .. I LRFMT="B" D SIR1
 Q
DCOM S BLRAZ1=LRCOMTAB D Z1 S BLRAZ=BLRAZ_LRDCOM
 ;  Comments
 I $D(LRDCOM(J)) S K=0,A=0 F  S A=+$O(LRDCOM(J,A)) Q:A<1  D
 . ;W:'('K&(LRDCOM="")) !
 . S BLRAZ1=LRCOMTAB D Z1 S BLRAZ=BLRAZ_LRDCOM(J,A) S K=1
 . S BLRADSP=BLRADSP+1,^TMP($J,"BLRA",BLRADSP,0)=BLRAZ
 Q
 ;
SIR1 S BLRAZ1=(II-1*13+15) D Z1
 S BLRAZ=BLRAZ_$S($D(LRRES(J)):$P(LRRES(J),U,II),1:"")
 S BLRAZ1=(II-1*13+21) D Z1 S BLRAZ=BLRAZ_$P(LRINT(J),U,II)_"  "
 Q
 ;
Z1 ;  Pad with trailing spaces
 F BLRAI=1:1:(BLRAZ1-$L(BLRAZ)) S BLRAZ=BLRAZ_" "
 Q
