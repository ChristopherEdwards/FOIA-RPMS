BLRDBG ; IHS/DIR/FJE - REFILES ENTRY IN IHS LAB TX LOG -- USED ONLY WHEN DEBUGGING ; [ 10/05/1999  9:35 AM ]
 ;;5.2;BLR;**1008,1009**;OCT 01, 1999
 ;
 S BLRLINK=1,DIC=9009022,DIC(0)="AQEM" D ^DIC ;IHS/DIR/FJE SETS BLRLINK TO BYPASS THE SCREEN IN PCC V LAB ENTRY FOR SITE/SPECIMEN
 Q:+Y<1
 S BLRLOGDA=+Y
SHOW K DA,DR S DA=BLRLOGDA D EN^DIQ
 W !!,"Do you wish to try and refile this entry?",!
 S DIR(0)="Y",DIR("A")="Enter Yes or No",DIR("B")="N" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 I $D(DIRUT) W !,"Entry NOT filed",! Q
 I +Y=0 W !,"Entry NOT filed",! Q
 ;D TOP^BLRQUE(BLRLOGDA,0)
 D DEBUG^BLRLINK
 Q
START ;STARTS LOOP FOR REFILING
 S BLRX=0 F  S BLRX=$O(^BLRTXLOG(BLRX)) Q:+BLRX=0  D ONE Q:$G(DUOUT)
 W !,"FINISHED",!! Q
 ;
ONE S BLRY=$G(^BLRTXLOG(BLRX,1))
 Q:$P(BLRY,U,6)=""
 S BLRLOGDA=BLRX,DIC="^BLRTXLOG(" D SHOW
 S BLRY=$G(^BLRTXLOG(BLRX,1))
 W:$L($P(BLRY,U,6)) !!,"ERROR** "_$P(BLRY,U,6)_" **REPORTED",*7,!!
 Q
