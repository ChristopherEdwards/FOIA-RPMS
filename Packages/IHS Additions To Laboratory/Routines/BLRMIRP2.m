BLRMIRP2 ; IHS/MSC/MKK - IHS Lab Micro Report, part 2; 14-Apr-2014 11:50 ; MKK
 ;;5.2;IHS LABORATORY;**1033**;NOV 01, 1997
 ;
EEP ; Ersatz EP
 D EEP^BLRGMENU
 Q
 ;
PRELIM ; EP - Print any preliminary data
 NEW CNT,COMMENT,NODE,PRELIM,STR
 ;
 D PRELNODE(.NODE)
 ;
 S (CNT,PRELIM)=0
 F PRELIM=$O(NODE(PRELIM))  Q:PRELIM<1  D
 . Q:$D(^LR(LRDFN,"MI",LRIDT,PRELIM))<1
 . ;
 . W ?4,$G(NODE(PRELIM)),!
 . S LINES=1+$G(LINES)
 . S COMMENT=0
 . F  S COMMENT=$O(^LR(LRDFN,"MI",LRIDT,PRELIM,COMMENT))  Q:COMMENT<1  D
 .. S STR=$G(^LR(LRDFN,"MI",LRIDT,PRELIM,COMMENT,0))
 .. D LINEWRAP^BLRGMENU(9,STR,71)
 .. W !
 .. S LINES=1+$G(LINES)
 .. S CNT=CNT+1
 ;
 W:CNT !
 Q
 ;
PRELNODE(NODE) ; EP - Create NODE array to hold Preliminary "locations" from Lab Data file
 NEW STR,WOT,WOT2
 ;
 K NODE
 S WOT=63.049999
 F  S WOT=$O(^DD(WOT))  Q:WOT<1!($E(WOT,1,5)'=63.05)  D
 . S WOT2=.9999999
 . F  S WOT2=$O(^DD(WOT,WOT2))  Q:WOT2<1  D
 .. S STR=$G(^DD(WOT,WOT2,0))
 .. S:$P(STR,"^")["PRELIMINARY" NODE($P($P(STR,"^",4),";"))=$P(STR,"^")
 Q
 ;
EHRPLIM ; EP - EHR: any preliminary data
 NEW CNT,COMMENT,NODE,PRELIM,STR,TABIT,TABIT2
 ;
 S TABIT=$J("",5),TABIT2=$J("",10)
 ;
 D PRELNODE(.NODE)
 ;
 S (CNT,PRELIM)=0
 F PRELIM=$O(NODE(PRELIM))  Q:PRELIM<1  D
 . Q:$D(^LR(LRDFN,"MI",LRIDT,PRELIM))<1
 . ;
 . D ADDLINE^BLRMIEHR(TABIT_$G(NODE(PRELIM))_":")
 . S COMMENT=0
 . F  S COMMENT=$O(^LR(LRDFN,"MI",LRIDT,PRELIM,COMMENT))  Q:COMMENT<1  D
 .. S STR=$G(^LR(LRDFN,"MI",LRIDT,PRELIM,COMMENT,0))
 .. D ADDLINE^BLRMIEHR(TABIT2_STR)
 .. S CNT=CNT+1
 ;
 D:CNT BLNKLINE^BLRMIEHR
 Q
