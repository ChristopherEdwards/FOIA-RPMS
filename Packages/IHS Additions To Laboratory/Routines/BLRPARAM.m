BLRPARAM ; IHS/HQT/MJL - IHS LAB SETUP PARAMETERS ; [ 12/19/2002  7:23 AM ]
 ;;5.2;LR;**1010,1015**;NOV 18, 2002
INIT ;EP
 S BLRFID="9009022",BLRLPKG=$O(^DIC(9.4,"C","LR",""))
 S BLRQSITE=$P($G(^AUTTSITE(1,0)),U)
 ;
 ;SET UP AUDITING IF NEEDED
 S SNAPSHOT=$P($G(^BLRSITE(BLRQSITE,0)),U,10) ;/IHS/ITSC/TPF 4/28/01
 ;D:$G(SNAPSHOT) ENTRYAUD^BLRUTIL("ENTERED INIT^BLRPARAM")  ;/IHS/ITSC/TPF 4/27/01
 ;
 S BLRFDN=0 F  S BLRFDN=$O(^DD(BLRFID,BLRFDN)) Q:'BLRFDN  S BLRX=$G(^DD(BLRFID,BLRFDN,0)) D
 .S BLRFDNM=$P(BLRX,U)
 .S BLRF(0,BLRFDNM)=BLRFDN
 .Q:'$P(BLRX,U,2)
 .S BLRFMN=+$P(BLRX,U,2)
 .;SETS UP THE BLRF ARRAY CONTAINING THE FIELDS TO SET UP IN BLRTXLOG
 .;SETTING UP THE FDA ARRAY TO UPDATE IS DEPENDENT ON THIS ARRAY 
 .S BLRFDN1=0
 .F  S BLRFDN1=$O(^DD(BLRFMN,BLRFDN1)) Q:'BLRFDN1  S BLRX=$G(^DD(BLRFMN,BLRFDN1,0)) D
 ..S BLRFDNM1=$P(BLRX,U),BLRF(0,BLRFDNM1)=BLRFDN1,BLRF(0,BLRFDNM1,"TYPE")=$S($P(BLRX,U,2)="W":"W",1:"M"),BLRF(0,BLRFDNM1,"FILE")=BLRFMN
 ; This sets the kill string for this multiple-field which is executed 
 ; if present in MULTI^BLRNFLTL.
 S BLRF(0,"COMMENTS","DIK")="S BLRMI=0 F  S BLRMI=$O(^BLRTXLOG(BLRENT,30,BLRMI)) Q:'BLRMI  S DA=BLRMI,DA(1)=BLRENT,DIK=""^BLRTXLOG(""_DA(1)_"",""_30_"","" D ^DIK"
 K BLRFDN,BLRFDNM,BLRFDNM1,BLRFDN,BLRFDN1,BLRFMN,BLRX
 Q
 ;
JOB ;EP
 L +^BLRLOCK:0 Q:'$T
 S BLRQUIET=1
 S ZTIO="",ZTSAVE("BLRQUIET")=""
 S ZTRTN="TSK^BLRTN",ZTDTH=$H
 S ZTDESC="BLRTN task"
 D ^%ZTLOAD
 L -^BLRLOCK
 Q
