BLRTNCON ; IHS/MSC/MKK - IHS Lab to PCC Create/Edit Transactions (Continued) ; 17-Dec-2015 15:37 ; MKK
 ;;5.2;IHS LABORATORY;**1038**;NOV 01, 1997;Build 6
 ;
 ; Code moved from BLRTN to here because BLRTN became too large.
 ;
EEP ; EP - Ersatz EP
 D EEP^BLRGMENU
 Q
 ;
GETCPT ; EP
 Q:BLRODTM<$P($G(^BLRCPT(BLRXII,0)),U,3)
 S BLRFOUND=1,BLRCPTP=BLRXII
 S BLRCPTN=0 F BLRNN=1:1 S BLRCPTN=$O(^BLRCPT(BLRXII,11,BLRCPTN)) Q:'BLRCPTN  S BLRCPDAT=$G(^BLRCPT(BLRXII,11,BLRCPTN,0)),BLRCPCD=$P(BLRCPDAT,U),BLRCPCST=$P(BLRCPDAT,U,2),BLRCPRC=$P(BLRCPDAT,U,3),BLRCPACT=$P(BLRCPDAT,U,4) D
 .S (BLRCPTM,BLRCPTQ)=""
 .S BLRCPMN=0 F BLRNN1=1:1 S BLRCPMN=$O(^BLRCPT(BLRXII,11,BLRCPTN,1,BLRCPMN)) Q:'BLRCPMN  S:BLRNN1>1 BLRCPTM=BLRCPTM_"," S BLRCPTM=BLRCPTM_$G(^BLRCPT(BLRXII,11,BLRCPTN,1,BLRCPMN,0))
 .S BLRCPQN=0 F BLRNN1=1:1 S BLRCPQN=$O(^BLRCPT(BLRXII,11,BLRCPTN,2,BLRCPQN)) Q:'BLRCPQN  S:BLRNN1>1 BLRCPTQ=BLRCPTQ_"," S BLRCPTQ=BLRCPTQ_$G(^BLRCPT(BLRXII,11,BLRCPTN,2,BLRCPQN,0))
 .;
 .; Stores the CPT Code and not BLRCPCD.  BLRCPCD is the IEN into file 81.
 .S:BLRNN>1 BLRCPTS=BLRCPTS_";" S BLRCPTS=BLRCPTS_$$GET1^DIQ(81,BLRCPCD,.01)_"|"_BLRCPCST_"|"_BLRCPRC_"|"_BLRCPACT_"|"_BLRCPTM_"|"_BLRCPTQ
 ;
 K BLRCPCD,BLRCPCST,BLRCPRC,BLRCPACT,BLRCPTN,BLRCPDAT,BLRCPTM,BLRCPMN,BLRCPTQ,BLRCPQN,BLRNN,BLRNN1
 Q
