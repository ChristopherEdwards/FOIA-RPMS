ADGCDE ; IHS/ADC/PDW/ENM - CHART DEF EDIT/DISPLAY ; [ 03/25/1999  11:48 AM ]
 ;;5.0;ADMISSION/DISCHARGE/TRANSFER;;MAR 25, 1999
 ;
 ;
CHOOSE ; -- ask user to choose edit or display
 NEW DIR,DIRUT S DIR("A")="Choose Action"
 S DIR(0)="SO^1:EDIT CHART DEFICIENCY ENTRY;2:LIST ENTRIES BY GROUPING"
 D ^DIR Q:$D(DIRUT)  D @Y,CHOOSE Q
 ;
1 ; -- edit chart def file
 NEW DIC,DIE,DA,X,Y,DR,DLAYGO
 W ! S DIC="^ADGCD(",DLAYGO=9009013,DIC(0)="AEMQZL"
 D ^DIC Q:Y<0
 L +^ADGCD(+Y):3 I '$T D  Q
 . W !,*7,"THIS CHART DEFICIENCY BEEN UPDATED BY AONTHER PERSON"
 S DIE="^ADGCD(",(DGX,DA)=+Y,DR="[ADGCDEDIT]"
 D ^DIE L -^ADGCD(DGX) K DGX D 1 Q
 ;
2 ; -- display by grouping
 NEW L,DIC,FLDS,BY,FR,TO
 S L=0,DIC="^ADGCD(",FLDS="[ADGCDLIST1]",BY="[ADGCDSORT]"
 S (FR,TO)="" D EN1^DIP,PRTOPT^ADGVAR Q
