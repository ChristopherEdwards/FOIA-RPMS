ADGDSIP2 ; IHS/ADC/PDW/ENM - DS CHART DEFICIENCY LIST ; [ 03/25/1999  11:48 AM ]
 ;;5.0;ADMISSION/DISCHARGE/TRANSFER;;MAR 25, 1999
 ;
 F DGI=1:1:5 S DGTOT(DGI)=0
A ; -- main
 D H,L Q:DGSTOP=U  D 1 Q
 ;
H ;
 W @IOF,!!?80-$L(DGFAC)/2,DGFAC
 W !,DGDUZ,?20,"DAY SURGERY INCOMPLETE CHARTS BY PROVIDER"
 W !,$E(DT,4,5)_"/"_$E(DT,6,7)_"/"_$E(DT,2,3),?34,"SUMMARY PAGE"
 W !!?25,"INCOMP",?41,"DELINQ"
 W !,"PROVIDER",?25,"CHARTS",?34,"SIG",?41,"CHARTS",?49,"OP RPT"
 W ?57,"SIGNATURE",!,DGLIN
 Q
 ;
L ;
 S DGPRVN=""
 F  S DGPRVN=$O(^TMP("DGZICPL1",$J,DGPRVN)) Q:DGPRVN=""!(DGSTOP=U)  D
 . Q:DGPRVN="Z"  Q:DGPRVN="ZZ"
 . S DGSTR=^TMP("DGZICPL1",$J,DGPRVN) D T
 Q
 ;
1 ;
 N X S DGTOT(1)=0,X=0
 F  S X=$O(^TMP("DGZICPL1",$J,"Z",X)) Q:X=""  S DGTOT(1)=DGTOT(1)+1
 ;
 S DGTOT(3)=0,X=0
 F  S X=$O(^TMP("DGZICPL1",$J,"ZZ",X)) Q:X=""  S DGTOT(3)=DGTOT(3)+1
 ;
 W !!,DGLIN,!,"TOTALS:"
 S DGX=17 F DGI=1:1:5 W ?(DGX+(DGI*8)),$J(DGTOT(DGI),3)
 Q
 ;
T I $Y>(IOSL-4) D NEWPG Q:DGSTOP=U
 W !,$E(DGPRVN,1,15),?25,$J($P(DGSTR,U),3)
 W ?33,$J($P(DGSTR,U,2),3),?41,$J($P(DGSTR,U,3),3)
 W ?49,$J($P(DGSTR,U,4),3),?57,$J($P(DGSTR,U,5),3)
 F DGJ=1:1:5 S DGTOT(DGJ)=DGTOT(DGJ)+$P(DGSTR,U,DGJ)
 Q
 ;
NEWPG ; -- end of page control
 ;--printer
 I IOST'?1"C-".E D H Q
 ;--terminal
 K DIR S DIR(0)="E" D ^DIR S DGSTOP=X
 Q:X=U  D H Q
