APSKAMN8 ;IHS/ANMC/SFB/MRS - COMPLETE PRINTOUT [ 09/28/94  1:05 PM ]
 ;;1.0;Aminoglycoside Kinetics;;OCT 31,1994
 ;===>PRINTS THE FINAL RESULTS
PRINT I APSKPTIN=0 D NOINFO^APSKAMN Q
 S %ZIS="PQ" D ^%ZIS G END:POP,QUE:$D(IO("Q"))
 D EN
 D ^%ZISC
 Q
QUE K IO("Q"),ZTSAVE
 F %="APSK*" S ZTSAVE(%)=""
 S ZTRTN="EN^APSKAMN8",ZTDESC="PRINT AMINOGLYCOSIDE KINETICS"
 D ^%ZTLOAD K ZTSK D ^%ZISC
 Q
EN U IO
 W !!,?14,"AMINOGLYCOSIDE KINETICS FINAL REPORT" W !!
 F APSKA=1:1:65 W "-"
 W !!
 F APSK=1:1:22 W "*"
 W " CONFIDENTIAL PATIENT INFORMATION "
 F APSK=1:1:22 W "*"
 W !,"Printed by: ",$P(^VA(200,DUZ,0),U)
 W !!,"FINAL RECOMMENDATION"
 W !,"--------------------"
 W !,APSKK3," MG/HR GIVEN OVER ",APSKT0," HOUR EVERY ",APSKT3," HOURS WILL PRODUCE:"
 W !,"    ",$J(APSKC4,3,2)," MCG/ML MAXIMUM"
 W !,"    ",$J(APSKC5,3,2)," MCG/ML MINIMUM"
 W !!!,"FINAL RESULTS"
 W !,"-------------"
 W !!,"NAME:",?6,APSKNAME,?40,"DOSING WT:",APSKWT," KG"
 W !,"DATE OF ANALYSIS: ",APSKDATE,?40,"CHART NUMBER: ",APSKCHRT
 I APSKM="2" G SKIP4
 W !!,"TEST DOSE: ",APSKD," MG IV FROM ",APSKZ(1)," TO ",APSKZ(2)," O'CLOCK"
 W !
 W !,"SERUM LEVELS",?17,"TIME",?32,"HOURS POST INF.",?53,"CONC.(MCG/ML)"
 W !,"PRE LEVEL",?17,APSKZ(13),?34,$J(APSKH(13),2,2)," (PRE-INF.)",?56,APSKC(13)
 W !,"POST #1",?17,APSKZ(3),?34,$J(APSKH(3),2,2),?56,APSKC(3)
 ;===>THE NEXT LINE IS PRINTED IF THIS WAS A PEAK/TROUGH CHECK
 I APSKSE="1" W !,"EXTRAP.",?17,APSKZ(4),?34,$J(APSKH(4),2,2),?56,APSKC(4)
 I APSKSE="1" G SKIP3
 W !,"POST #2",?17,APSKZ(4),?34,$J(APSKH(4),2,2),?56,APSKC(4)
 I APSKS(1)=2 G SKIP3
 W !,"POST #3",?17,APSKZ(5),?34,$J(APSKH(5),2,2),?56,APSKC(5)
 I APSKS(1)=3 G SKIP3
 W !,"POST #4",?17,APSKZ(6),?34,$J(APSKH(6),2,2),?56,APSKC(6)
 I APSKS(1)=4 G SKIP3
 W !,"POST #5",?17,APSKZ(7),?34,$J(APSKH(7),2,2),?56,APSKC(7)
 I APSKS(1)=5 G SKIP3
SKIP3 W !!,"COEFFICIENT OF DETERMINATION:",?35,$J(APSKR2,2,2)
 W !,"EXTRAPOLATED PEAK CONC.: ",?35,$J(APSKC1,2,2)," MCG/ML"
SKIP4 W !!,"HALF-LIFE:",?35,$J(APSKT1,2,2)," HOURS"
 W !,"ELIMINATION RATE CONSTANT:",?35,$J(APSKK1,2,2)," HR-1"
 W !,"VOLUME OF DISTRIBUTION:",?35,$J(APSKV1,3,2)," LITERS ( ",$J(APSKV2,2,2),"L/KG)"
 W !,"CLEARANCE:",?35,$J(1000*APSKK1*(APSKV1/APSKWT),2,2)," ML/HR-KG"
 W !! F APSKLINE=1:1:40 W "_"
 W !! F APSKLINE=1:1:40 W "_"
 W !! F APSKLINE=1:1:40 W "_"
 W !! F APSKLINE=1:1:40 W "_"
 W !!,?20 F APSKLINE=1:1:20 W "_"
 I $D(IOST),$E(IOST,1,2)="C-" D ENDIT
 Q
ENDIT K DIR S DIR(0)="E"
 D ^DIR G END:$D(DIRUT)
 K DIR
 W @IOF
END Q
