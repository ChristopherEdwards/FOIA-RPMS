APSPES2 ;IHS/MSC/PLS - SureScripts HL7 interface - con't;03-May-2011 10:01;PLS
 ;;7.0;IHS PHARMACY MODIFICATIONS;**1008,1011**;Sep 23, 2004;Build 17
 ; Return array of message data
 ; Input: MIEN - IEN to HLO MESSAGES and HLO MESSAGE BODY files
 ; Output: DATA
 ;         HLMSTATE
PARSE(DATA,MIEN,HLMSTATE) ;EP
 N SEG,CNT
 Q:'$$STARTMSG^HLOPRS(.HLMSTATE,MIEN)
 M DATA("HDR")=HLMSTATE("HDR")
 S CNT=0
 F  Q:'$$NEXTSEG^HLOPRS(.HLMSTATE,.SEG)  D
 .S CNT=CNT+1
 .M DATA(CNT)=SEG
 Q
 ; Process incoming RDS message
DISP ;EP
 ;todo- check APP ACK TYPE
 N DATA,ARY,SEGORC,SEGIEN,SEGRXD,ERR,RET
 N DRG,DCODE,DCODEQ,PVDIEN,DSPNUM
 Q:'$G(HLMSGIEN)
 D PARSE(.DATA,HLMSGIEN,.HLMSTATE)
 S SEGIEN=$$FSEGIEN^APSPES1(.DATA,"ORC")
 Q:'SEGIEN 0
 M SEGORC=DATA(SEGIEN)
 Q:$$GET^HLOPRS(.SEGORC,1,1)'="OK"
 S PVDIEN=$$GET^HLOPRS(.SEGORC,10,1)
 S RXIEN=$$GET^HLOPRS(.SEGORC,2,1)
 Q:'RXIEN
 S SEGIEN=$$FSEGIEN^APSPES1(.DATA,"RXD")
 Q:'SEGIEN
 M SEGRXD=DATA(SEGIEN)
 S DSPNUM=$$GET^HLOPRS(.SEGRXD,1)
 Q:'DSPNUM
 S DSPNUM=DSPNUM-1  ;Adjust fill offset - original = 0
 S DCODE=$$GET^HLOPRS(.SEGRXD,2,1)
 S DRG=$$GET^HLOPRS(.SEGRXD,2,2)
 S DCODEQ=$$GET^HLOPRS(.SEGRXD,2,3)
 S ARY("REASON")="X"
 S ARY("TYPE")="U"
 S ARY("RX REF")=$S(DSPNUM>5:DSPNUM+1,1:DSPNUM) ; adjust for 6=partial
 S ARY("COM")="DDrg:"_DRG_$S($L(DCODE):" ("_DCODEQ_":"_DCODE_")",1:"")
 S ARY("USER")=PVDIEN
 D UPTLOG^APSPFNC2(.RET,RXIEN,0,.ARY)
 I DATA("HDR","APP ACK TYPE")="AL" D
 .; Generate APP Ack
 .N PARMS,ACK,ERR
 .S PARMS("ACK CODE")="AA"
 .I '$$ACK^HLOAPI2(.HLMSTATE,.PARMS,.ACK,.ERR) W !,ERR Q
 .I '$$SENDACK^HLOAPI2(.ACK,.ERR) W !,ERR Q
 Q
DSP ;
 N HLMSGIEN,HLMSTATE,PARMS
 S HLMSGIEN=200000000001
 D PARSE(.DATA,HLMSGIEN,.HLMSTATE)
 S PARMS("ACK CODE")="AA"
 I '$$ACK^HLOAPI2(.HLMSTATE,.PARMS,.ACK,.ERR) W !,ERR Q
 I '$$SENDACK^HLOAPI2(.ACK,.ERR) W !,ERR Q
 Q
 ; Send bulletin
BULL(XMSUB,XMDUZ,WHO,MSG) ;EP
 N XMY,XMTEXT
 M XMY=WHO
 S XMTEXT="MSG("
 D ^XMD
 Q
 ; Return Ordering Provider for HL7 Message
OPRV(MSGIEN) ; EP
 N RXIEN,VAL
 S VAL=0
 S RXIEN=$$RXIEN(MSGIEN)
 Q +$P($G(^PSRX(RXIEN,0)),U,16)
 ; Return RX IEN
RXIEN(MSGIEN) ; EP
 D PARSE(.DATA,MSGIEN,.HLMSTATE)
 S SEGIEN=$$FSEGIEN^APSPES1(.DATA,"ORC")
 Q:'SEGIEN 0
 M SEGORC=DATA(SEGIEN)
 ;Q:$$GET^HLOPRS(.SEGORC,1,1)'="OK"
 S RXIEN=$$GET^HLOPRS(.SEGORC,2,1)
 Q +RXIEN
 ; Return Pharmacy Info for Activity Log
 ; Input - RXIEN - Prescription IEN
PHMINFO(RXIEN) ; EP -
 N PHM
 S PHM=$$GPHM^APSPES1(RXIEN)
 Q:'PHM "UNKNOWN"
 Q $$GET1^DIQ(9009033.9,PHM,.01)_"  "_$$HLPHONE^HLFNC($$GET1^DIQ(9009033.9,PHM,2.1))
 ;
REFRES ; EP - Refill request callback
 ;Build response to Refill Request
 N DATA,PARMS,SEG,ACK,ARY,ERR,ORID,SEGRX0,REFILL,RR
 N RXIEN,FN,DEA
 D PARSE^APSPES2(.DATA,$G(HLMSGIEN),.HLMSTATE)
 ; Create entry in APSP REFILL REQUEST file
 ; If possible, generate order
 S FN=9009033.91
 S RR=$$ADDRR(+$G(HLMSGIEN))
 S RXIEN=+$$GET1^DIQ(9009033.91,RR,.06,"I")
 S DEA=$$DEA($P($G(^PSRX(RXIEN,0)),U,6))
 I RR,RXIEN D   ;,'$$DEACLS(DEA,2) D
 .S ORID=$$GENRENEW(HLMSGIEN,RXIEN,$$GET1^DIQ(52,RXIEN,16,"I"),$$GETVAL(HLMSGIEN,"RXO",13,1),RR)
 .I ORID D
 ..S FDA(FN,RR_",",.02)=+ORID
 ..S ORITM=$$VALUE^ORCSAVE2(+ORID,"ORDERABLE")
 ..S:ORITM FDA(FN,RR_",",1.1)=ORITM
 ..S FDA(FN,RR_",",.03)=1
 ..D FILE^DIE("K","FDA")
 .E  D
 ..S MSGTXT=$P(ORID,U,2)
 ..D DENY1^APSPES3
 ;E  D
 ;.S MSGTXT="Schedule II not allowed."
 ;.D DENY1^APSPES3
 ; todo - Set Scheduled purge dt field (.09) in 778 to 30 days until processed.
 ;
 Q
SET(ARY,V,F,C,S,R) ;EP
 D SET^HLOAPI(.ARY,.V,.F,.C,.S,.R)
 Q
 ; Generate Renew Order
GENRENEW(MSGIEN,RXIEN,OPRV,REFILL,RRIEN) ;EP -
 N ORIFN,VAL,DFN,LOC,RET,FLDS,ORLST,DEA
 Q:'RXIEN 0
 S DFN=$P($G(^PSRX(RXIEN,0)),U,2)
 Q:'DFN 0
 S ORIFN=+$P($G(^PSRX(RXIEN,"OR1")),U,2)
 D VALID^ORWDXA(.VAL,ORIFN,"RN",OPRV)
 ;TODO - GENERATE DENIAL MESSAGE
 Q:$L(VAL) 0_U_VAL  ; Order can't be renewed.  VAL contains refusal explanation
 K VAL
 S ORLST(1)=ORIFN
 D RENEW^ORWDXC(.VAL,DFN,.ORLST)
 S LOC=+$P($G(^OR(100,ORIFN,0)),U,10)
 D RNWFLDS^ORWDXR(.RET,ORIFN)
 S FLDS(1)=$G(RET(0))
 S $P(FLDS(1),U,6)=$$GET1^DIQ(9009033.91,RRIEN,1.7,"I")  ; Set Pharmacy
 S FLDS("ORCHECK")=0
 S:$G(RRIEN) FLDS("SSRREQIEN")=$G(RRIEN)
 S FLDS("SSREFREQ")=1
 D PREPPTXT($NA(FLDS("SSREFREQ")),$G(RRIEN))
 S DEA=$$DEA($P($G(^PSRX(RXIEN,0)),U,6))
 ;S $P(FLDS(1),U,4)=$S($$DEACLS(DEA,"2345"):0,REFILL:1,1:0)  ; Refill
 S $P(FLDS(1),U,4)=0  ; Refill forced to zero for a total of 1 dispense
 K RET
 D RENEW^ORWDXR(.RET,.ORIFN,DFN,OPRV,LOC,.FLDS)
 ;TODO- CHECK ORIFN FOR NEW ORDER
 D:$G(ORIFN) EN^OCXOERR(DFN_U_+ORIFN_U_OPRV_"^^^^^1")
 Q +$P($P($G(RET(1)),U),"~",2)
 ; Add entry to REFILL REQUEST file
ADDRR(MSGIEN) ; EP -
 N FDA,FN,I,MSGID,ERR,IENS,DFN,PRV,SPI,RXIEN
 N ORID,ORITM,REF,HMSG,PHARM
 S FN=9009033.91,I="+1,",DFN=0
 S FDA(FN,I,.01)=$$GET1^DIQ(778,MSGIEN,.01)  ; Message ID
 S FDA(FN,I,.03)=0  ; Status
 S FDA(FN,I,.04)=$$GET1^DIQ(778,MSGIEN,.16,"I")  ; Message D/T
 S FDA(FN,I,.05)=MSGIEN  ; HLO Message IEN
 S FDA(FN,I,.07)=$$NOW^XLFDT()  ; Last Update D/T
 S RXIEN=$$RXIEN^APSPES2(MSGIEN)
 S:RXIEN DFN=$$GET1^DIQ(52,RXIEN,2,"I")
 S:'DFN DFN=$$HRCNF^APSPFUNC($$GETVAL(MSGIEN,"PID",3,1))
 S:DFN>0 FDA(FN,I,1.2)=DFN  ; Patient IEN
 S SPI=$$GETVAL(MSGIEN,"ORC",12,1)
 S PRV=$$FIND1^DIC(200,,"O",SPI,"ASPI")
 S:PRV FDA(FN,I,1.3)=PRV  ; Provider IEN
 S REF=$$GETVAL(MSGIEN,"RXO",13,1)
 ;S RXIEN=$$RXIEN^APSPES2(MSGIEN)
 S:RXIEN FDA(FN,I,.06)=RXIEN  ; Original Prescription
 ;S PHARM=$$GET1^DIQ(52,RXIEN,9999999.24,"I")
 ;.S:PHARM FDA(FN,I,1.7)=PHARM  ; Requesting Pharmacy
 ;E  D
 S PHARM=$$GETVAL(MSGIEN,"RXE",40,1)
 S:PHARM PHARM=$$FIND1^DIC(9009033.9,,"O",PHARM,"C")
 S:PHARM FDA(FN,I,1.7)=PHARM
 S FDA(FN,I,1.5)=$$GETVAL(MSGIEN,"ORC",7,3)  ; Days Supply
 S FDA(FN,I,1.4)=$$GETVAL(MSGIEN,"RXO",11,1)  ; Quantity
 D UPDATE^DIE(,"FDA","IENS","ERR")
 ;TODO - PROCESS ERR
 ;TODO - POPULATE OTHER FIELDS
 K ERR
 S I=+IENS(1)_","
 D GETHMSG(MSGIEN,.HMSG)
 S FDA(FN,I,5)=HMSG  ; HL7 Message
 S FDA(FN,I,3)=$$GETVAL(MSGIEN,"RXO",7,2)  ; SIG
 D ADDMI(I)  ; Add Medication Instructions
 D FILE^DIE("K","FDA","ERR")
 Q +$G(IENS(1))
 ;
 ; Extract data from segment
 ; Input: MSG - Message ien
 ;        SEG - Segment name
 ;        FLD - Field #
 ;        OFF - Offset in field (default to 1)
GETVAL(MSG,SEG,FLD,OFF) ;EP -
 N DATA,HLMSTATE,ARY,SEGIEN,SEGARY
 S OFF=$G(OFF,1)
 D PARSE(.DATA,MSG,.HLMSTATE)
 Q:'$D(DATA) ""
 S SEGIEN=$$FSEGIEN^APSPES1(.DATA,SEG)
 Q:'SEGIEN ""
 M SEGARY=DATA(SEGIEN)
 Q $$GET^HLOPRS(.SEGARY,FLD,OFF)
DEA(DRUG) ; Return DEA value
 N DEA
 S DEA=+$$GET1^DIQ(50,DRUG,3)
 Q DEA
DEACLS(DEA,CLS) ; Return boolean value for comparison
 Q CLS[DEA
 ;
PREPPTXT(RET,RRIEN) ; Return prepared text from Pharmacy
 N M,C
 S RRIEN=$G(RRIEN,0)
 S M=$P(^APSPRREQ(RRIEN,0),U,5)
 S @RET@(1)="Pharmacy: "_$$GETVAL(M,"RXE",40,2)_" ("_$$FMTPHN($$GETVAL(M,"RXE",45))_")"
 S @RET@(2)="Drug description: "_$$GETVAL(M,"RXO",1,2)
 S @RET@(3)="Quantity: "_$$GETVAL(M,"RXO",11)_" "_$$GET1^DIQ(9009033.7,$$FIND1^DIC(9009033.7,,,$$GETVAL(M,"RXO",12)),1)
 S @RET@(4)="Days Supply: "_$$GETVAL(M,"ORC",7,3)
 S @RET@(5)="Sig-Directions: "_$$GETVAL(M,"RXO",7,2)
 S @RET@(6)="Note: "_$$GETVAL(M,"RXO",6,2)
 S @RET@(7)="Refills: "_+$$GETVAL(M,"RXO",13,1)
 S @RET@(8)="Substitution allowed: "_$$ESUBST($$GETVAL(M,"RXO",9))
 S @RET@(9)="Last fill date: "_$$FMTE^XLFDT($$FMDATE^HLFNC($$GETVAL(M,"ORC",27,1)),"5DZ0")
 S @RET@(10)=" "
 S @RET@(11)="Diagnosis: "
 S C=12
 D GETDIAG(M,RET,.C)
 Q
 ; Return full HL7 message
GETHMSG(MSGIEN,DATA) ;EP
 N MSG,SEG,CNT
 S DATA=$NA(^TMP("REFREQ",$J))
 K @DATA
 Q:'$$GETMSG^HLOMSG(MSGIEN,.MSG)
 S CNT=1
 S SEG(1)=MSG("HDR",1)_MSG("HDR",2)
 D ADD(.SEG)
 F  Q:'$$HLNEXT^HLOMSG(.MSG,.SEG)  D
 .D ADD(.SEG)
 Q
 ; Return external text for Substitution
ESUBST(VAL) ;
 Q $S(VAL="N":"Not Authorized",VAL="T":"Allow therapeutic",1:"Allowed Generic")
I() ;EP -
 S CNT=CNT+1
 Q CNT
 ; Return external text for diagnosis
GETDIAG(MSG,RET,C) ; EP -
 N TXT,DATA,ARY,SEG,HLMSTATE,HDR
 S TXT=""
 I '$$STARTMSG^HLOPRS(.HLMSTATE,MSG,.HDR) Q
 F  Q:'$$NEXTSEG^HLOPRS(.HLMSTATE,.SEG)  D
 .I $$GET^HLOPRS(.SEG,0)="DG1" D
 ..S TXT=$$GET^HLOPRS(.SEG,3,1)_" ("_$$GET^HLOPRS(.SEG,3,3)_")"
 ..I $L(TXT) S C=C+1,@RET@(C)=TXT
 Q
 ; Add data to array
ADD(SEG) ; EP -
 N I
 S I=0 F  S I=$O(SEG(I)) Q:'I  S @DATA@($$I())=SEG(I)
 S @DATA@($$I())=""
 Q
ADD1(SEG) ;
 N QUIT,I,J,LINE
 S QUIT=0
 S (I,J)=1
 S LINE(1)=$E(SEG(1),1,255),SEG(1)=$E(SEG(1),81,9999)
 I SEG(1)="" K SEG(1)
 D SHIFT(.I,.J)
 S @VALMAR@($$I,0)=LINE(1)
 S I=1
 F  S I=$O(LINE(I)) Q:'I  D
 .S @VALMAR@($$I,0)=LINE(I)
 Q
 ;
SHIFT(I,J) ;
 I '$D(SEG(I)) S I=$O(SEG(0)) Q:'I
 I $L(LINE(J))<255 D
 .N LEN
 .S LEN=$L(LINE(J))
 .S LINE(J)=LINE(J)_$E(SEG(I),1,255-LEN)
 .S SEG(I)=$E(SEG(I),256-LEN,9999)
 .I SEG(I)="" K SEG(I)
 E  D
 .S J=J+1
 .S LINE(J)="-"
 D SHIFT(.I,.J)
 Q
 ; Return formatted phone number
FMTPHN(X) ;EP
 N RES
 I $E(X,1,10)?10N Q "("_$E(X,1,3)_")"_$E(X,4,6)_"-"_$E(X,7,10)_$S($L($E(X,11,20)):"  "_$E(X,11,20),1:"")
 I $E(X,1,7)?7N Q $E(X,1,3)_"-"_$E(X,4,7)_"  "_$E(8,20)
 I X?10N1" ".6UN Q "("_$E(X,1,3)_")"_$E(X,4,6)_"-"_$E(X,7,10)_$S($L($E(X,11,20)):"  "_$E(X,11,20),1:"")
 I X?3N1"-"3N1"-"4N.1" ".A Q "("_$E(X,1,3)_")"_$E(X,5,12)_"  "_$E(X,13,20)
 I X?3N1"-"4N Q X
 I X?3N1"-"4N.1" ".6UN Q X
 Q X
 ; Add medication instructions to FDA array
ADDMI(IENS) ; EP -
 N FN
 S FN=9009033.91
 Q
 ; Check status of APSP REFIIL REQUEST entry
 ; Input: IEN - IEN
 ;        STA - Status to check (if not passed, set to -1 and return status value)
RREQSTAT(DATA,IEN,STA) ; EP -
 S RES=$P($G(^APSPRREQ(IEN,0)),U,3)
 S:'$L($G(STA)) STA=-1
 S DATA=$S(STA=-1:RES,1:RES=STA)
 Q
