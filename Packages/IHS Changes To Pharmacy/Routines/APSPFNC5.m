APSPFNC5 ;IHS/MSC/PLS - Prescription Creation Support ;22-Jun-2011 15:10;PLS
 ;;7.0;IHS PHARMACY MODIFICATIONS;**1011**;Sep 23, 2004;Build 17
 ;=================================================================
 ;Return list of pharmacies
PHMLSTSC(DATA,SFLG,ZIP,RAD,NAME,NFLG,CITY,STATE) ;
 N PLST,ZARY,ZC,CNT
 S SFLG=$G(SFLG)   ;ZNC
 S NFLG=+$G(NFLG)  ;0-Starts with name;1-contains name;2-exact match
 S DATA=$NA(^TMP("APSPOPHM",$J))
 K @DATA
 S PLST=$NA(^TMP("APSPPLST",$J))
 K @PLST
 Q:'$L(SFLG)
 S CNT=0
 I SFLG["Z" D
 .Q:'$G(ZIP)
 .D GETZC^APSPFNC2(.ZARY,ZIP,RAD)
 .S ZC="",CNT=0 F  S ZC=$O(ZARY(ZC)) Q:'$L(ZC)  D
 ..S IEN=0 F  S IEN=$O(^APSPOPHM("ZIP",ZC,IEN)) Q:'IEN  S @PLST@(IEN)=1
 I SFLG["N" D
 .N LP,RXNM,NM
 .Q:'$L(NAME)
 .S NM=$$UP^XLFSTR(NAME)
 .S LP=0
 .I $D(@PLST) D
 ..F  S LP=$O(@PLST@(LP)) Q:'LP  D
 ...Q:'@PLST@(LP)
 ...S RXNM=$$UP^XLFSTR($P(^APSPOPHM(LP,0),U,10))
 ...S @PLST@(LP)=$S(NFLG=2:RXNM=NM,NFLG:RXNM[NM,1:$E(RXNM,1,$L(NM))=NM)
 .E  D
 ..F  S LP=$O(^APSPOPHM(LP)) Q:'LP  D
 ...S RXNM=$$UP^XLFSTR($P(^APSPOPHM(LP,0),U))
 ...S:$S(NFLG=2:RXNM=NM,NFLG:RXNM[NM,1:$E(RXNM,1,$L(NM))=NM) @PLST@(LP)=1
 I SFLG["C" D
 .N LP,CTY
 .Q:'$L(CITY)
 .S CTY=$$UP^XLFSTR(CITY)
 .S LP=0
 .I $D(@PLST) D
 ..F  S LP=$O(@PLST@(LP)) Q:'LP  D
 ...Q:'@PLST@(LP)
 ...S @PLST@(LP)=($$UP^XLFSTR($P($G(^APSPOPHM(LP,1)),U,4))=STATE)&($E($$UP^XLFSTR($P($G(^APSPOPHM(LP,1)),U,3)),1,$L(CTY))=CTY)
 .E  D
 ..F  S LP=$O(^APSPOPHM("D",STATE,LP)) Q:'LP  D
 ...S:($E($$UP^XLFSTR($P($G(^APSPOPHM(LP,1)),U,3)),1,$L(CTY))=CTY) @PLST@(LP)=1
 S LP=0 F  S LP=$O(@PLST@(LP)) Q:'LP  D
 .D:@PLST@(LP) ADDPHM^APSPFNC2(LP)
 Q
 ;Return list of states
GSTATES(DATA) ;EP
 N LP,ST
 F LP=1:1 S ST=$P($T(STATES+LP),";;",2) Q:'$L(ST)  D
 .S DATA(LP)=ST
 Q
STATES ;List of states
 ;;AL^ALABAMA
 ;;AK^ALASKA
 ;;AZ^ARIZONA
 ;;AR^ARKANSAS
 ;;CA^CALIFORNIA
 ;;CO^COLORADO
 ;;CT^CONNECTICUT
 ;;DE^DELAWARE
 ;;FL^FLORIDA
 ;;GA^GEORGIA
 ;;HI^HAWAII
 ;;ID^IDAHO
 ;;IL^ILLINOIS
 ;;IN^INDIANA
 ;;IA^IOWA
 ;;KS^KANSAS
 ;;KY^KENTUCKY
 ;;LA^LOUISIANA
 ;;ME^MAINE
 ;;MD^MARYLAND
 ;;MA^MASSACHUSETTS
 ;;MI^MICHIGAN
 ;;MN^MINNESOTA
 ;;MS^MISSISSIPPI
 ;;MO^MISSOURI
 ;;MT^MONTANA
 ;;NE^NEBRASKA
 ;;NV^NEVADA
 ;;NH^NEW HAMPSHIRE
 ;;NJ^NEW JERSEY
 ;;NM^NEW MEXICO
 ;;NY^NEW YORK
 ;;NC^NORTH CAROLINA
 ;;ND^NORTH DAKOTA
 ;;OH^OHIO
 ;;OK^OKLAHOMA
 ;;OR^OREGON
 ;;PA^PENNSYLVANIA
 ;;RI^RHODE ISLAND
 ;;SC^SOUTH CAROLINA
 ;;SD^SOUTH DAKOTA
 ;;TN^TENNESSEE
 ;;TX^TEXAS
 ;;UT^UTAH
 ;;VT^VERMONT
 ;;VA^VIRGINIA
 ;;WA^WASHINGTON
 ;;WV^WEST VIRGINIA
 ;;WI^WISCONSIN
 ;;WY^WYOMING
