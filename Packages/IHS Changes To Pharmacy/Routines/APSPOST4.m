APSPOST4 ; IHS/DSD/ENM - OUTPATIENT/INTERVENTION PROVIDER/DIV ;  [ 09/03/97   1:30 PM ]
 ;;6.0;IHS PHARMACY MODIFICATIONS;;09/03/97
EP ;
 S APSP1=0
 I $P($G(^PS(59.7,1,9999999)),"^",5)]"" Q
 I +$P($G(^PS(59.7,1,9999999)),"^",6) S APSP1=$P(^(9999999),"^",6)-1
 F  S APSP1=$O(^APSPQA(32.4,APSP1)) Q:'APSP1  S APSP2=$P($G(^APSPQA(32.4,APSP1,0)),"^",3) I APSP2]"" D SET S $P(^PS(59.7,1,9999999),"^",6)=APSP1
 S APSPM1=" ",APSPM2="The Outpatient Pharmacy Intervention/Provider update ",APSPM3="has finished on "
 D MSG^APSPOST2,NOW^%DTC S $P(^PS(59.7,1,9999999),"^",5)=$E(%,1,12)
 D ^APSPOSTP ;CONVERT CONTACTED PROVIDER FIELD 08/16/96
 K APSP1,APSP2,APSPM1,APSPM2,APSPM3,DIE,DA,DR
 Q
SET S APSP3=$G(^DIC(16,APSP2,"A3"))
 I +APSP3,$D(^VA(200,APSP3,0)) D SET1
 Q
SET1 S DIE="^APSPQA(32.4,",DA=APSP1,DR=".03////^S X=APSP3" D ^DIE
 Q
EP1 ; Entry Point for file 9009032.4 field .16(Division) CONVERSION
 S APSPVAR=1,APSP1=0
 F  S APSP1=$O(^APSPQA(32.4,APSP1)) Q:'APSP1  D SET2
 S APSPM1=" ",APSPM2="The Outpatient Pharmacy Intervention/Division update ",APSPM3="has finished on "
 D MSG^APSPOST2
 Q
SET2 S DIE="^APSPQA(32.4,",DA=APSP1,DR=".16////^S X=APSPVAR" D ^DIE
 Q
