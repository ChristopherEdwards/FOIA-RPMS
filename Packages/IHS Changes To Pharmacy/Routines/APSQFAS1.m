APSQFAS1 ;IHS/ASDS/ENM/POC - QUESTIONS FOR NEW RX ORDER ENTRY  
 ;;6.0;IHS PHARMACY MODIFICATIONS;**3**;FEB 20, 2001
START ;EP
 S (PSONEW("DFLG"),PSONEW("FIELD"),PSONEW1)=0
 ;
2 S PSONEW("FLD")=2 D PTSTAT^PSODIR1(.PSONEW) ; Get Patient Status
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
 ;
9 S PSONEW("FLD")=9 D PROV^PSODIR(.PSONEW) ; Get Provider
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
10 I $G(DUZ("AG"))="I" S PSONEW("FLD")=10 D EXP^PSODIR2(.PSONEW) ; Get Expiration Date - Indian Health Service ONLY
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
11 S PSONEW("FLD")=11 D CLINIC^PSODIR2(.PSONEW) ; Get Clinic
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
12 S PSONEW("FLD")=12 D MW^PSODIR2(.PSONEW) ; Get Mail/Window Info
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
14 S PSONEW("FLD")=14 D ISSDT^PSODIR2(.PSONEW) ; Get Issue Date
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
 ;IHS/DSD/ENM 10/05/94 FILL DATE EDIT DISABLED PSG REQ
15 ;S PSONEW("FLD")=15 D FILLDT^PSODIR2(.PSONEW) ; Get Fill date
 ;G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 S (PSONEW("FILL DATE"),APSPRFD)=PSONEW("ISSUE DATE") ;/DSD/ENM 4/7/94
 ;
16 S PSONEW("FLD")=16 D CLERK^PSODIR2(.PSONEW) ; Get Clerk Code
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
 S:$G(PSOBILRX)'=1 PSONEW("BST")=PSOBILST G:$G(PSOBILRX)'=1 END ;SHOW DEPENDING ON VAR PSOBILRX IHS/OKCAO/POC 8/18/2000
200 S PSONEW("FLD")=200 D BST^PSODIR1(.PSONEW) ;GET BILLING STATUS CODE
 G:PSONEW("DFLG") END G:PSONEW("FIELD") @PSONEW("FIELD")
 ;
 M APSQSAVE=PSONEW ;TO SAVE FOR OVER AND OVER ON APSQFAS3
END ;
 K PSONEW1
 Q
 ;
JUMP ;
 ;IHS/DSD/ENM 10/05/94 9999999.02:09=CHRONIC MED FIELD ADDED TO NEXT LINE
 ;S PSONEW("FIELD")=$S(+Y=.01:1,+Y=3:2,+Y=6:3,+Y=10:4,+Y=7:5,+Y=10.6:6,+Y=8:7,+Y=9:8,+Y=4:9,+Y=29:10,+Y=5:11,+Y=11:12,+Y=12:13,+Y=1:14,+Y=22:15,+Y=16:16,+Y=9999999.02:"09",1:PSONEW("FLD"))
 S PSONEW("FIELD")=$S(+Y=3:2,+Y=4:9,+Y=29:10,+Y=5:11,+Y=11:12,+Y=1:14,+Y=22:15,+Y=16:16,+Y=9999999.07:200,1:PSONEW("FLD")) ;IHS/OKCAO/POC 8/18/2000
 I PSONEW("FIELD")>PSONEW("FLD") W !,*7,"Cannot jump ahead ..",! S PSONEW("FIELD")=PSONEW("FLD")
 Q
