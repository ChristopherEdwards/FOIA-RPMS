ASDFORM ; IHS/ADC/PDW/ENM - PRINTS HS & OTHER FORMS ; [ 03/25/1999  11:48 AM ]
 ;;5.0;IHS SCHEDULING;;MAR 25, 1999
 ;
EF(IBCLINIC,DFN,IBAPPT) ;EP; prints encounter form
 NEW X
 S X="IBDF1B2" X ^%ZOSF("TEST") I '$T Q
 S IBFORM=+$$FORMS^IBDF1B2(IBCLINIC,DFN,IBAPPT) Q:'IBFORM
 NEW G,I,J,K,L,LL,P,V,X,Y,APDATE,GDATE,PRDATE
 NEW CNT,POP,ADDR,DGPGM,DIV,TDO,VAR,NAME,ORDER,SSN
 NEW SC,SDATE,SDCNT,SDI,SDIQ,SDREP,SDSCCOND,SDSP,SDSTART,SDX
 S IBPM=0,IBCOMP=1 D ^IBDF2A
 Q
 ;
HS(APCHSPAT,APCHSTYP) ;EP; prints health summary
 NEW G,I,J,K,L,LL,P,V,X,Y,APDATE,GDATE,PRDATE
 NEW CNT,DFN,POP,ADDR,DGPGM,DIV,TDO,VAR,NAME,ORDER,SSN
 NEW SC,SDATE,SDCNT,SDI,SDIQ,SDREP,SDSCCOND,SDSP,SDSTART,SDX
 I $Y>5 W @IOF
 D EN^APCHS
 K AUPNPAT,AUPNDOB,AUPNDOD,AUPNSEX,DIC
 K APCHSPDN,APCHSTAT,APCHSVTP,APCHSNDF,APCHSPRB
 Q
 ;
MP(DFN) ;EP; prints med profile
 NEW G,I,J,K,L,LL,P,V,X,Y,APDATE,GDATE,PRDATE
 NEW CNT,POP,ADDR,DGPGM,DIV,TDO,VAR,NAME,ORDER,SSN
 NEW SC,SDATE,SDCNT,SDI,SDIQ,SDREP,SDSCCOND,SDSP,SDSTART,SDX
 NEW ASDX,ASDY,FN,DA,D0,PLS,PSRT,PSLC,PSZCHN,ST,ST0,PSODTCT
 K ^UTILITY($J)
 S (FN,DA,D0)=DFN,PSRT="DATE",X=""
 S X1=DT,X2=-365 D C^%DTC S PSODTCT=X
 S X="PSOP" X ^%ZOSF("TEST") I $T S PLS="S" U IO D P^PSOP Q
 S X="PSP" X ^%ZOSF("TEST") I $T S PLS=1 U IO D P^PSP
 Q
 ;
AIU(DFN) ; EP; prints address/insurance update
 NEW G,I,J,K,L,LL,P,V,X,Y,APDATE,GDATE,PRDATE
 NEW CNT,POP,ADDR,DGPGM,DIV,TDO,VAR,NAME,ORDER,SSN
 NEW SC,SDATE,SDCNT,SDI,SDIQ,SDREP,SDSCCOND,SDSP,SDSTART,SDX
 NEW ASDX,ASDY
 S ASDX=$$VALI^XBDIQ1(9000001,DFN,.03) ;last reg update
 S ASDY=$$VALI^XBDIQ1(40.8,$$DIV^ASDUT,9999999.08) ;waiting days
 I $$FMADD^XLFDT(ASDX,ASDY)>DT Q  ;not time yet
 D SET^ASDAIU,END1^ASDAIU
 Q
 ;
