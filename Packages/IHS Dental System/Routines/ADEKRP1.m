ADEKRP1 ; IHS/HQT/MJL - PRINT COMPILED REPORTS ;  [ 03/24/1999   9:04 AM ]
 ;;6.0;ADE;;APRIL 1999
PRINT ;EP TASKMAN ENTRY FOR PRINT PHASE
 I '$D(^TMP("ADEP",ADEU)) G PRNEND
 I $D(ZTQUEUED) L +^TMP("ADEP",ADEU):1 I '$T S ADENOLOK=1 G PRNEND
 N FLDS
 F J=1:1:$L(ADEROPT,U) S FLDS=$P(ADEROPT,U,J),DHD=$P(FLDS,";",2),FLDS=$P(FLDS,";") D
 . N DIC,BY,ADED0,ADED1,ADED2,ADED3,Y,ADEX0,ADEX1,ADEX2,J,ADEZTSK
 . S IOP=ADEIOP
 . S %ZIS("IOPAR")=ADEIOPAR
 . D ^%ZIS
 . S IOP=ADEIOP
 . S %ZIS("IOPAR")=ADEIOPAR
 . S DIC="^ADEDUM(",BY="@NUMBER",(FR,TO)=""
 . S:FLDS["SINGLE" BY="#"_BY
 . S ADED0=0
 . I FLDS["ADEK-COMBINE-QUARTER" D
 . . S ADEX0="S ADED0=$O(^TMP(""ADEP"",ADEU,""CQ"",ADED0))"
 . . S ADEX1="S ADED1=$O(^TMP(""ADEP"",ADEU,""CQ"",ADED0,ADED1),-1)"
 . . S ADEX2="S ADED2=$O(^TMP(""ADEP"",ADEU,""CQ"",ADED0,ADED1,ADED2))"
 . I FLDS["ADEK-CALIF" D
 . . S ADED0=999999
 . . S ADEX0="S ADED0=$O(^TMP(""ADEP"",ADEU,"_$S(FLDS["Q":"""CFQ""",1:"""CF""")_",ADED0),-1)"
 . ;FHL 9/9/98 I $D(ZTSK) S ADEZTSK=ZTSK K ZTSK
 . I $D(ZTQUEUED) S ADEZTSK=ZTSK K ZTSK
 . D EN1^DIP
 . I $D(ADEZTSK) S ZTSK=ADEZTSK
PRNEND I $D(ZTQUEUED) S ZTREQ="@"
 I '$D(ADENOLOK) K ^TMP("ADEP",ADEU)
 K ADENOLOK
 Q
