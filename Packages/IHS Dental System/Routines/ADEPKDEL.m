ADEPKDEL ;IHS/HQW/MJL - DELETE ADE NAMESPACED ITEMS ;08:08 PM  [ 03/24/1999   8:35 AM ]
 ;;6.0;ADE;;APRIL 1999
 ;
 ;Deletes all ADE namespaced templates, options, help frames, keys
 ;EXCEPT option ADEMENU and keys ADEZMENU and ADEZMSUPFUN
 ;
START S ADEPKNSP="ADE"
 S ADEQUIT=ADEPKNSP_"{"
 F ADEPKGLO="^DIBT(","^DIPT(","^DIE(","^DIC(19,","^DIC(9.2," D DELETE
 S ADEPKGLO="^DIC(19.1," D DELETE
 W !
 ;Delete all menu items in ADEMENU
 S ADEMENU=$O(^DIC(19,"B","ADEMENU",0))
 G:'+ADEMENU A
 S DA(1)=ADEMENU
 S DIK="^DIC(19,"_ADEMENU_",10,"
 S DA=0
 F  S DA=$O(^DIC(19,ADEMENU,10,DA)) Q:'+DA  D ^DIK
A D EOJ
 Q
 ;
DELETE W !!,"Now deleting `",ADEPKNSP,"' namespaced ",$P(@(ADEPKGLO_"0)"),U)_"S..."
 S ADENSPC=ADEPKNSP
 I $D(@(ADEPKGLO_"""B"",ADENSPC)")) D
 . S DA=$O(@(ADEPKGLO_"""B"",ADENSPC,"""")"))
 . S DIK=ADEPKGLO
 . D ^DIK
 . K DIK,DA
 F  S ADENSPC=$O(@(ADEPKGLO_"""B"",ADENSPC)")) Q:ADENSPC=""!(ADENSPC]ADEQUIT)  D
 . S DA=$O(@(ADEPKGLO_"""B"",ADENSPC,"""")"))
 . I ADENSPC="ADEMENU"!(ADENSPC="ADEZMENU")!(ADENSPC="ADEZMSUPFUN") Q
 . W !,?3,ADENSPC
 . S DIK=ADEPKGLO
 . D ^DIK
 . K DIK,DA
 Q
 ;
EOJ K ADEPKGLO,ADEPKEY,ADESTP,ADEPKNSP,ADENSPC,ADEQUIT,ADERUN,ADEDOC,DUOUT,DTOUT
 Q
