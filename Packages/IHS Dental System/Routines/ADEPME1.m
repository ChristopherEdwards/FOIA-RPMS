ADEPME1 ; IHS/HQT/MJL  - MEDICAID ELIGIBLES PT 2 ;  [ 03/24/1999   9:04 AM ]
 ;;6.0;ADE;;APRIL 1999
 U IO S ADEPAG=0,Y=1,$P(ADELIN,"-",79)=""
 D HDR
 D PRINT
END D ^%ZISC Q
PRINT S ADEAGE=0 F J=0:0 S ADEAGE=$O(^ADEUTL("ADEPME",$J,ADEAGE)) Q:ADEAGE=""  Q:'Y  W !?10,"AGE GROUP: ",ADEAGE D P1
 Q
P1 S ADENAM=0 F J=0:0 S ADENAM=$O(^ADEUTL("ADEPME",$J,ADEAGE,ADENAM)) Q:ADENAM=""  Q:'Y  D P2
 Q
P2 S ADEDFN=0 F J=0:0 S ADEDFN=$O(^ADEUTL("ADEPME",$J,ADEAGE,ADENAM,ADEDFN)) Q:'ADEDFN  Q:'Y  D P3
 Q
P3 S ADENOD=^ADEUTL("ADEPME",$J,ADEAGE,ADENAM,ADEDFN)
 W !,ADENAM,?30,$P(ADENOD,U),?40,$P(ADENOD,U,2),?50,$P(ADENOD,U,3)
 D EOL
 Q
HDR S ADEPAG=ADEPAG+1 W @IOF,!,"DENTAL MEDICAID ELIGIBLE REPORT",?60,$E(DT,4,5)_"-"_$E(DT,6,7)_"-"_$E(DT,2,3)
 W !?60,"PAGE ",ADEPAG
 W !,"PATIENT",?30,"VISIT",?40,"MEDICAID",?50,"PROVIDER"
 W !,"NAME",?30,"DATE",?40,"NUMBER",?50,"NAME"
 W !,ADELIN
 Q
EOL Q:$Y'>(IOSL-5)
EOL1 I $P(IOST,"-")["C" W *7 R !,X:DTIME I ('$T)!(X["^") S Y=0 Q
 D HDR Q
HEADER W @IOF,ADEFAC," DENTAL VISIT STATISTICS ",?65,"PAGE ",ADEPAG,!,"FOR VISITS BETWEEN ",ADEBDT," AND ",ADEND
