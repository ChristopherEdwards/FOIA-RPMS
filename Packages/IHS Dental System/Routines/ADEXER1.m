ADEXER1 ; IHS/HQT/MJL  - DENTAL ERROR REPORT PT 2 ;  [ 03/24/1999   9:04 AM ]
 ;;6.0;ADE;;APRIL 1999
START ;EP
 S ADEBS6="",$P(ADEBS6,$C(8),7)="",(ADEDF,ADERC)=0,ADERNO=1
 U IO
 S:($P(IOST,"-",1)'="C") ADEBS6=" "
 W !!!,?12,"E R R O R   S E A R C H   B E G U N",!
 ;K ^ADERROR ; NON-FILEMAN, WORKING GLOBAL
 D KILLERR^ADEXER ;FHL 10/19/98
 D CHKY1
 I ADERNO>1 S ^ADERROR(0)=ADEBDT_U_ADEND_U_DT_U_ADERC_U_(ADERNO-1)
FIN W !,?15,"RECORDS PROCESSED: ",ADERC,!
 W !,?15,"TOTAL ERRORS: ",ADERNO-1,!
 W !,?12,"P R O C E S S I N G   C O M P L E T E D",!!
 I ADERNO'>1 G EXIT
 I $P(IOST,"-")["C" R "Press 'ENTER' to see Error List ( '^' to Quit): ",X:DTIME G:('$T)!(X["^") EXIT W:$D(IOF) @IOF G ^ADEXER3
 W:$D(IOF) @IOF G ^ADEXER3
EXIT D ^%ZISC ;U 0
 I $D(ZTQUEUED) S ZTREQ="@"
 K ADEA,ADEADACP,ADEADAF,ADEADAQ,ADEASF,ADEASITE,ADEB,ADEBDT,ADEBS6,ADEC,ADED,ADEDF,ADEDFN,ADEDMFLG,ADEDOB,ADEEDT,ADEERR,ADEFN,ADEFNO,ADEHRN,ADEIDX,ADENAT,ADENODE,ADERC,ADERDV,ADEREPD,ADESERV,ADESEX,ADESITE,ADESUFAC,ADESVCS,ADETCOST
QUIT K ADETYPE,ADEVDTE,ADEVDTP,ADEVISDT,ADEVNODE,ADEZIP,ADEZTSK,ADEDT,ADEND,ADERR,ADERNO,ADEOLD
 Q
CHKY1 ;GET FIRST DATE FROM "AC" XREF
 W !,?15,"RECORD SCANNING "
 S ADEDT=ADEBDT-1 F ADEIDX=0:0 S ADEDT=$O(^ADEPCD("AC",ADEDT)) Q:(ADEDT="")!(ADEDT>ADEND)  W "." D CHKY2
 W "COMPLETED",!
 Q
CHKY2 S ADEA=0 F ADEIDX=0:0 S ADEA=$O(^ADEPCD("AC",ADEDT,ADEA)) Q:'+ADEA  D CHKY3
 Q
CHKY3 I '$D(^ADEPCD(ADEA,0)) G ERR1^ADEXER2
 Q:$P(^ADEPCD(ADEA,0),U,6)
 I $P(^ADEPCD(ADEA,0),U,3)']"" G ERR3^ADEXER2
 I '$D(^ADEPCD(ADEA,"ADA",0)) G ERR10^ADEXER2
 I $O(^ADEPCD(ADEA,"ADA",0))="" G ERR10^ADEXER2
 S ADEVNODE=^ADEPCD(ADEA,0)
 S ADEVDTE="",ADEVDTP=""
 S ADEDFN=$P(ADEVNODE,U)
 G:'$D(^DPT(ADEDFN,0)) ERR4^ADEXER2
 ;G:'$D(^DPT(ADEDFN,11)) ERR7^ADEXER2
 ;G:$P(^DPT(ADEDFN,11),U,11)="" ERR7^ADEXER2
 S ADEVDTE=$E($P(ADEVNODE,U,2),2,7)
 I ADEVDTE="" G ERR5^ADEXER2
 S ADEREPD=$P(ADEVNODE,U,4)
 I ADEREPD="" G ERR6^ADEXER2
 I '$D(^DIC(16,ADEREPD,0)) G ERR6^ADEXER2
 I $P(^DIC(16,ADEREPD,0),U,9)="" G ERR12^ADEXER2
 S ADERC=ADERC+1
 Q
