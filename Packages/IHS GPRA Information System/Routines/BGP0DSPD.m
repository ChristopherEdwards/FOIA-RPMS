BGP0DSPD ; IHS/CMI/LAB - IHS summary page ;
 ;;10.0;IHS CLINICAL REPORTING;;JUN 18, 2010
 ;
START ;
 I BGPRTYPE'=1 Q
 I $G(BGPNPL) Q  ;not on gpra pat list
 I $G(BGPCPPL) Q  ;not on comp list
 S BGPQUIT=""
 D HEADER
 D W^BGP0DP("GPRA DEVELOPMENTAL MEASURES",0,2,BGPPTYPE)
 D W^BGP0DP("---------------------------",0,1,BGPPTYPE)
 S BGPC=0 F  S BGPC=$O(^TMP($J,"SUMMARY DEVEL",BGPC)) Q:BGPC'=+BGPC!(BGPQUIT)  D
 .I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER Q:BGPQUIT
 .S BGPC1=$O(^BGPSCAT("C",BGPC,0))
 .D W^BGP0DP("",0,1,BGPPTYPE)
 .D W^BGP0DP($P(^BGPSCAT(BGPC1,0),U),0,1,BGPPTYPE)
 .S BGPO="" F  S BGPO=$O(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO)) Q:BGPO=""!(BGPQUIT)  D
 ..S BGPPC=$O(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,0))
 ..Q:$P($G(^BGPINDTC(BGPPC,22)),U,13)  ;part measure displays last
 ..I BGPPTYPE="P",$Y>(BGPIOSL-4) D HEADER Q:BGPQUIT
 ..I $P(^BGPINDTC(BGPPC,0),U,4)["014.A"!($P(^BGPINDTC(BGPPC,0),U,4)["023.")!($P(^BGPINDTC(BGPPC,0),U,4)="016.A.1")!($P(^BGPINDTC(BGPPC,0),U,4)="016.A.5")!($P($G(^BGPINDTC(BGPPC,19)),U,13)) D  I 1
 ...D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,4),0,1,BGPPTYPE,1,1)
 ...I $P(^BGPINDTC(BGPPC,22),U,7)]"" D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,7),0,1,BGPPTYPE,1,1)  ;W !?1,$P(^BGPINDTC(BGPPC,22),U,7)
 ...I $P(^BGPINDTC(BGPPC,22),U,12)]"" D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,12),0,1,BGPPTYPE,1,1)
 ...I BGPPTYPE="P" D
 ....D W^BGP0DP($J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U),7,0),0,0,BGPPTYPE,1,26)
 ....D W^BGP0DP($J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,2),7,0),0,0,BGPPTYPE,1,34)
 ....D W^BGP0DP($J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,3),7,0),0,0,BGPPTYPE,1,41)
 ....;D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,2),0,0,BGPPTYPE,1,57)
 ...I BGPPTYPE="D" D
 ....D W^BGP0DP($P(^TMP($J,"SUMMARYDEL DEVEL",BGPC,BGPO,BGPPC),U),0,0,BGPPTYPE,2)
 ....D W^BGP0DP($P(^TMP($J,"SUMMARYDEL DEVEL",BGPC,BGPO,BGPPC),U,2),0,0,BGPPTYPE,3)
 ....D W^BGP0DP($P(^TMP($J,"SUMMARYDEL DEVEL",BGPC,BGPO,BGPPC),U,3),0,0,BGPPTYPE,4)
 ....D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,2),0,0,BGPPTYPE,5)
 ....;D S(X,1,1)
 ..E  D
 ...D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,4),0,1,BGPPTYPE,1,1)
 ...I $P(^BGPINDTC(BGPPC,22),U,7)]"" D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,7),0,1,BGPPTYPE,1,1)  ;W !?1,$P(^BGPINDTC(BGPPC,22),U,7)
 ...I $P(^BGPINDTC(BGPPC,22),U,12)]"" D W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,12),0,1,BGPPTYPE,1,1)  ;W !?1,$P(^BGPINDTC(BGPPC,22),U,7)
 ...I BGPPTYPE="P" D
 ....D W^BGP0DP($J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U),7,1)_"%",0,0,BGPPTYPE,1,26)
 ....D W^BGP0DP($J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,2),7,1)_"%",0,0,BGPPTYPE,1,34)
 ....D W^BGP0DP($J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,3),7,1)_"%",0,0,BGPPTYPE,1,41)
 ....;D W^BGP0DP($TR($P(^BGPINDTC(BGPPC,22),U,2),"$","^"),0,0,BGPPTYPE,1,57)
 ...I BGPPTYPE="D" D
 ....D W^BGP0DP($P(^TMP($J,"SUMMARYDEL DEVEL",BGPC,BGPO,BGPPC),U)_"%",0,0,BGPPTYPE,2,26)
 ....D W^BGP0DP($P(^TMP($J,"SUMMARYDEL DEVEL",BGPC,BGPO,BGPPC),U,2)_"%",0,0,BGPPTYPE,3,34)
 ....D W^BGP0DP($P(^TMP($J,"SUMMARYDEL DEVEL",BGPC,BGPO,BGPPC),U,3)_"%",0,0,BGPPTYPE,4,41)
 ....;D W^BGP0DP($TR($P(^BGPINDTC(BGPPC,22),U,2),"$","^"),0,0,BGPPTYPE,5,57)
 ...I $P(^BGPINDTC(BGPPC,22),U,9)]""!($P(^BGPINDTC(BGPPC,22),U,10)]"")!($P(^BGPINDTC(BGPPC,22),U,11)]"") D
 ....;D W^BGP0DP($TR($P(^BGPINDTC(BGPPC,22),U,10),"$","^"),0,0,BGPPTYPE,5,57),W^BGP0DP($P(^BGPINDTC(BGPPC,22),U,11),0,0,BGPPTYPE,6,68)
 ....D W^BGP0DP("",0,0,BGPPTYPE)
 I BGPPTYPE="P",$Y>(BGPIOSL-2) D HEADER Q:BGPQUIT
 D W^BGP0DP("* Not GPRA Developmental measure but included to show percentage of",0,2,BGPPTYPE)
 D W^BGP0DP("refusals with respect to GPRA Developmental measure.",0,1,BGPPTYPE)
 D W^BGP0DP("",0,1,BGPPTYPE)
 Q
 ;D PART
 ;Q
 ;
HEADER ;EP
 D HEADER^BGP0DPH
 D H1
 Q
H1 ;
 S X="GPRA DEVELOPMENTAL CLINICAL PERFORMANCE SUMMARY" D W^BGP0DP(X,1,1,BGPPTYPE)
 I $G(BGPAREAA) D W^BGP0DP("Area",0,1,BGPPTYPE,2,27),W^BGP0DP("Area",0,0,BGPPTYPE,3,35),W^BGP0DP("Area",0,0,BGPPTYPE,4,44)  ;,W^BGP0DP("Nat'l",0,0,BGPPTYPE,5,57)
 I '$G(BGPAREAA) D W^BGP0DP("Site",0,1,BGPPTYPE,2,27),W^BGP0DP("Site",0,0,BGPPTYPE,3,35),W^BGP0DP("Site",0,0,BGPPTYPE,4,44)  ;,W^BGP0DP("Nat'l",0,0,BGPPTYPE,5,57)
 D W^BGP0DP("Current",0,1,BGPPTYPE,2,26),W^BGP0DP("Previous",0,0,BGPPTYPE,3,34),W^BGP0DP("Baseline",0,0,BGPPTYPE,4,43)  ;,W^BGP0DP("2009",0,0,BGPPTYPE,5,57)
 D W^BGP0DP($TR($J("",80)," ","-"),0,1,BGPPTYPE)
 Q
CTR(X,Y) ;EP - Center X in a field Y wide.
 Q $J("",$S($D(Y):Y,1:IOM)-$L(X)\2)_X
 ;----------
USR() ;EP - Return name of current user from ^VA(200.
 Q $S($G(DUZ):$S($D(^VA(200,DUZ,0)):$P(^(0),U),1:"UNKNOWN"),1:"DUZ UNDEFINED OR 0")
 ;----------
LOC() ;EP - Return location name from file 4 based on DUZ(2).
 Q $S($G(DUZ(2)):$S($D(^DIC(4,DUZ(2),0)):$P(^(0),U),1:"UNKNOWN"),1:"DUZ(2) UNDEFINED OR 0")
 ;----------
PART ;
 D HEADERP
 S P1=$S($G(BGPNGR09):14,1:8)
 S BGPC=0 F  S BGPC=$O(^TMP($J,"SUMMARY DEVEL",BGPC)) Q:BGPC'=+BGPC!(BGPQUIT)  D
 .I $Y>(BGPIOSL-3) D HEADERP Q:BGPQUIT
 .S BGPC1=$O(^BGPSCAT("C",BGPC,0))
 .;W !
 .;W !,$P(^BGPSCAT(BGPC1,0),U)
 .S BGPO="" F  S BGPO=$O(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO)) Q:BGPO=""!(BGPQUIT)  D
 ..S BGPPC=$O(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,0))
 ..Q:'$P($G(^BGPINDTC(BGPPC,22)),U,13)  ;part measures only
 ..I $Y>(BGPIOSL-4) D HEADERP Q:BGPQUIT
 ..I $P(^BGPINDTC(BGPPC,0),U,4)["014.A"!($P(^BGPINDTC(BGPPC,0),U,4)["023.")!($P(^BGPINDTC(BGPPC,0),U,4)["016")!($P($G(^BGPINDTC(BGPPC,19)),U,13)) D  I 1
 ...W !,$P(^BGPINDTC(BGPPC,22),U,4)
 ...I $P(^BGPINDTC(BGPPC,22),U,7)]"" W !,$P(^BGPINDTC(BGPPC,22),U,7)
 ...I $P(^BGPINDTC(BGPPC,22),U,12)]"" W !,$P(^BGPINDTC(BGPPC,22),U,12)
 ...W ?26,$J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U),7,0)
 ...W ?34,$J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,2),7,0)
 ...W ?41,$J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,3),7,0)
 ...W ?53,$P(^BGPINDTC(BGPPC,22),U,P1),?64,$P(^BGPINDTC(BGPPC,22),U,2),?73,$P(^BGPINDTC(BGPPC,22),U,3)
 ..E  D
 ...W !,$P(^BGPINDTC(BGPPC,22),U,4)
 ...I $P(^BGPINDTC(BGPPC,22),U,7)]"" W !,$P(^BGPINDTC(BGPPC,22),U,7)
 ...I $P(^BGPINDTC(BGPPC,22),U,12)]"" W !,$P(^BGPINDTC(BGPPC,22),U,12)
 ...W ?26,$J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U),7,1),"%"
 ...W ?34,$J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,2),7,1),"%"
 ...W ?41,$J($P(^TMP($J,"SUMMARY DEVEL",BGPC,BGPO,BGPPC),U,3),7,1),"%"
 ...W ?53,$TR($P(^BGPINDTC(BGPPC,22),U,P1),"$","^"),?64,$TR($P(^BGPINDTC(BGPPC,22),U,2),"$","^"),?73,$P(^BGPINDTC(BGPPC,22),U,3)
 ...I $P(^BGPINDTC(BGPPC,22),U,9)]""!($P(^BGPINDTC(BGPPC,22),U,10)]"")!($P(^BGPINDTC(BGPPC,22),U,11)]"") W !?53,$TR($P(^BGPINDTC(BGPPC,22),U,9),"$","^"),?64,$TR($P(^BGPINDTC(BGPPC,22),U,10),"$","^"),?73,$P(^BGPINDTC(BGPPC,22),U,11)
 I $Y>(BGPIOSL-9) D HEADERP Q:BGPQUIT
 W !
 I $G(BGPNGR09) D
 .W !," * PART 2011 target represented here is a preliminary target since it will be"
 .W !,"adjusted for FY 2010 actual results and FY 2011 appropriations."
 W !,$S($G(BGPNGR09):"**",1:"*")," Federally Administered Activities measure.  National 2009 rate is for federal"
 W !,"sites only."
 W ! Q
 ;
HEADERP ;EP
 D HEADER^BGP0DPH
 D H1P
 Q
H1P ;
 S X="GPRA DEVELOPMENTAL & PART MEASURES CLINICAL PERFORMANCE SUMMARY" W !,$$CTR(X,80)
 I $G(BGPAREAA) W !?26," Area",?34," Area",?43," Area",?53,$S($G(BGPNGR09):"PART10",1:"PART09"),?64,"Nat'l",?73,"2011"
 I '$G(BGPAREAA) W !?26," Site",?34," Site",?43," Site",?53,$S($G(BGPNGR09):"PART10",1:"PART09"),?64,"Nat'l",?73,"2011"
 W !?26,"Current",?34,"Previous",?43,"Baseline",?53,"Target",?64,"2009",?73,"Target"
 W !,$TR($J("",80)," ","-")
 W !!,"PART MEASURE"
 W !,"------------"
 W !
 Q
