BGP3D1 ; IHS/CMI/LAB - calc indicators ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
PROC ;EP
 ;general setup for all gpra reports
 S BGPBT=$H
 D JRNL ;turn off journaling
 S BGPJ=$J,BGPH=$H
 ;calculate 3 years before end of each time frame
 S BGP3YE=$$FMADD^XLFDT(BGPED,-1096)
 S BGPP3YE=$$FMADD^XLFDT(BGPPED,-1096)
 S BGPB3YE=$$FMADD^XLFDT(BGPBED,-1096)
 ;process each patient
 S DFN=0 F  S DFN=$O(^AUPNPAT(DFN)) Q:DFN'=+DFN  D PROCCY,PROCPY,PROCBY
 S X=0 F  S X=$O(^BGPGPDC(BGPRPT,99999,X)) Q:X'=+X  F Y=1:1:119 I $P(^BGPGPDC(BGPRPT,99999,X,0),U,Y)="" S $P(^BGPGPDC(BGPRPT,99999,X,0),U,Y)=0
 S X=0 F  S X=$O(^BGPGPDP(BGPRPT,99999,X)) Q:X'=+X  F Y=1:1:119 I $P(^BGPGPDP(BGPRPT,99999,X,0),U,Y)="" S $P(^BGPGPDP(BGPRPT,99999,X,0),U,Y)=0
 S X=0 F  S X=$O(^BGPGPDB(BGPRPT,99999,X)) Q:X'=+X  F Y=1:1:119 I $P(^BGPGPDB(BGPRPT,99999,X,0),U,Y)="" S $P(^BGPGPDB(BGPRPT,99999,X,0),U,Y)=0
 S BGPET=$H
 Q
 ;
JRNL ;
 S %=$$NOJOURN^ZIBGCHAR("BGPGPDC"),%=$$NOJOURN^ZIBGCHAR("BGPGPDP"),%=$$NOJOURN^ZIBGCHAR("BGPGPDB")
 Q
PROCCY ;current time period
 K ^TMP($J)
 S (BGPACTUP,BGPACTCL,BGPDM1,BGPDM2,BGPDMD1,BGPDMD2,BGPDMD3,BGPDMD4,BGPDMD5,BGPIHD)=""
 Q:'$D(^DPT(DFN,0))
 Q:$P(^DPT(DFN,0),U,2)=""
 Q:"FM"'[$P(^DPT(DFN,0),U,2)
 S BGPEDATE=BGPED,BGPTIME=1,BGPBDATE=BGPBD,BGPGBL="^BGPGPDC("
 S BGP365=$$FMADD^XLFDT(BGPEDATE,-365)
 S BGPACTUP=$$ACTUP(DFN,BGP3YE,BGPEDATE,BGPTAXI) ;user pop
 I 'BGPACTUP Q  ;if not in user pop, don't use patient
 S BGPACTCL=$$ACTCL(DFN,BGP3YE,BGPEDATE) ;active clinical
 S BGPAGEB=$$AGE^AUPNPAT(DFN,BGPBDATE)
 S BGPAGEE=$$AGE^AUPNPAT(DFN,BGPEDATE)
 S BGPSEX=$P(^DPT(DFN,0),U,2)
 I $$V2IHD^BGP3D9(DFN,BGP365,BGPEDATE),$$FIRSTIHD^BGP3D9(DFN,BGPEDATE) S BGPIHD=1
 S BGPDM1=$$DM(DFN,,BGPEDATE) ;dm diagnosis ever?
 S BGPDM2=$$DM(DFN,BGP365,BGPEDATE) ;dm diagnosis in past year
 I BGPDM1 D  ;set up 4 dm demoninators
 .S BGPFDMD=$$FIRSTDM(DFN,BGPEDATE) ;1 OR 0 FIRST DX BEFORE BEG
 .S BGP2V=$$V2(DFN,BGP365,BGPEDATE)
 .S BGP2DMV=$$V2DM(DFN,$$DOB^AUPNPAT(DFN),BGPEDATE)
 .S BGP1DMV=$$V1DM(DFN,BGP365,BGPEDATE)
 .S BGPDMD1=0 I BGPFDMD S BGPDMD1=1
 .S BGPDMD2=0 I BGPACTCL,BGPFDMD S BGPDMD2=1
 .S BGPDMD3=0 I BGPFDMD,BGP2V,BGP2DMV,BGPACTCL S BGPDMD3=1
 .S BGPDMD4=0 I BGPFDMD,BGPAGEB>19,BGP2DMV,BGP1DMV,$$CREAT(DFN,BGPEDATE) S BGPDMD4=1
 .S BGPDMD5=0 I BGPFDMD,BGP2DMV,BGP1DMV,$$CREAT(DFN,BGPEDATE) S BGPDMD5=1
 D CALCIND
 K ^TMP($J,"A")
 Q
PROCPY ;
 S (BGPACTUP,BGPACTCL,BGPDM1,BGPDM2,BGPDMD1,BGPDMD2,BGPDMD3,BGPDMD4,BGPDMD5,BGPIHD)=""
 Q:'$D(^DPT(DFN,0))
 Q:$P(^DPT(DFN,0),U,2)=""
 Q:"FM"'[$P(^DPT(DFN,0),U,2)
 S BGPEDATE=BGPPED,BGPTIME=2,BGPBDATE=BGPPBD,BGPGBL="^BGPGPDP("
 S BGP365=$$FMADD^XLFDT(BGPEDATE,-365)
 S BGPACTUP=$$ACTUP(DFN,BGPP3YE,BGPEDATE,BGPTAXI) ;user pop
 I 'BGPACTUP Q  ;if not in user pop, don't use patient
 S BGPACTCL=$$ACTCL(DFN,BGPP3YE,BGPEDATE) ;active clinical
 S BGPAGEB=$$AGE^AUPNPAT(DFN,BGPBDATE)
 S BGPAGEE=$$AGE^AUPNPAT(DFN,BGPEDATE)
 S BGPSEX=$P(^DPT(DFN,0),U,2)
 I $$V2IHD^BGP3D9(DFN,BGP365,BGPEDATE),$$FIRSTIHD^BGP3D9(DFN,BGPEDATE) S BGPIHD=1
 S BGPDM1=$$DM(DFN,,BGPEDATE) ;dm diagnosis ever?
 S BGPDM2=$$DM(DFN,BGP365,BGPEDATE) ;dm diagnosis in past year
 I BGPDM1 D  ;set up 4 dm demoninators
 .S BGPFDMD=$$FIRSTDM(DFN,BGPEDATE) ;1 OR 0 FIRST DX BEFORE BEG
 .S BGP2V=$$V2(DFN,BGP365,BGPEDATE)
 .S BGP2DMV=$$V2DM(DFN,$$DOB^AUPNPAT(DFN),BGPEDATE)
 .S BGP1DMV=$$V1DM(DFN,BGP365,BGPEDATE)
 .S BGPDMD1=0 I BGPFDMD S BGPDMD1=1
 .S BGPDMD2=0 I BGPACTCL,BGPFDMD S BGPDMD2=1
 .S BGPDMD3=0 I BGPFDMD,BGP2V,BGP2DMV,BGPACTCL S BGPDMD3=1
 .S BGPDMD4=0 I BGPFDMD,BGPAGEB>19,BGP2DMV,BGP1DMV,$$CREAT(DFN,BGPEDATE) S BGPDMD4=1
 .S BGPDMD5=0 I BGPFDMD,BGP2DMV,BGP1DMV,$$CREAT(DFN,BGPEDATE) S BGPDMD5=1
 D CALCIND
 K ^TMP($J,"A")
 Q
PROCBY ;
 S (BGPACTUP,BGPACTCL,BGPDM1,BGPDM2,BGPDMD1,BGPDMD2,BGPDMD3,BGPDMD4,BGPDMD5,BGPIHD)=""
 Q:'$D(^DPT(DFN,0))
 Q:$P(^DPT(DFN,0),U,2)=""
 Q:"FM"'[$P(^DPT(DFN,0),U,2)
 S BGPEDATE=BGPBED,BGPTIME=3,BGPBDATE=BGPBBD,BGPGBL="^BGPGPDB("
 S BGP365=$$FMADD^XLFDT(BGPEDATE,-365)
 S BGPACTUP=$$ACTUP(DFN,BGPB3YE,BGPEDATE,BGPTAXI) ;user pop
 I 'BGPACTUP Q  ;if not in user pop, don't use patient
 S BGPACTCL=$$ACTCL(DFN,BGPB3YE,BGPEDATE) ;active clinical
 S BGPAGEB=$$AGE^AUPNPAT(DFN,BGPBDATE)
 S BGPAGEE=$$AGE^AUPNPAT(DFN,BGPEDATE)
 S BGPSEX=$P(^DPT(DFN,0),U,2)
 I $$V2IHD^BGP3D9(DFN,BGP365,BGPEDATE),$$FIRSTIHD^BGP3D9(DFN,BGPEDATE) S BGPIHD=1
 S BGPDM1=$$DM(DFN,,BGPEDATE) ;dm diagnosis ever?
 S BGPDM2=$$DM(DFN,BGP365,BGPEDATE) ;dm diagnosis in past year
 I BGPDM1 D  ;set up 4 dm demoninators
 .S BGPFDMD=$$FIRSTDM(DFN,BGPEDATE) ;1 OR 0 FIRST DX BEFORE BEG
 .S BGP2V=$$V2(DFN,BGP365,BGPEDATE)
 .S BGP2DMV=$$V2DM(DFN,$$DOB^AUPNPAT(DFN),BGPEDATE)
 .S BGP1DMV=$$V1DM(DFN,BGP365,BGPEDATE)
 .S BGPDMD1=0 I BGPFDMD S BGPDMD1=1
 .S BGPDMD2=0 I BGPACTCL,BGPFDMD S BGPDMD2=1
 .S BGPDMD3=0 I BGPFDMD,BGP2V,BGP2DMV,BGPACTCL S BGPDMD3=1
 .S BGPDMD4=0 I BGPFDMD,BGPAGEB>19,BGP2DMV,BGP1DMV,$$CREAT(DFN,BGPEDATE) S BGPDMD4=1
 .S BGPDMD5=0 I BGPFDMD,BGP2DMV,BGP1DMV,$$CREAT(DFN,BGPEDATE) S BGPDMD5=1
 D CALCIND
 K ^TMP($J,"A")
 Q
CALCIND ;
 D CALCIND^BGP3DCI
 Q
CREAT(P,EDATE) ;get all creatines all must be <5
 K BGPG
 S %=P_"^ALL LAB [DM AUDIT CREATININE TAX;DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I '$D(BGPG(1)) Q 1  ;no creatinines 5 or greater
 S X=0,E=1 F  S X=$O(BGPG(X)) Q:X'=+X  S R=$P(BGPG(X),U,2) I +R>5,+R<15 S E=""
 Q E
V1DM(P,D,EDATE) ;
 I '$G(P) Q ""
 I '$D(^AUPNVSIT("AC",P)) Q ""
 S PC=$O(^ATXAX("B","BGP PRIMARY CARE CLINICS",0))
 I 'PC Q ""
 ;S PP=$O(^ATXAX("B","BGP PRIMARY PROVIDER DISC",0))
 ;I 'PP Q ""
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(D)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S T=$O(^ATXAX("B","SURVEILLANCE DIABETES",0))
 I 'T Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G>2)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .Q:'$D(^AUPNVPRV("AD",V))
 .;Q:$P(^AUPNVSIT(V,0),U,6)'=DUZ(2) ;PER TERRY
 .Q:'$D(^AUPNVPOV("AD",V))
 .S (D,Y)=0 F  S Y=$O(^AUPNVPOV("AD",V,Y)) Q:Y'=+Y!(D)  I $D(^AUPNVPOV(Y,0)) S %=$P(^AUPNVPOV(Y,0),U) I $$ICD^ATXCHK(%,T,9) S D=1
 .Q:'D
 .Q:"V"[$P(^AUPNVSIT(V,0),U,3)
 .S Y=$$PRIMPROV^APCLV(V,"F")
 .Q:'Y
 .;Q:'$D(^ATXAX(PP,21,"B",Y))
 .Q:$P($G(^DIC(7,Y,9999999)),U,3)'="Y"
 .S Y=$$CLINIC^APCLV(V,"I")
 .Q:'Y
 .Q:'$D(^ATXAX(PC,21,"B",Y))
 .S G=G+1
 .Q
 Q $S(G<1:"",1:1)
 ;
V2(P,BDATE,EDATE) ;
 I '$D(^AUPNVSIT("AC",P)) Q ""
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G>2)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .Q:"V"[$P(^AUPNVSIT(V,0),U,3)
 .S G=G+1
 .Q
 Q $S(G<2:"",1:1)
 ;
V2DM(P,BDATE,EDATE) ;
 I '$G(P) Q ""
 I '$D(^AUPNVSIT("AC",P)) Q ""
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S T=$O(^ATXAX("B","SURVEILLANCE DIABETES",0))
 I 'T Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G>2)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .Q:"V"[$P(^AUPNVSIT(V,0),U,3)
 .S (D,Y)=0 F  S Y=$O(^AUPNVPOV("AD",V,Y)) Q:Y'=+Y!(D)  I $D(^AUPNVPOV(Y,0)) S %=$P(^AUPNVPOV(Y,0),U) I $$ICD^ATXCHK(%,T,9) S D=1
 .Q:'D
 .S G=G+1
 .Q
 Q $S(G<2:"",1:1)
 ;
DM(P,BDATE,EDATE) ;EP is patient diabetic 1 or 0
 I $G(BDATE)="" S BDATE=$$DOB^AUPNPAT(P)
 K BGPG
 S Y="BGPG("
 S X=P_"^LAST DX [SURVEILLANCE DIABETES;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BGPG(1)) Q 1  ;has a dx
 Q 0
 ;
ACTUP(P,BDATE,EDATE,T) ;EP - is this patient in user pop?
 I $$BEN^AUPNPAT(P,"C")'="01" Q 0  ;must be Indian/Alaskan Native
 S DOD=$$DOD^AUPNPAT(P) I DOD]"",DOD<BGPED Q 0
 S X=$P($G(^AUPNPAT(DFN,11)),U,18) I X="" Q 0
 I '$D(^ATXAX(T,21,"B",($P(^AUPNPAT(P,11),U,18)))),'$D(^ATXAX(T,21,"AA",$P(^AUPNPAT(P,11),U,18),$P(^AUPNPAT(P,11),U,18))) Q 0
 S X=$$LASTVD(P,BDATE,EDATE)
 Q $S(X:1,1:0)
 ;
ACTCL(P,BDATE,EDATE) ;EP - clinical user
 S (X,G,F,S)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(F)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:'$D(^AUPNVPRV("AD",V))
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .Q:"V"[$P(^AUPNVSIT(V,0),U,3)
 .S B=$$CLINIC^APCLV(V,"C")
 .Q:B=""
 .I 'G,$D(^BGPCTRL(1,11,"B",B)) S G=V  ;must be a primary clinic S G=V
 .I V'=G,$D(^BGPCTRL(1,12,"B",B)) S S=1
 .I G,S S F=1
 .Q
 Q $S(F:1,1:0)
 ;
FIRSTDM(P,EDATE) ;
 I $G(P)="" Q ""
 K BGPG
 S Y="BGPG("
 S X=P_"^FIRST DX [SURVEILLANCE DIABETES" S E=$$START1^APCLDF(X,Y)
 I '$D(BGPG(1)) Q ""
 S X=$$FMDIFF^XLFDT(EDATE,$P(BGPG(1),U))
 Q $S(X>365:1,1:"")
 ;
LASTVD(P,BDATE,EDATE) ;
 I '$D(^AUPNVSIT("AC",P)) Q ""
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:'$D(^AUPNVPRV("AD",V))
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .Q:"V"[$P(^AUPNVSIT(V,0),U,3)
 .S G=1
 .Q
 Q G
 ;
