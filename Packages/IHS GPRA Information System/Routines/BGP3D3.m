BGP3D3 ; IHS/CMI/LAB - indicator 3 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
I7 ;EP
 K BGPN1,BGPN2,BGPN3,BGPN4,BGPVALUE,BGPPAP,BGPI7,BGPI7DA,BGPI7DB
 S BGPI7DA=0,BGPI7DB=0,BGPN1=0,BGPN2=0
 S BGPI7=$$DEN7(DFN,BGPAGEB,BGPAGEE,BGPSEX,BGPEDATE)
 I BGPACTUP,BGPI7 S BGPI7DA=1
 I BGPACTCL,BGPI7 S BGPI7DB=1
 I 'BGPI7DA,'BGPI7DB S BGPSTOP=1 Q  ;not in either denom so quit
 S BGPPAP=$$PAP(DFN,BGPEDATE,3)
 S BGPN1=0 I $P(BGPPAP,U)=1 S BGPN1=1
 I $P(BGPPAP,U,3)="ref" S BGPN2=1
 S BGPVALUE=$S(BGPI7DA:1,1:"")_$S(BGPI7DB:",2",1:"")_"; "_$$DATE^BGP3UTL($P(BGPPAP,U,2))_" "_$P(BGPPAP,U,3)
 Q
 ;
I8 ;EP
 K BGPN1,BGPN2,BGPN3,BGPN4,BGPVALUE,BGPMAM,BGPI7,BGPI8DA,BGPI8DB
 S BGPI8DA=0,BGPI8DB=0,BGPN1=0,BGPN2=0
 S BGPI8=$$DEN8(DFN,BGPAGEB,BGPAGEE,BGPSEX,BGPEDATE)
 I BGPACTUP,BGPI8 S BGPI8DA=1
 I BGPACTCL,BGPI8 S BGPI8DB=1
 I 'BGPI8DA,'BGPI8DB S BGPSTOP=1 Q  ;not in either denom so quit
 S BGPMAM=$$MAM(DFN,BGPEDATE,2)
 S BGPN1=0 I $P(BGPMAM,U)=1 S BGPN1=1
 I $P(BGPMAM,U,3)="ref" S BGPN2=1
 S BGPVALUE=$S(BGPI8DA:1,1:"")_$S(BGPI8DB:",2",1:"")_"; "_$$DATE^BGP3UTL($P(BGPMAM,U,2))_" "_$P(BGPMAM,U,3)
 Q
 ;
I13 ;EP
 K BGPN1,BGPN2,BGPN3,BGPN4,BGPVALUE,BGPI13D
 S BGPI13D=0,BGPN1=0
 S BGPI13D=BGPACTUP
 S BGPVALUE=$$DENTSRV(DFN,BGPEDATE)
 S BGPN1=0 I $P(BGPVALUE,U)=1 S BGPN1=1
 S BGPVALUE=$$DATE^BGP3UTL($P(BGPVALUE,U,2))_";"_$P(BGPVALUE,U,3)
 Q
 ;
I15 ;EP
 K BGPN1,BGPVALUE,BGPN2,BGPN3,BGPN4,BGPN5
 S BGPN1=0
 I '$G(BGPDMD3) S BGPSTOP=1 Q  ;not in denominator so don't bother
 S BGPVALUE=$$DENTSRV(DFN,BGPEDATE)
 S BGPN1=0 I $P(BGPVALUE,U)=1 S BGPN1=1
 I BGPVALUE]"" S BGPVALUE=$$DATE^BGP3UTL($P(BGPVALUE,U,2))_";"_$P(BGPVALUE,U,3)
 Q
I23 ;EP - PHN
 K BGPN1,BGPN2,BPGN3,BGPN4,BGPVALUE
 S (BGPN1,BGPN2)=0
 S BGPVALUE=$$PHNV(DFN,BGP365,BGPEDATE,BGPHOME)
 S BGPN1=BGPVALUE
 S BGPVALUE=$P(BGPVALUE,U)_" all PHN; "_$P(BGPVALUE,U,2)_" home"
 Q
DEN8(P,AGEB,AGEE,SEX,EDATE) ;is women in ind 8
 I SEX'="F" Q 0
 I AGEB<52 Q 0
 I AGEE>69 Q 0
 I $$MAS(P,EDATE) Q 0
 Q 1
DEN7(P,AGEB,AGEE,SEX,EDATE) ;
 I SEX'="F" Q 0
 I AGEB<21 Q 0
 I AGEE>64 Q 0
 I $$HYSTER(P,EDATE) Q 0
 Q 1
MAM(P,EDATE,YEARS) ;
 K BGP S BDATE=$$FMADD^XLFDT(EDATE,-(365*YEARS))
 S %=P_"^LAST RAD 76091;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^76091"
 K BGP S %=P_"^LAST RAD 76092;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^76092"
 K BGP S %=P_"^LAST RAD 76090;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^76090"
 K BGP S %=P_"^LAST DX V76.11;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^V76.11"
 K BGP S %=P_"^LAST DX V76.12;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^V76.12"
 K BGP S %=P_"^LAST PROCEDURE 87.37;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^87.37"
 K BGP S %=P_"^LAST PROCEDURE 87.36;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^87.36"
 K BGP S %=P_"^LAST PROCEDURE 87.35;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^87.35"
 S T=$O(^ATXAX("B","BGP CPT MAMMOGRAM",0))
 I T D  I X]"" Q "1^"_X_"^"_"CPT"
 .S X=$$CPT^BGPDU(P,$$FMADD^XLFDT(EDATE,-(2*365)),EDATE,T,3)
 S T="MAMMOGRAM SCREENING",T=$O(^BWPN("B",T,0))
 I T D  I X]"" Q "1^"_X_"^WH"
 .S X=$$WH^BGPDU(P,$$FMADD^XLFDT(EDATE,-(2*365)),EDATE,T,3)
 S T="MAMMOGRAM DX BILAT",T=$O(^BWPN("B",T,0))
 I T D  I X]"" Q "1^"_X_"^WH"
 .S X=$$WH^BGPDU(P,$$FMADD^XLFDT(EDATE,-(2*365)),EDATE,T,3)
 S T="MAMMOGRAM DX UNILAT",T=$O(^BWPN("B",T,0))
 I T D  I X]"" Q "1^"_X_"^WH"
 .S X=$$WH^BGPDU(P,$$FMADD^XLFDT(EDATE,-(2*365)),EDATE,T,3)
 S T=$$REFUSAL^BGP3UTL1(P,71,$O(^RAMIS(71,"D",76091,0)),$$FMADD^XLFDT(EDATE,-(2*365)),EDATE)
 I T Q "1^"_$P(T,U,2)_"^ref"
 S T=$$REFUSAL^BGP3UTL1(P,71,$O(^RAMIS(71,"D",76092,0)),$$FMADD^XLFDT(EDATE,-(2*365)),EDATE)
 I T Q "1^"_$P(T,U,2)_"^ref"
 S T=$$REFUSAL^BGP3UTL1(P,71,$O(^RAMIS(71,"D",76090,0)),$$FMADD^XLFDT(EDATE,-(2*365)),EDATE)
 I T Q "1^"_$P(T,U,2)_"^ref"
 Q 0
PHNV(P,BDATE,EDATE,HOMELOC) ;
 S HOMELOC=$G(HOMELOC)
 K ^TMP($J,"A") S A="^TMP($J,""A"","
 S B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q "0^0^0^0^0^0^0^0^0^0"
 S (X,Y)=0,C="0^0^0^0^0^0^0^0^0^0" F  S X=$O(^TMP($J,"A",X)) Q:X'=+X  S V=$P(^TMP($J,"A",X),U,5) D  I Y S $P(C,U)=$P(C,U)+1 D HOME,AGE
 .S (D,Y)=0
 .F  S D=$O(^AUPNVPRV("AD",V,D)) Q:D'=+D  S Q=$P(^AUPNVPRV(D,0),U),%=$$VALI^XBDIQ1($S($P(^AUTTSITE(1,0),U,22):200,1:6),Q,$S($P(^AUTTSITE(1,0),U,22):53.5,1:2)) I % S %=$P($G(^DIC(7,+%,9999999)),U) I %=13!(%=32) S Y=1
 Q C
 ;
HOME ;
 S HV=0
 I $$CLINIC^APCLV(V,"C")=11 S $P(C,U,2)=$P(C,U,2)+1 S HV=1 Q
 Q:HOMELOC=""
 I HOMELOC=$P(^AUPNVSIT(V,0),U,6) S $P(C,U,2)=$P(C,U,2)+1 S HV=1
 Q
AGE ;
 S DAYS=$$FMDIFF^XLFDT($P($P(^AUPNVSIT(V,0),U),"."),$P(^DPT(P,0),U,3))
 S YRS=$$AGE^AUPNPAT(P,$P($P(^AUPNVSIT(V,0),U),"."))
 I DAYS<29 S $P(C,U,3)=$P(C,U,3)+1 S:HV=1 $P(C,U,4)=$P(C,U,4)+1 Q
 I DAYS>28,YRS<1 S $P(C,U,5)=$P(C,U,5)+1 S:HV=1 $P(C,U,6)=$P(C,U,6)+1 Q
 I YRS>0,YRS<65 S $P(C,U,7)=$P(C,U,7)+1 S:HV=1 $P(C,U,8)=$P(C,U,8)+1 Q
 I YRS>64 S $P(C,U,9)=$P(C,U,9)+1 S:HV=1 $P(C,U,10)=$P(C,U,10)+1 Q
 W BGPBOMB
 Q
DENTSRV(P,EDATE) ;
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,-365)
 S V="",%=P_"^LAST ADA 0000;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) S V=$P(BGPG(1),U)_"^"_$P(BGPG(1),U,2)
 K BGPG
 S %=P_"^LAST ADA 0190;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)),$P(BGPG(1),U)>$P(V,U) S V=$P(BGPG(1),U)_"^"_$P(BGPG(1),U,2)
 K BGPG S %=P_"^LAST EXAM DENTAL;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)),$P(BGPG(1),U)>$P(V,U) S V=$P(BGPG(1),U)_"^"_$P(BGPG(1),U,3)
 I V]"" Q "1^"_V
 Q ""
PAP(P,EDATE,YEARS) ;
 K BGP S BDATE=$$FMADD^XLFDT(EDATE,-(365*YEARS)),%=P_"^LAST LAB PAP SMEAR;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^VLab"
 K BGP S %=P_"^LAST DX V76.2;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^V76.2"
 K BGP S %=P_"^LAST DX V72.3;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^V72.3"
 K BGP S %=P_"^LAST PROCEDURE 91.46;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q "1^"_$P(BGP(1),U)_"^91.46"
 S T=$O(^ATXAX("B","BGP CPT PAP",0))
 I T D  I X]"" Q "1^"_X_"^"_"CPT"
 .S X=$$CPT^BGPDU(P,$$FMADD^XLFDT(EDATE,-(3*365)),EDATE,T,3)
 S T="PAP SMEAR",T=$O(^BWPN("B",T,0))
 I T D  I X]"" Q "1^"_X_"^WH"
 .S X=$$WH^BGPDU(P,$$FMADD^XLFDT(EDATE,-(3*365)),BGPEDATE,T,3)
 S T=$$REFUSAL^BGP3UTL1(P,60,$O(^LAB(60,"B","PAP SMEAR",0)),$$FMADD^XLFDT(EDATE,-(3*365)),EDATE)
 I T Q "1^"_$P(T,U,2)_"^ref"
 Q 0
MAS(P,EDATE) ;mastectomy before end of time frame
 K BGP S %=P_"^LAST PROCEDURE 85.42;DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q 1
 K BGP S %=P_"^LAST PROCEDURE 85.44;DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q 1
 K BGP S %=P_"^LAST PROCEDURE 85.46;DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q 1
 K BGP S %=P_"^LAST PROCEDURE 85.48;DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGP(")
 I $D(BGP(1)) Q 1
 Q 0
HYSTER(P,EDATE) ;EP has patient had hysterectomy?
 I '$G(P) Q ""
 S (F,S)=0 F  S F=$O(^AUPNVPRC("AC",P,F)) Q:F'=+F!(S)  S C=$P($$ICDOP^ICDCODE(+^AUPNVPRC(F,0)),U,2) D
 .S G=0 S:C=68.3!(C=68.4)!(C=68.5)!(C=68.6)!(C=68.7)!(C=68.9) G=1
 .Q:G=0
 .S D=$P(^AUPNVPRC(F,0),U,6) I D="" S D=$P($P(^AUPNVSIT($P(^AUPNVPRC(F,0),U,3),0),U),".")
 .I D>EDATE Q
 .S S=1
 I S=1 Q 1
 ;now check wh procedure file and history file
 S T="HYSTERECTOMY",T=$O(^BWPN("B",T,0))
 I T D  I X]"" Q 1
 .S X=$$WH^BGPDU(P,$$DOB^AUPNPAT(P),EDATE,T,2)
 Q ""
DNKA(V) ;EP - is this a DNKA visit?
 I '$G(V) Q ""
 NEW D,N S D=$$PRIMPOV^APCLV(V,"C")
 I D=".0860" Q 1
 S N=$$PRIMPOV^APCLV(V,"N")
 I $E(D)="V",N["DNKA" Q 1
 I $E(D)="V",N["DID NOT KEEP APPOINTMENT" Q 1
 I $E(D)="V",N["DID NOT KEEP APPT" Q 1
 Q 0
