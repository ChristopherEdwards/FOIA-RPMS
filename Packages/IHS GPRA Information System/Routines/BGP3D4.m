BGP3D4 ; IHS/CMI/LAB - indicator 3 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;IHS/CMI/LAB - patch 1 7-7-03 added HDL and Triglyceride check to 30-1
 ;  - fixed IMM refusal check for null reason
I14 ;EP
 K BGPN1,BGPN2,BGPN3,BGPN4
 S (BGPN1,BGPN2,BGPN3,BGPN4)=0
 S BGPN1=$$SEAL(DFN,BGP365,BGPEDATE)
 I BGPAGEB<12 S BGPN2=BGPN1
 I BGPAGEB>11,BGPAGEB<19 S BGPN3=BGPN1
 I BGPAGEB>18 S BGPN4=BGPN1
 S BGPVALUE=BGPN1_" sealants"
 Q
I25 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7)=0
 I BGPAGEB>49,BGPACTUP S BGPD1=1
 I BGPAGEB>49,BGPAGEB<65,BGPACTUP S BGPD2=1
 I BGPAGEB>64,BGPACTUP S BGPD3=1
 I BGPDMD3 S BGPD4=1
 I BGPAGEB>49,BGPACTCL S BGPD5=1
 I BGPAGEB>49,BGPAGEB<65,BGPACTCL S BGPD6=1
 I BGPAGEB>64,BGPACTCL S BGPD7=1
 I '(BGPD1+BGPD2+BGPD3+BGPD4+BGPD5+BGPD6+BGPD7) S BGPSTOP=1 Q
 S BGPVALUE=$$FLU(DFN,BGPEDATE) ;set to date of flu shot
 I BGPVALUE]"" S BGPN1=1
 S BGPDV=$S(BGPD1:"1,",1:"")_$S(BGPD5:"2,",1:"")_$S(BGPD4:"3",1:"")_"; "
 S BGPVALUE=BGPDV_BGPVALUE
 Q
I26 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPD1,BGPD2,BGPD3)=0
 I BGPAGEB>64,BGPACTUP S BGPD1=1
 I BGPDMD3 S BGPD3=1
 I BGPAGEB>64,BGPACTCL S BGPD2=1
 I '(BGPD1+BGPD2+BGPD3) S BGPSTOP=1 Q
 S BGPVALUE=$$PNEU(DFN,BGPEDATE) ;set to date of flu shot
 I BGPVALUE]"" S BGPN1=1
 S BGPDV=$S(BGPD1:"1,",1:"")_$S(BGPD2:"2,",1:"")_$S(BGPD3:"3",1:"")_"; "
 S BGPVALUE=BGPDV_BGPVALUE
 Q
I301 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPD1,BGPD2,BGPD3,BGPD4)=0
 I BGPAGEB<45 S BGPSTOP=1 Q  ;no need to process
 I BGPACTUP,'BGPDM1 S BGPD1=1
 I BGPACTCL,'BGPDM1 S BGPD2=1
 I BGPDMD3 S BGPD3=1
 S (BGPVALUE,BGPLP,BGPLDL,BGPHDL,BGPTRI)=""
 S BGPLP=$$LIPID(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 I BGPLP]"" S BGPN1=1
 S BGPLDL=$$LDL(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE) ;date^value
 I BGPN1'=1 D
 .S BGPTRI=$$TRIG(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 .S BGPHDL=$$HDL(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 .I BGPTRI,BGPHDL,BGPLDL S BGPN1=1
 I $P(BGPLDL,U,2)]"" D
 .S V=$P(BGPLDL,U,2)
 .I V]"",+V'>100 S BGPN2=1
 .I +V>100.99999,+V<131 S BGPN3=1
 .I +V>130.99999,+V<161 S BGPN4=1
 .I +V>160.9999 S BGPN5=1
 S BGPDV=$S(BGPD1:"1,",1:"")_$S(BGPD2:"2,",1:"")_$S(BGPD3:"3",1:"")_"; "
 S BGPVALUE=BGPDV_$S(BGPN1:"LP; ",1:"")
 S %=$S($P(BGPLDL,U,3):$$DATE^BGP3UTL($P(BGPLDL,U,3))_" "_$S($P(BGPLDL,U,2)]"":$P(BGPLDL,U,2),1:"no result"),1:"")
 S BGPVALUE=BGPVALUE_%
 Q
I302 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPD1,BGPD2,BGPD3,BGPD4)=0
 I BGPAGEB<45 S BGPSTOP=1 Q  ;no need to process
 I BGPACTUP,'BGPDM1 S BGPD1=1
 I BGPACTCL,'BGPDM1 S BGPD2=1
 I BGPDMD3 S BGPD3=1
 S BGPVALUE=$$MEANBP(DFN,BGP365,BGPEDATE)
 S BGPN1=$S($P(BGPVALUE,U,2)=1:1,1:0)
 S BGPN2=$S($P(BGPVALUE,U,2)=2:1,1:0)
 S BGPN3=$S($P(BGPVALUE,U,2)=3:1,1:0)
 S BGPN4=$S($P(BGPVALUE,U,2)=4:1,1:0)
 S BGPN5=$S($P(BGPVALUE,U,2)=5:1,1:0)
 S BGPVALUE=$S(BGPD1:"1,",1:"")_$S(BGPD2:"2,",1:"")_$S(BGPD3:"3",1:"")_"; "_$P(BGPVALUE,U)
 Q
MEANBP(P,BDATE,EDATE) ;
 S X=$$BPS(P,BDATE,EDATE,"I")
 S S=$$SYSMEAN(X) I S="" Q "u^5"
 S DS=$$DIAMEAN(X) I DS="" Q "u^5"
 I S>159!(DS>100) Q S_"/"_DS_" SUNC"_U_4
 I S>139&(S<160)!(DS>90&(DS<101)) Q S_"/"_DS_" UNC"_U_3
 I S>130&(S<140)!(DS>80&(DS<91)) Q S_"/"_DS_" CON"_U_2
 I S<131&(DS<81) Q S_"/"_DS_" OPT"_U_1
 Q ""
 ;
SYSMEAN(X) ;EP
 I X="" Q ""
 S C=0 F Y=1:1:2 I $P(X,";",Y)]"" S C=C+1
 I C'=2 Q ""
 S C=0 F Y=1:1:2 S C=$P($P(X,";",Y),"/")+C
 Q C\2
 ;
DIAMEAN(X) ;EP
 I X="" Q ""
 S C=0 F Y=1:1:2 I $P(X,";",Y)]"" S C=C+1
 I C'=2 Q ""
 S C=0 F Y=1:1:2 S C=$P($P(X,";",Y),"/",2)+C
 Q C\2
 ;
BPS(P,BDATE,EDATE,F) ;EP ;
 I $G(F)="" S F="E"
 S BGPGLL=0,BGPGV=""
 K BGPG
 S X=P_"^LAST 30 MEAS BP;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,"BGPG(")
 S BGPGL=0 F  S BGPGL=$O(BGPG(BGPGL)) Q:BGPGL'=+BGPGL!(BGPGLL=2)  S BGPGBP=$P($G(BGPG(BGPGL)),U,2) D
 .Q:$$CLINIC^APCLV($P(BGPG(BGPGL),U,5),"C")=30
 .S BGPGLL=BGPGLL+1
 .I F="E" S $P(BGPGV,";",BGPGLL)=BGPGBP_"  "_$$FMTE^XLFDT($P(BGPG(BGPGL),U))
 .I F="I" S $P(BGPGV,";",BGPGLL)=$P(BGPGBP," ")
 Q BGPGV
LIPID(P,BDATE,EDATE) ;
 K BGPG
 S %=P_"^LAST LAB [DM AUDIT LIPID PROFILE TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q 1
 S %="",E=+$$CODEN^ICPTCOD(80061),%=$$CPTI^BGPDU(P,BDATE,EDATE,E)
 Q %
 ;
TRIG(P,BDATE,EDATE) ;
 K BGPG
 S %=P_"^LAST LAB [DM AUDIT TRIGLYCERIDE TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q 1
 S E=+$$CODEN^ICPTCOD(84478),%=$$CPTI^BGPDU(P,BDATE,EDATE,E)
 Q %
HDL(P,BDATE,EDATE) ;
 K BGPG
 S %=P_"^LAST LAB [DM AUDIT HDL TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q 1
 S %="",E=+$$CODEN^ICPTCOD(83718),%=$$CPTI^BGPDU(P,BDATE,EDATE,E)
 Q %
 ;
LDL(P,BDATE,EDATE) ;
 K BGPZ
 K BGPG
 S %=P_"^ALL LAB [DM AUDIT LDL CHOLESTEROL TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I '$D(BGPG(1)) G LDLCPT
 ;reorder by date
 NEW X S X=0 F  S X=$O(BGPG(X)) Q:X'=+X  S BGPZ(9999999-$P(BGPG(X),U),X)=BGPG(X)
 S %="" D
 .S (D,X,G)=0 F  S D=$O(BGPZ(D)) Q:D'=+D!(G)  S X=0 F  S X=$O(BGPZ(D,X)) Q:X'=+X!(G)  D
 ..S E=$P(^AUPNVLAB(+$P(BGPZ(D,X),U,4),0),U,4)
 ..I +E S %="1^"_E_"^"_$P(BGPZ(D,X),U) S G=1 Q
 I %]"" Q %
 S D=$O(BGPZ("")),X=$O(BGPZ(D,0)) Q 1_"^^"_$P(BGPZ(D,X),U)
LDLCPT ;
 S E=+$$CODEN^ICPTCOD(83718),%=$$CPTI^BGPDU(P,BDATE,EDATE,E)
 Q $P(%,U)_"^^"_$P(%,U,2)
PNEU(P,EDATE) ;
 K BGPG
 S ED=EDATE
 S EDATE=$$FMTE^XLFDT(EDATE)
 S BDATE=$$FMTE^XLFDT($$DOB^AUPNPAT(P))
 S X=P_"^LAST IMM "_$S($$BI:33,1:19)_";DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_EDATE S E=$$START1^APCLDF(X,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" Imm 33"
 S X=P_"^LAST IMM 100;DURING "_$$FMTE^XLFDT($$DOB^AUPNPAT(P))_"-"_EDATE S E=$$START1^APCLDF(X,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" Imm 100"
 K BGPG S %=P_"^LAST PROCEDURE 99.55;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" 99.55"
 K BGPG S %=P_"^LAST DX V03.82;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" V03.82"
 K BGPG S %=P_"^LAST DX V03.89;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" V03.89"
 K BGPG S %=P_"^LAST DX V06.6;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" V06.6"
 S %="",E=+$$CODEN^ICPTCOD(90732),%=$$CPTI^BGPDU(P,$$FMADD^XLFDT(ED,-365),ED,E) I %]"" Q $$DATE^BGP3UTL($P(%,U,2))_" 90732"
 S G=$$REFUSAL^BGP3UTL1(P,9999999.14,$O(^AUTTIMM("C",33,0)),$$FMTE^XLFDT($$FMADD^XLFDT(EDATE,-365)),EDATE)
 I $P(G,U)=1 Q $$DATE^BGP3UTL($P(G,U,2))_" Refused"
 S G=$$REFUSAL^BGP3UTL1(P,9999999.14,$O(^AUTTIMM("C",100,0)),$$FMTE^XLFDT($$FMADD^XLFDT(EDATE,-365)),EDATE)
 I $P(G,U)=1 Q $$DATE^BGP3UTL($P(G,U,2))_" Refused"
 S (X,G)=0,Y=$O(^AUTTIMM("C",33,0)) F  S X=$O(^BIPC("AC",P,Y,X)) Q:X'=+X!(G)  D
 .S R=$P(^BIPC(X,0),U,3)
 .Q:R=""
 .Q:'$D(^BICONT(R,0))
 .Q:$P(^BICONT(R,0),U,1)'["Refusal"
 .S D=$P(^BIPC(X,0),U,4)
 .Q:D=""
 .;Q:$P(^BIPC(X,0),U,4)<$P(^DPT(P,0),U,3)
 .Q:$P(^BIPC(X,0),U,4)>ED
 .S G=1
 I G Q $$DATE^BGP3UTL($P(G,U,2))_" Refusal Imm pkg"
 Q ""
FLU(P,ED) ;
 K BGPG
 S BD=$$FMADD^XLFDT(ED,-365)
 S EDATE=$$FMTE^XLFDT(ED),BDATE=$$FMTE^XLFDT(BD)
 S X=P_"^LAST IMM "_$S($$BI:88,1:12)_";DURING "_BDATE_"-"_EDATE S E=$$START1^APCLDF(X,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" Imm 88"
 K BGPG S %=P_"^LAST PROCEDURE 99.52;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" 99.52"
 K BGPG S %=P_"^LAST DX V04.8;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" V04.8"
 K BGPG S %=P_"^LAST DX V06.6;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $$DATE^BGP3UTL($P(BGPG(1),U))_" V06.6"
 S T=$O(^ATXAX("B","BGP CPT FLU",0))
 I T D  I X]"" Q $$DATE^BGP3UTL(X)_" CPT"
 .S X=$$CPT^BGPDU(P,,ED,T,3)
 S G=$$REFUSAL^BGP3UTL1(P,9999999.14,$O(^AUTTIMM("C",88,0)),$$FMADD^XLFDT(ED,-365),ED)
 I $P(G,U)=1 Q $$DATE^BGP3UTL($P(G,U,2))_" Refused"
 S (X,G)=0,Y=$O(^AUTTIMM("C",88,0)) F  S X=$O(^BIPC("AC",P,Y,X)) Q:X'=+X!(G)  D
 .S R=$P(^BIPC(X,0),U,3)
 .Q:R=""
 .Q:'$D(^BICONT(R,0))
 .Q:$P(^BICONT(R,0),U,1)'["Refusal"
 .S D=$P(^BIPC(X,0),U,4)
 .Q:D=""
 .Q:$P(^BIPC(X,0),U,4)<BD
 .Q:$P(^BIPC(X,0),U,4)>ED
 .S G=1
 I G Q $$DATE^BGP3UTL($P(G,U,2))_" Refused Imm pkg"
 Q ""
BI() ;
 Q $S($O(^AUTTIMM(0))>100:1,1:0)
 ;
SEAL(P,BDATE,EDATE) ;
 K BGPG S BGPC=0
 ;S %=P_"^ALL ADA IH73;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 ;S X=0 F  S X=$O(BGPG(X)) Q:X'=+X  S V=+$P(BGPG(X),U,4),T=$P($G(^AUPNVDEN(V,0)),U,4) S:T=""!(T=0) T=1 S BGPC=BGPC+T
 ;K BGPG
 S %=P_"^ALL ADA 1351;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 S X=0 F  S X=$O(BGPG(X)) Q:X'=+X  S V=+$P(BGPG(X),U,4),T=$P($G(^AUPNVDEN(V,0)),U,4) S:T=""!(T=0) T=1 S BGPC=BGPC+T
 Q BGPC
 ;
