BGP3D6 ; IHS/CMI/LAB - indicator 31 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
IB ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9)=0
 I BGPAGEB<51 S BGPSTOP=1 Q
 S BGPD1=1
 S BGPVALUE=$$IBH(DFN,BGPEDATE)
 I BGPVALUE]"" S BGPN1=1
 I $P(BGPVALUE,U)["FOB" S BGPN2=1
 I $P(BGPVALUE,U)["DRE" S BGPN2=1
 I $P(BGPVALUE,U)["RECT" S BGPN2=1
 S BGPVALUE=$P(BGPVALUE,U,2)_" "_$P(BGPVALUE,U)
 S BGPDV=$S(BGPD1:"1,",1:"")_$S(BGPACTCL:"2",1:"")
 S BGPVALUE=BGPDV_";"_BGPVALUE
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P
 Q
IH ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9)=0
 I BGPAGEB<5 S BGPSTOP=1 Q
 I BGPACTUP S BGPD1=1
 I BGPAGEB>4,BGPAGEB<14 S BGPD2=1
 I BGPAGEB>13,BGPAGEB<18 S BGPD3=1
 I BGPAGEB>17,BGPAGEB<25 S BGPD4=1
 I BGPAGEB>24,BGPAGEB<45 S BGPD5=1
 I BGPAGEB>44,BGPAGEB<65 S BGPD6=1
 I BGPAGEB>64 S BGPD7=1
 ;I BGPSEX="F",BGPAGEB>17,BGPAGEB<50,$$PREG(DFN,BGP365,BGPEDATE) S BGPD8=1
 I BGPSEX="F",$$PREG(DFN,BGP365,BGPEDATE) S BGPD8=1 ;no age limit on pregnant women v2.1
 I '(BGPD1+BGPD2+BGPD3+BGPD4+BGPD5+BGPD6+BGPD7+BGPD8) Q
 S BGPVALUE=$$TOBACCO(DFN,BGP365,BGPEDATE),BGPN1=$S(BGPVALUE]"":1,1:0)
 S BGPSDX=$$DX(DFN,BGP365,BGPEDATE)
 I BGPSDX S BGPN1=1
 S F=$P(BGPVALUE,U)
 I F["CURRENT"!(BGPSDX]"") S BGPN2=1
 I F["CURRENT SMOKER"!(BGPSDX]"") S BGPN3=1
 I F="CURRENT SMOKELESS"!(F="CURRENT SMOKER & SMOKELESS") S BGPN4=1
 I BGPN2,$$PED(DFN,BGP365,BGPEDATE) S BGPN5=1
 I F="SMOKER IN HOME"!(F["ENVIRON") S BGPN6=1
 S V=$S(BGPD1:1)_$S(BGPACTCL:",2",1:"")_$S(BGPD8:",3",1:"")
 S V=V_"; "_$P(BGPVALUE,U,2)
 S S=$S(F="CURRENT SMOKER":"Cur Smk",F="CURRENT SMOKELESS":"Cur smkl",F["NON":"NTU",F="CURRENT SMOKER & SMOKELESS":"Cur smk/smkl",F["SMOKER IN HOME":"Smk Home",F["ENVIRON":"ETS",1:"")
 I S="" S S=$S(F="PREVIOUS SMOKER":"Prev smk",F="PREVIOUS SMOKELESS":"Prev smkl",F["PREVIOUS":"Prev smk/smkl",F="SMOKE FREE HOME":"Smk free home",1:"")
 S BGPVALUE=V_" "_S
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F
 Q
IBH(P,EDATE) ;
 NEW VALUE
 S VALUE=""
 S VALUE=$$FOB(P,EDATE)
 I VALUE]"" Q VALUE
 S VALUE=$$DRE(P,EDATE)
 I VALUE]"" Q VALUE
 S VALUE=$$RECT(P,EDATE)
 I VALUE]"" Q VALUE
 S VALUE=$$SIG(P,EDATE)
 I VALUE]"" Q VALUE
 S VALUE=$$BE(P,EDATE)
 I VALUE]"" Q VALUE
 S VALUE=$$COLO(P,EDATE)
 I VALUE]"" Q VALUE
 ;refusal
 S G=$$REFUSAL^BGP3UTL1(P,9999999.15,$O(^AUTTEXAM("B","RECTAL EXAM",0)),$$FMADD^XLFDT(EDATE,-365),EDATE)
 I $P(G,U)=1 Q "REFUSED"_$P(G,U,2)
 Q ""
DRE(P,EDATE) ;
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,2*(-365))
 K BGPG S %=P_"^LAST PROCEDURE 89.34;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "DRE 89.34"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST DIAGNOSIS V76.51;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "DRE DX V76.51"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST DIAGNOSIS V76.41;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "DRE DX V76.41"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 Q ""
RECT(P,EDATE) ;
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,2*(-365))
 S %=P_"^LAST EXAM RECTAL;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "RECTAL EXAM"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 Q ""
SIG(P,EDATE) ;
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,5*(-365))
 K BGPG S %=P_"^LAST PROCEDURE 45.24;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "SIG 45.24"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S R="" F %=1:1 S T=$T(SIGCPTS+%^BGPDU) Q:$P(T,";;",2)=""!(R)  S T=$P(T,";;",2),T=+$$CODEN^ICPTCOD(T) I T S E=$$CPTI^BGPDU(P,BDATE,EDATE,T) I E S R=1 S V="SIG CPT "_T_"^"_$$DATE^BGP3UTL($P(E,U,2))
 I R Q V
 K BGPG S %=P_"^LAST PROCEDURE 48.23;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "SIG 48.23"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 Q ""
COLO(P,EDATE) ;
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,10*(-365))
 K BGPG S %=P_"^LAST PROCEDURE 45.21;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "COLO 45.21"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST PROCEDURE 45.22;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "COLO 45.22"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST PROCEDURE 45.23;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "COLO 45.23"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST PROCEDURE 45.25;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "COLO 45.25"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S R="" F %=1:1 S T=$T(COLOCPTS+%^BGPDU) Q:$P(T,";;",2)=""!(R)  S T=$P(T,";;",2),T=+$$CODEN^ICPTCOD(T) I T S E=$$CPTI^BGPDU(P,BDATE,EDATE,T) I E S R=1,V="COLO CPT "_T_"^"_$$DATE^BGP3UTL($P(E,U,2))
 I R Q V
 Q ""
FOB(P,EDATE) ;
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,2*(-365))
 S %=P_"^LAST LAB [BGP GPRA FOB TESTS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "FOB V LAB"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 S E=+$$CODEN^ICPTCOD(82270) I E S %=$$CPTI^BGPDU(P,BDATE,EDATE,E) I $P(%,U)=1 Q "FOB CPT 82270"_"^"_$$DATE^BGP3UTL($P(%,U,2))
 S E=+$$CODEN^ICPTCOD(82274) I E S %=$$CPTI^BGPDU(P,BDATE,EDATE,E) I $P(%,U)=1 Q "FOB CPT 82274"_"^"_$$DATE^BGP3UTL($P(%,U,2))
 S E=+$$CODEN^ICPTCOD(89205) I E S %=$$CPTI^BGPDU(P,BDATE,EDATE,E) I $P(%,U)=1 Q "FOB CPT 89205"_"^"_$$DATE^BGP3UTL($P(%,U,2))
 S E=+$$CODEN^ICPTCOD("G0107") I E S %=$$CPTI^BGPDU(P,BDATE,EDATE,E) I $P(%,U)=1 Q "FOB CPT G0107"_"^"_$$DATE^BGP3UTL($P(%,U,2))
 Q ""
BE(P,EDATE) ;EP
 K BGPG
 S BDATE=$$FMADD^XLFDT(EDATE,5*(-365))
 K BGPG S %=P_"^LAST PROCEDURE 87.64;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "BE 87.64"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S R="" F %=1:1 S T=$T(BECPTS+%^BGPDU) Q:$P(T,";;",2)=""!(R)  S T=$P(T,";;",2),T=+$$CODEN^ICPTCOD(T) I T S E=$$CPTI^BGPDU(P,BDATE,EDATE,T) I E S R=1 S V="BE CPT "_T_"^"_$$DATE^BGP3UTL($P(E,U,2))
 I R Q V
 K BGPG S %=P_"^LAST RAD 74280;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "RAD 74280"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST RAD 74270;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "RAD 74270"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 K BGPG S %=P_"^LAST RAD 74275;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "RAD 74275"_"^"_$$DATE^BGP3UTL($P(BGPG(1),U))
 Q ""
 ;
PED(P,BDATE,EDATE) ;EP
 K BGPG
 S Y="BGPG("
 S X=P_"^FIRST EDUC TO-QT;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BGPG(1)) Q 1  ;no first prenatal dx
 S X=P_"^FIRST EDUC TO-LA;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BGPG(1)) Q 1
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .S B=$$CLINIC^APCLV(V,"C")
 .I B=94 S G=G+1 Q
 .S Z=0 F  S Z=$O(^AUPNVDEN("AD",V,Z)) Q:Z'=+Z!(G)  S B=$P($G(^AUPNVDEN(Z,0)),U) I B S B=$P($G(^AUTTADA(B,0)),U) I B=1320 S G=G+1
 .Q
 Q $S(G:1,1:"")
PREG(P,BDATE,EDATE) ;
 K BGPG
 S Y="BGPG("
 ;S X=P_"^FIRST DX V22.0;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 ;I '$D(BGPG(1)) Q ""  ;no first prenatal dx
 ;K BGPG
 S X=P_"^LAST 2 DX [BGP GPRA PREGNANCY DIAGNOSES;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BGPG(2)) Q 1
 S T=$O(^ATXAX("B","BGP GPRA PREGNANCY DIAGNOSES",0))
 S (X,G)=0 F  S X=$O(^AUPNPROB("AC",P,X)) Q:X'=+X!(G)  D
 .Q:$P(^AUPNPROB(X,0),U,8)>EDATE
 .Q:$P(^AUPNPROB(X,0),U,8)<BDATE
 .S Y=$P(^AUPNPROB(X,0),U)
 .Q:'$$ICD^ATXCHK(Y,T,9)
 .S G=1
 .Q
 Q G
DX(P,BDATE,EDATE) ;
 K BGPG
 S X=P_"^LAST DX [BGP GPRA SMOKING DXS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,"BGPG(")
 I $D(BGPG(1)) Q $P(BGPG(1),U,2)
 Q ""
TOBACCO(P,BDATE,EDATE) ;EP
 K BGPTOB,BGP
 D TOBACCO1
 I BGPTOB]"" Q BGPTOB
 D TOBACCO0
 I $D(BGPTOB) Q BGPTOB
 Q ""
TOBACCO1 ;check for tobacco documented in health factors
 K BGPTOB S BGPTOB=$$LASTHF(P,"TOBACCO",BDATE,EDATE) K O,D,H
 Q
TOBACCO0 ;lookup in health status
 S (X,Y)=0 F  S X=$O(^AUPNHF("AA",P,X)) Q:X'=+X!(Y)  I $$VAL^XBDIQ1(9999999.64,X,.03)="TOBACCO" S Y=X
 Q:'Y
 S E=$O(^AUPNHF("AA",P,Y,0)) Q:'E
 I (9999999-E)>EDATE Q  ;documented after time frame
 I (9999999-E)<BDATE Q  ;documented before year
 S Y=$P(^AUTTHF(Y,0),U)
 S BGPTOB=Y_"^"_$$DATE^BGP3UTL(9999999-E)
 K Y,E,X
 Q
 ;
LASTHF(P,C,BDATE,EDATE) ;EP - get last factor in category C for patient P
 S C=$O(^AUTTHF("B",C,0)) ;ien of category passed
 I '$G(C) Q ""
 S (H,D)=0 K O
 F  S H=$O(^AUTTHF("AC",C,H))  Q:'+H  D
 .Q:'$D(^AUPNVHF("AA",P,H))
 .S D="" F  S D=$O(^AUPNVHF("AA",P,H,D)) Q:D'=+D  D
 ..Q:(9999999-D)>EDATE  ;after time frame
 ..Q:(9999999-D)<BDATE  ;before time frame
 ..S O(D)=$O(^AUPNVHF("AA",P,H,D,""))
 .Q
 S D=$O(O(0))
 I D="" Q D
 Q $$VAL^XBDIQ1(9000010.23,O(D),.01)_"^"_$$DATE^BGP3UTL(9999999-D)
 ;
