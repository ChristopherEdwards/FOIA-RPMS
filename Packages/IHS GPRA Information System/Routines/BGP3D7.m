BGP3D7 ; IHS/CMI/LAB - indicator C ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
IC1 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9)=0
 I BGPAGEB<6 S BGPSTOP=1 Q
 I BGPACTUP S BGPD1=1
 I BGPACTCL,BGPACTUP S BGPD2=1
 I BGPDMD3 S BGPD3=1
 I BGPACTCL,BGPAGEB>5,BGPAGEB<12 S BGPD4=1
 I BGPACTCL,BGPAGEB>11,BGPAGEB<20 S BGPD5=1
 I BGPACTCL,BGPAGEB>19,BGPAGEB<40 S BGPD6=1
 I BGPACTCL,BGPAGEB>39,BGPAGEB<60 S BGPD7=1
 I BGPACTCL,BGPAGEB>59 S BGPD8=1
 I '(BGPD1+BGPD2+BGPD3+BGPD4+BGPD5+BGPD6+BGPD7+BGPD8) Q
 S BGPVALUE=$$PED(DFN,BGP365,BGPEDATE)
 I $P(BGPVALUE,U,1)]"" S BGPN1=1
 I $P(BGPVALUE,U,3)]"" S BGPN2=1
 S V=$S(BGPD1:1)_$S(BGPD2:",2",1:"")_$S(BGPD3:",3",1:"")
 S V=V_"; "_$$DATE^BGP3UTL($P(BGPVALUE,U,1))_" "_$P(BGPVALUE,U,2)_" "_$$DATE^BGP3UTL($P(BGPVALUE,U,3))_" "_$P(BGPVALUE,U,4)
 S BGPVALUE=V
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,T
 Q
IC2 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9)=0
 I '$$MED(DFN,BGP365,BGPEDATE) S BGPSTOP=1 Q  ;no meds for this patient
 I BGPACTUP S BGPD1=1
 I BGPACTCL,BGPACTUP S BGPD2=1
 I '(BGPD1+BGPD2) Q
 S BGPVALUE=$$MEDPED(DFN,BGP365,BGPEDATE)
 I $P(BGPVALUE,U,1)]"" S BGPN1=1
 S V=$S(BGPD1:1)_$S(BGPD2:",2",1:"")
 S V=V_"; "_$$DATE^BGP3UTL($P(BGPVALUE,U,1))_" "_$P(BGPVALUE,U,2)
 S BGPVALUE=V
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,T
 Q
ID ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9)=0
 I BGPAGEB<23 S BGPSTOP=1 Q
 I BGPAGEB>65 S BGPSTOP=1 Q
 I BGPACTUP S BGPD1=1
 I BGPACTCL,BGPACTUP S BGPD2=1
 I '(BGPD1+BGPD2) Q
 S BGPVALUE=$$CHOL(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 I $P(BGPVALUE,U,1)]"" S BGPN1=1
 S V=$S(BGPD1:1)_$S(BGPD2:",2",1:"")
 S V=V_"; "_$$DATE^BGP3UTL($P(BGPVALUE,U,1))_" "_$P(BGPVALUE,U,2)
 S BGPVALUE=V
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,T
 Q
IE1 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9,BGPCD4,BGPPCR)=0
 I BGPAGEB<13 S BGPSTOP=1 Q
 I 'BGPACTCL S BGPSTOP=1 Q  ;not active clinical pt
 I '$$V2HIV(DFN,BGP365,BGPEDATE) S BGPSTOP=1 Q
 I BGPACTCL S BGPD1=1
 I 'BGPD1 Q
 S BGPCD4=$$CD4(DFN,BGP365,BGPEDATE)
 S BGPPCR=$$PCR(DFN,BGP365,BGPEDATE)
 I BGPCD4,'BGPPCR S BGPN1=1
 I BGPPCR,'BGPCD4 S BGPN2=1
 I BGPPCR,BGPCD4 S BGPN3=1
 I (BGPN1+BGPN2+BGPN3) S BGPN4=1
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,T
 Q
MEDPED(P,BDATE,EDATE) ;
 K BGPG
 S Y="BGPG("
 S X=P_"^ALL EDUC;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I '$D(BGPG(1)) Q ""
 S (X,P,D,E)=0,%="",T="" F  S X=$O(BGPG(X)) Q:X'=+X!(E)  D
 .S T=$P(^AUPNVPED(+$P(BGPG(X),U,4),0),U)
 .Q:'T
 .Q:'$D(^AUTTEDT(T,0))
 .S T=$P(^AUTTEDT(T,0),U,2)
 .I $P(T,"-",2)="M"!(T="M-I")!(T="M-DI")!(T="M-FU")!(T="M-L") S E=1,$P(%,U,1)=$P(BGPG(1),U),$P(%,U,2)=T Q
 Q %
 ;
MED(P,BDATE,EDATE) ;
 K ^TMP($J,"A")
 S Y="^TMP($J,""A"","
 S X=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I '$D(^TMP($J,"A",1)) Q ""
 S (X,Y,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G)  D
 .S V=$P(^TMP($J,"A",X),U,5)
 .Q:'$O(^AUPNVMED("AD",V,0))
 .Q:$P(^AUPNVSIT(V,0),U,6)'=DUZ(2)
 .Q:$P(^AUPNVSIT(V,0),U,7)="E"
 .Q:$P(^AUPNVSIT(V,0),U,3)="C"
 .S G=1
 Q G
CHOL(P,BDATE,EDATE) ;
 K BGPG
 S %=P_"^LAST LAB [DM AUDIT LIPID PROFILE TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $P(BGPG(1),U)_"^LP"
 S %=P_"^LAST LAB [DM AUDIT CHOLESTEROL TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $P(BGPG(1),U)_"^CHOL"
 S %=P_"^LAST LAB [DM AUDIT LDL CHOLESTEROL TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $P(BGPG(1),U)_"^LDL"
 S %=P_"^LAST DX V77.91;DURING "_BDATE_"-"_EDATE,E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q $P(BGPG(1),U)_"^V77.91"
 S E=+$$CODEN^ICPTCOD(82465),%=$$CPTI^BGPDU(P,BDATE,EDATE,E) I %]"" Q $P(%,U)_"^82465"
 S E=+$$CODEN^ICPTCOD(80061),%=$$CPTI^BGPDU(P,BDATE,EDATE,E) I %]"" Q $P(%,U)-"^80061"
 S E=+$$CODEN^ICPTCOD(83721),%=$$CPTI^BGPDU(P,BDATE,EDATE,E) I %]"" Q $P(%,U)_"^83721"
 Q ""
CD4(P,BDATE,EDATE) ;
 K BGPG
 S %=P_"^LAST LAB [BGP CD4 TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q 1
 S E=+$$CODEN^ICPTCOD(86361),%=$$CPTI^BGPDU(P,BDATE,EDATE,E) I %]"" Q $P(%,U)_"^86361"
 ;now go through all labs and check loinc codes
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",%=P_"^ALL LAB;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,A)
 I '$D(^TMP($J,"A",1)) Q ""
 ;now go through all lab tests and see if any are the loinc codes in the taxonomy
 S T=$O(^ATXAX("B","BGP CD4 LOINC CODES",0))
 I 'T Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G)  S I=+$P(^TMP($J,"A",X),U,4) I $P($G(^AUPNVLAB(I,11)),U,13)]"" D
 .S J=$P(^AUPNVLAB(I,11),U,13)
 .S J=$P($G(^LAB(95.3,J,9999999)),U,2)
 .I $D(^ATXAX(T,21,"B",J)) S G=1
 Q G
PCR(P,BDATE,EDATE) ;
 K BGPG
 S %=P_"^LAST LAB [BGP PCR TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q 1
 S E=+$$CODEN^ICPTCOD(87536),%=$$CPTI^BGPDU(P,BDATE,EDATE,E) I %]"" Q $P(%,U)_"^87536"
 S E=+$$CODEN^ICPTCOD(87539),%=$$CPTI^BGPDU(P,BDATE,EDATE,E) I %]"" Q $P(%,U)_"^87539"
 ;now go through all labs and check loinc codes
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",%=P_"^ALL LAB;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,A)
 I '$D(^TMP($J,"A",1)) Q ""
 ;now go through all lab tests and see if any are the loinc codes in the taxonomy
 S T=$O(^ATXAX("B","BGP PCR LOINC CODES",0))
 I 'T Q ""
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G)  S I=+$P(^TMP($J,"A",X),U,4) I $P($G(^AUPNVLAB(I,11)),U,13)]"" D
 .S J=$P(^AUPNVLAB(I,11),U,13)
 .S J=$P($G(^LAB(95.3,J,9999999)),U,2)
 .I $D(^ATXAX(T,21,"B",J)) S G=1
 Q G
V2HIV(P,BDATE,EDATE) ;
 I '$G(P) Q ""
 I '$D(^AUPNVSIT("AC",P)) Q ""
 NEW BGP6M
 S BGP6M=$$FMADD^XLFDT(EDATE,-(6*30))
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S T=$O(^ATXAX("B","BGP HIV/AIDS DXS",0))
 I 'T Q ""
 S (X,G,H)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .Q:"CV"[$P(^AUPNVSIT(V,0),U,3)  ;eliminate contract health
 .S (D,Y)=0 F  S Y=$O(^AUPNVPOV("AD",V,Y)) Q:Y'=+Y!(D)  I $D(^AUPNVPOV(Y,0)) S %=$P(^AUPNVPOV(Y,0),U) I $$ICD^ATXCHK(%,T,9) S D=1
 .Q:'D
 .I $P($P(^AUPNVSIT(V,0),U),".")'<BGP6M S H=1
 .S G=G+1
 .Q
 I G>1,H=1 Q 1
 Q ""
 ;
PED(P,BDATE,EDATE) ;
 K BGPG
 S Y="BGPG("
 S X=P_"^ALL EDUC;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I '$D(BGPG(1)) Q ""
 S (X,P,D,E)=0,%="",T="" F  S X=$O(BGPG(X)) Q:X'=+X!(E&D)  D
 .S T=$P(^AUPNVPED(+$P(BGPG(X),U,4),0),U)
 .Q:'T
 .Q:'$D(^AUTTEDT(T,0))
 .S T=$P(^AUTTEDT(T,0),U,2)
 .I 'E,$P(T,"-",2)="EX"!($P(T,"-",2)="LA")!($P(T,"-",1)="OBS") S E=1,$P(%,U,1)=$P(BGPG(1),U),$P(%,U,2)=T
 .I 'D,$P(T,"-",2)="N"!($P(T,"-",2)="LA")!($P(T,"-",2)="DT")!($P(T,"-",1)="OBS") S D=1,$P(%,U,3)=$P(BGPG(1),U),$P(%,U,4)=T Q
 Q %
 ;
