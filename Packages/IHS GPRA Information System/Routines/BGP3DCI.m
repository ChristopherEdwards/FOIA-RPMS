BGP3DCI ; IHS/CMI/LAB - IHS area GPRA ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
CALCIND ;EP
 I BGPRTYPE=2,BGPTIME=1 S $P(^BGPGPDC(BGPRPT,99999,DFN,0),U,120)=DFN_U_$P(^AUTTLOC(DUZ(2),0),U,10)_U_$P($G(^AUTTLOC(DUZ(2),1)),U,3)_U_"2003"_U_$$D(BGPBDATE)_U_$$D(BGPEDATE)_U_$$D($$DOB^AUPNPAT(DFN,0))_U_$P(^DPT(DFN,0),U,2)_U_BGPACTUP_U_BGPACTCL
 I BGPRTYPE=2,BGPTIME=2 S $P(^BGPGPDP(BGPRPT,99999,DFN,0),U,120)=DFN_U_$P(^AUTTLOC(DUZ(2),0),U,10)_U_$P($G(^AUTTLOC(DUZ(2),1)),U,3)_U_"2003"_U_$$D(BGPBDATE)_U_$$D(BGPEDATE)_U_$$D($$DOB^AUPNPAT(DFN,0))_U_$P(^DPT(DFN,0),U,2)_U_BGPACTUP_U_BGPACTCL
 I BGPRTYPE=2,BGPTIME=3 S $P(^BGPGPDB(BGPRPT,99999,DFN,0),U,120)=DFN_U_$P(^AUTTLOC(DUZ(2),0),U,10)_U_$P($G(^AUTTLOC(DUZ(2),1)),U,3)_U_"2003"_U_$$D(BGPBDATE)_U_$$D(BGPEDATE)_U_$$D($$DOB^AUPNPAT(DFN,0))_U_$P(^DPT(DFN,0),U,2)_U_BGPACTUP_U_BGPACTCL
 S BGPIC=0 F  S BGPIC=$O(BGPIND(BGPIC)) Q:BGPIC'=+BGPIC  D
 .I BGPRTYPE=4,BGPINDT="G",$P(^BGPIND(BGPIC,0),U,7)'=1 Q
 .I BGPRTYPE=4,BGPINDT="A",$P(^BGPIND(BGPIC,0),U,8)'=1 Q
 .K BGPSTOP
 .I $D(^BGPIND(BGPIC,1)) X ^BGPIND(BGPIC,1)
 .I $D(BGPSTOP) Q  ;no need to set since no num/denom
 .;loop each individual to set numerator and denominator
 .S BGPI=0 F  S BGPI=$O(^BGPINDC("B",BGPIC,BGPI)) Q:BGPI'=+BGPI  D
 ..S (BGPNUM,BGPDEN)=0
 ..I BGPRTYPE'=4,BGPINDT="G",$P(^BGPINDC(BGPI,0),U,5)'=1 Q
 ..I BGPRTYPE'=4,BGPINDT="A",$P(^BGPINDC(BGPI,0),U,6)'=1 Q
 ..X ^BGPINDC(BGPI,1)
 ..X ^BGPINDC(BGPI,2) ;denominator 1 or 0
 ..;set field counter
 ..S BGPNF=$P(^BGPINDC(BGPI,0),U,9)
 ..S BGPN=$P(^DD(90243,BGPNF,0),U,4),N=$P(BGPN,";"),P=$P(BGPN,";",2)
 ..D S(BGPRPT,BGPGBL,N,P,BGPNUM)
 ..S BGPDF=$P(^BGPINDC(BGPI,0),U,8)
 ..S BGPN=$P(^DD(90243,BGPDF,0),U,4),N=$P(BGPN,";"),P=$P(BGPN,";",2)
 ..I BGPDEN'="NO" D S(BGPRPT,BGPGBL,N,P,BGPDEN)
 ..I BGPRTYPE=2 S D=$P(^BGPINDC(BGPI,0),U,11),N=$P(^BGPINDC(BGPI,0),U,14) D
 ...I BGPTIME=1 D
 ....I D S $P(^BGPGPDC(BGPRPT,99999,DFN,0),U,D)=BGPDEN
 ....I N S $P(^BGPGPDC(BGPRPT,99999,DFN,0),U,N)=BGPNUM
 ...I BGPTIME=2 D
 ....I D S $P(^BGPGPDP(BGPRPT,99999,DFN,0),U,D)=BGPDEN
 ....I N S $P(^BGPGPDP(BGPRPT,99999,DFN,0),U,N)=BGPNUM
 ...I BGPTIME=3 D
 ....I D S $P(^BGPGPDB(BGPRPT,99999,DFN,0),U,D)=BGPDEN
 ....I N S $P(^BGPGPDB(BGPRPT,99999,DFN,0),U,N)=BGPNUM
 .I $D(BGPLIST(BGPIC)) D STMP^BGP3UTL
 Q
 ;
S(R,G,N,P,V) ;
 I 'V Q  ;no value to add
 S $P(@(G_R_","_N_")"),U,P)=$P($G(@(G_R_","_N_")")),U,P)+V
 Q
D(D) ;
 I D="" Q ""
 Q $E(D,4,5)_"/"_$E(D,6,7)_"/"_(1700+$E(D,1,3))
