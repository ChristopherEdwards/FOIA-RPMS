BGP4D41 ; IHS/CMI/LAB - indicator 3 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;IHS/CMI/LAB - patch 1 7-7-03 added HDL and Triglyceride check to 30-1
 ;  - fixed IMM refusal check for null reason
I27 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4)=0
 I BGPAGEB<46 S BGPSTOP=1 Q  ;no need to process
 I BGPACTUP,'BGPDM1 S BGPD1=1
 I BGPACTCL,'BGPDM1 S BGPD2=1
 I BGPDMD2 S BGPD3=1
 S (BGPVALUE,BGPLP,BGPLDL,BGPHDL,BGPTRI)=""
 S BGPLP=$$LIPID^BGP4D2(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 I $P(BGPLP,U)=1 S BGPN1=1
 S BGPLDL=$$LDL^BGP4D2(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE) ;date^value
 I BGPN1'=1 D
 .S BGPTRI=$$TRIG^BGP4D2(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 .S BGPHDL=$$HDL^BGP4D2(DFN,$$FMADD^XLFDT(BGPEDATE,-(5*365)),BGPEDATE)
 .I BGPTRI,BGPHDL,BGPLDL S BGPN1=1
 I $P(BGPLDL,U) S BGPN6=1
 I $P(BGPLDL,U,2)]"" D
 .S V=$P(BGPLDL,U,3)
 .I V]"",+V'>100 S BGPN2=1
 .I +V>100.99999,+V<131 S BGPN3=1
 .I +V>130.99999,+V<161 S BGPN4=1
 .I +V>160.9999 S BGPN5=1
 S BGPDV=$S(BGPD1:"UP,",1:"")_$S(BGPD2:"AC,",1:"")_$S(BGPD3:"AD",1:"")_"; "
 S BGPVALUE=BGPDV_$S(BGPN1:"LP; ",1:"")
 S %=$S($P(BGPLDL,U,2):$$DATE^BGP4UTL($P(BGPLDL,U,2))_" "_$S($P(BGPLDL,U,3)]"":$P(BGPLDL,U,3),1:"no result"),1:"")
 S BGPVALUE=BGPVALUE_%
 Q
I28 ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPD1,BGPD2,BGPD3,BGPD4)=0
 I BGPAGEB<20 S BGPSTOP=1 Q  ;no need to process
 I BGPDM1 S BGPSTOP=1 Q  ;no dm patients in 04
 I BGPACTUP S BGPD1=1
 I BGPACTCL S BGPD2=1
 S BGPVALUE=$$MEANBP(DFN,$$FMADD^XLFDT(BGPEDATE,-(2*365)),BGPEDATE)
 S BGPN1=$S($P(BGPVALUE,U,2):1,1:0)  ;any value 2-6
 S BGPN2=$S($P(BGPVALUE,U,2)=2:1,1:0)
 S BGPN3=$S($P(BGPVALUE,U,2)=3:1,1:0)
 S BGPN4=$S($P(BGPVALUE,U,2)=4:1,1:0)
 S BGPN5=$S($P(BGPVALUE,U,2)=5:1,1:0)
 S BGPN6=$S($P(BGPVALUE,U,2)=6:1,1:0)
 S BGPVALUE=$S(BGPD1:"UP,",1:"")_$S(BGPD2:"AC,",1:"")_"; "_$P(BGPVALUE,U)
 Q
MEANBP(P,BDATE,EDATE) ;EP
 S X=$$BPS(P,BDATE,EDATE,"I")
 S S=$$SYSMEAN(X) I S="" Q "u^^"
 S DS=$$DIAMEAN(X) I DS="" Q "u^^"
 I S>159!(DS>100) Q S_"/"_DS_" STG 2 HTN"_U_6
 I S>139&(S<160)!(DS>90&(DS<100)) Q S_"/"_DS_" STG 1 HTN"_U_5
 I S>130&(S<140)!(DS>80&(DS<90)) Q S_"/"_DS_" PRE STG II"_U_4
 I S>119&(S<130)!(DS=80) Q S_"/"_DS_" PRE STG 1"_U_3
 I S<120&(DS<80) Q S_"/"_DS_" NORMAL"_U_2
 Q ""
 ;
SYSMEAN(X) ;EP
 I X="" Q ""
 S C=0 F Y=1:1:2 I $P(X,";",Y)]"" S C=C+1
 I C'=2 Q ""
 S C=0 F Y=1:1:2 S C=$P($P(X,";",Y),"/")+C
 Q C\2
 ;
DIAMEAN(X) ;EP
 I X="" Q ""
 S C=0 F Y=1:1:2 I $P(X,";",Y)]"" S C=C+1
 I C'=2 Q ""
 S C=0 F Y=1:1:2 S C=$P($P(X,";",Y),"/",2)+C
 Q C\2
 ;
BPS(P,BDATE,EDATE,F) ;EP ;
 I $G(F)="" S F="E"
 S BGPGLL=0,BGPGV=""
 K BGPG
 S X=P_"^LAST 40 MEAS BP;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,"BGPG(")
 S BGPGL=0 F  S BGPGL=$O(BGPG(BGPGL)) Q:BGPGL'=+BGPGL!(BGPGLL=2)  S BGPGBP=$P($G(BGPG(BGPGL)),U,2) D
 .Q:$$CLINIC^APCLV($P(BGPG(BGPGL),U,5),"C")=30
 .S BGPGLL=BGPGLL+1
 .I F="E" S $P(BGPGV,";",BGPGLL)=BGPGBP_"  "_$$FMTE^XLFDT($P(BGPG(BGPGL),U))
 .I F="I" S $P(BGPGV,";",BGPGLL)=$P(BGPGBP," ")
 Q BGPGV
