BGP4PDL2 ; IHS/CMI/LAB - print ind 1 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;
 ;this routine for Indicator I14 ONLY
I10 ;EP
 D H1 S BGPNODEN=1
 F BGPPC1="10.1" D PI
 Q
PI ;EP
 S BGPDENP=0
 K BGPDHOLD
 K BGPCYP,BGPBLP,BGPPRP
 S BGPPC2=0 F  S BGPPC2=$O(^BGPINDFC("AB",BGPPC1,BGPPC2)) Q:BGPPC2=""  S BGPPC=$O(^BGPINDFC("AB",BGPPC1,BGPPC2,0)) D PI1
 Q
PI1 ;
 I BGPRTYPE'=4,BGPINDT="G",$P(^BGPINDFC(BGPPC,0),U,5)'=1 Q
 I BGPRTYPE'=4,BGPINDT="A",$P(^BGPINDFC(BGPPC,0),U,6)'=1 Q
 I BGPRTYPE'=4,BGPINDT="H",$P($G(^BGPINDFC(BGPPC,12)),U,1)'=1 Q
 I BGPINDT="D",$P(^BGPINDFC(BGPPC,0),U,12)'=1 Q
 I BGPINDT="C",$P(^BGPINDFC(BGPPC,0),U,13)'=1 Q
 I BGPINDT="W",$P($G(^BGPINDFC(BGPPC,12)),U,2)'=1 Q
 I BGPINDT="P",$P($G(^BGPINDFC(BGPPC,12)),U,3)'=1 Q
 ;get numerator value of indicator and calc %
 S BGPDF=$P(^BGPINDFC(BGPPC,0),U,9)
 I $P(^BGPINDFC(BGPPC,0),U,4)[".1" S BGPDHOLD=BGPDF
 I $P(^BGPINDFC(BGPPC,0),U,4)'[".1" S BGPDF=BGPDHOLD
 S BGPNP=$P(^DD(90244.03,BGPDF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 S BGPCYD=$$V(1,BGPRPT,N,P) I $G(BGPAREAA) D SETEXA^BGP4DP1(1,N,P)
 S BGPPRD=$$V(2,BGPRPT,N,P) I $G(BGPAREAA) D SETEXA^BGP4DP1(2,N,P)
 S BGPBLD=$$V(3,BGPRPT,N,P) I $G(BGPAREAA) D SETEXA^BGP4DP1(3,N,P)
 S BGPNF=$P(^BGPINDFC(BGPPC,0),U,9)
 S BGPNP=$P(^DD(90244.03,BGPNF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 D SETN
 I $P($G(^BGPINDFC(BGPPC,14)),U) S ^TMP($J,"SUMMARYDEL",$P(^BGPINDFC(BGPPC,14),U,5),$P(^BGPINDFC(BGPPC,14),U,6),BGPPC)=BGPCYN_U_BGPPRN_U_BGPBLN
 ;write header for 1.A.1
 S X=$P(^BGPINDFC(BGPPC,0),U,15) D S(X,1,1)
 I $P(^BGPINDFC(BGPPC,0),U,16)]"" S X=$P(^BGPINDFC(BGPPC,0),U,16) D S(X,1,1)
 I $P(^BGPINDFC(BGPPC,0),U,19)]"" S Y=$P(^BGPINDFC(BGPPC,0),U,19) D S(Y,1,1)
 D H2
 Q
H2 ;
 S BGPX="",BGPX=BGPCYN,$P(BGPX,U,2)=$S(BGPCYP]"":$$SB($J(BGPCYP,5,1)),1:""),$P(BGPX,U,3)=BGPPRN,$P(BGPX,U,4)=$S(BGPPRP]"":$$SB($J(BGPPRP,5,1)),1:"")
 S $P(BGPX,U,5)=$$SB($J($$CALC(BGPCYN,BGPPRN),6)),$P(BGPX,U,6)=BGPBLN,$P(BGPX,U,7)=$S(BGPBLP]"":$$SB($J(BGPBLP,5,1)),1:"")
 S $P(BGPX,U,8)=$$SB($J($$CALC(BGPCYN,BGPBLN),6))
 D S(BGPX,,2)
 Q
H1 ;EP
 S Y="REPORT" D S(Y,1,2)
 S Y="%" D S(Y,,3)
 S Y="PREV YR" D S(Y,,4)
 S Y="%" D S(Y,,5)
 S Y="CHG from" D S(Y,,6)
 S Y="BASE" D S(Y,,7)
 S Y="%" D S(Y,,8)
 S Y="CHG from" D S(Y,,9)
 S Y="PERIOD" D S(Y,1,2)
 S Y="PERIOD" D S(Y,,4)
 S Y="PREV YR %" D S(Y,,6)
 S Y="PERIOD" D S(Y,,7)
 S Y="BASE %" D S(Y,,9)
 Q
SETN ;set numerator fields
 S BGPIIDEL=1
 D SETN^BGP4DP1
 I $P(^BGPINDFC(BGPPC,0),U,4)[".1" S (BGPCYP,BGPPRP,BGPBLP)=""
 Q
V(T,R,N,P) ;EP
 I $G(BGPAREAA) G VA
 NEW X
 I T=1 S X=$P($G(^BGPGPDCF(R,N)),U,P) Q $S(X]"":X,1:0)
 I T=2 S X=$P($G(^BGPGPDPF(R,N)),U,P) Q $S(X]"":X,1:0)
 I T=3 S X=$P($G(^BGPGPDBF(R,N)),U,P) Q $S(X]"":X,1:0)
 Q ""
VA ;
 NEW X,V,C S X=0,C="" F  S X=$O(BGPSUL(X)) Q:X'=+X  D
 .I T=1 S C=C+$P($G(^BGPGPDCF(X,N)),U,P)
 .I T=2 S C=C+$P($G(^BGPGPDPF(X,N)),U,P)
 .I T=3 S C=C+$P($G(^BGPGPDBF(X,N)),U,P)
 .Q
 Q $S(C:C,1:0)
C(X,X2,X3) ;
 D COMMA^%DTC
 Q X
S(Y,F,P) ;set up array
 I '$G(F) S F=0
 S %=$P(^TMP($J,"BGPDEL",0),U)+F,$P(^TMP($J,"BGPDEL",0),U)=%
 I '$D(^TMP($J,"BGPDEL",%)) S ^TMP($J,"BGPDEL",%)=""
 S $P(^TMP($J,"BGPDEL",%),U,P)=Y
 Q
CALC(N,O) ;ENTRY POINT
 NEW Z
 S Z=N-O,Z=$FN(Z,"+,",0)
 Q Z
SB(X) ;EP - Strip leading and trailing blanks from X.
 X ^DD("FUNC",$O(^DD("FUNC","B","STRIPBLANKS",0)),1)
 Q X
