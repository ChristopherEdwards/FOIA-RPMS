BGP5PDL3 ; IHS/CMI/LAB - print ind 1 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;
 ;this routine for Indicator I23 ONLY
I41 ;EP
 D H1^BGP5PDL1
 F BGPPC1="41.1" D PI^BGP5PDL1
 ;D H1
 S X=" " D S(X,1,1)
 F BGPPC1="41.2.1","41.2.2","41.2.3","41.2.4","41.2.5","41.2.6" D PI
 S X=" " D S(X,1,1)
 F BGPPC1="41.3.1","41.3.2","41.3.3","41.3.4","41.3.5","41.3.6" D PI
 Q
PI ;EP
 K BGPEXCT,BGPSDP
 S BGPPC=$O(^BGPINDVC("OR",BGPPC1,0))
 I BGPRTYPE'=4,BGPINDT="G",$P(^BGPINDVC(BGPPC,0),U,5)'=1 Q
 I BGPRTYPE'=4,BGPINDT="A",$P(^BGPINDVC(BGPPC,0),U,6)'=1 Q
 I BGPRTYPE'=4,BGPINDT="H",$P($G(^BGPINDVC(BGPPC,12)),U,1)'=1 Q
 I BGPINDT="D",$P(^BGPINDVC(BGPPC,0),U,12)'=1 Q
 I BGPINDT="C",$P(^BGPINDVC(BGPPC,0),U,13)'=1 Q
 I BGPINDT="W",$P($G(^BGPINDVC(BGPPC,12)),U,2)'=1 Q
 ;get numerator value of indicator and calc %
 S BGPPC=BGPPC D
 .S BGPDF=$P(^BGPINDVC(BGPPC,0),U,9)
 .;I $P(^BGPINDVC(BGPPC,0),U,4)[".3" S BGPDF=23.503
 .;I $P(^BGPINDVC(BGPPC,0),U,4)[".4" S BGPDF=23.504
 .S BGPNP=$P(^DD(90371.03,BGPDF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 .S BGPCYD=$$V(1,BGPRPT,N,P,1) I $G(BGPAREAA) D SETEXA^BGP5DP1(1,N,P)
 .S BGPPRD=$$V(2,BGPRPT,N,P,1) I $G(BGPAREAA) D SETEXA^BGP5DP1(2,N,P)
 .S BGPBLD=$$V(3,BGPRPT,N,P,1) I $G(BGPAREAA) D SETEXA^BGP5DP1(3,N,P)
 .S BGPNF=$P(^BGPINDVC(BGPPC,0),U,9)
 .S BGPNP=$P(^DD(90371.03,BGPNF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 .D SETN
 .I $P($G(^BGPINDVC(BGPPC,14)),U) D
 ..S ^TMP($J,"SUMMARYDEL",$P(^BGPINDVC(BGPPC,14),U,5),$P(^BGPINDVC(BGPPC,14),U,6),BGPPC)=BGPCYN_U_BGPPRN_U_BGPBLN
 ..Q:'$G(BGPAREAA)
 ..S X=0 F  S X=$O(BGPSDP(X)) Q:X'=+X  D  ;SDPX
 ...S ^TMP($J,"SUMMARYDEL DETAIL PAGE",$P(^BGPINDVC(BGPPC,14),U,5),$P(^BGPINDVC(BGPPC,14),U,6),BGPPC,X)=$P($G(BGPSDP(X,1)),U,2)_U_$P($G(BGPSDP(X,2)),U,2)_U_$P($G(BGPSDP(X,3)),U,2)_U_BGPCYN
 .;write header for 1.A.1
 .S X=$P(^BGPINDVC(BGPPC,0),U,15)_" "_$P(^BGPINDVC(BGPPC,0),U,16)_" "_$P(^BGPINDVC(BGPPC,0),U,19) D S(X,1,1)
 .D H2
 Q
H2 ;
 S BGPX="",BGPX=BGPCYN,$P(BGPX,U,2)="",$P(BGPX,U,3)=BGPPRN,$P(BGPX,U,4)="",$P(BGPX,U,5)=$$SB($J($$CALC(BGPCYN,BGPPRN),6)),$P(BGPX,U,6)=BGPBLN,$P(BGPX,U,7)=""
 S $P(BGPX,U,8)=$$SB($J($$CALC(BGPCYN,BGPBLN),6))
 D S(BGPX,,2)
 Q
H1 ;EP
 S Y="REPORT" D S(Y,1,2)
 S Y=" " D S(Y,,3)
 S Y="PREV YR" D S(Y,,4)
 S Y=" " D S(Y,,5)
 S Y="CHG from" D S(Y,,6)
 S Y="BASE" D S(Y,,7)
 S Y=" " D S(Y,,8)
 S Y="CHG from" D S(Y,,9)
 S Y="PERIOD" D S(Y,1,2)
 S Y="PERIOD" D S(Y,,4)
 S Y="PREV YR  " D S(Y,,6)
 S Y="PERIOD" D S(Y,,7)
 S Y="BASE  " D S(Y,,9)
 Q
SETN ;set numerator fields
 S BGPIIDEL=1
 D SETN^BGP5DP1
 Q
SL(V) ;
 I V="" S V=0
 Q $$STRIP^XLFSTR($J(V,5,1)," ")
V(T,R,N,P,ND) ;EP
 I $G(BGPAREAA) G VA
 NEW X
 I T=1 S X=$P($G(^BGPGPDCV(R,N)),U,P) Q $S(X]"":X,1:0)
 I T=2 S X=$P($G(^BGPGPDPV(R,N)),U,P) Q $S(X]"":X,1:0)
 I T=3 S X=$P($G(^BGPGPDBV(R,N)),U,P) Q $S(X]"":X,1:0)
 Q ""
VA ;
 NEW X,V,C S X=0,C="" F  S X=$O(BGPSUL(X)) Q:X'=+X  D
 .I T=1 S C=C+$P($G(^BGPGPDCV(X,N)),U,P)
 .I T=2 S C=C+$P($G(^BGPGPDPV(X,N)),U,P)
 .I T=3 S C=C+$P($G(^BGPGPDBV(X,N)),U,P)
 .I $G(BGPAREAA),$P($G(^BGPINDVC(BGPPC,14)),U) D  ;SPDX
 ..I T=1 S $P(BGPSDP(X,T),U,ND)=$P($G(^BGPGPDCV(X,N)),U,P) ;SPDX
 ..I T=2 S $P(BGPSDP(X,T),U,ND)=$P($G(^BGPGPDPV(X,N)),U,P) ;SPDX
 ..I T=3 S $P(BGPSDP(X,T),U,ND)=$P($G(^BGPGPDBV(X,N)),U,P) ;SPDX
 .Q
 Q $S(C:C,1:0)
C(X,X2,X3) ;
 D COMMA^%DTC
 Q X
S(Y,F,P) ;set up array
 I '$G(F) S F=0
 S %=$P(^TMP($J,"BGPDEL",0),U)+F,$P(^TMP($J,"BGPDEL",0),U)=%
 I '$D(^TMP($J,"BGPDEL",%)) S ^TMP($J,"BGPDEL",%)=""
 S $P(^TMP($J,"BGPDEL",%),U,P)=Y
 Q
CALC(N,O) ;ENTRY POINT
 NEW Z
 S Z=N-O,Z=$FN(Z,"+,",0)
 Q Z
SB(X) ;EP - Strip leading and trailing blanks from X.
 X ^DD("FUNC",$O(^DD("FUNC","B","STRIPBLANKS",0)),1)
 Q X
