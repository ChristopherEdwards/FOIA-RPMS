BGP6D22 ; IHS/CMI/LAB - measure I2 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
I2 ;EP
 K BGPN1,BGPN2,BGPN3,BGPN4,BGPVALUE,BGPLHGB,BGPN5,BGPN6,BGPN7,BGPN8
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPN7,BGPN8)=0
 S BGPD1=0
 I 'BGPDMD2 S BGPSTOP=1 Q  ;not active diabetic
 I BGPDMD2 S BGPD1=1
 I 'BGPDM1 S BGPSTOP=1 Q  ;don't process this measure, pt not diabetic
 S BGPLHGB=$$HGBA1C^BGP6D2(DFN,BGP365,BGPEDATE)
 S BGPN1=$P(BGPLHGB,U)
 S BGPVALUE=""
 I BGPN1 S BGPVALUE=BGPVALUE_"A1c: "_$$DATE^BGP6UTL($P(BGPLHGB,U,3))_" "_$P(BGPLHGB,U,4)
 ;I 'BGPN1 S BGPVALUE=BGPVALUE_"No A1c done"
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,J,K,G,I,L,T,BGPG
22 ;BPS to set numerator 2
 S BGPBP=$$MEANBP^BGP6D2(DFN,BGP365,BGPEDATE)
 I $P(BGPBP,U,2) S BGPN2=1
 I BGPN2 S BGPVALUE=BGPVALUE_";BPs: "_$P(BGPBP,U)
 S BGPN7=$S($P(BGPBP,U,2)=2:1,1:0)
23 ;
 S BGPLDL=$$LDL^BGP6D2(DFN,BGP365,BGPEDATE)
 S BGPN3=$P(BGPLDL,U)
 I BGPN3 S BGPVALUE=BGPVALUE_";LDL: "_$$DATE^BGP6UTL($P(BGPLDL,U,2))_" "_$P(BGPLDL,U,3)
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,BGPLDL,BGPHDL,BGPTRI,BGPLP
24 ;micro or pos urine AND GFR
 S BGPUP=$$POSUR^BGP6D21(DFN,BGP365,BGPEDATE)
 S BGPGFR=$$GFR^BGP6D21(DFN,BGP365,BGPEDATE)
 I $P(BGPUP,U) S BGPN4=1
 I BGPN4 D
 .I $P(BGPUP,U) S BGPVALUE=BGPVALUE_";POS/M/ESRD: "_$$DATE^BGP6UTL($P(BGPUP,U,4))_" "_$P(BGPUP,U,3)
 .;S BGPVALUE=BGPVALUE_$S(BGPGFR:";GFR",1:"")
 K BGPX,BGPC
25 ;
 S BGPEYE=$$EYE^BGP6D21(DFN,BGP365,BGPEDATE)
 S A=0 I $P(BGPEYE,U)=1 S A=1
 S B=0 I $P(BGPEYE,U)=2 S B=1
 S BGPN5=0 I A!(B) S BGPN5=1
 I BGPN5 S BGPVALUE=BGPVALUE_";EYE: "_$$DATE^BGP6UTL($P(BGPEYE,U,2))_" "_$P(BGPEYE,U,3)
 K BGPG
 K ^TMP($J,"A")
26 ;FOOT EXAM
 S BGPFOOT=$$FOOT(DFN,BGPBDATE,BGPEDATE)
 S BGPN8=$P(BGPFOOT,U)
 I BGPN8 S BGPVALUE=BGPVALUE_";FOOT EXAM: "_$$DATE^BGP6UTL($P(BGPFOOT,U,2))_" "_$P(BGPFOOT,U,3)
 I BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN8 S BGPN6=1
 S BGPVALUE="AD|||"_BGPVALUE I BGPN6 S BGPVALUE=$P(BGPVALUE,"|||")_"|||*ALL* "_$P(BGPVALUE,"|||",2)
 K BGPBP,BGPLDL,BGPEYE,BGPUP,BGPLHGB,BGPG,BGPX,BGPC,BGPGFR,BGPFOOT
 K ^TMP($J,"A")
 Q
IOMW ;EP
 S (BGPD1,BGPD2,BGPD3,BGPD4,BGPD5)=0
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPN7)=0
 I BGPAGEB<67 S BGPSTOP=1 Q
 I $P(^DPT(DFN,0),U,2)'="F" S BGPSTOP=1 Q
 S BGPFRAC=$$FRACTURE^BGP6EL3(DFN,$$FMADD^XLFDT(BGPBDATE,-180),$$FMADD^XLFDT(BGPBDATE,180))
 I '$P(BGPFRAC,U) S BGPSTOP=1 Q
 I BGPACTCL S BGPD1=1
 I BGPACTUP S BGPD2=1
 S BGPISD=$P(BGPFRAC,U,2),BGPISV=$P(BGPFRAC,U,3),BGPISV=$P(BGPFRAC,U,4)
 S BGPBMD=""
 I $P(BGPFRAC,U,3)="H" S BGPBMD=$$TXBMD^BGP6EL4(DFN,$P($P(^AUPNVSIT(BGPISV,0),U),"."),$$DSCHDATE^APCLV(BGPISV,"I"),1)
 I $P(BGPFRAC,U,3)'="H" S BGPBMD=$$TXBMD^BGP6EL4(DFN,BGPISD,$$FMADD^XLFDT(BGPISD,180))
 I $P(BGPBMD,U) S BGPN1=1
 S BGPVALUE=$S(BGPRTYPE=3:"AC",BGPD1:"UP,AC",1:"UP")
 S Y=""
 F X=5,6,7 S V=$P(BGPFRAC,U,X) I V]"" S:Y]"" Y=Y_";" S Y=Y_V
 S BGPVALUE=BGPVALUE_" Fracture: "_Y_" on "_$$DATE^BGP6UTL($P(BGPFRAC,U,2))_"|||"_$P(BGPBMD,U,2)
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,T,FBD,FED
 Q
IAS ;EP
 S (BGPN1,BGPN2,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8)=0
 I BGPAGEB<5 S BGPSTOP=1 Q
 I BGPAGEB>56 S BGPSTOP=1 Q
 I $$EMP(DFN,$$DOB^AUPNPAT(DFN),BGPEDATE) S BGPSTOP=1 Q  ;has dx of emphysema
 I $$COPD(DFN,$$DOB^AUPNPAT(DFN),BGPEDATE) S BGPSTOP=1 Q  ;has copd
 S (BGPASTH1,BGPASTH2)=$$ASSEV(DFN,BGPEDATE)
 I BGPASTH1="" S BGPASTH1=$$PERASTH(DFN,$$FMADD^XLFDT(BGPBDATE,-365),BGPBDATE)
 I BGPASTH2="" S BGPASTH2=$$PERASTH(DFN,BGPBDATE,BGPEDATE)
 I 'BGPASTH1!('BGPASTH2) K ^TMP($J,"A") S BGPSTOP=1 Q  ;not asthma in both time periods
 K ^TMP($J,"A")
 I BGPACTCL S BGPD1=1
 I BGPACTUP S BGPD2=1
 I BGPACTCL,BGPAGEB>4,BGPAGEB<10 S BGPD3=1
 I BGPACTCL,BGPAGEB>9,BGPAGEB<18 S BGPD4=1
 I BGPACTCL,BGPAGEB>17,BGPAGEB<57 S BGPD5=1
 I BGPACTUP,BGPAGEB>4,BGPAGEB<10 S BGPD6=1
 I BGPACTUP,BGPAGEB>9,BGPAGEB<18 S BGPD7=1
 I BGPACTUP,BGPAGEB>17,BGPAGEB<57 S BGPD8=1
 S BGPVALUE=$$ASTHTHER(DFN,BGPBDATE,BGPEDATE)
 I $P(BGPVALUE,U)=1 S BGPN1=1
 S BGPVALUE=$S(BGPRTYPE=3:"",BGPD2:"UP;",1:"")_$S(BGPD1:"AC",1:"")_" "_$P(BGPASTH1,U,2)_" "_$P(BGPASTH2,U,2)_"|||"_$P(BGPVALUE,U,2)_" "_$P(BGPVALUE,U,3)
 K ^TMP($J,"A")
 Q
IL ;EP
 S (BGPN1,BGPN2,BGPD1,BGPD2)=0
 I BGPAGEB<18 S BGPSTOP=1 Q
 I BGPACTCL S BGPD1=1
 I BGPACTUP S BGPD2=1
 I '(BGPD1+BGPD2) S BGPSTOP=1 Q
 S X=$$CREAT(DFN,BGP365,BGPEDATE) I 'X S BGPSTOP=1 Q  ;no serum creatinine test
 S BGPGFR=$$GFRV(DFN,BGP365,BGPEDATE)
 I $P(BGPGFR,U) D
 .S BGPN1=1
 .S V=$P(BGPGFR,U,2)
 .I V]"" S V=+V I V<60 S BGPN2=1
 .Q
 S BGPVALUE=$S(BGPD2:"UP;",1:"")_$S(BGPD1:"AC",1:"")_"|||"
 I $P(BGPGFR,U) S BGPVALUE=BGPVALUE_"GFR: "_$$DATE^BGP6UTL($P(BGPGFR,U,3))_" "_$P(BGPGFR,U,2)
 K BGPGFR
 Q
CREAT(P,BDATE,EDATE) ;EP
 K BGPC
 S BGPC=0
 S T=$O(^ATXAX("B","BGP CREATININE CPTS",0))
 I T D  I X Q 1
 .S X=$$CPT^BGP6DU(P,BDATE,EDATE,T,1)
 ;now get all loinc/taxonomy tests
 S T=$O(^ATXAX("B","BGP CREATININE LOINC CODES",0))
 S BGPLT=$O(^ATXLAB("B","DM AUDIT CREATININE TAX",0))
 S B=9999999-BDATE,E=9999999-EDATE S D=E-1 F  S D=$O(^AUPNVLAB("AE",P,D)) Q:D'=+D!(D>B)!(BGPC)  D
 .S L=0 F  S L=$O(^AUPNVLAB("AE",P,D,L)) Q:L'=+L!(BGPC)  D
 ..S X=0 F  S X=$O(^AUPNVLAB("AE",P,D,L,X)) Q:X'=+X!(BGPC)  D
 ...Q:'$D(^AUPNVLAB(X,0))
 ...I BGPLT,$P(^AUPNVLAB(X,0),U),$D(^ATXLAB(BGPLT,21,"B",$P(^AUPNVLAB(X,0),U))) S BGPC=1 Q
 ...Q:'T
 ...S J=$P($G(^AUPNVLAB(X,11)),U,13) Q:J=""
 ...Q:'$$LOINC(J,T)
 ...S BGPC=1
 ...Q
 Q BGPC
GFRV(P,BDATE,EDATE) ;
 S BGPC=""
 S T=$O(^LAB(60,"B","ESTIMATED GFR",0))
 S T1=$O(^ATXLAB("B","BGP GPRA ESTIMATED GFR TAX",0))
 S B=9999999-BDATE,E=9999999-EDATE S D=E-1 F  S D=$O(^AUPNVLAB("AE",P,D)) Q:D'=+D!(D>B)!(BGPC]"")  D
 .S L=0 F  S L=$O(^AUPNVLAB("AE",P,D,L)) Q:L'=+L!(BGPC]"")  D
 ..S X=0 F  S X=$O(^AUPNVLAB("AE",P,D,L,X)) Q:X'=+X!(BGPC]"")  D
 ...Q:'$D(^AUPNVLAB(X,0))
 ...I T,$P(^AUPNVLAB(X,0),U)=T S BGPC=1_U_$P(^AUPNVLAB(X,0),U,4)_U_(9999999-D) Q
 ...I T1,$D(^ATXLAB(T1,21,"B",$P(^AUPNVLAB(X,0),U))) S BGPC=1_U_$P(^AUPNVLAB(X,0),U,4)_U_(9999999-D) Q
 ...S J=$P($G(^AUPNVLAB(X,11)),U,13) Q:J=""
 ...S %=$P($G(^LAB(95.3,J,9999999)),U,2)
 ...I %="33914-3" S BGPC=1_U_$P(^AUPNVLAB(X,0),U,4)_U_(9999999-D) Q
 ...S J=$P($G(^LAB(95.3,J,0)),U)_"-"_$P($G(^LAB(95.3,J,0)),U,15)
 ...I J="33914-3" S BGPC=1_U_$P(^AUPNVLAB(X,0),U,4)_U_(9999999-D) Q
 ...Q
 Q BGPC
LOINC(A,B) ;
 NEW %
 S %=$P($G(^LAB(95.3,A,9999999)),U,2)
 I %]"",$D(^ATXAX(B,21,"B",%)) Q 1
 S %=$P($G(^LAB(95.3,A,0)),U)_"-"_$P($G(^LAB(95.3,A,0)),U,15)
 I $D(^ATXAX(B,21,"B",%)) Q 1
 Q ""
EMP(P,BDATE,EDATE) ;
 K BGPG
 S Y="BGPG("
 S X=P_"^LAST DX [BGP EMPHYSEMA DXS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BGPG(1)) Q 1  ;has a dx
 Q 0
COPD(P,BDATE,EDATE) ;
 K BGPG
 S Y="BGPG("
 S X=P_"^LAST DX [BGP COPD DXS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BGPG(1)) Q 1  ;has a dx
 Q 0
PERASTH(P,BDATE,EDATE) ;EP
 ;I $G(BDATE)="" S BDATE=$$DOB^AUPNPAT(P)
 ;item 1 - one visit to er w/493 OR hospitalization
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q 0  ;not asthma or hosp or meds
 S T=$O(^ATXAX("B","BGP ASTHMA DXS",0))
 S (X,G)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(G)  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .S K=0
 .I $P(^AUPNVSIT(V,0),U,7)="H" S K=1
 .I $$CLINIC^APCLV(V,"C")=30 S K=1
 .Q:'K
 .Q:"V"[$P(^AUPNVSIT(V,0),U,3)
 .S Y=$$PRIMPOV^APCLV(V,"I")
 .Q:'$$ICD^ATXCHK(Y,T,9)
 .S G=1_U_$$DATE^BGP6UTL($P($P(^AUPNVSIT(V,0),U),".")) ;got one
 ;
 I G Q 1_U_"DX ON HOSP/OR ER ON "_$P(G,U,2)  ;had prim dx on 30 or H so meets denom
PER3 ;
 ;now check for meds
 S BGPT=$O(^ATXAX("B","BGP ASTHMA DXS",0))
 S T=$O(^ATXAX("B","BGP HEDIS ASTHMA MEDS",0))
 S T3=$O(^ATXAX("B","BGP HEDIS ASTHMA NDC",0))
 S T1=$O(^ATXAX("B","BGP HEDIS ASTHMA INHALED MEDS",0))
 S T4=$O(^ATXAX("B","BGP HEDIS ASTHMA INHALED NDC",0))
 S T2=$O(^ATXAX("B","BGP HEDIS ASTHMA LEUK MEDS",0))
 S T5=$O(^ATXAX("B","BGP HEDIS ASTHMA LEUK NDC",0))
 S (X,G,M,D,E)=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:"AOS"'[$P(^AUPNVSIT(V,0),U,7)
 .S (D,Y)=0 F  S Y=$O(^AUPNVPOV("AD",V,Y)) Q:Y'=+Y!(D)  I $D(^AUPNVPOV(Y,0)) S %=$P(^AUPNVPOV(Y,0),U) I $$ICD^ATXCHK(%,BGPT,9) S D=1
 .I D S G=G+1 ;got one visit
 .S Y=0 F  S Y=$O(^AUPNVMED("AD",V,Y)) Q:Y'=+Y  D
 ..S S=0
 ..S Z=$P($G(^AUPNVMED(Y,0)),U) ;get drug ien
 ..I $D(^ATXAX(T1,21,"B",Z))!($$NDC(Z,T4)),$P(^AUPNVMED(Y,0),U,8)="" S M=M+1 Q  ;it is an inhaled steroid that wasn't d/c'ed so 1 dispensing event
 ..I $D(^ATXAX(T,21,"B",Z))!($$NDC(Z,T3)) D
 ...Q:$$LEUK(Z,T2,T5)  ;don't count if it is a leukotriene
 ...S J=$P(^AUPNVMED(Y,0),U,8)
 ...I J]"" S S=$$FMADD^XLFDT(J,$P($P(^AUPNVSIT(V,0),U),"."))
 ...I J="" S S=$P(^AUPNVMED(Y,0),U,7)
 ...S K=S/30,M=M+K
 ..I $D(^ATXAX(T2,21,"B",Z))!($$NDC(Z,T5)) D  Q
 ...S J=$P(^AUPNVMED(Y,0),U,8)
 ...I J]"" S S=$$FMADD^XLFDT(J,$P($P(^AUPNVSIT(V,0),U),"."))
 ...I J="" S S=$P(^AUPNVMED(Y,0),U,7)
 ...S K=S/30,M=M+K,E=E+K
 I G>3,M>1 Q 1_U_"4 POVS AND 2 MEDS"  ;had 4 povs and 2 dispensing events
 I M>3,E<M Q 1_U_"4 meds"  ;had 4 meds, not all were leuko
 I M>3,E=M,G>0 Q 1_U_"LEUKOTRIENE AND 1 DX"  ;had all leuk and 1 dx
 Q ""
 ;
ASSEV(P,EDATE) ;NOW CHECK ASTHMA PACKAGE SEVERITY
 NEW D,G,I
 S D=9999999-EDATE-1,G=""
 S D=0 F  S D=$O(^AUPNVAST("AS",P,D)) Q:D=""!(G]"")  D
 .S I="" F  S I=$O(^AUPNVAST("AS",P,D,I)) Q:I'=+I  D
 ..S S=^AUPNVAST("AS",P,D,I)
 ..I S>1 S G="1^Severity > 1 in V Asthma "_$$DATE^BGP6UTL((9999999-D))
 Q G
NDC(A,B) ;
 ;a is drug ien
 ;b is taxonomy ien
 S BGPNDC=$P($G(^PSDRUG(A,2)),U,4)
 I BGPNDC]"",B,$D(^ATXAX(B,21,"B",BGPNDC)) Q 1
 Q 0
LEUK(A,B,C) ;
 ;a drug ien
 ;b tax ien
 ;c tax ien for ndc
 I $D(^ATXAX(B,21,"B",A)) Q 1
 I $$NDC(A,C) Q 1
 Q ""
ASTHTHER(P,BDATE,EDATE) ;EP
 ;get number of asthma medication events
 K ^TMP($J,"A")
 S A="^TMP($J,""A"",",B=P_"^ALL MEDS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q 0  ;no MEDS
 S T=$O(^ATXAX("B","BGP HEDIS PRIMARY ASTHMA MEDS",0))
 S T3=$O(^ATXAX("B","BGP HEDIS PRIMARY ASTHMA NDC",0))
  S (X,G,M,E)=0,D="" F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(D]"")  S V=$P(^TMP($J,"A",X),U,5),Y=+$P(^TMP($J,"A",X),U,4) D
 .Q:'$D(^AUPNVSIT(V,0))
 .S Z=$P($G(^AUPNVMED(Y,0)),U) ;get drug ien
 .I $D(^ATXAX(T,21,"B",Z))!($$NDC(Z,T3)),$P(^AUPNVMED(Y,0),U,8)="" S D=1_U_$P(^PSDRUG(Z,0),U)_U_$$DATE^BGP6UTL($P($P(^AUPNVSIT(V,0),U),".")) Q
 Q D
FOOT(P,BDATE,EDATE) ;
 K BGPG S %=P_"^LAST EXAM DIABETIC FOOT EXAM;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,"BGPG(")
 I $D(BGPG(1)) Q "1^"_$P(BGPG(1),U)_"^Diab Foot Ex"
 K ^TMP($J,"A")
 S A="^TMP($J,""A"","
 S %=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(%,A)
 S X=0,Y=0 F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(Y)  S R=$$CLINIC^APCLV($P(^TMP($J,"A",X),U,5),"C") I R=65,'$$DNKA^BGP6D21($P(^TMP($J,"A",X),U,5)) S Y=1,D=$P(^TMP($J,"A",X),U)
 I Y Q 1_"^"_D_"^Cl: "_R
 S (X,Y)=0,D="" F  S X=$O(^TMP($J,"A",X)) Q:X'=+X!(Y)  S R=$$PRIMPROV^APCLV($P(^TMP($J,"A",X),U,5),"D") I (R=33!(R=84)!(R=25)),'$$DNKA^BGP6D21($P(^TMP($J,"A",X),U,5)) S Y=1,D=$P(^TMP($J,"A",X),U)
 I Y Q "1^"_D_"^Prv: "_R
 ;now check for refusal of diabetic eye exam
 S G=$$REFUSAL^BGP6UTL1(P,9999999.15,$O(^AUTTEXAM("B","DIABETIC FOOT EXAM, COMPLETE",0)),BDATE,EDATE)
 I $P(G,U)=1 Q "1^"_$P(G,U,2)_"^Refused"
 Q ""
