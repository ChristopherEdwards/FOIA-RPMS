BGP6D86 ; IHS/CMI/LAB - measure C ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
IRAR ;EP
 S (BGPN1,BGPN2,BGPN3,BGPN4,BGPN5,BGPN6,BGPD1,BGPD2,BGPD3,BGPD4,BGPD5,BGPD6,BGPD7,BGPD8,BGPD9,BGPD10,BGPD11,BGPD12)=0
 I 'BGPACTUP S BGPSTOP=1 Q
 I BGPAGEB<16 S BGPSTOP=1 Q  ;must be 16 or older
 I '$$RHEUAR^BGP6D862(DFN,BGPBDATE,BGPEDATE) S BGPSTOP=1 Q  ;no OSTEOARTHRITIS
 S BGPV=$$MEDSPRE^BGP6D863(DFN,BGPBDATE,BGPEDATE)
 I '$P(BGPV,U) S BGPSTOP=1 K ^TMP($J,"A") Q  ;no meds prescribed per logic
 S BGPN1=0
 I BGPACTCL S BGPD1=1
 I BGPACTUP S BGPD2=1
 K BGPIRZ,BGPIRV
 I $P(BGPV,U,2) D
 .S BGPCBC=$$CBC^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPLFT=$$LFT^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("A")=0,BGPIRV("A")="Has NSAID: "
 .I BGPCBC,BGPLFT S BGPIRZ("A")=1,BGPIRV("A")=BGPIRV("A")_"CBC: "_$$DATE^BGP6UTL($P(BGPCBC,U,2))_" LFT: "_$$DATE^BGP6UTL($P(BGPLFT,U,2))  ;NA MEDS AND HAS CBC,LFT
 .I 'BGPCBC S BGPIRV("A")=BGPIRV("A")_"No CBC"
 .I 'BGPLFT S BGPIRV("A")=BGPIRV("A")_" No LFT"
 I $P(BGPV,U,3) D
 .S BGPGOLD=$$GOLDLAB^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("B")=0,BGPIRV("B")="Has IM Gold: "
 .I BGPGOLD S BGPIRZ("B")=1,BGPIRV("B")=BGPIRV("B")_"CBC/Urine Protein: "_$P(BGPGOLD,U,2)
 .I 'BGPGOLD S BGPIRV("B")=$P(BGPGOLD,U,2)
 I $P(BGPV,U,4) D
 .S BGPCBC=$$CBC4^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("C")=0,BGPIRV("C")="Has AZATHIOPRINE: "
 .I BGPCBC S BGPIRZ("C")=1,BGPIRV("C")=BGPIRV("C")_"has 4 CBC's"
 .I 'BGPCBC S BGPIRV("C")=BGPIRV("C")_" does not have 4 CBC's"
 I $P(BGPV,U,5) D
 .S BGPCBC=$$CBC6^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("D")=0,BGPIRV("D")="Has Leflunomide: "
 .I BGPCBC S BGPIRV("D")=BGPIRV("D")_"has 6 CBC's"
 .I 'BGPCBC S BGPIRV("D")=BGPIRV("D")_" does not have 6 CBC's"
 .S BGPSERUM=$$SERUM6^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPSERUM S BGPIRV("D")=BGPIRV("D")_" has 6 Serum Creatinine's"
 .I 'BGPSERUM S BGPIRV("D")=BGPIRV("D")_" does not have 6 Serum Creatinine's"
 .S BGPLFT=$$LFT6^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPLFT S BGPIRV("D")=BGPIRV("D")_" has 6 LFT's"
 .I 'BGPLFT S BGPIRV("D")=BGPIRV("D")_" does not have 6 LFT's"
 .I BGPCBC,BGPSERUM,BGPLFT S BGPIRZ("D")=1
 I $P(BGPV,U,6) D
 .S BGPCBC=$$CBC6^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("E")=0,BGPIRV("E")="Has Methotrexate: "
 .I BGPCBC S BGPIRV("E")=BGPIRV("E")_"has 6 CBC's"
 .I 'BGPCBC S BGPIRV("E")=BGPIRV("E")_" does not have 6 CBC's"
 .S BGPSERUM=$$SERUM6^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPSERUM S BGPIRV("E")=BGPIRV("E")_" has 6 Serum Creatinine's"
 .I 'BGPSERUM S BGPIRV("E")=BGPIRV("E")_" does not have 6 Serum Creatinine's"
 .S BGPLFT=$$LFT6^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPLFT S BGPIRV("E")=BGPIRV("E")_" has 6 LFT's"
 .I 'BGPLFT S BGPIRV("E")=BGPIRV("E")_" does not have 6 LFT's"
 .I BGPCBC,BGPSERUM,BGPLFT S BGPIRZ("E")=1
 I $P(BGPV,U,7) D
 .S BGPCBC=$$CBC^BGP6D861(DFN,$$FMADD^XLFDT(BGPEDATE,-180),BGPEDATE)
 .S BGPIRZ("F")=0,BGPIRV("F")="Has Cycolsporin: "
 .I BGPCBC S BGPIRV("F")=BGPIRV("F")_"has CBC Past 180 days"
 .I 'BGPCBC S BGPIRV("F")=BGPIRV("F")_" does not have CBC past 180 days"
 .S BGPLFT=$$LFT^BGP6D861(DFN,$$FMADD^XLFDT(BGPBDATE,-180),BGPEDATE)
 .I BGPLFT S BGPIRV("F")=BGPIRV("F")_" has LFT past 180 days"
 .I 'BGPLFT S BGPIRV("F")=BGPIRV("F")_" does not have LFT 180 days"
 .S BGPPOT=$$POT^BGP6D861(DFN,$$FMADD^XLFDT(BGPBDATE,-180),BGPEDATE)
 .I BGPPOT S BGPIRV("F")=BGPIRV("F")_" has Potassium in 180 days"
 .I 'BGPPOT S BGPIRV("F")=BGPIRV("F")_" does not have Potassium"
 .S BGPSERUM=$$SERUM12^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPSERUM S BGPIRV("F")=BGPIRV("F")_" has 12 serum creatinine"
 .I 'BGPSERUM S BGPIRV("F")=BGPIRV("F")_" does not have 12 serum creatinine"
 .I BGPCBC,BGPSERUM,BGPLFT,BGPPOT S BGPIRZ("F")=1
 I $P(BGPV,U,8) D
 .S BGPCBC=$$CBC4^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("G")=0,BGPIRV("G")="Has Oral Gold: "
 .I BGPCBC S BGPIRV("G")=BGPIRV("G")_"has 4 CBC's"
 .I 'BGPCBC S BGPIRV("G")=BGPIRV("G")_" does not have 6 CBC's"
 .S BGPUP=$$UP4^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPUP S BGPIRV("G")=BGPIRV("G")_" has 4 Urine Proteins"
 .I 'BGPUP S BGPIRV("G")=BGPIRV("G")_" does not have 4 Urine Protein's"
 .I BGPCBC,BGPUP S BGPIRZ("G")=1
 I $P(BGPV,U,9) D
 .S BGPCBC=$$CBC^BGP6D861(DFN,$$FMADD^XLFDT(BGPEDATE,-180),BGPEDATE)
 .S BGPIRZ("H")=0,BGPIRV("H")="Has Mycophenolate: "
 .I BGPCBC S BGPIRV("H")=BGPIRV("H")_"has CBC Past 180 days"
 .I 'BGPCBC S BGPIRV("H")=BGPIRV("H")_" does not have CBC past 180 days"
 .I BGPCBC S BGPIRZ("H")=1
 I $P(BGPV,U,10) D
 .S BGPCBC=$$CBC4^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("I")=0,BGPIRV("I")="Has Penicillamine: "
 .I BGPCBC S BGPIRV("I")=BGPIRV("I")_"has 4 CBC's"
 .I 'BGPCBC S BGPIRV("I")=BGPIRV("I")_" does not have 6 CBC's"
 .S BGPUP=$$UP4^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .I BGPUP S BGPIRV("I")=BGPIRV("I")_" has 4 Urine Proteins"
 .I 'BGPUP S BGPIRV("I")=BGPIRV("I")_" does not have 4 Urine Protein's"
 .I BGPCBC,BGPUP S BGPIRZ("I")=1
 I $P(BGPV,U,11) D
 .S BGPCBC=$$CBC4^BGP6D861(DFN,BGPBDATE,BGPEDATE)
 .S BGPIRZ("J")=0,BGPIRV("J")="Has Sulfasalazine: "
 .I BGPCBC S BGPIRZ("J")=1,BGPIRV("J")=BGPIRV("J")_"has 4 CBC's"
 .I 'BGPCBC S BGPIRV("J")=BGPIRV("J")_" does not have 4 CBC's"
 S BGPN1=1 S X="" F  S X=$O(BGPIRZ(X)) Q:X=""  I BGPIRZ(X)=0 S BGPN1=0
 S BGPVALUE="UP"_$S(BGPD1:";AC",1:"")_"|||"
 S BGPVALUE=BGPVALUE_$S(BGPN1:"YES: ",1:"NO: ") S X="" F  S X=$O(BGPIRV(X)) Q:X=""  S BGPVALUE=BGPVALUE_" "_BGPIRV(X)
 K X,Y,Z,%,A,B,C,D,E,H,BDATE,EDATE,P,V,S,F,T
 K ^TMP($J,"A")
 Q
