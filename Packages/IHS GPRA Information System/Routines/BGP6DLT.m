BGP6DLT ; IHS/CMI/LAB - national patient list 20 Dec 2004 9:24 AM ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;
 ;
 W:$D(IOF) @IOF
 W !,$$CTR("Lab Taxonomy Report",80)
 W !,$$CTR("CRS 2006, Version 6.1",80)
INTRO ;
 D XIT
 S BGPTEXT="INTROT" F BGPJ=1:1 S BGPX=$T(@BGPTEXT+BGPJ) Q:$P(BGPX,";;",2)="END"  S BGPT=$P(BGPX,";;",2) W !,BGPT
 K DIR S DIR(0)="Y",DIR("A")="Do you wish to continue",DIR("B")="Y" KILL DA D ^DIR KILL DIR
 I $D(DIRUT) D XIT Q
 I 'Y D XIT Q
ZIS ;call to XBDBQUE
 ;S XBRC="",XBRP="PRINT^BGP6DLT",XBRX="XIT^BGP6DLT",XBNS="BGP"
 ;D ^XBDBQUE
 K ZTSK
 K IOP,%ZIS S %ZIS="PQM" D ^%ZIS I POP S IO=IO(0) Q
 G:$D(IO("Q")) QUE
NOQUE ;
 U IO
 D PRINT
 D ^%ZISC
 D XIT
 Q
QUE ;
 K ZTSAVE S ZTSAVE("BGP*")=""
 S ZTRTN="PRINT^BGP6DLT",ZTDESC="BGP 06 LAB TAX REPORT",ZTIO=ION,ZTDTH=""
 D ^%ZTLOAD
 D HOME^%ZIS
 D XIT
 Q
XIT ;
 D EN^XBVK("BGP")
 D ^XBFMK
 Q
 ;
PRINT ;
 S (BGPPAGE,BGPQUIT)=0
 S BGPIOSL=$S($G(BGPGUI):55,1:$G(IOSL))
 D HEADER
 S BGPSUBH="NATIONAL GPRA/GPRA PERFORMANCE REPORT TAXONOMIES"
 K BGPRT,BGPNO S BGPRT(1)="" ;,BGPRT(2)=""
 D N
 Q:BGPQUIT
 S BGPSUBH="CRS TAXONOMIES EXCLUDING NATIONAL GPRA REPORT TAXONOMIES"
 K BGPRT,BGPNO S BGPRT(2)="",BGPRT(3)="",BGPRT(4)="",BGPRT(5)="",BGPNO(1)=""
 W !!
 D N
 Q:BGPQUIT
 S BGPSUBH="CMS REPORT TAXONOMIES"
 K BGPRT,BGPNO S BGPRT(5)=""
 W !!
 D N
 Q:BGPQUIT
 D EOP
 Q
N ;GATHER UP AND DISPLAY ALL NATIONAL GPRA
 S BGPC=0
 I $Y>(BGPIOSL-4) D HEADER Q:BGPQUIT
 W !,BGPSUBH
 S BGPTNAME="" F  S BGPTNAME=$O(^BGPTAXS("B",BGPTNAME)) Q:BGPTNAME=""!(BGPQUIT)  D
 .S BGPTIEN=0,BGPTIEN=$O(^BGPTAXS("B",BGPTNAME,BGPTIEN))
 .Q:'BGPTIEN  ;oops, error in xref
 .Q:'$D(^BGPTAXS(BGPTIEN,0))  ;oops, error in xref
 .Q:$P(^BGPTAXS(BGPTIEN,0),U,2)'="L"  ;only lab taxonomies in this report
 .S (G,X)=0 F  S X=$O(^BGPTAXS(BGPTIEN,12,"B",X)) Q:X'=+X!(G)  D
 ..I $D(BGPRT(X)) S G=1
 .Q:'G
 .;now eliminate those in BGPNO
 .S (G,X)=0 F  S X=$O(^BGPTAXS(BGPTIEN,12,"B",X)) Q:X'=+X!(G)  D
 ..I $D(BGPNO(X)) S G=1
 .Q:G
 .S BGPLTI=$O(^ATXLAB("B",BGPTNAME,0))
 .S BGPC=BGPC+1
 .I 'BGPLTI W !!?3,BGPC,".",?8,BGPTNAME,!?8,"WARNING - You are missing this taxonomy in the Lab Taxonomy file." Q
 .;SET UP STRING OF ALL LAB TEST NAMES
 .K BGPLABS S (BGPLC,BGPLC1)=0
 .S BGPX=0 F  S BGPX=$O(^ATXLAB(BGPLTI,21,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S X=$P($G(^LAB(60,BGPX,0)),U) D
 ...Q:X=""
 ...;I '(BGPLC#2) S BGPLC1=BGPLC1+1 S BGPLABS(BGPLC1)=X,BGPLC=BGPLC+1 Q
 ...S BGPLC1=BGPLC1+1,BGPLABS(BGPLC1)=X
 ...Q
 ..Q
 .I $Y>(BGPIOSL-5) D HEADER Q:BGPQUIT
 .W !!?3,BGPC,".",?8,BGPTNAME,!?8,"Members: "
 .I '$D(BGPLABS) W ?17,"NONE"
 .S BGPY=0 F  S BGPY=$O(BGPLABS(BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ..I $Y>(IOSL-5) D HEADER Q:BGPQUIT
 ..W:BGPY>1 ! W ?17,BGPLABS(BGPY)
 .Q
 Q
HEADER ;EP
 G:'BGPPAGE HEADER1
 K DIR I $E(IOST)="C",IO=IO(0),'$D(ZTQUEUED),'$D(IO("S")) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S BGPQUIT=1 Q
HEADER1 ;
 W:$D(IOF) @IOF S BGPPAGE=BGPPAGE+1
 I $G(BGPGUI) W "ZZZZZZZ",!  ;maw
 W $P(^VA(200,DUZ,0),U,2),?70,"Page ",BGPPAGE,!
 W $$CTR("*** Lab Taxonomy Report ***",80),!
 S X="Date Report Run: "_$$FMTE^XLFDT(DT) W $$CTR(X,80),!
 S X="Site where Run: "_$P(^DIC(4,DUZ(2),0),U) W $$CTR(X,80),!
 S X="Report Generated by: "_$P(^VA(200,DUZ,0),U) W $$CTR(X,80),!
 W $$CTR("CRS Version 6.1",80)
 S X=$$REPEAT^XLFSTR("-",80) W !,X
 W !
 Q
CTR(X,Y) ;EP - Center X in a field Y wide.
 Q $J("",$S($D(Y):Y,1:IOM)-$L(X)\2)_X
 ;----------
EOP ;EP - End of page.
 Q:$E(IOST)'="C"
 Q:$D(ZTQUEUED)!'(IOT["TRM")!$D(IO("S"))
 NEW DIR
 K DIR,DIRUT,DFOUT,DLOUT,DTOUT,DUOUT
 W ! S DIR("A")="End of report.  Press ENTER to continue",DIR(0)="E" D ^DIR KILL DIR
 Q
 ;----------
USR() ;EP - Return name of current user from ^VA(200.
 Q $S($G(DUZ):$S($D(^VA(200,DUZ,0)):$P(^(0),U),1:"UNKNOWN"),1:"DUZ UNDEFINED OR 0")
 ;----------
LOC() ;EP - Return location name from file 4 based on DUZ(2).
 Q $S($G(DUZ(2)):$S($D(^DIC(4,DUZ(2),0)):$P(^(0),U),1:"UNKNOWN"),1:"DUZ(2) UNDEFINED OR 0")
 ;----------
 ;
INTROT ;introductory text
 ;;
 ;;                   Site-Populated Lab Taxonomy Report
 ;;
 ;;This will produce a report of all site-populated lab taxonomies for CRS 2006.
 ;;The report is organized by (1) taxonomies included in the National GPRA/GPRA
 ;;Performance Reports, (2) taxonomies included in all other CRS reports except
 ;;those exclusive to the CMS report, and (3) taxonomies exclusive to the CMS
 ;;report.  
 ;;
 ;;Each lab taxonomy is listed with the lab tests that have been assigned by
 ;;your facility for inclusion in the taxonomy.
 ;;
 ;;You are only able to produce a printed version of this report.
 ;;
 ;;END
