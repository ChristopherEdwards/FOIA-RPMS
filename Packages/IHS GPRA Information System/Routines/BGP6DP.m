BGP6DP ; IHS/CMI/LAB - IHS gpra print ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;
PRINT ;
 K ^TMP($J)
 K BGPDELIM
 S BGPIOSL=$S($G(BGPGUI):55,1:$G(IOSL))
 I $G(BGPAREAA) D SETEXCEL
 I BGPROT="D" G DEL
 S BGPQHDR=0
 D ^BGP6DH
 I BGPQHDR D KITM Q
 S BGPGPG=0
 S BGPQUIT=""
 D PRINT1
 K ^TMP($J)
 I BGPROT="P" D KITM Q
 ;
DEL ;create delimited output file
 I '$D(BGPGUI) D ^%ZISC ;close printer device
 K ^TMP($J)
 D ^BGP6PDL ;create ^tmp of delimited report
 K ^XTMP("BGP6D",BGPJ,BGPH)
 K ^XTMP("BGP6DNP",BGPJ,BGPH)
 K ^XTMP("BGP06CPL",BGPJ,BGPH)
 K ^TMP($J)
 Q
 ;
PRINT1 ;EP
 S BGPIC=0 F  S BGPIC=$O(BGPIND(BGPIC)) Q:BGPIC=""!(BGPQUIT)  D
 .D HEADER^BGP6DPH ;header for all measures
 .I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 .W !,$P(^BGPINDS(BGPIC,0),U,3),!
 .I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 .W !,"Denominator(s):"
 .S BGPX=0 F  S BGPX=$O(^BGPINDS(BGPIC,61,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S BGPY=0 F  S BGPY=$O(^BGPINDS(BGPIC,61,"B",BGPX,BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ...I $P(^BGPINDS(BGPIC,61,BGPY,0),U,2)'[BGPRTYPE Q  ;not a denom def for this report
 ...I BGPRTYPE=4,$P(^BGPINDS(BGPIC,61,BGPY,0),U,3)'[BGPINDT Q  ;don't display
 ...S BGPZ=0 F  S BGPZ=$O(^BGPINDS(BGPIC,61,BGPY,1,BGPZ)) Q:BGPZ'=+BGPZ!(BGPQUIT)  D
 ....I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ....W !,^BGPINDS(BGPIC,61,BGPY,1,BGPZ,0)
 ....Q
 ...;W !
 ...Q
 ..Q
 .I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 .W !!,"Numerator(s):"
 .S BGPX=0 F  S BGPX=$O(^BGPINDS(BGPIC,62,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S BGPY=0 F  S BGPY=$O(^BGPINDS(BGPIC,62,"B",BGPX,BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ...I $P(^BGPINDS(BGPIC,62,BGPY,0),U,2)'[BGPRTYPE Q  ;not a denom def for this report
 ...I BGPRTYPE=4,BGPINDT'="S",$P(^BGPINDS(BGPIC,62,BGPY,0),U,3)'[BGPINDT Q  ;don't display
 ...S BGPZ=0 F  S BGPZ=$O(^BGPINDS(BGPIC,62,BGPY,1,BGPZ)) Q:BGPZ'=+BGPZ!(BGPQUIT)  D
 ....I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ....W !,^BGPINDS(BGPIC,62,BGPY,1,BGPZ,0)
 ....Q
 ...;W !
 ...Q
 ..Q
 .W ! S BGPX=0 F  S BGPX=$O(^BGPINDS(BGPIC,11,BGPX)) Q:BGPX'=+BGPX  D
 ..I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..W !,^BGPINDS(BGPIC,11,BGPX,0)
 .W ! S BGPX=0 F  S BGPX=$O(^BGPINDS(BGPIC,51,BGPX)) Q:BGPX'=+BGPX  D
 ..I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..W !,^BGPINDS(BGPIC,51,BGPX,0)
 .W ! S BGPX=0 F  S BGPX=$O(^BGPINDS(BGPIC,52,BGPX)) Q:BGPX'=+BGPX  D
 ..I $Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..W !,^BGPINDS(BGPIC,52,BGPX,0)
 .X ^BGPINDS(BGPIC,3)
 .I $G(BGPNPL),$D(BGPINDL(BGPIC)),'$D(BGP6NPLT) S BGPINDN=BGPIC D NPL1^BGP6NPLP ;printed nat gpra list
 .I $G(BGPNPL),$D(BGPINDL(BGPIC)),$D(BGP6NPLT) S BGPINDN=BGPIC D CT^BGP6DSTM ;printed nat gpra SEARCH TEMPLATE
 ;
 D ^BGP6DSP
 D ^BGP6DS
 I $G(BGPCPPL) D CPPL1^BGP6DCLP  ;print comp pt list
 D EXIT
 Q
KITM ;
 K ^TMP($J)
 K ^XTMP("BGP6D",BGPJ,BGPH)
 K ^XTMP("BGP6DNP",BGPJ,BGPH)
 K ^XTMP("BGP06CPL",BGPJ,BGPH)
 Q
SETEXCEL ;EP
 I $G(BGPAREAA) D  Q
 .S X=0 F  S X=$O(BGPSUL(X)) Q:X'=+X  D
 ..S N=^BGPGPDCS(X,0)
 ..S L=$P(N,U,9),L=$O(^AUTTLOC("C",L,0)) S D=$P($G(^AUTTLOC(L,1)),U,3),L=$S(L:$P(^DIC(4,L,0),U),1:"?????")
 ..S BGPEI(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP6UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPEI(X),U,P)=$$DATE^BGP6UTL($P(N,U,Y)),P=P+1
 ..S BGPGRAN1(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP6UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPGRAN1(X),U,P)=$$DATE^BGP6UTL($P(N,U,Y)),P=P+1
 ..S BGPGRAN2(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP6UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPGRAN2(X),U,P)=$$DATE^BGP6UTL($P(N,U,Y)),P=P+1
 ..Q
 .Q
 ;S X=BGPRPT
 ;S N=^BGPGPDCS(X,0)
 ;S L=$P(N,U,9),L=$O(^AUTTLOC("C",L,0)) S D=$P($G(^AUTTLOC(L,1)),U,3),L=$S(L:$P(^DIC(4,L,0),U),1:"?????")
 ;S BGPEI(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP6UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPEI(X),U,P)=$$DATE^BGP6UTL($P(N,U,Y)),P=P+1
 Q
EXIT ;
 I $E(IOST)="C",IO=IO(0),'$D(ZTQUEUED) W ! S DIR(0)="EO",DIR("A")="End of report.  Press ENTER" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 Q
C(X,X2,X3) ;
 D COMMA^%DTC
 Q X
