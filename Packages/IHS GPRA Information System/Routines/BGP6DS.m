BGP6DS ; IHS/CMI/LAB - IHS gpra print ;
 ;;16.1;IHS CLINICAL REPORTING;;MAR 22, 2016;Build 170
 ;
START ;
 Q:$G(BGPAREAA)
 Q:'$D(BGPLIST)
 I $G(BGPNPL) Q
 I $G(BGPCPPL) Q  ;not on comprehensive pt
 S BGPQUIT="",BGPGPG=0
 ;D HEADER
 S BGPORDSE=0 F  S BGPORDSE=$O(^BGPINDM("AOI",BGPORDSE)) Q:BGPORDSE'=+BGPORDSE!(BGPQUIT)  S BGPL=$O(^BGPINDM("AOI",BGPORDSE,0)) I $D(BGPLIST(BGPL)) D
 .S BGPFIRSP=1 S BGPCOUNT=0,BGPPCNT=0
 .D HEADER Q:BGPQUIT
 .S BGPCNT=$G(BGPLIST(BGPL))
 .I 'BGPCNT S BGPCNT=0 G GO
 .I BGPCNT<100 S BGPCNT=1 G GO
 .I BGPLIST'="R" S BGPCNT=1 G GO
 .;I BGPCNT<100 S BGPCNT=BGPCNT\10 G GO
 .;I BGPCNT<100 G GO
 .S BGPCNT=10
GO .;
 .D WTITLE^BGP6DP(BGPL)  ;W !,$P(^BGPINDM(BGPL,0),U,3),!
 .I $G(BGPDNT) G CALC
 .D W^BGP6DP("Denominator(s):",0,1,BGPPTYPE)
 .S BGPNODE=$S(BGPRTYPE=1:81,BGPRTYPE=4:61,BGPRTYPE=7:83,BGPRTYPE=9:85,1:61)
 .I BGPRTYPE=4 S BGPNODE=$S(BGPINDM="D":87,BGPINDM="C":89,BGPINDM="W":92,BGPINDM="I":94,BGPINDM="P":96,1:BGPNODE)
 .S BGPX=0 F  S BGPX=$O(^BGPINDM(BGPL,BGPNODE,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S BGPY=0 F  S BGPY=$O(^BGPINDM(BGPL,BGPNODE,"B",BGPX,BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ...;I '$D(^BGPINDM(BGPL,BGPNODE,BGPY,11,"B",BGPRTYPE)) Q  ;not this report type
 ...;I BGPRTYPE=4,'$D(^BGPINDM(BGPL,BGPNODE,BGPY,12,"B",BGPINDM)) Q  ;not this measure type on selected
 ...D WP
 .I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 .D W^BGP6DP("Numerator(s):",0,2,BGPPTYPE)
 .S BGPNODE=$S(BGPRTYPE=1:82,BGPRTYPE=4:62,BGPRTYPE=7:84,BGPRTYPE=9:86,1:62)
 .I BGPRTYPE=4 S BGPNODE=$S(BGPINDM="D":88,BGPINDM="C":91,BGPINDM="W":93,BGPINDM="I":95,BGPINDM="P":97,1:BGPNODE)
 .S BGPX=0 F  S BGPX=$O(^BGPINDM(BGPL,BGPNODE,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S BGPY=0 F  S BGPY=$O(^BGPINDM(BGPL,BGPNODE,"B",BGPX,BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ...;I '$D(^BGPINDM(BGPL,62,BGPY,11,"B",BGPRTYPE)) Q  ;not this report type
 ...;I BGPRTYPE=4,'$D(^BGPINDM(BGPL,BGPNODE,BGPY,12,"B",BGPINDM)) Q  ;not this measure type on selected
 ...D WP
 .S BGPNODE=11
 .I BGPRTYPE=1,$O(^BGPINDM(BGPL,54,0)) S BGPNODE=54
 .I BGPRTYPE=7,$O(^BGPINDM(BGPL,56,0)) S BGPNODE=56
 .I BGPRTYPE=9,$O(^BGPINDM(BGPL,58,0)) S BGPNODE=58
 .D W^BGP6DP("Logic:",0,2,BGPPTYPE) S BGPX=0 F  S BGPX=$O(^BGPINDM(BGPL,BGPNODE,BGPX)) Q:BGPX'=+BGPX  D
 ..I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..D W^BGP6DP(^BGPINDM(BGPL,BGPNODE,BGPX,0),0,1,BGPPTYPE)
 .D W^BGP6DP("Performance Measure Description:",0,2,BGPPTYPE) S BGPX=0 F  S BGPX=$O(^BGPINDM(BGPL,$S($G(BGPNGR09):57,1:51),BGPX)) Q:BGPX'=+BGPX  D
 ..I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..D W^BGP6DP(^BGPINDM(BGPL,$S($G(BGPNGR09):57,1:51),BGPX,0),0,1,BGPPTYPE)
 .I $O(^BGPINDM(BGPL,52,0)) D W^BGP6DP("Past Performance and/or Target:",0,2,BGPPTYPE) S BGPX=0 F  S BGPX=$O(^BGPINDM(BGPL,52,BGPX)) Q:BGPX'=+BGPX  D
 ..I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..D W^BGP6DP(^BGPINDM(BGPL,52,BGPX,0),0,1,BGPPTYPE)
 .I $O(^BGPINDM(BGPL,55,0)) D W^BGP6DP("Source:",0,2,BGPPTYPE) S BGPX=0 F  S BGPX=$O(^BGPINDM(BGPL,55,BGPX)) Q:BGPX'=+BGPX  D
 ..I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 ..D W^BGP6DP(^BGPINDM(BGPL,55,BGPX,0),0,1,BGPPTYPE)
 .I '$O(^BGPINDM(BGPL,55,0)) D W^BGP6DP("",0,1,BGPPTYPE)
CALC .D H1
 .Q:BGPQUIT
 .S BGPCOM="" F  S BGPCOM=$O(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM)) Q:BGPCOM=""!(BGPQUIT)  D
 ..S BGPSEX="" F  S BGPSEX=$O(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX)) Q:BGPSEX=""!(BGPQUIT)  D
 ...S BGPAGE="" F  S BGPAGE=$O(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX,BGPAGE)) Q:BGPAGE=""!(BGPQUIT)  D
 ....S DFN=0 F  S DFN=$O(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX,BGPAGE,DFN)) Q:DFN'=+DFN!(BGPQUIT)  S BGPCOUNT=BGPCOUNT+1 D PRINTL
 ....Q
 ...Q
 ..Q
 .I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER Q:BGPQUIT
 .D W^BGP6DP("Total # of Patients on list: "_+$G(BGPPCNT),0,2,BGPPTYPE)
 .D W^BGP6DP("",0,1,BGPPTYPE)
 D W^BGP6DP("",0,1,BGPPTYPE)
 Q
WP ;
 K ^UTILITY($J,"W")
 S BGPZ=0,BGPLCNT=0
 S DIWL=1,DIWR=80,DIWF="",BGPZ=0 F  S BGPZ=$O(^BGPINDM(BGPL,BGPNODE,BGPY,1,BGPZ)) Q:BGPZ'=+BGPZ  D
 .S BGPLCNT=BGPLCNT+1
 .S X=^BGPINDM(BGPL,BGPNODE,BGPY,1,BGPZ,0) S:BGPLCNT=1 X=" - "_X D ^DIWP
 .Q
WPS ;
 S Z=0 F  S Z=$O(^UTILITY($J,"W",DIWL,Z)) Q:Z'=+Z  D
 .I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER^BGP6DPH Q:BGPQUIT
 .D W^BGP6DP(^UTILITY($J,"W",DIWL,Z,0),0,1,BGPPTYPE)
 K DIWL,DIWR,DIWF,Z
 K ^UTILITY($J,"W"),X
 Q
 ;
PRINTL ;print one line
 Q:(BGPCOUNT#BGPCNT)
 I BGPPTYPE="P",$Y>(BGPIOSL-3) D HEADER Q:BGPQUIT  D
 .;S X=0 F  S X=$O(^BGPINDM(BGPL,72,X)) Q:X'=+X  W !,^BGPINDM(BGPL,72,X,0)
 .D H1
 Q:BGPQUIT
 S BGPPCNT=BGPPCNT+1
 I BGPPTYPE="P" W !,$E($P(^DPT(DFN,0),U),1,22),?24,$$HRN^AUPNPAT(DFN,DUZ(2)),?31,$E(BGPCOM,1,14),?46,BGPSEX,?49,BGPAGE,?54,$$DATE^BGP6UTL($$DOB^AUPNPAT(DFN)) D
 .W !,$P(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX,BGPAGE,DFN),"|||",1)
 .;W:BGPL=43 " NUMERATOR: "
 .W ?30,$P(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX,BGPAGE,DFN),"|||",2)
 I BGPPTYPE="D" D
 .S X=$E($P(^DPT(DFN,0),U),1,22)_U_$$HRN^AUPNPAT(DFN,DUZ(2))_U_$E(BGPCOM,1,14)_U_BGPSEX_U_BGPAGE_U_$$EDT^BGP6UTL($$DOB^AUPNPAT(DFN))
 .S Y=$P(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX,BGPAGE,DFN),"|||",1) S $P(X,U,7)=Y
 .S Y=$P(^XTMP("BGP6D",BGPJ,BGPH,"LIST",BGPL,BGPCOM,BGPSEX,BGPAGE,DFN),"|||",2) S $P(X,U,8)=Y
 .D W^BGP6DP(X,0,1,BGPPTYPE)
 Q
 ;
HEADER ;EP
 I BGPPTYPE'="P" G HEADER1
 G:'BGPGPG HEADER1
 K DIR I $E(IOST)="C",IO=IO(0),'$D(ZTQUEUED) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S BGPQUIT=1 Q
HEADER1 ;
 I BGPPTYPE="P" W:$D(IOF) @IOF S BGPGPG=BGPGPG+1
 I $G(BGPGUI),BGPPTYPE="P" D W^BGP6DP("ZZZZZZZ",0,1,BGPPTYPE)  ;!  ;maw
 D W^BGP6DP("***** CONFIDENTIAL PATIENT INFORMATION, COVERED BY THE PRIVACY ACT *****",1,1,BGPPTYPE)
 I BGPPTYPE="P" S X=$P(^VA(200,DUZ,0),U,2),$E(X,35)=$$FMTE^XLFDT(DT),$E(X,70)="Page "_BGPGPG D W^BGP6DP(X,1,1,BGPPTYPE)
 I BGPPTYPE'="P" S X=$P(^VA(200,DUZ,0),U,2),$P(X,U,2)=$$FMTE^XLFDT(DT) D W^BGP6DP(X,0,1,BGPPTYPE)
 D W^BGP6DP("*** IHS 2016 Clinical Performance Measure Patient List  ***",1,1,BGPPTYPE)
 D W^BGP6DP($P(^DIC(4,DUZ(2),0),U),1,1,BGPPTYPE)
 S X="Report Period: "_$$FMTE^XLFDT(BGPBD)_" to "_$$FMTE^XLFDT(BGPED) D W^BGP6DP(X,1,1,BGPPTYPE)
 D W^BGP6DP($S(BGPLIST="A":"Entire Patient List",BGPLIST="R":"Random Patient List",1:"Patient List by Provider: "_BGPLPROV),1,1,BGPPTYPE)
 D W^BGP6DP($TR($J("",80)," ","-"),0,1,BGPPTYPE)
 Q
H1 ;
 S BGPTSUB=$S(BGPFIRSP:71,1:72)
 S X=0,C=0 F  S X=$O(^BGPINDM(BGPL,BGPTSUB,X)) Q:X'=+X  S C=C+1
 I BGPPTYPE="P",$Y>(BGPIOSL-(C+6)) D HEADER Q:BGPQUIT
 D W^BGP6DP("UP=User Pop; AC=Active Clinical; AD=Active Diabetic; AAD=Active Adult Diabetic",0,1,BGPPTYPE)
 D W^BGP6DP("PREG=Pregnant Female; IMM=Active IMM Pkg Pt; CHD=Active Coronary Heart Disease;",0,1,BGPPTYPE)
 D W^BGP6DP("HR=High Risk Patient",0,1,BGPPTYPE)
 D W^BGP6DP("",0,1,BGPPTYPE)
 S X=0 F  S X=$O(^BGPINDM(BGPL,BGPTSUB,X)) Q:X'=+X  D W^BGP6DP(^BGPINDM(BGPL,BGPTSUB,X,0),0,1,BGPPTYPE)
 D W^BGP6DP("",0,1,BGPPTYPE)
 D W^BGP6DP("PATIENT NAME",0,1,BGPPTYPE,1)
 D W^BGP6DP("HRN",0,0,BGPPTYPE,2,24)  ;,?24,"HRN",
 D W^BGP6DP("COMMUNITY",0,0,BGPPTYPE,3,31)  ;?31,"COMMUNITY",
 D W^BGP6DP("SEX",0,0,BGPPTYPE,4,45)  ;?45,"SEX",
 D W^BGP6DP("AGE",0,0,BGPPTYPE,5,49)  ;?49,"AGE",
 D W^BGP6DP("DOB",0,0,BGPPTYPE,6,54)
 D W^BGP6DP("DENOMINATOR",0,$S(BGPPTYPE="P":1,1:0),BGPPTYPE,7,1)  ;?53,"DENOMINATOR",
 D W^BGP6DP("NUMERATOR",0,0,BGPPTYPE,8,30)  ;?65,"NUMERATOR"
 D W^BGP6DP($TR($J("",80)," ","-"),0,1,BGPPTYPE)
 S BGPFIRSP=0
 Q
