BGP6HEP1 ; IHS/CMI/LAB - print ind 1 ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;
IP ;EP
 I $Y>(BGPIOSL-13) D HEADER^BGP6DPH Q:BGPQUIT  W !!,^BGPHEIS(BGPIC,53,1,0)
 D H1^BGP6DPH
 S BGPPC1=$P(^BGPHEIS(BGPIC,0),U,6) Q:BGPQUIT  D PI
 Q
PI ;EP
 S BGPDENP=0
 S BGPPC2=0 F  S BGPPC2=$O(^BGPHEIIS("AB",BGPPC1,BGPPC2)) Q:BGPPC2=""  S BGPPC=$O(^BGPHEIIS("AB",BGPPC1,BGPPC2,0)) D PI1
 Q
PI1 ;
 ;I BGPPC2=7.4 S BGPDENP=0
 ;I BGPPC2=7.7 S BGPDENP=0
 I BGPPC1=8 S BGPDENP=0
 I BGPPC1=18 S BGPDENP=0
 I BGPPC1=9 S BGPDENP=0
 I BGPPC1=5 S BGPDENP=0
 ;I BGPPC2="10.2" S BGPDENP=0
 ;I BGPPC2="10.3" S BGPDENP=0
 I BGPPC1=12 S BGPDENP=0
 I BGPPC1=15 S BGPDENP=0
 I BGPPC1=21 S BGPDENP=0
 ;I BGPPC1=13 S BGPDENP=0
 I BGPPC2="13.2.1" S BGPDENP=0
 I BGPPC2="13.3.1" S BGPDENP=0
 I BGPPC1=11 S BGPDENP=0
 S BGPDF=$P(^BGPHEIIS(BGPPC,0),U,8)
 ;get denominator value of measure
 S BGPNP=$P(^DD(90375.03,BGPDF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 S BGPCYD=$$V(1,BGPRPT,N,P)
 S BGPPRD=$$V(2,BGPRPT,N,P)
 S BGPBLD=$$V(3,BGPRPT,N,P)
 ;write out denominator
 I 'BGPDENP D
 .I $E($P(^BGPHEIIS(BGPPC,0),U,4),1,3)="I.B"!($E($P(^BGPHEIIS(BGPPC,0),U,4),1,3)="I.F") Q
 .I $P(^BGPHEIIS(BGPPC,0),U,4)="I.A.1" D HEADER^BGP6DPH Q:BGPQUIT  W !!,^BGPHEIS(BGPIC,53,1,0) D H1^BGP6DPH
 .I $Y>(BGPIOSL-13) D HEADER^BGP6DPH Q:BGPQUIT  W !!,^BGPHEIS(BGPIC,53,1,0) D H1^BGP6DPH
 .W !!,$P(^BGPHEIIS(BGPPC,0),U,17)
 .I $P(^BGPHEIIS(BGPPC,0),U,18)]"" D
 ..I $P(^BGPHEIIS(BGPPC,0),U,4)="025.I.1" W !,$P(^BGPHEIIS(BGPPC,0),U,18) Q
 ..I BGPRTYPE'=4,$P(^BGPHEIIS(BGPPC,0),U,18)["GPRA D" Q
 ..W !,$P(^BGPHEIIS(BGPPC,0),U,18)
 .I $P(^BGPHEIIS(BGPPC,0),U,21)]""  D
 ..I $P(^BGPHEIIS(BGPPC,0),U,4)="025.I.1" W !,$P(^BGPHEIIS(BGPPC,0),U,21) Q
 ..I BGPRTYPE'=4,$P(^BGPHEIIS(BGPPC,0),U,21)["GPRA D" Q
 ..W !,$P(^BGPHEIIS(BGPPC,0),U,21)
 .W ?20,$$C(BGPCYD,0,8),?35,$$C(BGPPRD,0,8),?58,$$C(BGPBLD,0,8),!
 .S BGPDENP=1
 I $P(^BGPHEIIS(BGPPC,0),U,4)["I." D
 .S BGPDF=$P(^BGPHEIIS(BGPPC,0),U,8)
 .;get denominator value of measure
 .S BGPNP=$P(^DD(90375.03,BGPDF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 .S BGPCYD=$$V(1,BGPRPT,N,P)
 .S BGPPRD=$$V(2,BGPRPT,N,P)
 .S BGPBLD=$$V(3,BGPRPT,N,P)
 S BGPNF=$P(^BGPHEIIS(BGPPC,0),U,9)
 S BGPNP=$P(^DD(90375.03,BGPNF,0),U,4),N=$P(BGPNP,";"),P=$P(BGPNP,";",2)
 D SETN
 ;write header for 1.A.1
 I $E(BGPPC2,1,3)=1.1 W !
 I BGPPC2=8.3!(BGPPC2=8.4)!(BGPPC2=8.7)!(BGPPC2="8.10") W !
 W !,$P(^BGPHEIIS(BGPPC,0),U,15)
 I $P(^BGPHEIIS(BGPPC,0),U,16)]"" W !?1,$P(^BGPHEIIS(BGPPC,0),U,16)
 I $P(^BGPHEIIS(BGPPC,0),U,19)]"" W !?1,$P(^BGPHEIIS(BGPPC,0),U,19)
 D H2^BGP6DPH
 Q
SETN ;set numerator fields
 S BGPCYN=$$V(1,BGPRPT,N,P)
 S BGPPRN=$$V(2,BGPRPT,N,P)
 S BGPBLN=$$V(3,BGPRPT,N,P)
 S BGPCYP=$S(BGPCYD:((BGPCYN/BGPCYD)*100),1:"")
 S BGPPRP=$S(BGPPRD:((BGPPRN/BGPPRD)*100),1:"")
 S BGPBLP=$S(BGPBLD:((BGPBLN/BGPBLD)*100),1:"")
 Q
SL(V) ;
 I V="" S V=0
 Q $$STRIP^XLFSTR($J(V,5,1)," ")
V(T,R,N,P) ;EP
 I $G(BGPAREAA) G VA
 I T=1 Q $P($G(^BGPHEDCS(R,N)),U,P)
 I T=2 Q $P($G(^BGPHEDPS(R,N)),U,P)
 I T=3 Q $P($G(^BGPHEDBS(R,N)),U,P)
 Q ""
VA ;
 NEW X,V,C S X=0,C="" F  S X=$O(BGPSUL(X)) Q:X'=+X  D
 .I T=1 S C=C+$P($G(^BGPHEDCS(X,N)),U,P)
 .I T=2 S C=C+$P($G(^BGPHEDPS(X,N)),U,P)
 .I T=3 S C=C+$P($G(^BGPHEDBS(X,N)),U,P)
 .Q
 Q C
C(X,X2,X3) ;
 D COMMA^%DTC
 Q X
