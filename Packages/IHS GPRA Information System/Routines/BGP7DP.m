BGP7DP ; IHS/CMI/LAB - IHS gpra print ;
 ;;7.0;IHS CLINICAL REPORTING;;JAN 24, 2007
 ;
 ;
PRINT ;
 K ^TMP($J)
 K BGPDELIM
 S BGPIOSL=$S($G(BGPGUI):55,1:$G(IOSL))
 I $G(BGPAREAA) D SETEXCEL
 I BGPROT="D" G DEL
 S BGPQHDR=0
 D ^BGP7DH
 I BGPQHDR D KITM Q
 S BGPGPG=0
 S BGPQUIT=""
 D PRINT1
 K ^TMP($J)
 I BGPROT="P" D KITM Q
 ;
DEL ;create delimited output file
 I '$D(BGPGUI) D ^%ZISC ;close printer device
 K ^TMP($J)
 D ^BGP7PDL ;create ^tmp of delimited report
 K ^XTMP("BGP7D",BGPJ,BGPH)
 K ^XTMP("BGP7DNP",BGPJ,BGPH)
 K ^XTMP("BGP07CPL",BGPJ,BGPH)
 K ^TMP($J)
 Q
 ;
PRINT1 ;EP
 S BGPIC=0 F  S BGPIC=$O(BGPIND(BGPIC)) Q:BGPIC=""!(BGPQUIT)  D
 .D HEADER^BGP7DPH ;header for all measures
 .I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 .W !,$P(^BGPINDA(BGPIC,0),U,3),!
 .I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 .W !,"Denominator(s):"
 .S BGPX=0 F  S BGPX=$O(^BGPINDA(BGPIC,61,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S BGPY=0 F  S BGPY=$O(^BGPINDA(BGPIC,61,"B",BGPX,BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ...I $P(^BGPINDA(BGPIC,61,BGPY,0),U,2)'[BGPRTYPE Q  ;not a denom def for this report
 ...I BGPRTYPE=4,$P(^BGPINDA(BGPIC,61,BGPY,0),U,3)'[BGPINDT Q  ;don't display
 ...S BGPZ=0 F  S BGPZ=$O(^BGPINDA(BGPIC,61,BGPY,1,BGPZ)) Q:BGPZ'=+BGPZ!(BGPQUIT)  D
 ....I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 ....W !,^BGPINDA(BGPIC,61,BGPY,1,BGPZ,0)
 ....Q
 ...;W !
 ...Q
 ..Q
 .I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 .W !!,"Numerator(s):"
 .S BGPX=0 F  S BGPX=$O(^BGPINDA(BGPIC,62,"B",BGPX)) Q:BGPX'=+BGPX!(BGPQUIT)  D
 ..S BGPY=0 F  S BGPY=$O(^BGPINDA(BGPIC,62,"B",BGPX,BGPY)) Q:BGPY'=+BGPY!(BGPQUIT)  D
 ...I $P(^BGPINDA(BGPIC,62,BGPY,0),U,2)'[BGPRTYPE Q  ;not a denom def for this report
 ...I BGPRTYPE=4,BGPINDT'="S",$P(^BGPINDA(BGPIC,62,BGPY,0),U,3)'[BGPINDT Q  ;don't display
 ...S BGPZ=0 F  S BGPZ=$O(^BGPINDA(BGPIC,62,BGPY,1,BGPZ)) Q:BGPZ'=+BGPZ!(BGPQUIT)  D
 ....I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 ....W !,^BGPINDA(BGPIC,62,BGPY,1,BGPZ,0)
 ....Q
 ...;W !
 ...Q
 ..Q
 .S BGPNODE=11 I BGPRTYPE=1,$O(^BGPINDA(BGPIC,54,0)) S BGPNODE=54
 .W ! S BGPX=0 F  S BGPX=$O(^BGPINDA(BGPIC,BGPNODE,BGPX)) Q:BGPX'=+BGPX  D
 ..I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 ..W !,^BGPINDA(BGPIC,BGPNODE,BGPX,0)
 .W ! S BGPX=0 F  S BGPX=$O(^BGPINDA(BGPIC,51,BGPX)) Q:BGPX'=+BGPX  D
 ..I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 ..W !,^BGPINDA(BGPIC,51,BGPX,0)
 .W ! S BGPX=0 F  S BGPX=$O(^BGPINDA(BGPIC,52,BGPX)) Q:BGPX'=+BGPX  D
 ..I $Y>(BGPIOSL-3) D HEADER^BGP7DPH Q:BGPQUIT
 ..W !,^BGPINDA(BGPIC,52,BGPX,0)
 .X ^BGPINDA(BGPIC,3)
 .I $G(BGPNPL),$D(BGPINDL(BGPIC)),'$D(BGP7NPLT) S BGPINDN=BGPIC D NPL1^BGP7NPLP ;printed nat gpra list
 .I $G(BGPNPL),$D(BGPINDL(BGPIC)),$D(BGP7NPLT) S BGPINDN=BGPIC D CT^BGP7DSTM ;printed nat gpra SEARCH TEMPLATE
 ;
 D ^BGP7DSP
 D ^BGP7DSPN
 D ^BGP7SDP
 D ^BGP7SDPN
 D ^BGP7DS
 I $G(BGPCPPL) D CPPL1^BGP7DCLP  ;print comp pt list
 D EXIT
 Q
KITM ;
 K ^TMP($J)
 K ^XTMP("BGP7D",BGPJ,BGPH)
 K ^XTMP("BGP7DNP",BGPJ,BGPH)
 K ^XTMP("BGP07CPL",BGPJ,BGPH)
 Q
SETEXCEL ;EP
 I $G(BGPAREAA) D  Q
 .S X=0 F  S X=$O(BGPSUL(X)) Q:X'=+X  D
 ..S N=^BGPGPDCA(X,0)
 ..S (D,L)=""
 ..S L=$P(N,U,9) I L S L=$O(^AUTTLOC("C",L,0)) I L S D=$P($G(^AUTTLOC(L,1)),U,3),L=$S(L:$P(^DIC(4,L,0),U),1:"?????")
 ..S BGPEI(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP7UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPEI(X),U,P)=$$DATE^BGP7UTL($P(N,U,Y)),P=P+1
 ..S BGPGRAN1(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP7UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPGRAN1(X),U,P)=$$DATE^BGP7UTL($P(N,U,Y)),P=P+1
 ..S BGPGRAN2(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP7UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPGRAN2(X),U,P)=$$DATE^BGP7UTL($P(N,U,Y)),P=P+1
 ..Q
 .Q
 ;S X=BGPRPT
 ;S N=^BGPGPDCA(X,0)
 ;S L=$P(N,U,9),L=$O(^AUTTLOC("C",L,0)) S D=$P($G(^AUTTLOC(L,1)),U,3),L=$S(L:$P(^DIC(4,L,0),U),1:"?????")
 ;S BGPEI(X)=L_U_$P(N,U,9)_U_D_U_$$DATE^BGP7UTL($P(N,U,13)) S P=5 F Y=1:1:6 S $P(BGPEI(X),U,P)=$$DATE^BGP7UTL($P(N,U,Y)),P=P+1
 Q
EXIT ;
 I $E(IOST)="C",IO=IO(0),'$D(ZTQUEUED) W ! S DIR(0)="EO",DIR("A")="End of report.  Press ENTER" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 Q
C(X,X2,X3) ;
 D COMMA^%DTC
 Q X
