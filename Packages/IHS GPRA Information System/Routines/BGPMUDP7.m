BGPMUDP7 ; IHS/MSC/SAT - MU EP measures NQF0385 ;08-SEP-2011 11:26;DU
 ;;11.1;IHS CLINICAL REPORTING SYSTEM;**1**;JUN 27, 2011;Build 106
 ;Printed output reports for this measure
POCC ;EP
 D P1
 K ^TMP("BGPMU0385")
 Q
P1 ;Write individual measure
 N X,Y,Z,LIST1,LIST2,LIST3
 N DEN1,NUM1,NUM2,PC1,PC2,STRING1,STRING2,STRING3,SUMCT
 N PRD1,PRD2,PRD3,PRD4,PRN1,PRN2,PRN3,PRN4,PRN5,PRN6,PRD5,PRD6
 S SUMCT=0
 S STRING1=$$NUM385("C")
 S STRING2=$$NUM385("P")
 S STRING3=$$NUM385("B")
 D SUMMARY1(STRING1,STRING2,STRING3)
 ;Population
 S PRD11=$P(STRING1,U,5)-$P(STRING2,U,5)
 S PRD14=$P(STRING1,U,9)-$P(STRING2,U,9)
 S PRN11=$P(STRING1,U,5)-$P(STRING3,U,5)
 S PRN14=$P(STRING1,U,9)-$P(STRING3,U,9)
 D HEADER^BGPMUPH Q:BGPQUIT
 D HDRBLK^BGPMUPH
 I $Y>(BGPIOSL-3) D HEADER^BGPMUPH,HDRBLK^BGPMUPH Q:BGPQUIT
 W !,"Pts 18+ w/stage III",?33,$P(STRING1,U,1),?44,$P(STRING2,U,1),?64,$P(STRING3,U,1)
 W !,"colon cancer"
 I $Y>(BGPIOSL-3) D HEADER^BGPMUPH,HDRBLK^BGPMUPH Q:BGPQUIT
 W !,"# Excluded (Exc)",?33,+$P(STRING1,U,4),?44,+$P(STRING2,U,4),?64,+$P(STRING3,U,4)
 I $Y>(BGPIOSL-3) D HEADER^BGPMUPH,HDRBLK^BGPMUPH Q:BGPQUIT
 W !,"Pts 18+ w/stage III",?33,$P(STRING1,U,3),?44,$P(STRING2,U,3),?64,$P(STRING3,U,3)
 W !,"colon cancer less Exc"
 I $Y>(BGPIOSL-3) D HEADER^BGPMUPH,HDRBLK^BGPMUPH Q:BGPQUIT
 ;
 W !!,"# w/adjuvant chemotherapy",?33,+$P(STRING1,U,2),?38,$J($P(STRING1,U,5),5,1),?44,+$P(STRING2,U,2),?49,$J($P(STRING2,U,5),5,1),?56,$J($FN(PRD11,",+",1),6),?64,+$P(STRING3,U,2),?68,$J($P(STRING3,U,5),5,1),?74,$J($FN(PRN11,",+",1),6)
 I $Y>(BGPIOSL-3) D HEADER^BGPMUPH,HDRBLK^BGPMUPH Q:BGPQUIT
 W !,"# w/o adjuvant chemotherapy",?33,+$P(STRING1,U,8),?38,$J($P(STRING1,U,9),5,1),?44,+$P(STRING2,U,8),?49,$J($P(STRING2,U,9),5,1),?56,$J($FN(PRD14,",+",1),6),?64,+$P(STRING3,U,8),?68,$J($P(STRING3,U,9),5,1),?74,$J($FN(PRN14,",+",1),6)
 I $Y>(BGPIOSL-3) D HEADER^BGPMUPH,HDRBLK^BGPMUPH Q:BGPQUIT
 ;
 I $D(BGPLIST(BGPIC)) D P2
 ;
 Q
 ;
NUM385(TF) ;Get the numbers for this measure
 N ARRAY,DEN,NUM,EXC,NOT,PC1,PC11,PC2,PC14,PC21,PC13,NNUM
 S DEN1=+$G(^TMP("BGPMU0385",$J,TF,"DEN",1))
 S NUM1=+$G(^TMP("BGPMU0385",$J,TF,"NUM",1))
 S NOT1=+$G(^TMP("BGPMU0385",$J,TF,"NOT",1))
 S EXC1=+$G(^TMP("BGPMU0385",$J,TF,"EXC",1))
 ;
 S NNUMD1=DEN1-EXC1
 I DEN1=0 S (PC1D1,PC11D1,PC13D1,PC14D1)=0
 I DEN1>0&(NNUMD1=0) D
 .S (PC1D1,PC11D1,PC14D1)=0
 .S PC13D1=$$ROUND^BGPMUA01((EXC1/DEN1),3)*100
 I DEN1>0&(NNUMD1>0) D
 .S PC1D1=$$ROUND^BGPMUA01((NUM1/NNUMD1),3)*100
 .S PC11D1=$$ROUND^BGPMUA01((NNUMD1/DEN1),3)*100
 .S PC13D1=$$ROUND^BGPMUA01((EXC1/DEN1),3)*100
 .S PC14D1=$$ROUND^BGPMUA01((NOT1/NNUMD1),3)*100
 ;         1        2         3         4        5         6        7        8       9       10  11    12     13
 S ARRAY=(+DEN1)_U_+NUM1_U_(+NNUMD1)_U_+EXC1_U_(+PC1D1)_U_PC11D1_U_PC13D1_U_+NOT1_U_+PC14D1
 Q ARRAY
 ;
P2 ;Do the Details
 N PT,NODE,NAME,VST,BMI,FOL,X,PTCT,BGPARR,LINE
 D HEADERL^BGPMUPH
 S X="Patients 18+ with Stage IIIA through IIIC colon cancer who had at least 2 office" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="visit encounters with the EP during the reporting period AND who were prescribed" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="or administered adjuvant chemotherapy for colon cancer during the reporting" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="period, if any." D W^BGPMUPP(X,0,1,BGPPTYPE)
 ;
 S X="Patients who do not meet the numerator criteria are listed first (NM:), followed" D W^BGPMUPP(X,0,2,BGPPTYPE)
 S X="by patients who do meet the numerator criteria (M:).  Excluded patients are" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="listed last." D W^BGPMUPP(X,0,1,BGPPTYPE)
 ;
 S X="The following are the abbreviations used in the denominator and numerator" D W^BGPMUPP(X,0,2,BGPPTYPE)
 S X="columns:" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="CCDX=Colon Cancer Diagnosis" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="CCST=Colon Cancer Stage III" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="EN=Encounter" D W^BGPMUPP(X,0,1,BGPPTYPE)
 S X="MED=Chemotherapy for colon cancer" D W^BGPMUPP(X,0,1,BGPPTYPE)
 ;
 S PTCT=0
 I $Y>(BGPIOSL-5) D HEADERL^BGPMUPH Q:BGPQUIT
 W !!,"PATIENT NAME",?23,"HRN",?30,"COMMUNITY",?42,"SEX",?46,"AGE",?50,"DENOMINATOR",?65,"NUMERATOR"
 S LINE="",$P(LINE,"-",81)="" W !,LINE
 I BGPLIST="D"!(BGPLIST="A") D
 .K BGPARR
 .D PTLSORT^BGPMUUTL(.BGPARR,"^TMP(""BGPMU0385"","_$J_",""PAT"",""C"",""NOT"",1)")
 .S PT=0 F  S PT=$O(BGPARR(PT)) Q:PT=""  D
 ..S PTCT=PTCT+1
 ..S NODE=$G(BGPARR(PT))
 ..D DATA(NODE)
 I BGPLIST="N"!(BGPLIST="A") D
 .K BGPARR
 .D PTLSORT^BGPMUUTL(.BGPARR,"^TMP(""BGPMU0385"","_$J_",""PAT"",""C"",""NUM"",1)")
 .S PT=0 F  S PT=$O(BGPARR(PT)) Q:PT=""  D
 ..S PTCT=PTCT+1
 ..S NODE=$G(BGPARR(PT))
 ..D DATA(NODE)
 I BGPLIST="A" D
 .K BGPARR
 .D PTLSORT^BGPMUUTL(.BGPARR,"^TMP(""BGPMU0385"","_$J_",""PAT"",""C"",""EXC"",1)")
 .S PT=0 F  S PT=$O(BGPARR(PT)) Q:PT=""  D
 ..S PTCT=PTCT+1
 ..S NODE=$G(BGPARR(PT))
 ..D DATA(NODE)
 W !!,"Total # of patients on list: "_PTCT
 Q
DATA(NODE) ;GET DATA
 N NAME,HRN,DEN,NUM,AGE,DFN,SEX,COMM,LINE
 S DFN=$P(NODE,U,1)
 S NAME=$E($$GET1^DIQ(2,$P(NODE,U,1),.01),1,22)
 S HRN=$$HRN^AUPNPAT(DFN,DUZ(2))
 S AGE=$$AGE^AUPNPAT(DFN,BGPED)
 S DEN=$P(NODE,U,2)
 S NUM=$P(NODE,U,3)
 S COMM=$E($$GET1^DIQ(9000001,DFN,1118),1,11)
 S SEX=$P(^DPT(DFN,0),U,2)
 I $Y>(BGPIOSL-2) D
 .D HEADERL^BGPMUPH Q:BGPQUIT
 .W !,"PATIENT NAME",?23,"HRN",?30,"COMMUNITY",?42,"SEX",?46,"AGE",?50,"DENOMINATOR",?65,"NUMERATOR"
 .S LINE="",$P(LINE,"-",81)="" W !,LINE
 W !,NAME,?23,HRN,?30,COMM,?43,SEX,?46,AGE,?50,$P(DEN,";",1),?65,NUM
 F BGPI=2:1:$L(DEN,";") D
 .W !,?50,$P(DEN,";",BGPI)
 Q
SUMMARY1(STRING1,STRING2,STRING3,CT) ;Summmary setup
 N DESC,DESC2,LINE
 K ^TMP("BGPMU SUMMARY",$J,BGPIC)
 S ^TMP("BGPMU SUMMARY",$J,BGPIC)="0385^72"
 S DESC1="18+ # w/adjuvant chemotherapy"
 S LINE=""
 ;
 S LINE="MU.EP.0385.1"_U_DESC1_U_+$P(STRING1,U,4)_U_+$P(STRING1,U,1)_U_+$P(STRING1,U,2)_U_+$P(STRING1,U,5)_U_U_U_U_U
 S LINE=LINE_+$P(STRING2,U,4)_U_+$P(STRING2,U,1)_U_+$P(STRING2,U,2)_U_+$P(STRING2,U,5)_U_+$P(STRING3,U,4)_U_+$P(STRING3,U,1)_U_+$P(STRING3,U,2)_U_+$P(STRING3,U,5)
 S ^TMP("BGPMU SUMMARY",$J,BGPIC,1)=LINE
 ;
 Q
XML385 ;Populate the BGPXML array with data for each population/numerator
 ; BGPXMLOUT(i)=PQRI number^""^Denominator Count^Numerator Count^Exclusion Count
 S BGPXML(1)="9"_U_""_U_+$G(^TMP("BGPMU0385",$J,"C","DEN",1))_U_+$G(^TMP("BGPMU0385",$J,"C","NUM",1))_U
 K ^TMP("BGPMU0385",$J)
 Q
