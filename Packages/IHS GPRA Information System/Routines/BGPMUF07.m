BGPMUF07 ; IHS/MSC/MGH - MI measure NQF0068 ;02-Aug-2011 14:56;DU
 ;;11.1;IHS CLINICAL REPORTING SYSTEM;**1**;JUN 27, 2011;Build 106
 ;Code to collect meaningful use report for IVD Antiplatelet or Aspirin
ENTRY ;EP
 N START,END,BGPNUM,BGPDEN,BGPNUM,STRING,OUTCT,NFCT,INCT
 N IEN,INV,VISIT,DATA,VDATE,VALUE,FIRST,VIEN,RESULT,IVDSTRT,IVDEND
 N CNT,IVD,NUM,OUTENC,NFENC,VENC,IVDDX
 S (BGPDEN,BGPNUM,RESULT)=0
 S IVDSTRT=$$FMADD^XLFDT(BGPEDATE,-730),IVDEND=$$FMADD^XLFDT(BGPEDATE,-426)
 S STRING="",IVDDX=0
 S (IVD,NUM)=0
 ;Pts must be >18
 ;No need to check further if no age match
 Q:BGPAGEE<18
 S CNT=0
 ;First check for IVD DX as an outpatient since this is more common 
 S START=9999999-IVDSTRT,END=9999999-BGPEDATE,VALUE=0
 S FIRST=END-0.1 F  S FIRST=$O(^AUPNVSIT("AA",DFN,FIRST)) Q:FIRST=""!($P(FIRST,".",1)>START)!(+IVD)  D
 .S IEN=0 F  S IEN=$O(^AUPNVSIT("AA",DFN,FIRST,IEN)) Q:'+IEN!(+IVD)  D
 ..;Check provider, Only visits for chosen provider
 ..Q:'$$PRV^BGPMUUT1(IEN,BGPPROV)
 ..S OUTENC=$$VSTCPT^BGPMUUT1(DFN,IEN,"BGPMU ENCOUNTER OUTPT")
 ..I +OUTENC D VSTSTORE Q
 ..S NFENC=$$VSTCPT^BGPMUUT1(DFN,IEN,"BGPMU ACUTE INPT ENC")
 ..I +NFENC D VSTSTORE Q
 ..S VENC=$$VSTPOV^BGPMUUT3(DFN,IEN,"BGPMU ENC OUTPATIENT ICD")
 ..I +VENC D VSTSTORE Q
 ;skip to numerator checking if IVD Dx found
 I +IVD G NUMCHKS
 ;check for other procedures or diagnoses
 S CNT=0
 S START=9999999-IVDSTRT,END=9999999-IVDEND,VALUE=0
 S FIRST=END-0.1 F  S FIRST=$O(^AUPNVSIT("AA",DFN,FIRST)) Q:FIRST=""!($P(FIRST,".",1)>START)!(+IVD)  D
 .S IEN=0 F  S IEN=$O(^AUPNVSIT("AA",DFN,FIRST,IEN)) Q:'+IEN!(+IVD)  D
 ..;Check provider, Only visits for chosen provider
 ..Q:'$$PRV^BGPMUUT1(IEN,BGPPROV)
 ..S NFENC=$$VSTCPT^BGPMUUT1(DFN,IEN,"BGPMU ACUTE INPT ENC")
 ..I +NFENC D
 ...S VDATE=$P($G(^AUPNVSIT(IEN,0)),U,1)
 ...S CNT=CNT+1
 ...S VIEN(CNT)=IEN_U_VDATE_U_$$DATE^BGPMUUTL(VDATE)
 ...S STRING(CNT)=$$DATE^BGPMUUTL(VDATE)
 ...S IVD=$$INPTDEN(DFN,IEN,IVDSTRT,IVDEND)
NUMCHKS ;If the patient had IVD, check to see if they are in the numerator
 Q:'IVD
 S NUM=$$NUM(DFN,BGPBDATE,BGPEDATE)
 D TOTAL1(DFN,IVD,NUM)
 Q
VSTSTORE ;Store compliant visit into array
 S VDATE=$P($G(^AUPNVSIT(IEN,0)),U,1)
 S CNT=CNT+1
 S VIEN(CNT)=IEN_U_VDATE_U_$$DATE^BGPMUUTL(VDATE)
 S STRING(CNT)=$$DATE^BGPMUUTL(VDATE)
 S IVD=$$OUTPTDEN(DFN,IEN)
 Q
TOTAL1(DFN,IVD,NUM) ;See where this patient ends up
 N PTCNT,DENCT,NUM1CT,NOTNUM1,TOTALS,DXTIME,DEN
 S TOTALS=$G(^TMP("BGPMU0068",$J,BGPMUTF,"TOT"))
 S DENCT=+$G(^TMP("BGPMU0068",$J,BGPMUTF,"DEN"))
 S NUM1CT=+$G(^TMP("BGPMU0068",$J,BGPMUTF,"NUM"))
 S NOTNUM1=+$G(^TMP("BGPMU0068",$J,BGPMUTF,"NOT"))
 S PTCNT=TOTALS
 S PTCNT=PTCNT+1
 S (DEN,DXTIME)=""
 S DENCT=DENCT+1 S ^TMP("BGPMU0068",$J,BGPMUTF,"DEN")=DENCT
 I $P(IVD,U,3)'="" S DXTIME=$$DATE^BGPMUUTL($P(IVD,U,3))
 S DEN=$P(IVD,U,2)_DXTIME_";EN:"_STRING(1)
 I +NUM D
 .S NUM1CT=NUM1CT+1 S ^TMP("BGPMU0068",$J,BGPMUTF,"NUM")=NUM1CT
 .I BGPMUTF="C" S ^TMP("BGPMU0068",$J,"PAT",BGPMUTF,"NUM",PTCNT)=DFN_U_DEN_U_$P(NUM,U,3)
 I +NUM=0 D
 .S NOTNUM1=NOTNUM1+1 S ^TMP("BGPMU0068",$J,BGPMUTF,"NOT")=NOTNUM1
 .I BGPMUTF="C" S ^TMP("BGPMU0068",$J,"PAT",BGPMUTF,"NOT",PTCNT)=DFN_U_DEN
 S ^TMP("BGPMU0068",$J,BGPMUTF,"TOT")=PTCNT
 ;Setup iCare array for patient
 S BGPICARE("MU.EP.0068.1",BGPMUTF)=1_U_+NUM_U_""_U_DEN_";"_$P(NUM,U,2)_";"_$P(NUM,U,3)
 Q
OUTPTDEN(DFN,VIEN) ; Get the denominator
 N RESULT,IVDA,IVDB,IVDDX,DOB
 S RESULT=0
 ;Check for IVD Dx
 S DOB=$$GET1^DIQ(2,DFN,.03,"I")
 S IVDA=$$VSTPOV^BGPMUUT3(DFN,VIEN,"BGPMU IVD DX")
 ; PROBLEM check not in ORT - S IVDB=$$PLTAX^BGPMUUT1(DFN,"BGPMU IVD DX","A",END)
 I +IVDA S IVDDX=IVDA
 ;I +IVDB S IVDDX=IVDB
 ;I +IVDA!(+IVDB) S RESULT=1_U_"IVD:"_$P(IVDDX,U,2)_U_$P(IVDDX,U,3) Q RESULT
 I +IVDA S RESULT=1_U_"IVD:"_U_$P(IVDDX,U,3) Q RESULT
 Q RESULT
INPTDEN(DFN,VIEN,START,END) ;Evaluate Inpatient visit denominator criteria
 N RESULT,PTCA,PTCAP,AMI,CABG,CABGP
 S RESULT=0
 ;Check for PTCA Codes (14 to 24 months hence)
 S PTCA=$$CPT^BGPMUUT1(DFN,IVDSTRT,IVDEND,"BGPMU PTCA CPT")
 ;I +PTCA S RESULT=1_U_"PTCA:"_$P(PTCA,U,2)_U_$P(PTCA,U,3) Q RESULT
 I +PTCA S RESULT=1_U_"PTCA:"_U_$P(PTCA,U,3) Q RESULT
 S PTCAP=$$LASTPRC^BGPMUUT2(DFN,IVDSTRT,IVDEND,"BGPMU PTCA PROCEDURE")
 I +PTCAP S RESULT=1_U_"PTCA:"_U_$P(PTCAP,U,3) Q RESULT
 ;Check for AMI Dx (during visit)
 S AMI=$$VSTPOV^BGPMUUT3(DFN,VIEN,"BGPMU ACUTE MI DX")
 I +AMI S RESULT=1_U_"AMI:"_U_$P(AMI,U,3) Q RESULT
 ;Check for CABG procedure (during visit)
 S CABG=$$VSTCPT^BGPMUUT1(DFN,VIEN,"BGPMU CABG CPT")
 I +CABG S RESULT=1_U_"CABG:"_U_$P(CABG,U,3) Q RESULT
 S CABGP=$$VSTICD0^BGPMUUT3(DFN,VIEN,"BGPMU CABG PROCEDURE")
 I +CABGP S RESULT=1_U_"CABG:"_U_$P(CABGP,U,3) Q RESULT
 Q RESULT
NUM(DFN,BGPBDATE,BGPEDATE) ;check for Antiplatelet Rx
 N FOUND,PMED
 S FOUND=0
 S PMED=$$FIND^BGPMUUT8(DFN,"BGPMU IVD ANTIPLATELET NDCS",BGPBDATE,"",BGPEDATE)
 I +PMED S FOUND=1_U_$P(PMED,U,2)_U_$P(PMED,U,3)
 Q FOUND
