AICDPRTD ; IHS/OHPRD/ACC - PROGRAM TO PRINT ICD DIAGNOSIS FILE IN CODE ORDER ;
 ;;3.51;IHS ICD/CPT lookup & grouper;;MAY 30, 1991
 ; NEEDED TO PRINT IN CODE ORDER DUE TO LOSS OF B INDEX
 ;
 K ZTSK
 K IO,IOP,%ZIS S %ZIS="PQ" D ^%ZIS I POP S IO=IO(0) Q
 G:$D(IO("Q")) QUE
NOQUE D EN
 Q
QUE K ZTSAVE
 S ZTRTN="EN^AICDPRTD",ZTDESC="PRINT ICD DIAGNOSIS FILE BY CODE",ZTIO=ION,ZTDTH=DT
 D ^%ZTLOAD
 D HOME^%ZIS
 K ZTDESC,ZTDTH,ZTIO,ZTRTN,ZTSAVE,ZTSK
 Q
 ;
EN ; ENTRY POINT - FROM TASKMAN FOR QUEUED PRINTING
 U IO
 W:$P(IOST,"-",1)="C" @IOF,!
 W "--- ICD9 (DIAGNOSIS) CODE LISTING ---",!!
 S AICDCODE=" " F AICDI=0:0 S AICDCODE=$O(^ICD9("BA",AICDCODE)) Q:AICDCODE=""  S AICDDFN=$O(^(AICDCODE,0)) D PCODE
 W:$P(IOST,"-",1)="P" @IOF
 D ^%ZISC
 K AICDDFN
 Q
PCODE ;
 W AICDCODE,?10,$E("*",$P(^ICD9(AICDDFN,0),U,9)) S X=$S($D(^(1)):^(1),1:$P(^(0),U,3)_" <brief form>")
 F  W ?12,$E(X,1,IOM-12),! S X=$E(X,IOM-11,$L(X)) Q:X=""
 I $P(IOST,"-",1)="P",$Y>(IOSL-1) W @IOF
 Q
