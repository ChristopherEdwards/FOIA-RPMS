BYIMIMM4 ;IHS/CIM/THL - IMMUNIZATION DATA INTERCHANGE;
 ;;2.0;BYIM IMMUNIZATION DATA EXCHANGE INTERFACE;**3,4**;NOV 01, 2013;Build 189
 ;
 ;-----
MULT ;EP;PROCESS MULTIPLE INBOUND MESSAGES
 K BYIMQUIT
 N AUTOIMP,AUTOADD,DIR,FILE
 S AUTOIMP=1
 S AUTOADD=0
 D PATH^BYIMIMM
 Q:IPATH=""
 S DIR=$$LIST^%ZISH(IPATH,"*",.DIR)
 S FILE=""
 S XX=0
 F  S XX=$O(DIR(XX)) Q:'XX  S:DIR(XX)]"" FILE(DIR(XX))=""
 S XX=""
 F  S XX=$O(FILE(XX)) Q:XX=""  D
 .S:XX["izdata"&(XX[".dat"!(XX[".hl7")) FILE=XX
 .Q:FILE=""
 .N DA
 .S DA=0
 .F  S DA=$O(^BYIMPARA("FILE",FILE,DUZ(2),DA)) Q:'DA  I $P($G(^BYIMPARA(DUZ(2),2,+DA,0)),U,3)="I" S BYIMQUIT=""
 .I $D(BYIMQUIT) K BYIMQUIT Q
 .D I1^BYIMIMM1
 Q
 ;-----
ASITE(BYIMDA) ;EP;ADDITIONAL EXPORT SITES
 Q:'$G(BYIMDA)
 I $O(^BYIMPARA(BYIMDA,3,0)) D AS Q
 K DIR
 S DIR(0)="YO"
 S DIR("A")="Add export/import information for additional states"
 S DIR("B")="NO"
 W !!
 D ^DIR
 K DIR
 Q:'Y
AS N BYIMAS
 S BYIMQUIT=0
 F  D ASACT Q:BYIMQUIT
 Q
 ;-----
ASACT ;ADDITIONAL SITE ACTION
 I '$O(^BYIMPARA(BYIMDA,3,0)) D ASADD
 I '$O(^BYIMPARA(BYIMDA,3,0)) S BYIMQUIT=1 Q
 D ASD
 K DIR
 S DIR(0)="SO^1:Edit site;2:Add site;3:Delete site"
 W !!
 D ^DIR
 K DIR
 I 'Y S BYIMQUIT=1 Q
 I Y=1 D ASEDIT Q
 I Y=2 D ASADD Q
 I Y=3 D ASDEL
 Q
 ;-----
ASSEL ;SELECT ADDITION SITE
 I J=1 S Y=1 D ASSEL1 Q
 K DIR
 S DIR(0)="NO^1:"_J
 S DIR("A")="Select site number"
 W !!
 D ^DIR
 K DIR
ASSEL1 Q:'Y
 Q:'$G(BYIMAS(Y))
 S DA=BYIMAS(Y)
 Q
 ;-----
ASD ;DISPLAY OTHER EXPORT/IMPORT SITES
 W @IOF
 W !!?10,"Additional EXPORT/IMPORT Site Directories"
 W !!?5,"NUM",?10,"SITE/STATE"
 W !?5,"---",?10,"--------------------------------------------------"
 N X0,X1
 S J=0
 S BYIMAS=0
 F  S BYIMAS=$O(^BYIMPARA(BYIMDA,3,BYIMAS)) Q:'BYIMAS  D
 .S X0=$G(^BYIMPARA(BYIMDA,3,BYIMAS,0))
 .S X1=$G(^BYIMPARA(BYIMDA,3,BYIMAS,1))
 .S J=J+1
 .S BYIMAS(J)=BYIMAS
 .W !?5,J,?10,$P(X0,U)
 .W !?15,"OUTBOUND: ",$P(X0,U,2)
 .W !?15," INBOUND: ",$P(X0,U,3)
 .W !?15,"   QUERY: ",$P(X1,U)
 .W !?15,"RESPONSE: ",$P(X1,U,2)
 Q
 ;-----
ASADD ;ADD SITES
 N BYIMQUIT
 S BYIMQUIT=0
 S J=0
 N BYIMAS
 S J=J+1
 K DIR
 S DIR(0)="FO^1:10"
 S DIR("A")="NEW interface state name"
 S DIR("?")="Enter a name to identify the additional interface state, 3-10 characters"
 W !!
 D ^DIR
 K DIR
 K DIE,DIC,DINUM,DR,DA,DD,DO,DIK,DLAYGO
 I X=""!(X[U) S BYIMQUIT=1 Q
 S (NSITE,BYIMAS(J))=X
 S DA(1)=BYIMDA
 S DIC="^BYIMPARA("_BYIMDA_",3,"
 S DIC(0)="L"
 S:'$D(^BYIMPARA(BYIMDA,3,0)) ^BYIMPARA(BYIMDA,3,0)="^90480.03"
 D FILE^DICN
 I Y<0 S BYIMQUIT=1 Q
 K DIE,DIC,DINUM,DR,DA,DD,DO,DIK,DLAYGO
 N NSDA
 S (NSDA,DA)=+Y
 N X,Y,Z,ZZ
 S X=$P(^BYIMPARA(BYIMDA,0),U,2)
 S V=$S(X["\":"\",1:"/")
 S NSITE=$$LC(NSITE)
 S X=$$LC(X)
 I X["hlrbridge" D  I 1
 .S X=$P(X,"hl7bridge")
 .S:$E(X,$L(X))'=V X=X_V
 .S Y=X_"hl7bridge"_V_NSITE_V_"responses"
 .S Z=X_"hl7bridge"_V_NSITE_V_"queries"
 .S ZZ=X_"hl7bridge"_V_NSITE_V_"qresponses"
 .S X=X_"hl7bridge"_V_NSITE_V_"requests"
 E  D
 .S X=$P(X,"requests")
 .S:$E(X,$L(X))'=V X=X_V
 .S Y=X_NSITE_V_"responses"
 .S Z=X_NSITE_V_"queries"
 .S ZZ=X_NSITE_V_"qresponses"
 .S X=X_NSITE_V_"requests"
 S $P(^BYIMPARA(BYIMDA,3,NSDA,0),U,2)=X
 S $P(^BYIMPARA(BYIMDA,3,NSDA,0),U,3)=Y
 S $P(^BYIMPARA(BYIMDA,3,NSDA,1),U)=Z
 S $P(^BYIMPARA(BYIMDA,3,NSDA,1),U,2)=ZZ
 S $P(^BYIMPARA(BYIMDA,3,NSDA,0),U,4,13)=$P(^BYIMPARA(BYIMDA,0),U,4,13)
 S DA=NSDA
 D ASE1
 Q
 ;-----
ASEDIT ;EDIT SITE
 D ASSEL
 Q:'$G(DA)
ASE1 S DA(1)=BYIMDA
 S DR=".01T;.02T;.03T;1.01T;1.02T;.07T;.1T;.11T;.12T;.13T;.14T;.15T"
 S DIE="^BYIMPARA("_BYIMDA_",3,"
 W !!
 D ^DIE
 K DA,DR,DIE
 Q
 ;-----
ASDEL ;DELETE SITE
 D ASSEL
 Q:'DA
 S X=^BYIMPARA(BYIMDA,3,DA,0)
 W !?10,$P(X,U)
 K DIR
 S DIR(0)="YO"
 S DIR("A")="Delete export/import site: "_$P(X,U)
 S DIR("B")="NO"
 W !
 D ^DIR
 K DIR
 Q:Y'=1
 S DA(1)=BYIMDA
 S DIK="^BYIMPARA("_BYIMDA_",3,"
 D ^DIK
 K DA,DIK
 Q
 ;-----
CP(DFN) ;EP;DETERMINE VARICELLA EXPOSURE
 ;DFN   = PATIENT DFN
 Q:'$D(^BIPC("B",+DFN))
 N BIX,VDAT
 S BIX=0
 F  S BIX=$O(^BIPC("B",DFN,BIX)) Q:'BIX  S:$P($G(^BIPC(BIX,0)),U,3)=12 VDAT=$P(^(0),U,4)
 Q:$L($G(VDAT))'=7
 S VDAT=VDAT+17000000
 S RXA="RXA|0|1|"_VDAT_"|"_VDAT_"|998^No vaccine administered^CVX|999"
 S OBX="OBX|1|CE|59784-9^Disease with presumed immunity ^LN|1|38907003^Varicella infection^SCT||||||F|CR|"
 N LINE
 S LINE=$O(^UTILITY("INH",$J,9999999999),-1)+1
 S X=U_"UTILITY(""INH"","_$J_","_LINE_")"
 S @X=RXA
 S X=U_"UTILITY(""INH"","_$J_","_(LINE+1)_")"
 S @X=OBX
 Q
 ;-----
IMMDUP ;EP;TO DEDUP IMMUNIZATIONS
 S DIK="^AUPNVIMM("
 S DFN=0
 F  S DFN=$O(^AUPNVIMM("AC",DFN)) Q:'DFN  D
 .K TMP
 .S IEN=0
 .F  S IEN=$O(^AUPNVIMM("AC",DFN,IEN)) Q:'IEN  D
 ..S X=$P($G(^AUPNVIMM(IEN,0)),U,1,3)
 ..S VIS=$P(X,U,3)
 ..Q:'$P(X,U,2)!'X!'VIS
 ..S DAT=$P($G(^AUPNVSIT(VIS,0)),".")
 ..Q:'DAT
 ..S X=$P(X,U,1,2)_U_DAT
 ..I +X=242 S $P(X,U)=148
 ..I +X=243 S $P(X,U)=148
 ..S TMP(X,IEN)=""
 .S X=""
 .F  S X=$O(TMP(X)) Q:X=""  D
 ..S J=0
 ..S IEN=0
 ..F  S IEN=$O(TMP(X,IEN)) Q:'IEN  D
 ...S J=J+1
 ...Q:J<2
 ...W:'$D(ZTQUEUED) !,DFN,?10,J,?15,IEN,?25,X,?45,$P(^AUTTIMM(+X,0),U,3)
 ...M ^BYIMTMP("BYIM IMM DUPS",DA)=^AUPNVIMM(DA)
 ...S DA=IEN
 ...;D ^DIK
 Q
 ;-----
HFSA(DEST,BYIMHDIR,BYIMHFNM) ;EP - export from this destination
 N BYIMBP,BYIMY
 F BYIMBP="FORMAT CONTROLLER","OUTPUT CONTROLLER" D
 .S SH=0,BYIMY=$$CHK^BHLBCK(BYIMBP,SH)
 I '$G(DEST) D  Q
 .S ^BYIMTMP("EXP FAIL",$H,"NO DEST")=BYIMHDIR_U_BYIMHFNM_U_DUZ
 .S BYIMFAIL=$G(BYIMFAIL)+1
 I '$D(^INLHDEST(DEST)) D  Q
 .S ^BYIMTMP("EXP FAIL",$H,DEST,"NO MESSAGE")=BYIMHDIR_U_BYIMHFNM_U_DUZ
 .S BYIMFAIL=$G(BYIMFAIL)+1
 S Y=$$OPEN^%ZISH(BYIMHDIR,BYIMHFNM,"W")
 I Y D  Q
 .D EXPBULL(BYIMHFNM,DEST,BYIMHDIR)
 .S ^BYIMTMP("EXP FAIL",$H,+$G(DEST),"NO OPEN")=BYIMHDIR_U_BYIMHFNM_U_DUZ
 .S BYIMFAIL=$G(BYIMFAIL)+1
 N BYIMH,BYIMU
 S BYIMH=0
 F  S BYIMH=$O(^INLHDEST(DEST,0,BYIMH)) Q:'BYIMH  D
 .S BYIMU=0
 .F  S BYIMU=$O(^INLHDEST(DEST,0,BYIMH,BYIMU)) Q:'BYIMU  D
 ..S DFN=$$DFN(BYIMU),^TMP("BYIMIMM4",BYIMU)=DFN
 ..D:DFN REFUSAL^BYIMSEG1(BYIMU,DFN)
 ..D LPINTHU(BYIMU)
 ..K ^INLHDEST(DEST,0,BYIMH,BYIMU)
 D ^%ZISC
 D LOGDFN
 Q
 ;-----
LPINTHU(BYIMUIEN)       ;EP - loop through UIF and set to file
 N BYIMUDA
 S BYIMUDA=0
 F  S BYIMUDA=$O(^INTHU(BYIMUIEN,3,BYIMUDA)) Q:'BYIMUDA  D
 .N XX
 .S XX=$G(^INTHU(BYIMUIEN,3,BYIMUDA,0))
 .Q:XX=""
 .I XX'["|CR|" D
 ..S BYIMUDA=BYIMUDA+1
 ..S XX=XX_$G(^INTHU(BYIMUIEN,3,BYIMUDA,0))
 ..Q:XX["|CR|"
 ..S BYIMUDA=BYIMUDA+1
 ..S XX=XX_$G(^INTHU(BYIMUIEN,3,BYIMUDA,0))
 .Q:XX'["|CR|"
 .I XX["FHS|" S $P(XX,"|",9)=BYIMHFNM,$P(XX,"|",11)=$E($TR($H,","),1,7)
 .I XX["BHS|" S $P(XX,"|",11)=$E($TR($H,","),1,7)
 .I 'BYIMIN1,$E(XX,1,4)["IN1|"!($E(XX,1,4)["IN2|") Q
 .S:"|MSH|FHS|BHS|BTS|FTS|"'[("|"_$E(XX,1,3)_"|") XX=$TR(XX,"\&")
 .U IO W $P(XX,"|CR|"),!
 Q
 ;-----
DEXIT ;EP;CLEAN UP AFTER IZAD
 K ^BYIMTMP($J,"BYIM DISP")
 Q
 ;-----
ALOT(LDA,IVDA) ;EP;ACTIVATE LOT NUMBER
 Q:'LDA
 Q:'$D(^AUTTIML(LDA,0))
 S NEWLOT(LDA)=^AUTTIML(LDA,0)
 I '$P(NEWLOT(LDA),U,4) D
 .S $P(NEWLOT(LDA),U,4)=IVDA
 .S ^AUTTIML("C",IVDA,LDA)=""
 S $P(^AUTTIML(LDA,0),U,3)=0
 S $P(^AUTTIML(LDA,0),U,4)=IVDA
 Q
 ;-----
ILOT(LDA) ;EP;INACTIVATE LOT NUMBER
 Q:'LDA
 Q:'$D(^AUTTIML(LDA,0))
 S:$G(NEWLOT(LDA))]"" ^AUTTIML(LDA,0)=$G(NEWLOT(LDA))
 K NEWLOT
 Q
 ;-----
LV(DFN,IVDA,LOTDA) ;EP;CALCULATE LAST V IMM FOR PATIENT
 Q:'$G(DFN)!'$G(IVDA)!'$G(LOTDA)
 N XX,YY,ZZ
 S XX=$O(^AUPNVIMM("AC",DFN,9999999999),-1)
 Q:'XX
 S XX0=$G(^AUPNVIMM(XX,0))
 Q:+XX0'=IVDA
 Q:$P(XX0,U,5)
 S $P(^AUPNVIMM(XX,0),U,5)=LOTDA
 Q
 ;-----
MAN(MAN) ;EP;CHECK IMMUNIZATION MANUFACTURER
 Q:MAN=""
 S:MAN["\T\" MAN=$P(MAN,"\T\")_"&"_$P(MAN,"\T\",2)
 N X,Y,Z
 S X=MAN
 X ^%ZOSF("UPPERCASE")
 S MAN=Y
 S MANA=$P(MAN,U)
 S MANN=$P(MAN,U,2)
 S MANDA=$O(^AUTTIMAN("B",MANN,0))
 Q:MANDA
 S X=MANN
 S DIC="^AUTTIMAN("
 S DIC(0)="L"
 S DIC("DR")=".02////"_MANA
 D FILE^DICN
 I Y>0 S MANDA=+Y
 Q
 ;-----
LOT(LOT,MAN,IVDA) ;EP;EVALUATE LOT AND MANUFACTURER DATA
 S LOTDA=""
 S MANDA=""
 Q:$G(LOT)=""
 D:MAN]"" MAN^BYIMIMM4(MAN)
 S X=LOT
 X ^%ZOSF("UPPERCASE")
 S LOT=Y
 S LOTDA=$O(^AUTTIML("B",LOT,0))
 I LOTDA D ALOT(LOTDA,IVDA) Q
 S X=LOT
 S DIC="^AUTTIML("
 S DIC(0)="L"
 S DIC("DR")=".02////"_$G(MANDA)_";.03////1;.04////"_IVDA
 D FILE^DICN
 Q:Y<0
 S LOTDA=+Y
 D ALOT(LOTDA,IVDA)
 Q
 ;-----
LOG(FILE,ACT,DFNCNT,IMMCNT,NODFNCNT,NEWIMCNT,ADDIMCNT,PATH,DFN) ;EP;LOG AUTO IMPORT FILES THAT HAVE BEEN PROCESSED
 ;FILE     = NAME OF FILE IMPORTED OR EXPORTED
 ;ACT      = ACTION - 'I'MPORT OR 'E'XPORT
 ;DFNCNT   = NUMBER OF PATIENTS
 ;IMMCNT   = NUMBER OF IMMUNIZATIONS
 ;NODFNCNT = NUMBER OF PATIENTS FOR WHOM THERE IS NO PATIENT MATCH
 ;NEWIMCNT = NUMBER OF NEW IMMUNIZATIONS
 ;ADDIMCNT = NUMBER OF NEW IMMUNIZATIONS ADDED TO V IMMUNIZATIONS
 ;PATH     = DRIVE/DIRECTORY FILE SENT TO
 Q:$G(FILE)=""!($G(ACT)="")
 S X=FILE
 K DIE,DIC,DINUM,DR,DA,DD,DO,DIK,DLAYGO
 S DA(1)=DUZ(2)
 S DIC="^BYIMPARA("_DA(1)_",2,"
 S DIC(0)="L"
 S DIC("DR")=".02////"_DT_";.03////"_ACT_";.04////"_$G(DFNCNT)_";.05////"_$G(IMMCNT)
 S:$G(NODFNCNT) DIC("DR")=DIC("DR")_";.06////"_NODFNCNT
 S:$G(NEWIMCNT) DIC("DR")=DIC("DR")_";.07////"_NEWIMCNT
 S:$G(ADDIMCNT) DIC("DR")=DIC("DR")_";.08////"_ADDIMCNT
 S:$G(PATH)]"" DIC("DR")=DIC("DR")_";.09////"_PATH
 D FILE^DICN
 K DIE,DIC,DINUM,DR,DA,DD,DO,DIK,DLAYGO
 N BYIMACT
 S BYIMACT=$S(ACT="I":"imported from",1:"exported to")
 ;-----
BULLETIN ;EP;CREATE EXPORT/IMPORT BULLETIN
 N %X,%Y,X,XMB,XMDT,XMDUZ,Y1
 S XMB="BYIM EXPORT/IMPORT MESSAGE"
 S XMB(1)="The file '"_FILE_"' was "_BYIMACT_" the State Immunization Registry on "_$$HTE^XLFDT($H)
 S XMDUZ=.5
 D ^XMB
 Q
 ;-----
EXPBULL(BYIMHFNM,DEST,BYIMHDIR) ;EP;EXPORT FILE CREATION FAILED
 N %X,%Y,X,XMB,XMDT,XMDUZ,Y1
 S XMB="BYIM EXPORT FILE FAILED"
 S XMB(1)=BYIMHFNM
 S XMB(2)=$P($G(^INRHD(+DEST,0)),U)
 S XMB(3)=BYIMHDIR
 S XMDUZ=.5
 D ^XMB
 Q
 ;-----
LC(X) ;LOWERCASE
 S X=$TR(X,"ABCDEFGHIJKLMNOPQRSTUVWXYZ ","abcdefghijklmnopqrstuvwxyz_")
 Q X
 ;-----
QPATH(BYIMDA) ;EP;SET QUERY DIRECTORIES
 Q:'$G(BYIMDA)
 N V,X,Y,Z
 S X=$P($G(^BYIMPARA(BYIMDA,0)),U,2)
 S X=$$LC(X)
 S V=$S(X["\":"\",1:"/")
 I X["hl7bridge" D  I 1
 .S X=$P(X,"hl7bridge")
 .S:$E(X,$L(X))'=V X=X_V
 .S Y=X_"hl7bridge"_V_"queries"
 .S Z=X_"hl7bridge"_V_"qresponses"
 E  D
 .S X=$P(X,"requests")
 .S:$E(X,$L(X))'=V X=X_V
 .S Y=X_"queries"
 .S Z=X_"qresponses"
 S $P(^BYIMPARA(BYIMDA,1),U)=Y
 S $P(^BYIMPARA(BYIMDA,1),U,2)=Z
 Q
 ;-----
RXA(BYIMDA) ;EP;TO SET THE IIS CODE FOR RXA-11.4
 Q:'$G(BYIMDA)
 N BYIMRXA,DISP,BYIMQUIT
 I $O(^BYIMPARA(BYIMDA,5,0)) D RXA1 Q
 W !!,"Some state Immunization Information Systems (IIS) require facilities to send"
 W !,"a code to identify the facility at which the immunization was administered or"
 W !,"identify the vaccine inventory location instead of the facility name."
 W !!,"If you need to add state IIS assigned code(s) please enter 'Y' below."
 W !,"You will be prompted to select the name of your facility and then"
 W !,"enter the state assigned code."
 K DIR
 S DIR(0)="YO"
 S DIR("A")="Add State IIS Assigned Facility Codes"
 S DIR("B")="NO"
 W !!
 D ^DIR
 K DIR
 Q:'Y
RXA1 N BYIMX
 S BYIMQUIT=0
 F  D RXAACT Q:BYIMQUIT
 Q
 ;-----
RXAACT ;ADDITIONAL SITE ACTION
 I '$O(^BYIMPARA(BYIMDA,5,0)) D RXAADD
 I '$O(^BYIMPARA(BYIMDA,5,0)) S BYIMQUIT=1 Q
 D RXADISP
 K DIR
 S DIR(0)="SO^1:Edit Codes;2:Add codes;3:Delete codes"
 W !!
 D ^DIR
 K DIR
 I 'Y S BYIMQUIT=1 Q
 I Y=1 D RXAEDIT Q
 I Y=2 D RXAADD Q
 I Y=3 D RXADEL
 Q
 ;-----
RXAADD ;SELECT FACILITIES FOR STATE RXA CODE
 N BYIMX,BYIMY
 S DIC="^DIC(4,"
 S DIC(0)="AEMQZ"
 ;S DIC("S")="I $D(^AUTTSITE(""B"",Y))!$D(^AUTTSITE(1,19251,""B"",Y))"
 S DIC("A")="Select Facility for the IIS assigned Code: "
 W !
 D ^DIC
 I Y<1 S BYIMQUIT=1 Q
 S BYIMX=+Y
 S BYIMY=$P(^DIC(4,+Y,0),U)
 K DIR
 S DIR(0)="FO^1:50"
 S DIR("A")="Enter State IIS Code assigned for **"_BYIMY_"**"
 W !
 D ^DIR
 K DIR
 I X="" S BYIMQUIT=1 Q
 S BYIMY=X
 S X=BYIMX
 S DA(1)=DUZ(2)
 S DIC="^BYIMPARA("_DUZ(2)_",5,"
 S DIC(0)="L"
 S DIC("DR")=".02////"_BYIMY
 S DINUM=X
 D FILE^DICN
 I Y<1 S BYIMQUIT=1 Q
 Q
 ;------
RXADISP ;DISPLAY EXISTING IIS RXA CODES
 N DISP,J,X,Y,Z
 S J=0
 S X=0
 F  S X=$O(^BYIMPARA(DUZ(2),5,X)) Q:'X  S Y=$G(^(X,0)) D:Y
 .S Z=$P($G(^DIC(4,X,0)),U)
 .Q:Z=""
 .S J=J+1
 .S X(Z)=Y
 S J=0
 S X=""
 F  S X=$O(X(X)) Q:X=""  D
 .S J=J+1
 .S $E(DISP(J),11)=J
 .S $E(DISP(J),16)=X
 .S $E(DISP(J),48)=$P(X(X),U,2)
 .S BYIMRXA(J)=+X(X)
 S BYIMJ=J
 W @IOF
 W !?5,"State IIS Assigned Administered-at or Vaccine Inventory Location Code"
 W !!?10,"NO."
 W ?15,"Facility"
 W ?47,"IIS Code"
 W !?10,"---",?15,"------------------------------",?47,"--------------------"
 S J=0
 F  S J=$O(DISP(J)) Q:'J  D
 .W !,DISP(J)
 Q
 ;------
RXAEDIT ;EDIT SITE
 D RXASEL
 Q:'$G(DA)
RXAE1 S DA(1)=BYIMDA
 S DR=".02T"
 S DIE="^BYIMPARA("_BYIMDA_",5,"
 W !!
 D ^DIE
 K DA,DR,DIE
 Q
 ;-----
RXADEL ;DELETE SITE
 D RXASEL
 Q:'$G(DA)
 S X=+^BYIMPARA(BYIMDA,5,DA,0)
 S X=$P($G(^DIC(4,X,0)),U)
 W !?10,X
 K DIR
 S DIR(0)="YO"
 S DIR("A")="Delete export/import site: "_X
 S DIR("B")="NO"
 W !
 D ^DIR
 K DIR
 Q:Y'=1
 S DA(1)=BYIMDA
 S DIK="^BYIMPARA("_BYIMDA_",5,"
 D ^DIK
 K DA,DIK
 Q
 ;-----
RXASEL ;SELECT ADDITION SITE
 I BYIMJ=1 S Y=1 D RXASEL1 Q
 K DIR
 S DIR(0)="NO^1:"_BYIMJ
 S DIR("A")="Select Facility number"
 W !!
 D ^DIR
 K DIR
RXASEL1 Q:'Y
 Q:'$G(BYIMRXA(Y))
 S DA=BYIMRXA(Y)
 Q
 ;-----
LOGDFN ;EP;LOG ALL PATIENTS INCLUDED IN EXPORT
 N DFN,TYPE
 S TYPE="E"
 S DFN=0
 F  S DFN=$O(^BYIMTMP("LOG",DFN)) Q:'DFN  D
 .D LOGD(DFN,TYPE)
 .K ^BYIMTMP("LOG",DFN)
 Q
 ;-----
LOGD(DFN,TYPE) ;EP;LOG EACH IMMUNIZATION EXPORTED OR IMPORTED
 N IMM,X,Y,Z
 S:$G(TYPE)="" TYPE="E"
 S DFNCNT=$G(DFNCNT)+1
 S IMM=0
 F  S IMM=$O(^AUPNVIMM("AC",DFN,IMM)) Q:'IMM  D
 .I $G(BYIMALL)'=2,$D(^BYIMEXP("D",IMM)) Q
 .Q:$G(BYIMMU2)
 .K DIE,DIC,DINUM,DR,DA,DD,DO,DIK,DLAYGO
 .S DIC="^BYIMEXP("
 .S DIC(0)="L"
 .S DIC("DR")=".02////"_DT_";.03////"_IMM_";.04////"_TYPE
 .S X=DFN
 .D FILE^DICN
 .K DIE,DIC,DINUM,DR,DA,DD,DO,DIK,DLAYGO
 .S IMMCNT=$G(IMMCNT)+1
 Q
 ;-----
DFN(UIF) ;FIND PATIENT DFN
 N X,Y,Z,LOC,HRN,DFN
 S X=$P($P($G(^INTHU(UIF,3,2,0)),"|",4),U)
 S LOC=$E(X,1,6)
 S HRN=+$E(X,7,12)
 Q:'LOC!'HRN ""
 S LOC=$O(^AUTTLOC("C",LOC,0))
 Q:'LOC ""
 S DFN=""
 S X=0
 F  S X=$O(^AUPNPAT("D",HRN,X)) Q:'HRN!DFN  S Y=0 F  S Y=$O(^AUPNPAT("D",HRN,X,Y)) Q:'Y!DFN  S:Y=LOC DFN=X
 Q DFN
 ;
