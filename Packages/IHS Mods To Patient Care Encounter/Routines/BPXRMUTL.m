BPXRMUTL ;IHS/MSC/MGH - Populate the Health Factor Status;18-Jun-2007 09:57;MGH
 ;;1.5;CLINICAL REMINDERS;**1005**;Jun 19,2000
 ;HFPOP-POPULATE THE HEALTH FACTOR REMINDERS WITH STATUS OF OTHER
 ;KILLXTMP-KILL OFF XTMP GLOBAL FOR PATIENT DFN TO FORCE REPOPULATION
 Q
HFPOP ;EP POPULATE HEALTH FACTOR REMINDERS WITH STATUS OF OTHER
 N DIR,Y,LIST,ELIST,OTHER,I
 S DIR("A")="Wish to populate the reminder health factors with the status of other"
 S DIR("B")="YES"
 S DIR(0)="Y"
 D ^DIR
 I Y'=1 D ENDPOP Q
 D LIST^DIC(9999999.64,,,"I","*",,,,,,"LIST($J,0)")
 M ELIST=LIST($J,0,"DILIST")  ;MOVE FROM LIST TO ELIST TO MAKE EASIER TO WRITE
 ;K LIST
 W !!,"Will attempt to file in the health factor resolutions file these entries below"
 H 3
 S OTHER=$$FIND1^DIC(801.9,,,"OTHER")
 I 'OTHER W !,"CAN'T FIND 'OTHER' IN FILE 801.9" D ENDPOP Q
 S I=0 F  S I=$O(ELIST(2,I)) Q:I=""  D
 .K FDA,IEN
 .S FDA(801.95,"?+1,",.01)=I
 .S FDA(801.9501,"?+2,?+1,",.01)=OTHER
 .D UPDATE^DIE("","FDA","IEN")
 .W !,ELIST(1,I),?($X+5),$S($G(IEN(1,0))'="+":"NOT ",1:""),"ADDED"
 .W ?($X+5),"STATUS OTHER ",$S($G(IEN(2,0))'="+":"NOT ",1:""),"ADDED"
 .Q
 ;
ENDPOP ;
 K DIR,X,Y,FDA,IEN,LIST,ELIST
 Q
 ;
KILLXTMP ; EP FORMAT WILL BE ^XTMP("PXRMDFN_DFN_""
 S DIC=9000001
 S DIC(0)="AEMQ"
 S DIC("A")="WHICH PATIENT WILL I KILL THE ^XTMP(""PXRMDFN"" GLOBAL? //"
 D ^DIC
 I +Y<0 D ENDS Q
 ;
 S DFN=+Y
 S ROOT="^XTMP(""PXRMDFN"
 S GLBSTOP=ROOT_DFN_""""
 S GLB=ROOT_DFN_""")"
 S G1=GLB
 ;
 S DIR(0)="Y",DIR("B")="NO",DIR("A")="SURE YOU WANT TO KILL THE "_GLB
 D ^DIR
 ;B "L+"
 I $D(DIRUT)!(Y'=1) D ENDS Q
 F  S G1=$Q(@G1) Q:(G1="")!(G1'[GLBSTOP)  D
 .K @G1
ENDS ;
 K DIR,DIC,Y,DFN,ROOT,GLBSTOP,GLB,G1,X,Y
 Q
 ;
KILLALL ;KILL ^XTMP("PXRMDFN"
 ;WE WANT ALL OUR ^XTMP("PXRMDFN" NODES TO BE REBUILT TO INCLUDE THE 'HRCN NODE THAT MAY NOT BE THERE
 N START,END,NODE
 S START="^XTMP(""PXRMDFN"")"
 S END=$E(START,1,$L(START)-2)
 S NODE=START
 F  S NODE=$Q(@NODE) Q:NODE'[END  K @NODE
 Q
