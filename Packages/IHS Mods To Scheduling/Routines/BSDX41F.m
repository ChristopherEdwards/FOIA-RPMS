BSDX41F ; IHS/OIT/HMW/MSC/SAT - WINDOWS SCHEDULING RPCS ;
 ;;3.0;IHS WINDOWS SCHEDULING;;DEC 09, 2010
 ;
FMH ; ******* FAMILY HISTORY * 9000014 *******  (APCHS6)
 G FMH^APCHS61
PMH ; ******** PERSONAL HISTORY * 9000013 *******  (APCHS6)
 ;
 Q:'$D(^AUPNPH("AC",APCHSPAT))
 X APCHSCKP Q:$D(APCHSQIT)  X:'APCHSNPG APCHSBRK
 ;
 S APCHSDFN="" F APCHSQ=0:0 S APCHSDFN=$O(^AUPNPH("AC",APCHSPAT,APCHSDFN)) Q:APCHSDFN=""  D PHDSP
 ;
PMHX K APCHSDFN,APCHSN,APCHSICD,APCHSICL,APCHSNRQ,APCHSDAT,APCHSDTH
 Q
PHDSP S APCHSN=^AUPNPH(APCHSDFN,0)
 S APCHSICD=$P(APCHSN,U,1) D GETICDDX^APCHSUTL
 S Y=$P(APCHSN,U,3) X APCHSCVD S APCHSDAT=Y
 S APCHSDTH=$P(APCHSN,U,5) I APCHSDTH]"" S Y=APCHSDTH X APCHSCVD S APCHSDTH=Y
 S APCHSNRQ=$P(APCHSN,U,4)
 D GETNARR^APCHSUTL
 K APCHSDTE S:APCHSDTH]"" APCHSNTE="(onset: "_APCHSDTH_")"
 X APCHSCKP Q:$D(APCHSQIT)  S BSDXTMP=APCHSDAT S APCHSICL=10 D PRTICD
 Q
 ;
HOS ;  HISTORY OF SURGERY * 9000010.08 (V PROCEDURE) & V CPT ******* (APCHS6)
 K APCHHOSA,APCHHOSC
 I '$D(^AUPNVPRC("AC",APCHSPAT)),'$D(^AUPNVCPT("AC",APCHSPAT)),'$D(^AUPNVTC("AC",APCHSPAT)) G HOSX
 X APCHSCKP Q:$D(APCHSQIT)  X:'APCHSNPG APCHSBRK
 S APCHSCNT=0
 ; <DISPLAY>
 S APCHSIVD=0 F  S APCHSIVD=$O(^AUPNVPRC("AA",APCHSPAT,APCHSIVD)) Q:'APCHSIVD  D
 .S APCHSDFN=0 F  S APCHSDFN=$O(^AUPNVPRC("AA",APCHSPAT,APCHSIVD,APCHSDFN)) Q:'APCHSDFN  D
 ..S APCHSICD=$P(^AUPNVPRC(APCHSDFN,0),U)
 ..S APCHSN=^AUPNVPRC(APCHSDFN,0)
 ..D HOSCHK Q:APCHSICD=""
 ..S APCHSCNT=APCHSCNT+1
 ..S APCHCSVD=+^AUPNVSIT($P(APCHSN,U,3),0)\1
 ..D GETICDOP^APCHSUTL
 ..S Y=$P(APCHSN,U,3),Y=+^AUPNVSIT(Y,0)\1 X APCHSCVD S APCHSDAT=Y
 ..S APCHSNRQ=$P(APCHSN,U,4)
 ..I APCHSNRQ D GETNARR^APCHSUTL
 ..I APCHSNRQ="" S APCHSNRQ=$P($$ICDOP^ICDCODE($P(APCHSN,U,1),+^AUPNVSIT($P(APCHSN,U,3),0)\1),U,5)
 ..S APCHSDS="DATE?" D
 ...S Y=$P(APCHSN,U,6) I Y]"" X APCHSCVD S APCHSDS=Y Q
 ...S Y=(9999999-APCHSIVD) X APCHSCVD S APCHSDS=Y
 ..D GETOPRV
 ..S APCHHOSA(APCHSIVD,"PRC",APCHSDFN)=APCHSDS_U_APCHSNRQ_U_APCHSOP_U_APCHSICD
 ;now go through v cpt
 S APCHT=$O(^ATXAX("B","APCH HS MAJOR PROCEDURE CPTS",0))
 S APCHCPTI=0 F  S APCHCPTI=$O(^AUPNVCPT("AA",APCHSPAT,APCHCPTI)) Q:APCHCPTI'=+APCHCPTI  D
 .I '$$ICD^ATXCHK(APCHCPTI,APCHT,1) Q  ;not a cpt wanted on this component
 .S APCHSIVD=0 F  S APCHSIVD=$O(^AUPNVCPT("AA",APCHSPAT,APCHCPTI,APCHSIVD)) Q:APCHSIVD=""  D
 ..S APCHSIEN=0 F  S APCHSIEN=$O(^AUPNVCPT("AA",APCHSPAT,APCHCPTI,APCHSIVD,APCHSIEN)) Q:APCHSIEN'=+APCHSIEN  D
 ...S Y=(9999999-APCHSIVD) X APCHSCVD S APCHSDS=Y
 ...S APCHSN=^AUPNVCPT(APCHSIEN,0)
 ...S APCHSICD=$P(APCHSN,U,1)
 ...D GETCPT^APCHSUTL
 ...S APCHSNRQ=$P(APCHSN,U,4)
 ...I APCHSNRQ D GETNARR^APCHSUTL
 ...;cmi/anch/maw 8/28/2007 mods for CSV
 ...N APCHSVDT
 ...S APCHSVDT=$P(+^AUPNVSIT($P(APCHSN,U,3),0),".")
 ...;I APCHSNRQ="" S APCHSNRQ=$P(^ICPT($P(APCHSN,U,1),0),U,2)  cmi/anch/maw 8/28/2007 orig line
 ...I APCHSNRQ="" S APCHSNRQ=$P($$CPT^ICPTCOD($P(APCHSN,U,1),APCHSVDT),U,3)  ;cmi/anch/maw 8/28/2007 code set versioning
 ...;cmi/anch/maw 8/28/2007 end of mods
 ...S APCHHOSA(APCHSIVD,"CPT",APCHSIEN)=APCHSDS_U_APCHSNRQ_U_$S($P($G(^AUPNVCPT(APCHSIEN,12)),U,4):$$VAL^XBDIQ1(9000010.18,APCHSIEN,1204),1:$$VAL^XBDIQ1(9000010.18,APCHSIEN,1202))_U_APCHSICD
 ...S APCHHOSC(APCHSIVD,"CPT",$P(^ICPT($P(APCHSN,U,1),0),U,1))=""
 ;now get all tran codes hcpcs
 S APCHSIEN=0 F  S APCHSIEN=$O(^AUPNVTC("AC",APCHSPAT,APCHSIEN)) Q:APCHSIEN=""  D
 .Q:'$D(^AUPNVTC(APCHSIEN))
 .S V=$P(^AUPNVTC(APCHSIEN,0),U,3)
 .Q:'V
 .Q:'$D(^AUPNVSIT(V,0))
 .S V=$P($P(^AUPNVSIT(V,0),U),".")
 .S Y=V X APCHSCVD S APCHSDS=Y
 .S APCHSIVD=9999999-V
 .S APCHCPT=$$VAL^XBDIQ1(9000010.33,APCHSIEN,.07)
 .S APCHCPTI=$P(^AUPNVTC(APCHSIEN,0),U,7)
 .I '$$ICD^ATXCHK(APCHCPTI,APCHT,1) Q  ;not a cpt wanted on this component
 .Q:$D(APCHHOSC(APCHSIVD,"CPT",APCHCPT))
 .;S APCHSNRQ=$P(^ICPT(APCHCPTI,0),U,2)
 .S APCHSNRQ=$P($$CPT^ICPTCOD(APCHCPTI,V),U,3)
 .S APCHSICD=APCHCPTI
 .D GETCPT^APCHSUTL
 .S APCHHOSA(APCHSIVD,"CPT",APCHSIEN)=APCHSDS_U_APCHSNRQ_U_$S($P($G(^AUPNVTC(APCHSIEN,12)),U,4):$$VAL^XBDIQ1(9000010.33,APCHSIEN,1204),1:$$VAL^XBDIQ1(9000010.33,APCHSIEN,1202))_U_APCHSICD
 ;now display the procedures/cpt codes
 S APCHSIVD=0 F  S APCHSIVD=$O(APCHHOSA(APCHSIVD)) Q:APCHSIVD=""!($D(APCHSQIT))  D
 . X APCHSCKP Q:$D(APCHSQIT)
 . S APCHIEN=0 F  S APCHIEN=$O(APCHHOSA(APCHSIVD,"PRC",APCHIEN)) Q:APCHIEN'=+APCHIEN!($D(APCHSQIT))  D
 .. S APCHSOP=$P(APCHHOSA(APCHSIVD,"PRC",APCHIEN),U,3)
 .. S APCHSNRQ=$P(APCHHOSA(APCHSIVD,"PRC",APCHIEN),U,2)
 .. S APCHSDS=$P(APCHHOSA(APCHSIVD,"PRC",APCHIEN),U,1)
 .. S APCHSICD=$P(APCHHOSA(APCHSIVD,"PRC",APCHIEN),U,4)
 .. S BSDXTMP=APCHSDS_$$FILL^BSDX41(10-$L(APCHSDS))_$E(APCHSOP,1,15) S APCHSNTE="" S APCHSICL=26 D PRTICD
 . S APCHIEN=0 F  S APCHIEN=$O(APCHHOSA(APCHSIVD,"CPT",APCHIEN)) Q:APCHIEN'=+APCHIEN!($D(APCHSQIT))  D
 .. S APCHSOP=$P(APCHHOSA(APCHSIVD,"CPT",APCHIEN),U,3)
 .. S APCHSNRQ=$P(APCHHOSA(APCHSIVD,"CPT",APCHIEN),U,2)
 .. S APCHSDS=$P(APCHHOSA(APCHSIVD,"CPT",APCHIEN),U,1)
 .. S APCHSICD=$P(APCHHOSA(APCHSIVD,"CPT",APCHIEN),U,4)
 .. S BSDXTMP=APCHSDS_$$FILL^BSDX41(10-$L(APCHSDS))_$E(APCHSOP,1,15) S APCHSNTE="" S APCHSICL=26 D PRTICD
 I 'APCHSCNT X APCHSCKP Q:$D(APCHSQIT)  S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)="Minor procedures are on file but have not been displayed."_$C(30)
 ;
 ; now display refusals for icd procedures
 S APCHSFN=80.1,APCHST="PROCEDURE"
 S APCHSS="S %=0,APCHSICD=$P(^AUPNPREF(APCHSI,0),U,6) Q:'APCHSICD  D HOSCHK^APCHS6 I APCHSICD S %=1"
 D DISPREF
 S APCHSFN=81,APCHST="CPT"
 S APCHSS="S %=0,APCHCPT=$P(^AUPNPREF(APCHSI,0),U,6) Q:'APCHCPT  I $$ICD^ATXCHK(APCHCPT,$O(^ATXAX(""B"",""APCH HS MAJOR PROCEDURE CPTS"",0)),1) S %=1"
 D DISPREF
HOSX K APCHSFN,APCHSOP,APCHST,APCHSS,APCHSDFN,APCHSICD,APCHSNRQ,APCHSDAT,APCHSDS,APCHSICL,APCHSIVD,APCHSCOD,APCHSCNT,APCHSOPN,APCHSOP,Y
 K APCHHOSA,APCHHOSC
 Q
 ;
HOSCHK ;
 ;S APCHSCOD=+^ICD0(APCHSICD,0)  cmi/anch/maw
 S APCHSCOD=$P($$ICDOP^ICDCODE(APCHSICD),U,2)  ;cmi/anch/maw CSV
 I APCHSCOD\1>85 S APCHSICD="" Q
 I APCHSCOD=69.7 S APCHSICD="" Q
 I APCHSCOD\1=23 S APCHSICD="" Q
 I APCHSCOD\1=24 S APCHSICD="" Q
 I $E(APCHSCOD,1,4)="38.9" S APCHSICD="" Q
 I APCHSCOD=73.09 S APCHSICD="" Q
 I APCHSCOD="38.29" S APCHSICD="" Q  ;blood draw
 I APCHSCOD="57.94" S APCHSICD="" Q  ;insertion of urinary catheter
 Q
GETOPRV ;get Operating Prov
 NEW APCHSOPN
 S APCHSOP=""
 S APCHSOPN=$P(APCHSN,U,11)
 Q:'+APCHSOPN
 S APCHSOP=$E($S($P($G(^AUTTSITE(1,0)),U,22):$P(^VA(200,APCHSOPN,0),U),1:$P(^DIC(16,APCHSOPN,0),U)),1,15) ;provider name
 Q
 ;;
 ;
CPTALL ;EP - display all cpt codes, date limits are applicable
 I '$D(^AUPNVCPT("AA",APCHSPAT)),'$D(^AUPNVTC("AC",APCHSPAT)) Q
 ; <DISPLAY>
 K APCHCPTA
 S APCHCPTI=0 F  S APCHCPTI=$O(^AUPNVCPT("AA",APCHSPAT,APCHCPTI)) Q:APCHCPTI=""  D
 .S APCHSIVD="" F  S APCHSIVD=$O(^AUPNVCPT("AA",APCHSPAT,APCHCPTI,APCHSIVD)) Q:APCHSIVD=""!(APCHSIVD>APCHSDLM)  D
 ..S APCHIEN=0 F  S APCHIEN=$O(^AUPNVCPT("AA",APCHSPAT,APCHCPTI,APCHSIVD,APCHIEN)) Q:APCHIEN'=+APCHIEN  D
 ...S APCHCPT=$$VAL^XBDIQ1(9000010.18,APCHIEN,.01)
 ...S APCHCPTA(APCHSIVD,APCHCPT,APCHIEN)=$P($$CPT^ICPTCOD(APCHCPTI,(9999999-APCHSIVD)),U,3)_U_$$VAL^XBDIQ1(9000010.18,APCHIEN,.16)_U_$$VALI^XBDIQ1(9000010,$P(^AUPNVCPT(APCHIEN,0),U,3),.06)
 ...S Y=$$VALI^XBDIQ1(9000010,$P(^AUPNVCPT(APCHIEN,0),U,3),.08) S $P(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN),U,4)=Y
 ;now get tran codes
 S APCHIEN=0 F  S APCHIEN=$O(^AUPNVTC("AC",APCHSPAT,APCHIEN)) Q:APCHIEN=""  D
 .Q:'$D(^AUPNVTC(APCHIEN))
 .S V=$P(^AUPNVTC(APCHIEN,0),U,3)
 .Q:'V
 .Q:'$D(^AUPNVSIT(V,0))
 .S V=$P($P(^AUPNVSIT(V,0),U),".")
 .S APCHSIVD=9999999-V
 .Q:APCHSIVD>APCHSDLM
 .S APCHCPT=$$VAL^XBDIQ1(9000010.33,APCHIEN,.07)
 .Q:APCHCPT=""
 .S APCHCPTI=$P(^AUPNVTC(APCHIEN,0),U,7)
 .Q:$D(APCHCPTA(APCHSIVD,APCHCPT))
 .S APCHCPTA(APCHSIVD,APCHCPT,APCHIEN)=$P($$CPT^ICPTCOD(APCHCPTI,(9999999-APCHSIVD)),U,3)_U_1_U_$$VALI^XBDIQ1(9000010,$P(^AUPNVTC(APCHIEN,0),U,3),.06)
 .S Y=$$VALI^XBDIQ1(9000010,$P(^AUPNVTC(APCHIEN,0),U,3),.08) S $P(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN),U,4)=Y
 G:'$D(APCHCPTA) CPTALLX
 X APCHSCKP Q:$D(APCHSQIT)
 X:'APCHSNPG APCHSBRK
 S APCHSIVD=0 F  S APCHSIVD=$O(APCHCPTA(APCHSIVD)) Q:APCHSIVD=""!($D(APCHSQIT))  D
 .X APCHSCKP Q:$D(APCHSQIT)  I APCHSNPG D
 ..S BSDXTMP=$$FILL^BSDX41(27)_"CODE"
 ..S BSDXTMP=BSDXTMP_$$FILL^BSDX41(34-$L(BSDXTMP))_"CPT NARRATIVE"
 ..S BSDXTMP=BSDXTMP_$$FILL^BSDX41(72-$L(BSDXTMP))_"UNITS"
 ..S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 .S BSDXTMP=$$DATE^APCHSMU((9999999-APCHSIVD))
 .S APCHCPT="" F  S APCHCPT=$O(APCHCPTA(APCHSIVD,APCHCPT)) Q:APCHCPT=""!($D(APCHSQIT))  D
 ..S APCHIEN=0 F  S APCHIEN=$O(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN)) Q:APCHIEN'=+APCHIEN!($D(APCHSQIT))  D
 ...X APCHSCKP Q:$D(APCHSQIT)  I APCHSNPG D
 ....S BSDXTMP=BSDXTMP_$$FILL^BSDX41(28-$L(BSDXTMP))_"CODE"
 ....S BSDXTMP=BSDXTMP_$$FILL^BSDX41(35-$L(BSDXTMP))_"CPT NARRATIVE"
 ....S BSDXTMP=BSDXTMP_$$FILL^BSDX41(72-$L(BSDXTMP))_"UNITS"
 ....S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 ....S BSDXTMP=""
 ...S %=$P(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN),U,3)
 ...I % S BSDXTMP="        "_$P($G(^AUTTLOC(%,0)),U,2)
 ...S %=$P(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN),U,4)
 ...I % S BSDXTMP=BSDXTMP_$$FILL^BSDX41(22-$L(BSDXTMP))_$P($G(^DIC(40.7,%,9999999)),U)
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(28-$L(BSDXTMP))_APCHCPT
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(35-$L(BSDXTMP))_$E($P(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN),U,1),1,36)
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(73_$L(BSDXTMP))_$P(APCHCPTA(APCHSIVD,APCHCPT,APCHIEN),U,2)
 ...S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 ;
 ;display CPT refusals
 S APCHST="CPT",APCHSFN=81 D DISPREF
 K APCHST,APCHSFN
CPTALLX K APCHSIVD,APCHSDAT,APCHCPT,APCHIEN,APCHCPTA,APCHCPTI
 Q
 ;
MREDISP ;
 S APCHSIVD=0,APCHSIVD=$O(APCHCPTA(APCHCPT,APCHSIVD)) D
 .S APCHIEN=0,APCHIEN=$O(APCHCPTA(APCHCPT,APCHSIVD,APCHIEN)) D
 ..X APCHSCKP Q:$D(APCHSQIT)  I APCHSNPG D
 ...S BSDXTMP="CODE"_"  "_"DATE"
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(17-$L(BSDXTMP))_"CPT NARRATIVE"
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(54-$L(BSDXTMP))_"UNITS"
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(60-$L(BSDXTMP))_"FACILITY"
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(74-$L(BSDXTMP))_"CLN"
 ...S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 ..S BSDXTMP="      "_$$DATE^APCHSMU((9999999-APCHSIVD))
 ..S BSDXTMP=BSDXTMP_$$FILL^BSDX41(17-$L(BSDXTMP))_$E($P(APCHCPTA(APCHCPT,APCHSIVD,APCHIEN),U,1),1,35)
 ..S BSDXTMP=BSDXTMP_$$FILL^BSDX41(54-$L(BSDXTMP))_$P(APCHCPTA(APCHCPT,APCHSIVD,APCHIEN),U,2)
 ..S %=$P(APCHCPTA(APCHCPT,APCHSIVD,APCHIEN),U,3)
 ..I % S BSDXTMP=BSDXTMP_$$FILL^BSDX41(60-$L(BSDXTMP))_$P($G(^AUTTLOC(%,0)),U,2)
 ..S %=$P(APCHCPTA(APCHCPT,APCHSIVD,APCHIEN),U,4)
 ..I % S BSDXTMP=BSDXTMP_$$FILL^BSDX41(74-$L(BSDXTMP))_$P($G(^DIC(40.7,%,9999999)),U)
 ..S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 .Q
 Q
 ;
PRTICD ;ENTRY POINT
 I APCHSICF="N" S:APCHSNRQ="" APCHSNRQ="<no narrative provided>" S APCHSICD=""
 S APCHSTXT=$G(APCHSICD)
 S:'$D(APCHSNTE) APCHSNTE=""
 I APCHSNTE]"" S APCHSNTE=" "_APCHSNTE
 D PRTTXT
 Q
 ;
PRTTXT ;PEP - PUBLISHED ENTRY POINT
 ; GENERALIZED TEXT PRINTER
 S:'$D(APCHSNTE) APCHSNTE=""
 S APCHSDLT=1,APCHSILN=IOM-APCHSICL-1
 F APCHSQ=0:0 D PRTTXT1 Q:APCHSTXT=""  D PRTTXT2
 K APCHSNTE
 K APCHSILN,APCHSDLT,APCHSF,APCHSC,APCHSTXT
 Q
PRTTXT1 ;
 S:APCHSNRQ]""&(($L(APCHSNRQ)+$L(APCHSTXT)+2)<255) APCHSTXT=$S(APCHSTXT]"":APCHSTXT_"; ",1:"")_APCHSNRQ,APCHSNRQ=""
 S:APCHSNTE]""&(APCHSNRQ="")&(($L(APCHSNTE)+$L(APCHSTXT)+2)<255) APCHSTXT=APCHSTXT_APCHSNTE,APCHSNTE=""
 Q
PRTTXT2 D GETFRAG
 ;X APCHSCKP Q:$D(APCHSQIT)
 S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$$FILL^BSDX41(APCHSICL-$L(BSDXTMP))_APCHSF_$C(30)
 S APCHSICL=APCHSICL+APCHSDLT,APCHSILN=APCHSILN-APCHSDLT,APCHSDLT=0
 Q
GETFRAG I $L(APCHSTXT)<APCHSILN S APCHSF=APCHSTXT,APCHSTXT="" Q
 F APCHSC=APCHSILN:-1:0 Q:$E(APCHSTXT,APCHSC)=" "
 S:APCHSC=0 APCHSC=APCHSILN
 S APCHSF=$E(APCHSTXT,1,APCHSC-1),APCHSTXT=$E(APCHSTXT,APCHSC+1,255)
 Q
DISPREF ;EP
 X APCHSCKP Q:$D(APCHSQIT)
 S APCHSRC=0
 S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 S APCHSX="" F  S APCHSX=$O(^AUPNPREF("AA",APCHSPAT,APCHSFN,APCHSX)) Q:APCHSX=""!($D(APCHSQIT))  D
 .S APCHSD=0 F  S APCHSD=$O(^AUPNPREF("AA",APCHSPAT,APCHSFN,APCHSX,APCHSD)) Q:APCHSD=""!(APCHSD>APCHSDLM)!($D(APCHSQIT))  D
 ..S APCHSI=0 F  S APCHSI=$O(^AUPNPREF("AA",APCHSPAT,APCHSFN,APCHSX,APCHSD,APCHSI)) Q:APCHSI=""!($D(APCHSQIT))  D
 ...I $D(APCHSS) X APCHSS Q:'%
 ...S APCHSRC=APCHSRC+1
 ...I APCHSRC=1 I APCHST]"" S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30) S BSDXTMP=APCHST_" Refusals "
 ...X APCHSCKP Q:$D(APCHSQIT)
 ...S BSDXTMP=$$VAL^XBDIQ1(9000022,APCHSI,.04)_" -- "_$$VAL^XBDIQ1(9000022,APCHSI,.07)
 ...S BSDXTMP=BSDXTMP_$$FILL^BSDX41(60-$L(BSDXTMP))_"("_$$DATE^APCHSMU(9999999-APCHSD)_")"
 ...S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 ..Q
 .Q
 S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 K APCHST,APCHSX,APCHSD,APCHSS,APCHSFN,APCHSI
 Q
