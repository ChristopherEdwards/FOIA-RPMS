BSDX41P ; IHS/OIT/HMW/MSC/SAT - WINDOWS SCHEDULING RPCS ;
 ;;3.0;IHS WINDOWS SCHEDULING;;DEC 09, 2010
 ;
RAD ; ******************* RAD TESTS - ALL * 9000010.12 *******
 ; <SETUP>
 Q:'$D(^AUPNVRAD("AA",APCHSPAT))
 K APCHSRRT
 X APCHSBRK
 ; <DISPLAY>
 S APCHST="" F APCHSQ=0:0 S APCHST=$O(^AUPNVRAD("AA",APCHSPAT,APCHST)) Q:APCHST=""  S APCHSTX=$P(^RAMIS(71,APCHST,0),U,1),APCHSTL=$L(APCHSTX) X APCHSCKP Q:$D(APCHSQIT)  D RADBLD
 ; <CLEANUP>
 ;now display RAD refusals
 S APCHST="RADIOLOGY EXAM",APCHSFN=71 D DISPREF^BSDX41F
 K APCHST,APCHSFN
RADX K APCHST,APCHSTX,APCHSTL,APCHSIVD,APCHSDFN,APCHSRDG,APCHSVDF,APCHSDAT,X,Y
 K APCHSNFL,APCHSNSH,APCHSNAB,APCHSVSC,APCHSITE
 Q
RADBLD S APCHSIVD="" F APCHSQ=0:0 S APCHSIVD=$O(^AUPNVRAD("AA",APCHSPAT,APCHST,APCHSIVD)) Q:APCHSIVD=""!(APCHSIVD>APCHSDLM)  D RADBLD1
 Q
RADBLD1 ;S Y=-APCHSIVD\1+9999999 X APCHSCVD S APCHSDAT=Y
 ;X APCHSCKP Q:$D(APCHSQIT)
 S Y=-APCHSIVD\1+9999999 X APCHSCVD S APCHSDAT=Y
 S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=$C(30)
 S BSDXTMP=APCHSDAT
 S APCHSDFN=0 F APCHSQ=0:0 S APCHSDFN=$O(^AUPNVRAD("AA",APCHSPAT,APCHST,APCHSIVD,APCHSDFN)) Q:'APCHSDFN  D
 .Q:'$D(^AUPNVRAD(APCHSDFN,0))
 .S APCHSEDT=$P($P($G(^AUPNVRAD(APCHSDFN,12)),U,1),".")
 .D RADDSP
 Q
RADDSP ;
 S APCHS0=$P(^AUPNVRAD(APCHSDFN,0),U,1)
 S APCHSRTX=$P(^RAMIS(71,$P(APCHS0,U),0),U,1)
 S BSDXTMP=BSDXTMP_$$FILL^BSDX41(10-$L(BSDXTMP))_APCHSRTX
 I APCHSEDT]"",APCHSEDT'=9999999-APCHSIVD S BSDXTMP=BSDXTMP_"  ("_$$FMTE^XLFDT(APCHSEDT,5)_")"
 S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$C(30)
 S BSDXTMP=""
 I $P(APCHS0,U,5)]"" S BSDXTMP=$$FILL^BSDX41(10)_"RESULT:  " S APCHSDCD=$P(APCHS0,U,5) S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=BSDXTMP_$S(APCHSDCD]"":APCHSDCD,1:"<none recorded>")_$C(30)
 I $P(APCHS0,U,6)]"" S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)="  "_"Diagnostic Code: "_$$VAL^XBDIQ1(9000010.22,APCHSDFN,.06)_$C(30)
 I $G(^AUPNVRAD(APCHSDFN,11))]"" S BSDXTMP=$$FILL^BSDX41(10)_"IMPRESSION:  " S APCHSICL=12,APCHSNRQ=$G(^AUPNVRAD(APCHSDFN,11)),APCHSTXT="",APCHSICD="" D PRTTXT^BSDX41F
 I $G(^AUPNVRAD(APCHSDFN,11))="" S BSDXI=BSDXI+1 S ^BSDXTMP($J,BSDXI)=$C(30)
 K APCHSTXT,APCHSNRQ
 Q
 ;
