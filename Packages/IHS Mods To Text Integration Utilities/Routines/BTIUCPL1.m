BTIUCPL1 ; IHS/MSC/JS - TIU V Care Plan Object cont. ;02-Jan-2014 15:53;DU
 ;;1.0;TEXT INTEGRATION UTILITIES;**1012,1013**;MAR 20, 2013;Build 33
 ;5/22/13 - EHR TEST WITH HARDSET 'PARAM' = 265 (PROBLEM IEN)
 ;
OUT ; set report data @TARGET for object report
 N NODE,PROBDX,STRING
 I FNUM=9000092 D
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="--------------  Care Plan ------------"
 .N PRIEN S PRIEN=+$G(CPLARR(FNUM,IEN_",",.01,"E")) D
 ..I PRIEN N ICD9IEN S ICD9IEN=+$G(^AUPNPROB(PRIEN,0))
 ..;I ICD9IEN S PROBDX=$P($G(^ICD9(ICD9IEN,0)),U,3)
 ..I ICD9IEN S PROBDX=$$GET1^DIQ(9000011,PRIEN,.05)
 ..N PROBSTAT S PROBSTAT=$G(CPLARR(FNUM_".11",IEN_",",1,"E"))
 ..S CNT=CNT+1
 ..S @TARGET@(CNT,0)=$G(PROBDX)_STRPAD_$G(PROBSTAT)
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=SPACE
 .S CNT=CNT+1
 .N SIGNBY S SIGNBY=$G(CPLARR(FNUM,IEN_",",.07,"E"))
 .N SIGNDT S SIGNDT=$G(CPLARR(FNUM,IEN_",",.08,"E")) S SIGNDT=$P(SIGNDT,"@",1)
 .S @TARGET@(CNT,0)="GOALS - "_$G(SIGNDT)_" by "_$G(SIGNBY)
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="CARE PLAN INSTRUCTION -"
 .S STRING="",NODE=0
 .F  S NODE=$O(CPLARR(FNUM,IEN_",","1200",NODE)) Q:NODE'?1N  D
 ..S STRING=$G(CPLARR(FNUM,IEN_",","1200",NODE))
 ..S CNT=CNT+1
 ..S @TARGET@(CNT,0)=STRPAD_STRING
 ..Q
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=SPACE
 I FNUM=9000010.58 D
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="-------------- Visit Instructions --------------"
 .S CNT=CNT+1
 .N INSTDT S INSTDT=$G(CPLARR(FNUM,IEN_",",.05,"E"))
 .N INSTBY S INSTBY=$G(CPLARR(FNUM,IEN_",",.04,"E"))
 .S @TARGET@(CNT,0)="VISIT INSTRUCTIONS - "_INSTDT_" by "_INSTBY
 .S STRING="",NODE=0
 .F  S NODE=$O(CPLARR(FNUM,IEN_",","1100",NODE)) Q:NODE'?1N  D
 ..S STRING=$G(CPLARR(FNUM,IEN_",","1100",NODE))
 ..S CNT=CNT+1
 ..S @TARGET@(CNT,0)=STRPAD_STRING
 ..Q
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=SPACE
 I FNUM=9000010.61 D
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="--------------------- Treatment/Regimen --------------------"
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="TX/REGIMEN/FU"
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_$G(CPLARR(FNUM,IEN_",",.01,"E"))
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=SPACE
 I FNUM=9000010.16 D
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="--------------------- Patient Education --------------------"
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="EDUCATION"
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_$G(CPLARR(FNUM,IEN_",",.01,"E"))
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_$G(CPLARR(FNUM,IEN_",",81101,"E"))
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=SPACE
 I FNUM=123 D
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)="-------------------- Consults -----------------------------"
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_"Date of Request - "_CPLARR(FNUM,IEN_",","3","E")
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_"Provider - "_CPLARR(FNUM,IEN_",","10","E")
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_"Procedure - "_CPLARR(FNUM,IEN_",","4","E")
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=STRPAD_"To Service - "_CPLARR(FNUM,IEN_",","1","E")_" Attention: "_CPLARR(FNUM,IEN_",","7","E")
 .S CNT=CNT+1
 .S @TARGET@(CNT,0)=SPACE
