BTIUPCC6 ; IHS/ITSC/LJF - IHS PCC OBJECTS ;03-Aug-2009 15:59;MGH
 ;;1.0;TEXT INTEGRATION UTILITIES;**1006**;NOV 04, 2004
BMI(DFN,TIUCAP,NUM) ;EP -- returns BMI based on last ht and wt
 ; TIUCAP=1 if caption with measurement name is to be returned
 N VTWT,VTHT,RSWT,RSHT,BMI,WTDT,RMAX,X,IDT,HNUM,START,END,WNUM,IDATE,RCNT,DATE,WTDT,RESULT,LINE,APCHMDT
 S VTWT=$$VTYPE("WT"),VTHT=$$VTYPE("HT")
 K ^TMP("BTIUVM",$J),^TMP("BTIUPCC",$J)
 I '$D(NUM) S NUM=1
 S WNUM=NUM+2,HNUM=NUM+10,RMAX=NUM
 S START=DT+1,END=0
 S:START<END X=START,START=END,END=X
 S START=9999999-$S(START#1:START,1:START+.9),END=9999999-END
 S RCNT=0,IDT=START
 ;PCTILE=+G(PCTILE)
 D BLDXRF(VTWT,NUM),BLDXRF(VTHT,NUM)
 F  Q:'IDT!(RCNT=RMAX)  D
 .S VIEN=$C(1)
 .F  S VIEN=$O(^TMP("BTIUVM",$J,VTWT,IDT,VIEN),-1) Q:'VIEN  D  Q:RCNT=RMAX
 ..D GETMSR(VIEN,.RSWT,.DATE,.LOC,.ENTERBY)
 ..;IHS/MSC/MGH Save the wt date to use if the ht isn't too old patch 4
 ..S WTDT=DATE
 ..S RSHT=$$FNDHT(IDT)
 ..Q:'RSHT
 ..S RSWT=RSWT*.45359,RSHT=RSHT*.0254,RSHT=RSHT*RSHT,BMI=+$J(RSWT/RSHT,0,2)
 ..;S:PCTILE BMI=$$BMIPCT(BMI,DFN,DATE)
 ..Q:'BMI
 ..S RESULT(IDT)=BMI,RCNT=RCNT+1
 ..S DATE=WTDT
 .S IDT=$O(^TMP("BTIUVM",$J,VTWT,IDT))
 S CNT=0
 S IDATE="" F  S IDATE=$O(RESULT(IDATE)) Q:IDATE=""!(CNT>NUM)  D
 .S BMI=$G(RESULT(IDATE))
 .S DATE=9999999-IDATE,DATE=$$FMTE^XLFDT(DATE)
 .S CNT=CNT+1
 .S LINE=BMI_" ("_$P(DATE,"@",1)_")"
 .I TIUCAP S Y=$S(CNT=1:"BMI:",1:$$SP(4)) S LINE=Y_" "_LINE
 .S ^TMP("BTIUPCC",$J,CNT,0)=LINE
 I '$D(^TMP("BTIUPCC",$J)) Q "No bmi calculated"
 Q "~@^TMP(""BTIUPCC"",$J)"
FNDHT(IDT) ;Find closest height before weight
 N VIEN,RESULT,DOB,SEX,TAGE,X2,X1,SX
 S VIEN=$O(^TMP("BTIUVM",$J,VTHT,IDT,""))
 I 'VIEN D
 .;IHS/MSC/MGH Added in HS logic for lookback days for ht. Patch 4
 .N ID1,ID2,GOOD
 .S DOB=$P(^DPT(DFN,0),U,3),X2=DOB,X1=DT D ^%DTC S TAGE=X
 .S ID1=$O(^TMP("BTIUVM",$J,VTHT,IDT),-1),ID2=$O(^TMP("BTIUVM",$J,VTHT,IDT))
 .I ID1 D
 ..S X1=$P(ID1,".",1),X2=$P(IDT,".",1)
 ..I X1=X2 S ID2=ID1
 .I ID2 D
 ..S X1=9999999-(ID2\1)
 ..S APCHMDT=$$FMDIFF^XLFDT(X1,DOB,1)
 ..S X1=9999999-(IDT\1),X2=9999999-ID2
 ..S X=$$FMDIFF^XLFDT(X1,X2,1)
 ..I ((X>90)&(APCHMDT<1096))!((X>180)&(APCHMDT<4381))!((X>365)&(APCHMDT<6571))!((APCHMDT<6571)&(TAGE>6571)) S ID2=""
 ..S:ID2 VIEN=$O(^TMP("BTIUVM",$J,VTHT,ID2,""))
 D:VIEN GETMSR(VIEN,.RESULT)
 Q $G(RESULT)
GETMSR(VIEN,RESULT,DATE,LOC,ENTERBY) ;
 N X,X12
 S X=$G(^AUPNVMSR(VIEN,0)),X12=$G(^(12))
 S DATE=+X12,ENTERBY=+$P(X12,U,4)
 S RESULT=$$TRIM^XLFSTR($P(X,U,4)),X=+$P(X,U,3)
 S X=$G(^AUPNVSIT(X,0))
 S:'DATE DATE=+X
 S LOC=+$P(X,U,22),DATE(0)=DATE*10000\1/10000
 Q
 ; Build temp xref for measurement type
BLDXRF(VTYP,VNUM) ;
 N X,Y,Z,CNT
 S X=0
 K ^TMP("BTIUVM",$J,VTYP)
 S CNT=0
 F  S X=$O(^AUPNVMSR("AA",DFN,VTYP,X)),VIEN=0 Q:'X!(CNT>VNUM)  D
 .F  S VIEN=$O(^AUPNVMSR("AA",DFN,VTYP,X,VIEN)) Q:'VIEN  D
 ..S Z=$G(^AUPNVMSR(VIEN,0)),Y=+$G(^(12)),Y=$S(Y:9999999-Y,1:X)
 ..Q:+Z'=VTYP
 ..Q:$P(Z,U,2)'=DFN
 ..S CNT=CNT+1
 ..S ^TMP("BTIUVM",$J,VTYP,Y,VIEN)=""
 Q
VTYPE(X,VMSR) ;
 N FNUM
 S:'$D(VMSR) VMSR=$$VMSR
 S FNUM=$S(VMSR:9999999.07,1:120.51)
 Q +$$FIND1^DIC(FNUM,"","X",$$UP^XLFSTR(X),"B^"_$S(VMSR:"D",1:"APCE^C"))
 ; Returns true if V file is used for vital measurements
VMSR() Q ''$$GET^XPAR("ALL","BEHOVM USE VMSR")
PAD(DATA,LENGTH) ; -- SUBRTN to pad length of data
 Q $E(DATA_$$REPEAT^XLFSTR(" ",LENGTH),1,LENGTH)
 ;
SP(NUM) ; -- SUBRTN to pad spaces
 Q $$PAD(" ",NUM)
