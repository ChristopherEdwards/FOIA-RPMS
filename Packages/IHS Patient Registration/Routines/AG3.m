AG3 ; IHS/ASDS/EFG - ENTRY UTILITY FOR NON-MANDATORY DATA ; 
 ;;7.1;PATIENT REGISTRATION;**2,4**;JAN 31, 2007
 ;
 W !!,">>This option asks all questions one after the other with no escaping<<",!!
L1 D S1,SSN^AG3A G:$D(DUOUT) S1
 I $D(DUOUT)!(X="") G:AGOPT(14)'="Y" L4 W !,"Without a Social Security Number a BIC cannot be issued - try again!" D S1,SSN^AG3A G S1:$D(DUOUT)
 ;IHS/SD/TPF 4/24/2006 AG*7.1*2 ITEM 12 PAGE 11
 ;ASK MARITAL STATUS
ASKMSTAT ;
 K DIE,DR,DIC,DIR,DA
 S DIE="^DPT("
 S DR=.05
 S DA=AGPATDFN
 D ^DIE
 G:$D(DUOUT)!($D(DTOUT))!($D(DIROUT)) L1
 ;END NEW CODE
L4 ;D S1,COB^AG3A G L1:$D(DUOUT)
 D S1,COB^AG3A G ASKMSTAT:$D(DUOUT)  ;IHS/SD/TPF 4/24/2006 AG*7.1*2 ITEM 12 PAGE 11
L5 D S1,SOB^AG3A G L4:$D(DUOUT)
 ;IHS/SD/TPF 4/24/2006 AG*7.1*2 ITEM 5 PAGE 11
ASKWEB ;
 D WEB^AGED1
 ;END NEW CODE
L6 ;D S1,ST^AG3A G L5:$D(DUOUT),L10:X=""
 D S1,ST^AG3A G ASKWEB:$D(DUOUT),L10:X=""  ;IHS/SD/TPF 4/24/2006 AG*7.1*2 ITEM 5 PAGE 11
L7 D S1,CITY^AG3A G L6:$D(DUOUT)
L8 D S1,STATE^AG3A G L7:$D(DUOUT)
L9 D S1,ZIP^AG3A G L8:$D(DUOUT)
L10 D S1,HPH^AG3A G L9:$D(DUOUT)
 ;BAR*71.*4 SCR D151
 W !!
 W !,$G(NEWST)
 W !,$G(NEWCITY),",",$G(NEWSTATE),"  ",$G(NEWZIP)
 W !,$G(NEWHPH)
 W !!
 N DUOUT,DTOUT,DFOUT
 K DIR
 S DIR(0)="YO"
 S DIR("A")="Is this address correct"
 S DIR("B")="Y"
 D ^DIR
 G:'Y!$D(DTOUT)!$D(DUOUT)!$D(DIRUT) L6
 W !!,"Adding to PREVIOUS MAIL ADDRESSES FIELD...." H 2
 D UPDTHADD^AGUTILS(DFN,"N")  ;N SIGNIFIES THIS IS AN EDIT AND NOT A POST INSTALL FORCE
 ;END BAR*7.1*4
L11 D S1,OPH^AG3A G L10:$D(DUOUT)
L11A D S1,MSGPH^AGED1 G L11:$D(DUOUT)  ;IHS/SD/TPF 3/13/2008 AG*7.1*4 SCR
L12OPT I AGOPT(3)="Y" D S1,ALIAS^AGOPT2 G L11:$D(DUOUT) I 1
 E  G L11:$D(DUOUT)
 G ^AG4
S1 K DFOUT,DTOUT,DUOUT,DLOUT,DQOUT Q
DUOUT ;EP
 G L12OPT
