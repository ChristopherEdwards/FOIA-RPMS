AGADDREG ; IHS/ASDS/EFG - ADDITIONAL REGISTRATION INFO ;   
 ;;7.1;PATIENT REGISTRATION;;AUG 25,2005
 ;
 I AGOPT(9)'="Y" G END
VAR D DRAW
 I $D(AGSEENLY) K DIR S DIR("A")="Press RETURN " D READ^AGED1
 Q:$D(AGSEENLY)
 K DIR
 W !
 S DIR("A")="Do you wish to "_$S($Y<8:"enter",1:"edit")_" ADDITIONAL REGISTRATION INFORMATION? (Y/N) "
 S DIR("B")="N"
 D READ^AGED1
 Q:$D(DTOUT)!(Y="/.,")!(Y="^^")
 G END:Y=""!(Y="N")!(Y="^")
 G EDIT:Y="Y"!(Y="y")
 G:$D(AG("ED"))&'$D(AGXTERN) @("^AGED"_AG("ED"))
DRAW ;EP
 S AG("PG")=8 D ^AGED Q:'$O(^AUPNPAT(DFN,13,0))
 W !,$$CJ^XLFSTR("ADDITIONAL REGISTRATION INFO",IOM),!
 W ! F I=0:0 S I=$O(^AUPNPAT(DFN,13,I)) Q:'I  W ^(I,0),!
 Q
EDIT S DIE="^AUPNPAT(",DR=1301,DA=DFN D ^DIE,UPDATE1^AGED(DUZ(2),DFN,11,"")
END K AG,DTOUT,DQOUT,DFOUT,DLOUT,DA,DR,Y
 Q
ADD ;Add a Patient.
 I AGOPT(9)="Y" W ! S DIE="^AUPNPAT(",DA=DFN,DR=1301 D ^DIE
