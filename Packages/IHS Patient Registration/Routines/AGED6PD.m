AGED6PD ;IHS/ASDS/TPF - EDIT/DISPLAY RAILROAD PHARMACY PAGE ;    
 ;;7.1;PATIENT REGISTRATION;**1,2**;JAN 31, 2007
 ;
 ;
EN(COMPIEN) ;EP - CALLED BY AGED6
 ;
 S ID0=$P(COMPIEN,",")
 S ID1=$P(COMPIEN,",",3)
 S AG("PG")="4RRED"
 S ROUTID=$P($T(+1)," ")
 S CALLER="SCREEN"
VAR ;
 I $G(EXIT) K EXIT Q
 D DRAW
 I $D(AGSEENLY) K DIR S DIR(0)="E",DIR("A")="Enter Response" D ^DIR Q
 W !,AGLINE("EQ")
 K DIR
 I $D(^XUSEC("AGZMGR",DUZ)) D
 .S DIR("A")="ENTER ACTION (<E>dit a field,<D>elete eligibility date:"
 .S DIR(0)="SAO^E:EDIT;D:DELETE"
 E  S DIR("A")="ENTER ACTION <E>dit a field:",DIR(0)="SAO^E:EDIT"
 D ^DIR
 Q:Y=$G(AGOPT("ESCAPE"))
 I '$D(AGSEENLY) I $D(MYERRS("C","E")),(Y'?1N.N),(Y'="E"),(Y'="V"),(Y'="A"),(Y'="D") W !,"ERRORS ON THIS PAGE. PLEASE EDIT BEFORE EXITING!!" H 3 G VAR
 I Y="" D CLEAN(ID0) Q
 Q:$D(DTOUT)!$D(DUOUT)!$D(DIROUT)
 I Y="E" D EDIT G VAR
 I Y="D" D EDIT G VAR
 W !,"COMMAND "_Y_" UNKNOWN!" H 3
 G VAR
EDIT ;EP
 K DIR
 S DIR("A")="CHANGE which item? (1-"_$G(AG("N"))_") NONE// "
 S DIR(0)="LO^1:"_$G(AG("N"))
 D ^DIR
 I Y=$G(AGOPT("ESCAPE")) S EXIT=1 Q
 S CHOICES=Y
 I '$D(AGSEENLY) I ($D(MYERRS("C","E"))&(Y'?1N.N)),(Y'["V"),(Y'=$G(AGOPT("ESCAPE"))) W !,"ERRORS ON THIS PAGE. PLEASE FIX BEFORE EXITING!!" H 3 Q
 Q:Y=$G(AGOPT("ESCAPE"))
 Q:$D(DLOUT)!(Y["N")!$D(DUOUT)
 Q:$D(DFOUT)!$D(DTOUT)
 I $D(DQOUT)!(+Y<1)!(+Y>AG("N")) W !!,"You must enter a number from 1 to ",AG("N") H 2 Q
 S AGY=Y
 F AGI=1:1 S AG("SEL")=+$P(AGY,"|",AGI) Q:AG("SEL")<1!(AG("SEL")>AG("N"))  D
 .I AG("SEL")>12 D EDITPOLM^AGEDPRVI(.POLMEMBS,CHOICES) Q
 .D @($P(AG("C"),"|",AG("SEL")))
 ;AFTER EDITING THE SELECTION MUST BE UPDATED SO ANY ERRORS CORRECTED WILL BE REFLECTED ON THE REDRAWN SCREEN
 S COMPIEN=ID0_",11,"_ID1_","_0
 ;S AGSELECT=$$UPDTSEL^AGUTILS("FINDPVT",.AGINS,COMPIEN)
 S AGSELECT=$$UPDTSEL^AGUTILS(.AGINS,COMPIEN,$P(AGSELECT,U,2))  ;AG*7.1*1 IM18549 ERROR IN ERROR MSG UPDATE  
 D UPDATE1^AGED(DUZ(2),ID0,3,"")
 K AGI,AGY
 Q
CLEAN(ID0) ;EP - CLEAN EMPTY RECORD. IF NO PRIVATE INSURER HAS BEEN ENTERED,
 ;THE RECORD IS MEANINGLESS
 I '$O(^AUPNPRVT(ID0,11,0)) D CLEANZER(ID0)
 Q
CLEANZER(ID0) ;EP - CLEAN ZERO NODE WITH NO INFOR
 K DIK,DA
 S DIK="^AUPNPRVT(",DA=ID0 D ^DIK
 Q
END ;EP - CLEAN UP THE VARS
 K AG,DLOUT,DTOUT,DFOUT,DQOUT,DA,DIC,DR,AGSCRN,Y
 K ADA,WDA,ADT,WDT,REC,NEWENTRY,POLPTR,ID0,ID1,CALLER
 Q
DRAW ;EP - MAIN SCREEN DRAW
 D HDR
 D GETAW
 Q
HDR ;
 S AGPAT=$P($G(^DPT(ID0,0)),U)
 S AGCHRT=$S($D(^AUPNPAT(ID0,41,DUZ(2),0)):$P($G(^AUPNPAT(ID0,41,DUZ(2),0)),U,2),1:"xxxxx")
 S AG("AUPN")=""
 S:$D(^AUPNPAT(ID0,0)) AG("AUPN")=^(0)
 S AGLINE("-")=$TR($J(" ",78)," ","-")
 S AGLINE("EQ")=$TR($J(" ",78)," ","=")
 S $P(AGLINE("PGLN"),"=",81)=""
 W $$S^AGVDF("IOF"),!
 S ROUTID=$P($T(+1)," ")
 S SUBS=$P($G(AGSELECT),U,11)
 D PROGVIEW^AGUTILS(DUZ,SUBS)
 W "IHS REGISTRATION ",$S($D(AGSEENLY):"VIEW SCREEN",1:"EDITOR")
 W ?36,"Railroad Pharmacy"
 W ?80-$L($P($G(^DIC(4,DUZ(2),0)),U)),$P($G(^DIC(4,DUZ(2),0)),U)
 S AGLINE("-")=$TR($J(" ",80)," ","-")
 S AGLINE("EQ")=$TR($J(" ",80)," ","=")
 W !,AGLINE("EQ")
 W !,$E(AGPAT,1,23)
 W ?23,$$DTEST^AGUTILS(ID0)
 I $D(AGCHRT) W ?42,"HRN#:",AGCHRT
 ;GET ELIG STAT
 S AGELSTS=$P($G(^AUPNPAT(DFN,11)),U,12)
 W ?56,"(",$S(AGELSTS="C":"CHS & DIRECT",AGELSTS="I":"INELIGIBLE",AGELSTS="D":"DIRECT ONLY",AGELSTS="P":"PENDING VERIFICATION",1:"NONE"),")"
 W !,AGLINE("EQ")
 ;K AG("EDIT")  AG:*7.1*2 TO ALLOW QUIT FROM AG6 WHEN EDITING PATIENT RATHER THAN ADDING A PATIENT
 Q
GETAW ;EP - DISPLAY THE SCREEN
 K AG("C")
 F AG=1:1 D  Q:$G(AGSCRN)[("*END*")
 . S AGSCRN=$P($T(@1+AG),";;",2,15)
 . Q:AGSCRN[("*END*")
 . S CAPTION=$P(AGSCRN,U)  ;FLD CAP
 . I $E(CAPTION)="-" W !,CAPTION Q  ;- DENOTES SECTION
 . S DIC=$P(AGSCRN,U,3)    ;FILE OR SUBFILE #
 . S DR=$P(AGSCRN,U,4)      ;FLD #
 . S NEWLINE=$P(AGSCRN,U,5)  ;NEWLINE OR INDENT
 . S CAPDENT=$P(AGSCRN,U,2)   ;CAP INDENT
 . S ITEMNUM=$P(AGSCRN,U,6)   ;ITEM #
 . S TAGCALL=$TR($P($P(AGSCRN,U,7),"|",1),"~",U)   ;TAG TO CALL TO EDIT THIS FLD
 . S EXECUTE=$P(AGSCRN,"|",2)      ;USE TO DISP FLD WHICH IS DEPENDENT ON ANOTHER FLD
 . S PREEXEC=$P(AGSCRN,"|",3)     ;PLACE CODE TO BE XECUTED PRIOR TO DISP OF THE FLD
 . S PRECAPEX=$P(AGSCRN,"|",4)   ;PLACE CODE TO EXECUTE BEF CAP/FLD LBL
 . S POSTEXEC=$P(AGSCRN,"|",5)    ;PLACE ANYTHING HERE TO BE EXECUTED AFT DISP OF THE FLD
 . S:TAGCALL'="" $P(AG("C"),"|",ITEMNUM)=TAGCALL   ;SELECTION STRING
 . W @NEWLINE
 . W ITEMNUM
 . W $S(ITEMNUM'="":") ",1:"")
 . I PRECAPEX="" W @CAPDENT,$S($G(CAPTION)'="":CAPTION_": ",$G(CAPTION)="":"",1:$P($G(^DD(DIC,DR,0)),U)_": ")
 . I PRECAPEX'="" X PRECAPEX I $T W @CAPDENT,$S($G(CAPTION)'="":CAPTION_": ",$G(CAPTION)="":"",1:$P($G(^DD(DIC,DR,0)),U)_": ")
 .;IF EDITING DISP DATA
 .;E DISP ONLY THE CAPS
 .I 'NEWENTRY D
 .. S D0=ID0
 .. I DIC'["." S D0=D0_","
 .. E  S D0=ID1_","_D0_","
 .. S FLAG=""
 .. I DIC=9999999.18 S D0=$G(AGINSPTR) I DR=.21 S FLAG="I"
 .. ;LOOP TO HANDLE MULTIPLE DR'S FOR ONE CAP
 .. N PIECE
 .. S VDR=DR
 .. F PIECE=1:1 S DR=$P(VDR,";",PIECE) Q:DR=""  D
 ... I $P(PREEXEC,";",PIECE)'="" X $P(PREEXEC,";",PIECE)
 ... I $P(EXECUTE,";",PIECE)="" D
 .... I DIC=9999999.18,(DR=.01) S PLANPTR=$P($G(^AUPNRRE(ID0,11,ID1,0)),U,4)
 .... I DIC=9999999.18 S D0=$G(PLANPTR)
 .... I DIC=9000005.11,(DR=.05) S COVPTR=$P($G(^AUPNRRE(ID0,11,ID1,0)),U,11),GRPPTR=$P($G(^AUPNRRE(ID0,11,ID1,0)),U,11)
 .... I DIC=9000005.11,(DR=.04) S AGELP("INS")=$$GET1^DIQ(DIC,D0,DR,FLAG)
 .... S DIC("S")="I $D(AGELP(""INS"")),$P(^(0),U,2)=AGELP(""INS"")"
 .... I DIC=9999999.77 S D0=$G(GRPPTR)
 .... I DIC=9999999.6519 D  Q:DT1=""
 ..... S D0=$G(COVPTR),DT1=$O(^AUTTPIC(37,19,"B",DT+.01),-1) Q:DT1=""
 ..... S DT1=$O(^AUTTPIC(37,19,"B",DT1,""))
 ..... S D0=DT1_","_D0_","
 .... I DIC=9000005.11,(DR=.01) D GETDATES(ID0) Q   ;DISPLAY DATES AFTER STANDARD REG. SORT
 .... I DIC=9999999.6519,((DR=.23)!(DR=.24)!(DR=.25)) W $J($$GET1^DIQ(DIC,D0,DR,FLAG),10,2)
 .... E  W $$GET1^DIQ(DIC,D0,DR,FLAG)
 ... I $P(EXECUTE,";",PIECE)'="" S D0=$TR(D0,",") X $P(EXECUTE,";",PIECE)
 ... I $P(POSTEXEC,";",PIECE)'="" X $P(POSTEXEC,";",PIECE)
 ..K PIECE,VDR
 S AG("N")=$L(AG("C"),"|")
 W !,$G(AGLINE("-"))
 K MYERRS,MYVARS
 D FETCHERR^AGEDERR(AG("PG"),.MYERRS)
 S MYVARS("DFN")=ID0,MYVARS("FINDCALL")="FINDPVT"
 S MYVARS("SELECTION")=$G(AGSELECT),MYVARS("SITE")=DUZ(2)
 I '$G(NEWENTRY) D EDITCHEK^AGEDERR(.MYERRS,.MYVARS,1)
 D VERIF^AGUTILS
 Q
GETDATES(ID0) ;EP - GET THE DTS USING LIST^DIC
 K RESULT,ERROR
 S FLAGS=""
 S FIELDS=";.01I;.02I;.03I"
 D LIST^DIC(9000005.11,","_ID0_",",FIELDS,FLAGS,"*",,,,,,"RESULT","ERROR")
 D DATESORT(.RESULT)
 Q
DATESORT(RESULT) ;EP - TAKE LIST RETURNED BY FILE^DIC AND SORT IT
 ;BASED ON SPECS
 N DATESORT,SPECSUB,EFFDT,ENDDT,CVG
 S REC=0
 F  S REC=$O(RESULT("DILIST","ID",REC)) Q:'REC  D
 .S ENDDT=RESULT("DILIST","ID",REC,.02)
 .S EFFDT=RESULT("DILIST","ID",REC,.01)
 .S CVG=RESULT("DILIST","ID",REC,.03)
 .Q:CVG'="D"
 .S SPECSUB=$S(ENDDT="":"O",1:"T")  ;O=OPEN ENDED, T=TERM DATE
 .I SPECSUB="O" S DATESORT(SPECSUB,EFFDT)=ENDDT_U_CVG
 .E  S DATESORT(SPECSUB,-ENDDT)=EFFDT_U_CVG
 S DEFEDDT=$O(DATESORT("O",""))  ;GET DEFAULT EDIT DT. FIRST ONE IN DISP
 I DEFEDDT="" S DEFEDDT=$O(DATESORT("T","")) S:DEFEDDT'="" DEFEDDT=$P(DATESORT("T",DEFEDDT),U)
 D SHOWNEW(.DATESORT)
 Q
SHOWNEW(DATESORT) ;EP
 N SPECSUB,DATE,DATE1,CVG,EFFDT,ENDDT,REC
 S SPECSUB=""
 S REC=1
 I '$D(DATESORT("O")) F  S SPECSUB=$O(DATESORT(SPECSUB)) Q:SPECSUB=""  D ALLTERM Q
 F  S SPECSUB=$O(DATESORT(SPECSUB)) Q:SPECSUB=""  D
 .S DATE=""
 .F  S DATE=$O(DATESORT(SPECSUB,DATE)) Q:DATE=""  D
 ..S DATE1=$P(DATESORT(SPECSUB,DATE),U)
 ..S CVG=$P(DATESORT(SPECSUB,DATE),U,2)
 ..I SPECSUB="O" S EFFDT=DATE,ENDDT=""
 ..E  S EFFDT=DATE1,ENDDT=-DATE
 ..I REC'=1 W !
 ..S Y=EFFDT X ^DD("DD")
 ..W ?3,Y
 ..;W ?57,CVG
 ..S Y=ENDDT X ^DD("DD")
 ..W ?40,Y
 ..W ?79,$S($$ISACTIVE^AGINS(EFFDT,ENDDT):"A",1:"I")
 ..I $$ISACTIVE^AGINS(EFFDT,ENDDT) S DEFEDDT=EFFDT
 ..S REC=REC+1
 Q
ALLTERM ;EP
 S DATE=""
 F  S DATE=$O(DATESORT(SPECSUB,DATE),-1) Q:DATE=""  D
 .S DATE1=$P(DATESORT(SPECSUB,DATE),U)
 .S CVG=$P(DATESORT(SPECSUB,DATE),U,2)
 .I SPECSUB="O" S EFFDT=DATE,ENDDT=""
 .E  S EFFDT=DATE1,ENDDT=-DATE
 .I REC'=1 W !
 .S Y=EFFDT X ^DD("DD")
 .W ?3,Y
 .;W ?57,CVG
 .S Y=ENDDT X ^DD("DD")
 .W ?40,Y
 .W ?79,$S($$ISACTIVE^AGINS(EFFDT,ENDDT):"A",1:"I")
 .I $$ISACTIVE^AGINS(EFFDT,ENDDT) S DEFEDDT=EFFDT
 .S REC=REC+1
 Q
WMSG ;EP - DISP THIS MSG IF THIS IS A NEW ENTRY
 W !,"Editing Pharmacy Railroad Part D Eligibility record"
 Q
 ;;;;;;;;;;;;;;;;;;;;;;;;;;
 ; EDIT RAILROAD PART D FLDS
 ;;;;;;;;;;;;;;;;;;;;;;;;;;
EDITNAME(ID0,ID1) ;EP - EDIT MEDICARE NAME
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=".05R"
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITGEN(ID0,ID1) ;EP - EDIT GENDER
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=".08R"
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITID(ID0,ID1) ;EP - EDIT ID NUmber
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=".06R"
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITDOB(ID0,ID1) ;EP - EDIT DOB
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=".09R"
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITPC(ID0,ID1) ;EP - EDIT PERSON CODE
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=.07
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITCOV(ID0,ID1) ;EP - EDIT  COVERAGE TYPE PTR
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=.12
 D ^DIE
 K DIE,DIC,DA,DR
 Q
ADDEFF(ID0) ;EP - ADD/EDIT NEW EFFECTIVE DATE
 K DIE,DIC,DA,DR
 I $G(DEFEDDT)'="" S DIC("B")=DEFEDDT   ;IF DEFAULT USE IT
 S DA(1)=ID0
 S DIC(0)="AE"
 S DIC="^AUPNRRE("_DA(1)_",11,"
 S DIC("S")="I $P($G(^(0)),U,3)=""D"""
 D ^DIC
 I +Y<0 Q
 K DIE,DIC,DA,DR
 D EFFDT(ID0,+Y)
 Q
EFFDT(ID0,ID1) ;EP - EDIT EFFECTIVE DATE
 K DIE,DIC,DA,DR,DIDEL,ONLYONE,IEN
 I $D(^XUSEC("AGZMGR",DUZ)) S DIDEL=9000005.11
 S IEN=0 F ONLYONE=1:1 S IEN=$O(^AUPNRRE(ID0,11,IEN)) Q:'IEN
 S DA=ID1
 S DA(1)=ID0
 S DIE="^AUPNRRE("_DA(1)_",11,"
 I ONLYONE=1 D
 .W !!,"THERE IS ONLY ONE ELGIBILITY DATE RECORD FOR THIS PHARMACY ELIGIBILITY"
 .W !,"IF YOU DELETE THIS DATE THE ENTIRE PHARMACY DATA WILL BE DELETED FOR THIS"
 .W !,"PATIENT"
 .W !
 .S DR=".01R"
 .S DR(2,9000005.11)=".01R"
 E  S DR=".01",DR(2,9000005.11)=".01"
 D ^DIE
 Q:'$D(DA)  ;IF NO DA, ENTRY WAS DELETED
 K DIE,DIC,DA,DR
 I $D(^AUPNRRE(ID0,11,ID1,0)) D EDITEXP(ID0,ID1)
 E  S EXIT=1
 Q
EDITEXP(ID0,ID1) ;EP - EDIT EXPIRATION DATE
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=.02
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITGRP(ID0,ID1) ;EP - EDIT GROUP NAME
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=.11
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITPLAN(ID0,ID1) ;EP - EDIT PLAN
 K DIE,DIC,DA,DR
 S DA(1)=ID0
 S DA=ID1
 S DIE="^AUPNRRE("_DA(1)_",11,"
 S DR=".04R"
 D ^DIE
 K DIE,DIC,DA,DR
 Q
EDITSTBN(COVPTR) ;EP - EDIT START BENEFIT
 K DIE,DIC,DA,DR
 W !,"SPECIFICATIONS NOT YET PROVIDED!" H 3
 K DIE,DIC,DA,DR
 Q
EDITDED(COVPTR) ;EP - EDIT DEDUCTIBLE
 K DIE,DIC,DA,DR
 W !,"SPECIFICATIONS NOT YET PROVIDED!" H 3
 K DIE,DIC,DA,DR
 Q
EDITCOP(COVPTR) ;EP - EDIT CO-PAY
 K DIE,DIC,DA,DR
 W !,"SPECIFICATIONS NOT YET PROVIDED!" H 3
 K DIE,DIC,DA,DR
 Q
 ;FIELD EDITS
 ; ****************************************************************
 ; ON LINES BELOW:
 ; U "^" DELIMITED
 ; AGSCRN CONTAINS THE $TEXT OF EACH LINE BELOW STARTING AT TAG '1'
 ; PIECE  VAR       DESC
 ; -----  --------  -----------------------------------------------
 ; 1      CAPTION    FLD CAP ASSIGNED BY PROGRAMMER OVERRIDES FLD LBL IF POPULATED
 ; 2      CAPDENT    POSITION ON LINE TO DISP CAP
 ; 3      DIC        FILE OR SUBFILE #
 ; 4      DR         FLD # - THESE CAN BE SEPARATED BY ";" THIS ALLOWS
 ;                   MULTIPLE FLDS TO BE PRINTED WITH THE SAME CAP AS IN
 ;                   'CITY,STATE,ZIP'
 ; 5      NEWLINE    NEW LINE OR NOT (MUST BE EITHER A '!' OR '?#') USE THIS TO INDENT THE LINE
 ; 6      ITEMNUM    ITEM # ASSIGNMENT. USE THIS TO ASSIGN THE ITEM # USED TO CHOOSE THIS
 ;                   FLD ON THE SCREEN
 ; 7      TAGCALL    TAG TO CALL WHEN THIS FLD IS CHOSEN BY USER TO BE EDITED
 ;
 ; BAR "|" DELIMITED
 ; PIECE  VAR        DESC
 ; -----  --------   ----------------------------------------------
 ; 2      EXECUTE    EXECUTE CODE TO GET FLD THAT ANOTHER IS POINTING TO.
 ;                   EXECUTED AFT FLD PRINT. IF MUTLIPLE FLDS ARE PRINTED
 ;                   THEN MULTIPLE EXECUTE CODES CAN BE SEPARATED BY ";".
 ; 3      PREEXEC    EXECUTE CODE TO DO BEF FLD PRINTS. USE TO SCREEN OUT
 ;                   PRINTING A FLD VALUE. FOR MULTIPLES SEPARATE BY ";"
 ; 4      PRECAPEX   EXECUTE CODE TO DO BEF PRINTING THE CAP OR FLD LBL.
 ;                   USE TO SCREEN OUT PRINTING A CAP/FLD LBL
 ; 5      POSTEXEC   EXECUTE CODE TO DO AFT PRINTING THE FLD DATA
 ;                   FOR MULTIPLES SEPARATE BY ";"
 ;
 ;FIELDS BELOW DELETED PER ADRIAN 12/13/05
 ;Coverage Type^?3^9000005.11^.12^?49^^^||||
 ;Co-Pay.......^?3^9999999.6519^.25^?44^11^EDITCOP(COVPTR)||||
 ;Deductible...^?3^9999999.6519^.24^?44^10^EDITDED(COVPTR)^||||
 ;Begin Date...^?3^9999999.6519^.01^?49^^||||
 ;Start Benefit^?3^9999999.6519^.23^?44^9^EDITSTBN(COVPTR)^||||
 ;Ins. Type^?3^9999999.18^.21^?23^^^||||
1 ;
 ;;------------------RAILROAD PART D DATA-----------------------------------------
 ;;Medicare Name^?3^9000005.11^.05^!^1^EDITNAME(ID0,ID1)^||||W ?49,$C(124)
 ;;Gender (M/F)^?3^9000005.11^.08^?49^4^EDITGEN(ID0,ID1)^||||
 ;;ID Number^?3^9000005.11^.06^!^2^EDITID(ID0,ID1)^||||W ?49,$C(124)
 ;;Date of Birth^?3^9000005.11^.09^?49^5^EDITDOB(ID0,ID1)^||||
 ;;Person Code^?3^9000005.11^.07^!^3^EDITPC(ID0,ID1)^||||
 ;;-ELIGIBILITY DATES--------------------------------------------------------------
 ;;-----Effective Date-----------------------Expire Date---------------------------
 ;;^?3^9000005.11^.01^!^6^ADDEFF(ID0)^||||
 ;;--------------------------------------------------------------------------------
 ;;Grp Name^?3^9000005.11^.11^!^7^EDITGRP(ID0,ID1)^||||
 ;;Grp Number^?3^9999999.77^.02^?45^^^||||
 ;;--------------------------------------------------------------------------------
 ;;^?3^9999999.18^.01^!^8^EDITPLAN(ID0,ID1)^||||
 ;;^?3^9999999.18^.02^!^^^||||
 ;;^?3^9999999.18^.03^!^^||||W ","
 ;;^?3^9999999.18^.04^?0^^||||W "  "
 ;;^?3^9999999.18^.05^?0^^||||
 ;;^?3^9999999.18^.06^!^^||||
 ;;*END*
