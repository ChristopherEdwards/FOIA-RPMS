AGMSPINT ; IHS/SD/EFG - ROUTINE FOR MSP INTERVIEW PROCESS ;
 ;;7.1;PATIENT REGISTRATION;**4,8**;AUG 25, 2005
 ;
EN K DIC
 D PTLK^AG
 Q:'$D(DFN)
 D CLEAR  ;CLEAR ALL QUESTION RESULTS VARIABLES
 K DIC,Y,DD,DO,DIR,DIE,DR
 S DIC="^AUPNMSP("
 S DIC(0)="AELQMZ"
 S DIC("DR")=""
 S DIC("S")="I $P($G(^AUPNMSP(Y,0)),U,2)=$G(AUPNPAT)"
 D ^DIC
 Q:Y<0
 S AG("DA")=+Y
 S DA=+Y
 S DIE=DIC
 K DR,DIC,DIR
 S DR=".02////"_DFN
 D ^DIE
 K DIC
 D ^XBCLS
PROCESS ;
 S AG("MCRCHK")=""
 D HDR
 D EN^AGMSPI1
 Q:$D(Y)                                                                         ;AG*7.1*8
 D ASKSIGDT
 K AG("DA"),DIE,DIC,DIR
 Q
 ;ASK FOR SIGNATURE DATE
ASKSIGDT ;EP
 ;BEGIN AG*7.1*4 SCR
 W !!
 S DA=AG("DA")
 S DR=".03R;S:X=""N"" Y=0;.04R"
 D ^DIE
 Q:$D(Y)                                               ;AG*7.1*8
 ;END
 S DA=AG("DA")
 S DR=100
 D ^DIE
 Q
 ;ADD SIGNATURE ONLY
ADDSIG ;EP - CALLED FROM OPTION 'AG ADD MSP SIGNATURE DATE'
 K DIC,Y,DD,DO
 S DIC="^AUPNMSP("
 S DIC(0)="AEMQZ"
 S DIC("S")="I $P($G(^(7)),U)="""""
 D ^DIC Q:Y=-1
 S AG("DA")=+Y
 K DIC
 D ^XBCLS
 S DIE="^AUPNMSP("
 D ASKSIGDT
 K AG("DA"),DIE,DIC,DIR
 Q
HDR ;
 S AGPAT=$P(^DPT(DFN,0),U)
 S AGCHRT=$S($D(^AUPNPAT(DFN,41,DUZ(2),0)):$P(^AUPNPAT(DFN,41,DUZ(2),0),U,2),1:"xxxxx")
 S Y=DT D DD^%DT S AG("TODAY")=Y
 S AGLINE("-")=$TR($J(" ",79)," ","-")
 S AGLINE("EQ")=$TR($J(" ",79)," ","=")
 S AGLINE("_")=$TR($J(" ",79)," ","_")
 W !,?10,"WARNING: Confidential Patient Information, Privacy Act Applies"
 W !,AGLINE("EQ")
 W !,?18,"MEDICARE SECONDARY PAYER QUESTIONNAIRE INTERVIEW"
 W !,AGLINE("-")
 W !,AGPAT,"  (",AGCHRT,")"
 W ?53,"TODAY'S DATE: ",AG("TODAY")
 W !,AGLINE("EQ"),!
 Q
CLEAR ;EP
 K PARTI1,PARTI2,PARTI3,PARTI4
 K PARTII1,PARTII2,PARTII3
 K PARTIII1
 K PARTIV1,PARTIV2,PARTIV3,PARTIV4
 K PARTV1,PARTV2,PARTV3,PARTV4
 K PARTVI1,PARTVI2,PARTVI3,PARTVI4,PARTIV5,PARTVI6,PARTVI7
 Q
