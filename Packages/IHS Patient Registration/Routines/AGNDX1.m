AGNDX1 ; IHS/ASDS/EFG - PRINT A SELECTED PATIENT'S INDEX CARD ;  
 ;;7.1;PATIENT REGISTRATION;;AUG 25,2005
 D PTLK^AG
DFN ;EP - With Pre-defined DFN.
 Q:'$D(DFN)
 D ALIAS^AGNDXP
 G END:$D(DTOUT)!$D(DFOUT)!$D(DUOUT)
DEV S %ZIS="OPQ" D ^%ZIS I POP S IOP=ION D ^%ZIS Q
 G:'$D(IO("Q")) START K IO("Q") I $D(IO("S"))!($E(IOST)'="P") W *7,!,"Please queue to system printers." D ^%ZISC G DEV
 X ^%ZOSF("UCI") S ZTRTN="START^AGNDX1",ZTUCI=Y,ZTDESC="Index Card for "_$P(^DPT(DFN,0),U)_"." S:$D(AGALIAS) G="AGALIAS",ZTSAVE(G)="" F G="DFN" S ZTSAVE(G)=""
 D ^%ZTLOAD G:'$D(ZTSK) DEV K AG,AGALIAS,G,ZTDESC,ZTRTN,ZTSAVE,ZTSK,ZTUCI D ^%ZISC
 Q
START ;EP - From TaskMan.
 U IO D ^AGNDXP
END D ^%ZISC K AG,AGALIAS,I,AGLINE,IOP,AGTOT,X,Y D:$D(ZTQUEUED) KILL^%ZTLOAD
 Q
