AGSEENLY ; IHS/ASDS/EFG - PATIENT DATA VIEW ; MAR 19, 2010 
 ;;7.1;PATIENT REGISTRATION;**7**;AUG 25,2005
 ;This routine is called from the CONTRACT HEALTH system.
 D ^AGVAR
L1 D ^AG W !!!?27,"Patient Data View System",!!
 D PTLK^AG
L2 ;PEP - CREATED ON 9/14/04 FOR VIEW PATIENT RECORD (VPR)
 ;PER A REQUEST FROM LINDA FELS
 I $D(DFN) S AGSEENLY="",AGPAGE=1 G L5
 K AG,AGSEENLY,DIC,AG("LKDATA"),AG("LKERR"),AG("LKPRINT"),AGPAGE
 Q
L5 ;
 D @($P($T(@AGPAGE),";;",2)) W !,AGLINE("EQ")
 I AGPAGE<11 S DIR("A")="                               Press RETURN " D READ
 G L1:$D(DTOUT)!$D(DFOUT)
 I $D(DUOUT) S AGPAGE=AGPAGE-1 G L5:AGPAGE>0,L1
 I $D(AG("ED")) S AGPAGE=AG("ED") G L5
 S AGPAGE=AGPAGE+1 G L5:AGPAGE<11,L1
READ ;
 K DFOUT,DTOUT,DUOUT,DQOUT,DLOUT,AG("ED"),AG("ERR"),DIROUT
 S DIR(0)="FO^1:3"
 D ^DIR
 Q:$D(DTOUT)
 S:Y="/.,"!(Y="^^") DFOUT=""
 S:Y="" DLOUT=""
 S:Y="^" (DUOUT,Y)=""
 S:Y?1"?".E!(Y["^") (DQOUT,Y)=""
 I $E(Y,1)="P" D
 . S AG("ED")=+$E(Y,2,99)
 . I AG("ED")<1!(AG("ED")>10) D  ;AG*7.1*7
 .. W *7,!!,"Use only pages 1 through 10."  ;AG*7.1*7
 .. H 2
 .. K AG("ED")
 .. S AG("ERR")=""
 Q
 ;CHANGED OPTION 5 TO POINT TO BENEFIT COORDINATOR SCREEN
 ;AG*7.1*7;ADDED PAGE 10
1 ;;^AGED1
2 ;;DRAW^AGED2
3 ;;DRAW^AGED3
4 ;;^AGED4A
5 ;;^AGEDBEA
6 ;;^AGED13
7 ;;^AGED8
8 ;;^AGED11
9 ;;^AGED11A
10 ;;^AGED10A
