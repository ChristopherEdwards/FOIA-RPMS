AGTMCOV ; IHS/ASDS/EFG - Table Maintenance of COVERAGE TYPES ; 
 ;;7.1;PATIENT REGISTRATION;**1,2**;JAN 31, 2007
 ;
 K DUOUT,DTOUT  ;AG*7.1*1 IHS/SD/TPF 3/1/2006 ALPHA PATCH 1 ISSUE
 S U="^"
 W !
INS K DR,DIE,DIR,DIC,D0,DD  ;AG*7.1*1 IHS/SD/TPF 3/1/2006 ALPHA PATCH 1 ISSUE
 W !! K DIC S DIC="^AUTNINS(",DIC("S")="I $P(^(1),U,7)'=0",DIC("A")="Select INSURER: ",DIC(0)="QEAM" D ^DIC K DIC
 G XIT:X=""!$D(DUOUT)!$D(DTOUT)
 I +Y<1 G INS
 S AG("INS")=+Y
 S:$G(AG("INS"))'="" AGINSTYP=$P($G(^AUTNINS(AG("INS"),2)),U)
EDIT W !! K DIC S DIC="^AUTTPIC(",DIC("A")="Select COVERAGE TYPE to Edit: ",DIC(0)="QEALM",DIC("S")="I $P(^(0),U,2)=AG(""INS"")",DIC("DR")=".02////"_AG("INS") D ^DIC K DIC
 G XIT:X=""!$D(DUOUT)!$D(DTOUT)
 I +Y<1 G EDIT
 K DR,DIE,DIR,DIC,D0,DD
 S DA=+Y
 S DIE="^AUTTPIC(",DR="W !;.03;.04;.06;11;13;15;17;19"
 S AGCOVNAM=$P($G(^AUTTPIC(DA,0)),U)
 S DR(2,9999999.6519)=".01;.02;.03;.04;.05;.06;.07;.08;.09;.11;.12;.13;.14"
 I $G(AGINSTYP)="R" D
 .I AGCOVNAM="PART A" S DR(2,9999999.6519)=".01;.15;.16;.17;.18;.19"
 .I AGCOVNAM="PART B" S DR(2,9999999.6519)=".01;.21;.22"
 D ^DIE K DR G XIT:$D(Y)
 G INS
XIT K AG,DIR,DIC,DIE,DR
 Q
