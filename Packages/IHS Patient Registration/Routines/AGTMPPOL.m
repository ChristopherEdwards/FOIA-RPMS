AGTMPPOL ; IHS/ASDS/EFG - REPORT OF PI POLICY HOLDERS ;  
 ;;7.1;PATIENT REGISTRATION;;AUG 25,2005
 ;
 W ! K DIR S DIR(0)="Y",DIR("A")="Limit Report to a Specific INSURER",DIR("B")="Y" D ^DIR K DIR G XIT:$D(DTOUT)!$D(DUOUT) I Y'=1 S AG(1)="A",AG(2)="ZZ" G ZIS
SEL K DIC W ! S DIC="^AUTNINS(",DIC(0)="QEAM" D ^DIC K DIC
 G XIT:X=""!$D(DTOUT)!$D(DUOUT),SEL:+Y<1
 S (AG(1),AG(2))=$P(^AUTNINS(+Y,0),U)
ZIS W !!!,"NOTE: This report requires 132 character width."
 W ! S %ZIS="NQ",%ZIS("B")="" D ^%ZIS G:'$D(IO)!(POP) XIT
 S AG("ION")=ION G:$D(IO("Q")) QUE
PRQUE ;ENTER FROM TASK MANAGER
 S L=0,DIC="^AUPNPRVT(",FLDS="[AG TM PI ELIGIBLE]",BY="[AG TM PI ELIGIBLE]",FR=AG(1)_",?,?",TO=AG(2)_",?,?"
 S IOP=AG("ION")_";132",AGPGPG=1,DOUT=$I
 D EN1^DIP
XIT D ^%ZISC K AG
 Q
QUE K IO("Q") S ZTRTN="PRQUE^AGTMPPOL",ZTDESC="REPORT OF PI POLICY HOLDERS" F AG="AG(" S ZTSAVE(AG)=""
 D ^%ZTLOAD W:$D(ZTSK) !,"REQUEST QUEUED!",! G XIT
