AGVDF ; IHS/ASDS/EFG - VIDEO DISPLAY FEATURES ; 
 ;;7.1;PATIENT REGISTRATION;;AUG 25,2005
S(X) ;EP -
 S AGM1("X")=$X
 I '$D(AGMVDF(+IOST(0),X)) D
 .S AGM1("LN")=$T(@X),AGM1(1)=$P(AGM1("LN"),";;",2),AGM1(2)=$P(AGM1("LN"),";;",3),AGM1(3)=$P(AGM1("LN"),";;",4)
 .S AGMVDF(+IOST(0),X)=$P($G(^%ZIS(2,+IOST(0),AGM1(1))),"^",AGM1(2),AGM1(3))
 .I AGMVDF(+IOST(0),X)="" S AGMVDF(+IOST(0),X)="*0"
 W @AGMVDF(+IOST(0),X)
 S $X=AGM1("X")
 S X="" K AGM1 Q X
 ;GLOBAL LOCATIONS TO FOLLOW
HIN ;;7;;1;;1;;HI INTENSITY ON
HIF ;;7;;2;;2;;HI INTENSITY OFF
RVN ;;5;;4;;4;;REVERSE VIDEO ON
RVF ;;5;;5;;5;;REVERSE VIDEO OFF
ULN ;;6;;4;;4;;UNDERLINE ON
ULF ;;6;;5;;5;;UNDERLINE OFF
DTP ;;17;;1;;1;;DOUBLE HIGH TOP HALF
DTB ;;17;;2;;2;;DOUBLE HIGH BOTTOM HALF
BLN ;;5;;8;;8;;BLINK ON
BLF ;;5;;9;;9;;BLINK OFF
IOF ;;1;;2;;2;;FORM FEED/CLEAR SCREEN
10 ;;5;;1;;1;;TEN PITCH
12 ;;5;;2;;2;;TWELVE PITCH
16 ;;12.1;;1;;250;;SIXTEEN PITCH
