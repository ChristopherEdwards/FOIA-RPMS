AUPNLK1I ; IHS/CMI/LAB - INITIALIZATION FOR ^AUPNLK1 ; [ 05/09/2003  8:00 AM ]
 ;;99.1;IHS DICTIONARIES (PATIENT);**2,9,10**;JUN 13, 2003
 ;IHS/CMI/LAB - patch 2 Y2K
 ;
INIT ; INITIALIZATION - FIX UP AUPX AND SET UP XREFS
 I AUPX?.E1L.E F AUPI=1:1:$L(AUPX) S:$E(AUPX,AUPI)?1L AUPX=$E(AUPX,0,AUPI-1)_$C($A(AUPX,AUPI)-32)_$E(AUPX,AUPI+1,$L(AUPX))
 I AUPX[", " F AUPL=0:0 Q:'$F(AUPX,", ")  S AUPX=$E(AUPX,1,($F(AUPX,", ")-2))_$E(AUPX,$F(AUPX,", "),$L(AUPX))
 K AUPREFS S AUPREFS=$S(DIC(0)'["M":"B",AUPX?1A1N.N:$S($L(AUPX)<6:"CN,RM",1:"CN,RM"),AUPX?4N!(AUPX?4N1A):"SSN,CN,RM",AUPX?9N.E:"SSN,CN,RM",1:"")
 S:AUPREFS="" AUPREFS=$S(AUPX?1N.N:$S($L(AUPX)<5:"CN,RM,BS,SSN",1:"CN,RM,SSN"),AUPX?1N.E:"CN,RM",1:"B,CN,RM") S:$D(AUPIX) AUPREFS=AUPIX_","_AUPREFS
 ; - - - - - IHS DOB - - - - -
 ;beginning Y2K IHS/CMI/LAB
 ;change "X" to %DT="PX", change allowable input from 6N to 8N
 ;I DIC(0)["M",AUPX'?1N.N S %DT="X",X=$S(AUPX?1"B"6N:$E(AUPX,2,7),1:AUPX) D ^%DT S:Y'=-1 AUPDT=Y,AUPREFS=AUPREFS_",ADOB" K %DT,Y ;Y2000 IHS/CMI/LAB
 I DIC(0)["M",AUPX'?1N.N S %DT="PX",X=$S(AUPX?1"B"8N:$E(AUPX,2,9),1:AUPX) D ^%DT S:Y'=-1 AUPDT=Y,AUPREFS=AUPREFS_",ADOB" K %DT,Y ;Y2000 IHS/CMI/LAB
 ;end Y2K fix
 ; - - - - - IHS TEMPORARY CHART # - - - - -
 I AUPX?1"T"5N,$D(^AUPNPAT("D",AUPX)) S (AUPIFN,Y)=$O(^AUPNPAT("D",AUPX,0)) D SETAUP^AUPNLKUT
 ; - - - - - - - - - - - - - - - - - - - - -
 Q
