AUPNVXRF ; IHS/CMI/LAB - EDITS FOR AUPNVSIT (VISIT:9000010) 24-MAY-1993 ; 30 Sep 2010  2:13 PM
 ;;2.0;IHS PCC SUITE;**10**;MAY 14, 2009;Build 88
 ;
AST ;EP - called from cross reference on Problem file .01
 I $T(EP^BATREG)]"",$$ICD^ATXCHK(X,$O(^ATXAX("B","BGP ASTHMA DXS",0)),9),$D(^BATREG(0)),$G(APCDPAT) D EP^BATREG(APCDPAT)
 Q
ASTV ;EP - called from cross reference on .01 of V POV
 I $T(EP^BATREG)]"",$$ICD^ATXCHK(X,$O(^ATXAX("B","BGP ASTHMA DXS",0)),9),$D(^BATREG(0)),$G(APCDPAT),$G(APCDVSIT) D EP^BATREG(APCDPAT,DA,APCDVSIT)
 Q
 ;
ASDT01S ;EP CALLED FROM XREF ON .01 CARE PLAN
 I $P(^AUPNCPL(DA,0),U,4)="" Q
 NEW A
 S A=0 F  S A=$O(^AUPNCPL(DA,11,A)) Q:A'=+A  D
 .Q:$P(^AUPNCPL(DA,11,A,0),U,1)=""
 .Q:$P(^AUPNCPL(DA,11,A,0),U,3)=""
 .S ^AUPNCPL("ASDT",X,$P(^AUPNCPL(DA,0),U,4),(9999999-$P(^AUPNCPL(DA,11,A,0),U,3)),$P(^AUPNCPL(DA,11,A,0),U,1),DA,A)=""
 .Q
 Q
ASDT01K ;EP CALLED FROM XREF ON .01 CARE PLAN
 I $P(^AUPNCPL(DA,0),U,4)="" Q
 NEW A
 S A=0 F  S A=$O(^AUPNCPL(DA,11,A)) Q:A'=+A  D
 .Q:$P(^AUPNCPL(DA,11,A,0),U,1)=""
 .Q:$P(^AUPNCPL(DA,11,A,0),U,3)=""
 .K ^AUPNCPL("ASDT",X,$P(^AUPNCPL(DA,0),U,4),(9999999-$P(^AUPNCPL(DA,11,A,0),U,3)),$P(^AUPNCPL(DA,11,A,0),U,1),DA,A)
 .Q
 Q
ASDT04S ;EP CALLED FROM XREF ON .01 CARE PLAN
 NEW A
 S A=0 F  S A=$O(^AUPNCPL(DA,11,A)) Q:A'=+A  D
 .Q:$P(^AUPNCPL(DA,11,A,0),U,1)=""
 .Q:$P(^AUPNCPL(DA,11,A,0),U,3)=""
 .S ^AUPNCPL("ASDT",$P(^AUPNCPL(DA,0),U,1),X,(9999999-$P(^AUPNCPL(DA,11,A,0),U,3)),$P(^AUPNCPL(DA,11,A,0),U,1),DA,A)=""
 .Q
 Q
ASDT04K ;EP CALLED FROM XREF ON .01 CARE PLAN
 NEW A
 S A=0 F  S A=$O(^AUPNCPL(DA,11,A)) Q:A'=+A  D
 .Q:$P(^AUPNCPL(DA,11,A,0),U,1)=""
 .Q:$P(^AUPNCPL(DA,11,A,0),U,3)=""
 .K ^AUPNCPL("ASDT",$P(^AUPNCPL(DA,0),U,1),X,(9999999-$P(^AUPNCPL(DA,11,A,0),U,3)),$P(^AUPNCPL(DA,11,A,0),U,1),DA,A)
 .Q
 Q
