BCHRC2 ; IHS/CMI/LAB - CHR Report 2 ; 
 ;;2.0;IHS RPMS CHR SYSTEM;;OCT 23, 2012;Build 27
 ;IHS/CMI/LAB - tmp to xtmp
 ;
 I '$G(DUZ(2)) W $C(7),$C(7),!!,"SITE NOT SET IN DUZ(2) - NOTIFY SITE MANAGER!!",!! Q
 S BCHJOB=$J,BCHBTH=$H
 D INFORM
GETDATES ;
BD ;get beginning date
 W ! S DIR(0)="D^:DT:EP",DIR("A")="Enter BEGINNING Date of Service for Report" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 I $D(DIRUT) G XIT
 S BCHBD=Y
ED ;get ending date
 W ! S DIR(0)="D^"_BCHBD_":DT:EP",DIR("A")="Enter ENDING Date of Service for Report" S Y=BCHBD D DD^%DT S DIR("B")=Y,Y="" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 I $D(DIRUT) G BD
 S BCHED=Y
 S X1=BCHBD,X2=-1 D C^%DTC S BCHSD=X
 ;
PROG ;
 S BCHPRG=""
 S DIR(0)="Y",DIR("A")="Include data from ALL CHR Programs",DIR("B")="N",DIR("?")="If you wish to include visits from ALL programs answer Yes.  If you wish to tabulate for only one program enter NO." D ^DIR K DIR
 G:$D(DIRUT) BD
 I Y=1 S BCHPRG="" G CHRT
PROG1 ;enter program
 K X,DIC,DA,DD,DR,Y S DIC("A")="Which CHR Program: ",DIC="^BCHTPROG(",DIC(0)="AEMQ" D ^DIC K DIC,DA G:Y<0 PROG
 S BCHPRG=+Y
CHRT ;
 K BCHPROVT
 S DIR(0)="S^O:One CHR;A:All CHRs",DIR("A")="Include Data for",DIR("B")="A" KILL DA D ^DIR KILL DIR
 I $D(DIRUT) G PROG
 S BCHPROVT=Y
 I BCHPROVT="A" G REG
CHR1 ;
 K DIC
 S DIC=200,DIC(0)="AEMQ",DIC("A")="Enter the CHR: " D ^DIC
 I Y=-1 G CHRT
 S BCHCHR1=+Y
REG ;
 S BCHREG="",BCHREGN=""
 S DIR(0)="S^R:Registered Patients;N:Non-Registered Patients;B:Both Registered and Non-Registered Patients",DIR("A")="Include which Patients",DIR("B")="B" KILL DA D ^DIR KILL DIR
 I $D(DIRUT) G CHRT
 S BCHREG=Y,BCHREGN=Y(0)
ZIS ;CALL TO XBDBQUE
 S XBRP="^BCHRC2P",XBRC="PROC^BCHRC2",XBRX="XIT^BCHRC2",XBNS="BCH"
 D ^XBDBQUE
 D XIT
 Q
ERR W $C(7),$C(7),!,"Must be a valid date and be Today or earlier. Time not allowed!" Q
XIT ;
 K BCHPRG,BCHTT,BCHFT,BCHF,BCHT,BCHREF,BCHQUIT,BCHJOB,BCHBTH,BCHBT,BCHET,BCHBD,BCHED,BCHBDD,BCHEDD,BCHSD,BCHODAT,BCHPROG,BCHX,BCHR,BCHR0,BCHPG,BCHDT
 K X,Y
 Q
 ;
INFORM ;
 W:$D(IOF) @IOF
 W !?20,"**********  CHR REPORT NO. 4  **********"
 W !!?26,"NUMBER OF REFERRALS FROM/TO",!!,"You must enter the time frame and the program for which the report",!,"will be run.",!!
 Q
 ;
 ;
PROC ;EP - PROCESS REFERRAL REPORT
 D XTMP^BCHUTIL("BCHRC2","CHR CHR REPORT")
 S (BCHBT,BCHBTH)=$H,BCHJOB=$J
 S X=0 F  S X=$O(^BCHTREF(X)) Q:X'=+X  S ^XTMP("BCHRC2",BCHJOB,BCHBT,X,"FROM")=0,^XTMP("BCHRC2",BCHJOB,BCHBT,X,"TO")=0
 S ^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","FROM")=0,^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","TO")=0
 D D,EOJ
 Q
 ;
EOJ ;
 S BCHET=$H
 Q
D ; Run by date of service
 S X1=BCHBD,X2=-1 D C^%DTC S BCHSD=X
 S BCHODAT=BCHSD_".9999" F  S BCHODAT=$O(^BCHR("B",BCHODAT)) Q:BCHODAT=""!((BCHODAT\1)>BCHED)  D D1
 Q
 ;
D1 ;
 S (BCHR,BCHRCNT)=0 F  S BCHR=$O(^BCHR("B",BCHODAT,BCHR)) Q:BCHR'=+BCHR  I $D(^BCHR(BCHR,0)),$P(^(0),U,2)]"",$P(^(0),U,3)]"" S BCHR0=^(0) D PROCESS
 Q
PROCESS ;
 S BCHPAT=$P(BCHR0,U,4)
 S BCHNRPAT=$P($G(^BCHR(BCHR,11)),U,12)
 I 'BCHPAT,'BCHNRPAT Q   ;no patient  - PATIENT RELATED ONLY
 I BCHREG="R",BCHPAT="" Q
 I BCHREG="N",BCHNRPAT="" Q
 I BCHPAT,BCHNRPAT S BCHNRPAT=""
 I BCHPAT Q:'$D(^DPT(BCHPAT,0))
 S BCHPROG=$P(BCHR0,U,2)
 I BCHPRG,BCHPRG'=BCHPROG Q
 S BCHPROV=$P(BCHR0,U,3)
 I BCHPROVT="O",BCHCHR1'=BCHPROV Q
 Q:$P(BCHR0,U,12)'=1  ;ONLY # SERVED 1
 Q:$P(BCHR0,U,29)  ;no group records
 S (BCHX,BCHC)=0 F  S BCHX=$O(^BCHRPROB("AD",BCHR,BCHX)) Q:BCHX'=+BCHX  I $P(^BCHRPROB(BCHX,0),U,4),$P(^BCHTSERV($P(^BCHRPROB(BCHX,0),U,4),0),U,3)'=99 S BCHC=BCHC+1
 Q:'BCHC  ;if none are not travel do not count
 ;S BCHREF=$P(BCHR0,U,7) I BCHREF S ^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"FROM")=^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"FROM")+$P(BCHR0,U,12),^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","FROM")=^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","FROM")+$P(BCHR0,U,12)
 ;S BCHREF=$P(BCHR0,U,8) I BCHREF S ^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"TO")=^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"TO")+$P(BCHR0,U,12),^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","TO")=^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","TO")+$P(BCHR0,U,12)
 S X=0 F  S X=$O(^BCHR(BCHR,41,X)) Q:X'=+X  D
 .S BCHREF=$P(^BCHR(BCHR,41,X,0),U,1)
 .Q:'BCHREF
 .S ^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"FROM")=^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"FROM")+$P(BCHR0,U,12),^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","FROM")=^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","FROM")+$P(BCHR0,U,12)
 S X=0 F  S X=$O(^BCHR(BCHR,42,X)) Q:X'=+X  D
 .S BCHREF=$P(^BCHR(BCHR,42,X,0),U,1)
 .Q:'BCHREF
 .S ^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"TO")=^XTMP("BCHRC2",BCHJOB,BCHBT,BCHREF,"TO")+$P(BCHR0,U,12),^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","TO")=^XTMP("BCHRC2",BCHJOB,BCHBT,"TOTAL","TO")+$P(BCHR0,U,12)
 Q
