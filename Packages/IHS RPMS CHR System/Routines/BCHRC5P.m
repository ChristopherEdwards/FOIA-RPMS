BCHRC5P ; IHS/TUCSON/LAB - print dx by age ;  [ 12/06/00  8:49 AM ]
 ;;1.0;IHS RPMS CHR SYSTEM;**7,11**;OCT 28, 1996
 ;IHS/CMI/LAB - tmp to xtmp
START ;
 S Y=BCHBD D DD^%DT S BCHBDD=Y S Y=BCHED D DD^%DT S BCHEDD=Y
 S Y=DT D DD^%DT S BCHDT=Y
 ;S BCHPG=0 D HEAD
 S BCHPG=0
 K BCHQUIT
 I '$D(^XTMP("BCHRC5",BCHJOB,BCHBT,"HA")) W !!,"NO DATA TO REPORT" G DONE
HA ;
 D @("HEAD"_(2-($E(IOST,1,2)="C-"))) ; LAB
 ;
 I $Y>(IOSL-4) D HEAD G:$D(BCHQUIT) DONE
 W !?4,"TOTAL",?29,$J(BCHTM,5),?36,$J(BCHTF,5)
 S BCHX=0,J=45 F  S BCHX=$O(^XTMP("BCHRC5",BCHJOB,BCHBT,"TOTAL AGE",BCHX)) Q:BCHX'=+BCHX!($D(BCHQUIT))  D
 .S M=^XTMP("BCHRC5",BCHJOB,BCHBT,"TOTAL AGE",BCHX,"M"),F=^XTMP("BCHRC5",BCHJOB,BCHBT,"TOTAL AGE",BCHX,"F")
 .W ?J,$J(M,5) S J=J+7 W ?J,$J(F,5) S J=J+7
 .Q
 S BCHX=0 F  S BCHX=$O(^XTMP("BCHRC5",BCHJOB,BCHBT,"HA",BCHX)) Q:BCHX'=+BCHX!($D(BCHQUIT))  D
 .I $Y>(IOSL-4) D HEAD Q:$D(BCHQUIT)
 .W !,$P(^BCHTPROB(BCHX,0),U,2),"  ",$P(^BCHTPROB(BCHX,0),U)
 .S M=$S($D(^XTMP("BCHRC5",BCHJOB,BCHBT,"HA",BCHX,"TOTAL","M")):^("M"),1:0),F=$S($D(^XTMP("BCHRC5",BCHJOB,BCHBT,"HA",BCHX,"TOTAL","F")):^("F"),1:0) W ?29,$J(M,5),?36,$J(F,5)
 .S J=45 F I=1:1:$L(BCHRBIN,";") S M=$S($D(^XTMP("BCHRC5",BCHJOB,BCHBT,"HA",BCHX,I,"M")):^("M"),1:"."),F=$S($D(^XTMP("BCHRC5",BCHJOB,BCHBT,"HA",BCHX,I,"F")):^("F"),1:".") W ?J,$J(M,5) S J=J+7 W ?J,$J(F,5) S J=J+7
 ;
DONE D DONE^BCHUTIL1
 K ^XTMP("BCHRC5",BCHJOB,BCHBT),BCHJOB,BCHBT,BCHX
 Q
HEAD ;
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S BCHQUIT="" Q
HEAD1 ; if terminal
 W:$D(IOF) @IOF
HEAD2 ; if printer
 S BCHPG=BCHPG+1
 W !,$P(^VA(200,DUZ,0),U,2),?56,"DATE GENERATED:  ",BCHDT,?124,"Page ",BCHPG,!
 W $$CTR^BCHRLU($$LOC^BCHRLU),!
 W !?46,"**********  CHR REPORT NO. 5  **********"
 W !!?44,"CLIENT CONTACTS BY HEALTH PROBLEM, AGE AND SEX"
 S BCHPROGN=$S(BCHPRG:$P(^BCHTPROG(BCHPRG,0),U)_" ("_$P(^(0),U,5)_")",1:"ALL"),X=$L(BCHPROGN)+10
 W !!?((132-X)/2),"PROGRAM:  ",BCHPROGN
 W !?43,"REPORT DATES:  ",BCHBDD,"  TO  ",BCHEDD,!
 W !,"HEALTH PROBLEM",?28,"---ALL AGES---" S J=50 F I=1:1:$L(BCHRBIN,";") S K=$P(BCHRBIN,";",I) Q:K=""  W ?J,K S J=J+14
 ;W !,"HEALTH PROBLEM",?28,"---ALL AGES---",?50,"0-4",?64,"5-9",?77,"10-19",?93,"20-34",?108,"35-54",?123,"55+"
 ;W !?32,"M      F" S J=48 F I=1:1:$L(BCHRBIN,";") W ?J,"M      F" S J=J+$S($P(BCHRBIN,";",I)>3:15,1:14)
 W !?32,"M      F" S J=48 F I=1:1:$L(BCHRBIN,";") W ?J,"M      F" S J=J+14
 ;W !?32,"M      F",?47,"M      F",?62,"M      F",?76,"M      F",?92,"M      F",?107,"M      F",?121,"M      F"
 W !,$TR($J(" ",132)," ","-")
 Q
