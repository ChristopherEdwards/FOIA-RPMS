BCHRC6P ; IHS/TUCSON/LAB - print dx by age ;  [ 12/06/00  8:50 AM ]
 ;;1.0;IHS RPMS CHR SYSTEM;**7,11**;OCT 28, 1996
 ;IHS/CMI/LAB - tmp to xtmp
START ;
 S Y=BCHBD D DD^%DT S BCHBDD=Y S Y=BCHED D DD^%DT S BCHEDD=Y
 S Y=DT D DD^%DT S BCHDT=Y
 S BCHPG=0
 K BCHQUIT
 I '$D(^XTMP("BCHRC6",BCHJOB,BCHBT)) W !!,"NO DATA TO REPORT" G DONE
 ;
 D @("HEAD"_(2-($E(IOST,1,2)="C-")))
 ;
 I $Y>(IOSL-4) D HEAD G:$D(BCHQUIT) DONE
 W !,"TOTAL"
 F I=1:1:10 S V="V"_I S @V=$P(^XTMP("BCHRC6",BCHJOB,BCHBT,"TOTAL"),U,I)
 S V1=V1/60 W ?25,$J($FN(V1,",",0),10)
 S V2=V2/60 W ?37,$J($FN(V2,",",0),10)
 S V3=V3/60 W ?49,$J($FN(V3,",",0),10)
 S V4=V4/60 W ?61,$J($FN(V4,",",0),10)
 W ?73,$J($FN(V5,",",0),10)
 W ?85,$J($FN(V6,",",0),10)
 W ?97,$J($FN(V7,",",0),10)
 S V8=$S(V7:V10/V7,1:0) W ?109,$J($FN(V8,",",1),10)
 W ?121,$J($FN(V9,",",0),10)
PROV ;print each provider
 S BCHX="" F  S BCHX=$O(^XTMP("BCHRC6",BCHJOB,BCHBT,"PROVIDER",BCHX)) Q:BCHX=""!($D(BCHQUIT))  D
 .I $Y>(IOSL-4) D HEAD G:$D(BCHQUIT) DONE
 .W !,$E(BCHX,1,24)
 .F I=1:1:10 S V="V"_I S @V=$P(^XTMP("BCHRC6",BCHJOB,BCHBT,"PROVIDER",BCHX),U,I)
 .S V1=V1/60 W ?25,$J($FN(V1,",",0),10)
 .S V2=V2/60 W ?37,$J($FN(V2,",",0),10)
 .S V3=V3/60 W ?49,$J($FN(V3,",",0),10)
 .S V4=V4/60 W ?61,$J($FN(V4,",",0),10)
 .W ?73,$J($FN(V5,",",0),10)
 .W ?85,$J($FN(V6,",",0),10)
 .W ?97,$J($FN(V7,",",0),10)
 .S V8=$S(V7:V10/V7,1:0) W ?109,$J($FN(V8,",",1),10)
 .W ?121,$J($FN(V9,",",0),10)
DONE D DONE^BCHUTIL1
 K ^XTMP("BCHRC6",BCHJOB,BCHBT),BCHJOB,BCHBT
 Q
HEAD ;
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S BCHQUIT="" Q
HEAD1 ; if terminal
 W:$D(IOF) @IOF
HEAD2 ; if printer
 S BCHPG=BCHPG+1
 W !,$P(^VA(200,DUZ,0),U,2),?56,"DATE GENERATED:  ",BCHDT,?124,"Page ",BCHPG,!
 W $$CTR^BCHRLU($$LOC^BCHRLU),!
 W !?46,"**********  CHR REPORT NO. 6  **********"
 W !!?59,"PROVIDER DATA"
 S BCHPROGN=$S(BCHPRG:$P(^BCHTPROG(BCHPRG,0),U)_" ("_$P(^(0),U,5)_")",1:"ALL"),X=$L(BCHPROGN)+10
 W !!?((132-X)/2),"PROGRAM:  ",BCHPROGN
 W !?43,"REPORT DATES:  ",BCHBDD,"  TO  ",BCHEDD,!
 W !!?25,"   SERVICE",?37,"    TRAVEL",?49,"     LEAVE",?61,"     TOTAL",?73,"0 NUM SERV",?85,"1 NUM SERV",?97,"     GROUP",?109,"   AVERAGE",?121,"TOT NUMBER"
 W !,"PROVIDER",?25,"     HOURS",?37,"     HOURS",?49,"     HOURS",?61,"     HOURS",?73,"ACTIVITIES",?85,"ACTIVITIES",?97,"ACTIVITIES",?109,"  GRP SIZE",?121,"    SERVED"
 W !,$TR($J(" ",132)," ","-")
 Q
