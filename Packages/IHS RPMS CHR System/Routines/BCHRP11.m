BCHRP11 ; IHS/TUCSON/LAB - DETAILED/BRIEF LISTING OF RECORDS, REPORT 1 ;  [ 06/21/99  9:35 PM ]
 ;;1.0;IHS RPMS CHR SYSTEM;**8**;OCT 28, 1996
 ;IHS/CMI/LAB - patch 8 Y2K
 ;
 ;
HEADER ;EP
 I 'BCHPG G HEADER1
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S BCHQUIT="" Q
HEADER1 ;
 W:$D(IOF) @IOF S BCHPG=BCHPG+1
 W ?($S(BCHRTYPE="B":16,1:38)),"**********  CONFIDENTIAL PATIENT INFORMATION  **********",!
 W ?(BCHCW-$L($P(^DIC(4,DUZ(2),0),U))/2),$P(^DIC(4,DUZ(2),0),U),?($S(BCHCW=132:120,1:72)),"Page ",BCHPG
 S BCHTEXT="CHR RECORD LISTING"
 W !?(BCHCW-$L(BCHTEXT)/2),BCHTEXT,!
 S BCHTEXT="Record Dates:  "_BCHBDD_" and "_BCHEDD
 W ?(BCHCW-$L(BCHTEXT)/2),BCHTEXT,!
 I $G(BCHSPAG) S BCHLENG=$L(BCHSRTR)+$L(BCHSORV)+2 S:BCHCW<BCHLENG BCHLENG=BCHCW W !?((BCHCW-BCHLENG)/2),BCHSORV,":  ",BCHRSRT,!
 W $TR($J(" ",BCHCW)," ","=")
 ;begin Y2K - reformat header
 I BCHRTYPE="B" W !," DATE",?11,"PROGRAM",?19,"CHR",?23,"PATIENT NAME",?44,"LOC",?51,"ASSESSMENTS - POVS" ;Y2000
 E   W !," DATE",?11,"PROGRAM",?19,"CHR",?23,"PATIENT NAME",?44,"LOC",?51,"ASSESSMENTS - POVS",?86,"REF BY",?96,"REF TO",?105,"EVAL",?110,"TRV",?116,"# SERVED" ;Y2000
 ;end Y2K
 W !,$TR($J(" ",BCHCW)," ","-")
 Q
