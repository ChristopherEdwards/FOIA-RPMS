BCHRP21 ; IHS/TUCSON/LAB - PROCESS REPORT ;  [ 04/02/01  9:35 AM ]
 ;;1.0;IHS RPMS CHR SYSTEM;**7,12**;OCT 28, 1996
 ;IHS/CMI/LAB tmp to xtmp
 ;
 ;
 ;
 ;
START ;
 D XTMP^BCHUTIL("BCHRP2","CHR ACTIVITY REPORT")
 S (BCHBT,BCHBTH)=$H,BCHJOB=$J
 D D,END
 Q
 ;
D ; Run by date of service
 S X1=BCHBD,X2=-1 D C^%DTC S BCHSD=X
 S BCHODAT=BCHSD_".9999" F  S BCHODAT=$O(^BCHR("B",BCHODAT)) Q:BCHODAT=""!((BCHODAT\1)>BCHED)  D D1
 Q
 ;
END ;
 S BCHET=$H
 D EOJ
 Q
EOJ ;
 Q
D1 ;
 S (BCHR,BCHRCNT)=0 F  S BCHR=$O(^BCHR("B",BCHODAT,BCHR)) Q:BCHR'=+BCHR  I $D(^BCHR(BCHR,0)),$P(^(0),U,2)]"",$P(^(0),U,3)]"" S BCHR0=^(0) D PROC
 Q
PROC ;
 S BCHPROG=$P(BCHR0,U,2),BCHPROGN=$P(^BCHTPROG(BCHPROG,0),U)_" ("_$P(^(0),U,5)_")"
 I BCHPRG,BCHPRG'=BCHPROG Q
 S BCHLOC=$P(BCHR0,U,6) Q:BCHLOC=""  S BCHLOCN=$P(^BCHTACTL(BCHLOC,0),U)
 S BCHPROV=$P(BCHR0,U,3) Q:BCHPROV=""  S BCHPNAME=$P(^VA(200,BCHPROV,0),U)
 S BCHX=0 F  S BCHX=$O(^BCHRPROB("AD",BCHR,BCHX)) Q:BCHX'=+BCHX  S BCHACT=$P(^BCHRPROB(BCHX,0),U,4) I BCHACT]"" S BCHACTN=$P(^BCHTSERV(BCHACT,0),U)_" ("_$P(^(0),U,3)_")" D
 .S $P(^(BCHACTN),U)=$S($D(^XTMP("BCHRP2",BCHJOB,BCHBT,"RECORDS",BCHPROGN,BCHLOCN,BCHPNAME,BCHACTN)):$P(^(BCHACTN),U)+1,1:1)
 .S $P(^XTMP("BCHRP2",BCHJOB,BCHBT,"RECORDS",BCHPROGN,BCHLOCN,BCHPNAME,BCHACTN),U,2)=$P(^XTMP("BCHRP2",BCHJOB,BCHBT,"RECORDS",BCHPROGN,BCHLOCN,BCHPNAME,BCHACTN),U,2)+$P(^BCHRPROB(BCHX,0),U,5)
 Q
