BRNRUP2 ; IHS/OIT/LJF - CONTINUED REPORT UTILITY PRINT LOGIC
 ;;2.0;RELEASE OF INFO SYSTEM;*1*;APR 10, 2003
 ;IHS/OIT/LJF 10/25/2007 PATCH 1 Added this routine
 ;
DONE ;EP
 I $D(ZTQUEUED),BRNCTYP="F" G DONE1
 D TIME
DONE1 ;
 K ^XTMP("BRNVL",BRNJOB,BRNBT),^XTMP("BRNFLAT",$J)
 D DEL^BRNRU
 D EN^XBVK("BRN")
 D EN^XBVK("VALM")
 Q
 ;
HEAD ;ENTRY POINT
 I 'BRNPG G HEAD1
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S BRNQUIT="" Q
 ;
HEAD1 ;EP
 I BRNCTYP="F",$D(ZTQUEUED) Q
 W:$D(IOF) @IOF S BRNPG=BRNPG+1
 I $G(BRNTITL)="" S BRNTEXT="ROI DISCLOSURE REQUEST LISTING",BRNLENG=$L(BRNTEXT) W !?((BRNTCW-BRNLENG)/2),BRNTEXT,?(BRNTCW-8),"Page ",BRNPG
 I $G(BRNTITL)]"" S BRNLENG=$L(BRNTITL) W !?((BRNTCW-BRNLENG)/2),BRNTITL,?(BRNTCW-8),"Page ",BRNPG
 S BRNLENG=46 S:BRNTCW<BRNLENG BRNLENG=BRNTCW W !?((BRNTCW-BRNLENG)/2),"Request Dates:  ",BRNBDD," and ",BRNEDD,!
 I BRNCTYP="S" S BRNLENG=$L(BRNSORV)+23 W !?((BRNTCW-BRNLENG)/2),"ROI SUB-TOTALS BY:  ",BRNSORV,!
 I $G(BRNSPAG) S BRNLENG=$L(BRNSORV)+$L(BRNSRTR)+2 S:BRNTCW<BRNLENG BRNLENG=BRNTCW W !?((BRNTCW-BRNLENG)/2),BRNSORV,":  ",BRNSRTR,!
 I BRNHEAD]"" W !,BRNHEAD,!
 W BRNDASH,!
 I BRNCTYP="S" W !,BRNSORV,":"
 Q
 ;
TIME ;ENTRY POINT - END OF REPORT TIME DISPLAY
 I $D(BRNET) S BRNTS=(86400*($P(BRNET,",")-$P(BRNBT,",")))+($P(BRNET,",",2)-$P(BRNBT,",",2)),BRNH=$P(BRNTS/3600,".") S:BRNH="" BRNH=0 D
 . S BRNTS=BRNTS-(BRNH*3600),BRNM=$P(BRNTS/60,".") S:BRNM="" BRNM=0 S BRNTS=BRNTS-(BRNM*60),BRNS=BRNTS
 . W !!,"RUN TIME (H.M.S): ",BRNH,".",BRNM,".",BRNS
 I $E(IOST)="C",IO=IO(0) S DIR(0)="EO",DIR("A")="End of report.  HIT RETURN" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 W:$D(IOF) @IOF
 K BRNTS,BRNS,BRNH,BRNM,BRNET
 Q
