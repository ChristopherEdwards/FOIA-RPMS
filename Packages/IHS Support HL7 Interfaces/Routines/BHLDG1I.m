BHLDG1I ; cmi/sitka/maw - BHL File Inbound DG1 segment ;
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound DG1 segment
 ;
MAIN ;-- this is the main routine driver
 Q:'$G(BHLVSIT)
 D FILE,EOJ
 Q
 ;
FILE ;-- get the data and file it
 F M="DH","AD","NB","PV" S BHLDGC(M)=1
 S BHLDA=0 F  S BHLDA=$O(@BHLTMP@(BHLDA)) Q:BHLDA=""  D
 . S BHLDC=$P($G(@BHLTMP@(BHLDA,3)),CS)
 . Q:BHLDC=""
 . S BHLDCI=$O(^ICD9("BA",BHLDC,0))
 . S BHLDTP=$G(@BHLTMP@(BHLDA,6))
 . S BHLDCL=$G(@BHLTMP@(BHLDA,19))
 . I $O(BHL("ZDX",0)) D FILE^BHLZDXI
 . I '$D(BHLDGC(BHLDTP)) S BHLERCD="NODGTP" X BHLERR Q
 . I BHLDTP="PV" D PV Q
 . D @BHLDTP,PV
 Q
 ;
DH ;death dx
 S BHLFL=9000001,BHLFLD=1114,BHLX=APCDALVR("APCDVSIT"),BHLVAL=BHLDC
 X BHLDIE
 Q
 ;
AD ;-- admitting dx
 S BHLVIEN=$O(^AUPNVINP("AD",APCDALVR("APCDVSIT"),0))
 I BHLVIEN="" S BHLERCD="NOVHOSP" X BHLERR Q
 S BHLFL=9000010.02,BHLFLD=.12,BHLX=BHLVIEN,BHLVAL=BHLDC X BHLDIE
 Q
 ;
NB ;-- newborn diagnosis      
 S BHLVIEN=$O(^AUPNVCHS("AD",APCDALVR("APCDVSIT"),0))
 I BHLVIEN="" S BHLERCD="NOVCHS" X BHLERR Q
 S BHLFL=9000010.03,BHLFLD=.09,BHLX=BHLVIEN,BHLVAL=BHLDC X BHLDIE
 Q
 ;
PV ;-- purpose of visit
 K BHLPOVE
 S BHLPDA=0 F  S BHLPDA=$O(^AUPNVPOV("AD",BHLVSIT,BHLPDA)) Q:BHLPDA=""!($D(BHLPOVE))  D
 . I $P(^AUPNVPOV(BHLPDA,0),U)=BHLDCI S BHLPOVE=1 Q
 Q:$D(BHLPOVE)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.07 (ADD)]"
 S APCDALVR("APCDTPOV")=BHLDC
 S APCDALVR("APCDTNQ")=$G(BHLPVN)
 S APCDALVR("APCDTSTG")=$G(BHLSTG)
 S APCDALVR("APCDTMOD")=$G(BHLMOD)
 S APCDALVR("APCDTCD")=$G(BHLCAU)
 S APCDALVR("APCDTFR")=$G(BHLFR)
 S APCDALVR("APCDTCI")=$G(BHLCOI)
 S APCDALVR("APCDTPS")=$G(BHLDCL)
 D ^APCDALVR
 I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVPOV" X BHLERR
 Q
 ;
EOJ ;-- kill variables and quit
 K BHLDA,BHLDC,BHLDTP,BHLDCL,BHLVIEN,BHLFL,BHLFLD,BHLX,BHLVAL,BHLPVN
 K BHLSTG,BHLMOD,BHLCAU,BHLFR,BHLCOI,BHLDCL
 Q
 ;
