BHLIN1 ; cmi/sitka/maw - BHL GIS IN1 Supplement ;   
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;;
 ;
 ;this routine will supplement GIS IN1 segments
 ;
INS ;-- setup insurance arrays
 K INDA(9000003.1),INDA(9000004),INDA(9000003),INDA(9000005)
 I '$G(BHL("VDT")) S BHL("VDT")=DT
 I $$MCR^AUPNPAT(INDA,BHL("VDT")) D
 . S BHLRCNT=0
 . S BHLEDA=0 F  S BHLEDA=$O(^AUPNMCR(INDA,11,BHLEDA)) Q:'BHLEDA  D
 .. S BHLED=$P($G(^AUPNMCR(INDA,11,BHLEDA,0)),U)
 .. S BHLEXD=$P($G(^AUPNMCR(INDA,11,BHLEDA,0)),U,2)
 .. Q:BHLED=""
 .. I BHLEXD="" S BHLEXD=9999999
 .. Q:BHLED>BHL("VDT")
 .. Q:BHLEXD<BHL("VDT")
 .. S BHLRCNT=BHLRCNT+1
 .. S INDA(9000003.11,BHLEDA)=""
 I $$PI^AUPNPAT(INDA,BHL("VDT")) D INDA^BHLIN1PI
 I $$MCD^AUPNPAT(INDA,BHL("VDT")) D
 . S BHLRCNT=0
 . S BHLDA=0 F  S BHLDA=$O(^AUPNMCD("B",INDA,BHLDA)) Q:'BHLDA  D
 .. S BHLEDA=0 F  S BHLEDA=$O(^AUPNMCD(BHLDA,11,BHLEDA)) Q:'BHLEDA  D
 ... S BHLED=$P($G(^AUPNMCD(BHLDA,11,BHLEDA,0)),U)
 ... S BHLEXD=$P($G(^AUPNMCD(BHLDA,11,BHLEDA,0)),U,2)
 ... Q:BHLED=""
 ... I BHLEXD="" S BHLEXD=9999999
 ... Q:BHLED>BHL("VDT")
 ... Q:BHLEXD<BHL("VDT")
 ... S BHLRCNT=BHLRCNT+1
 ... S INDA(9000004,BHLRCNT)=BHLDA
 ... S INDA(9000004.11,BHLEDA)=""
 D RR
 Q
 ;
RR ;-- get railroad insurance ien if eligible
 Q:'$O(^AUPNRRE("B",INDA,0))
 N BHLDA,BHLED,BHLEXD
 S INDA(9000005,1)=INDA
 S BHLDA=0 F  S BHLDA=$O(^AUPNRRE(INDA,11,BHLDA)) Q:'BHLDA  D
 . S BHLED=$P($G(^AUPNRRE(INDA,11,BHLDA,0)),U)
 . S BHLEXD=$P($G(^AUPNRRE(INDA,11,BHLDA,0)),U,2)
 . Q:BHLED=""
 . I BHLEXD="" S BHLEXD=9999999
 . Q:BHLED>BHL("VDT")
 . Q:BHLEXD<BHL("VDT")
 . S INDA(9000005.11,BHLDA)=""
 Q
 ;
