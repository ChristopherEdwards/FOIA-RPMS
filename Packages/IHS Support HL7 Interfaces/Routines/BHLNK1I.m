BHLNK1I ; cmi/sitka/maw - BHL Process Inbound NK1 Segment ;  
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound NK1 segment
 ;
MAIN ;-- this is the main routine driver
 D PROCESS,EOJ
 Q
 ;
PROCESS ;-- let's go through and setup the data by relationship type
 S BHLDA=0 F  S BHLDA=$O(@BHLTMP@(BHLDA)) Q:BHLDA=""  D
 . S BHLCR=$P($G(@BHLTMP@(BHLDA,7)),CS,2)
 . Q:BHLCR=""
 . S BHLNM=$G(@BHLTMP@(BHLDA,2))
 . Q:BHLNM=""
 . S BHLREL=$P($G(@BHLTMP@(BHLDA,3)),CS,2)
 . Q:BHLREL=""
 . S BHLST1=$P($G(@BHLTMP@(BHLDA,4)),CS)
 . S BHLST2=$P($G(@BHLTMP@(BHLDA,4)),CS,2)
 . S BHLCTY=$P($G(@BHLTMP@(BHLDA,4)),CS,3)
 . S BHLST=$P($G(@BHLTMP@(BHLDA,4)),CS,4)
 . S BHLST=$S(BHLST'="":$O(^DIC(5,"C",BHLST,0)),1:"")
 . S BHLZIP=$P($G(@BHLTMP@(BHLDA,4)),CS,5)
 . S BHLPH=$G(@BHLTMP@(BHLDA,5))
 . I BHLCR="SELF" D
 .. S BHLCR="SELF"
 .. S BHLEN=$G(@BHLTMP@(BHLDA,13))
 .. S BHLES=$G(@BHLTMP@(BHLDA,34))
 . I BHLCR="SPOUSE" D
 .. S BHLEN=$G(@BHLTMP@(BHLDA,13))
 . I BHLCR="EMERGENCY CONTACT" S BHLCR="EC"
 . I BHLCR="NEXT OF KIN" S BHLCR="NOK"
 . Q:'$L($T(@BHLCR))
 . D @BHLCR
 Q
 ;
EC ;-- file emergency contact
 S BHLX=BHLPAT
 S BHLFL=2,BHLFLD=.331,BHLVAL=BHLNM X BHLDIE
 ;S BHLFLD=.332,BHLVAL=BHLREL X BHLDIE  ;gets triggered from 9000001
 S BHLFLD=.333,BHLVAL=BHLST1 X BHLDIE
 S BHLFLD=.334,BHLVAL=BHLST2 X BHLDIE
 S BHLFLD=.336,BHLVAL=BHLCTY X BHLDIE
 S BHLFLD=.337,BHLVAL=BHLST X BHLDIE
 S BHLFLD=.338,BHLVAL=BHLZIP X BHLDIE
 S BHLFLD=.339,BHLVAL=BHLPH X BHLDIE
 S BHLFL=9000001,BHLFLD=3102,BHLVAL=BHLREL X BHLDIE
 Q
 ;
NOK ;-- file next of kin
 S BHLFL=2,BHLFLD=.211,BHLVAL=BHLNM,BHLX=BHLPAT X BHLDIE
 ;S BHLFLD=.212,BHLVAL=BHLREL X BHLDIE  ;gets triggered from 9000001
 S BHLFLD=.213,BHLVAL=BHLST1 X BHLDIE
 S BHLFLD=.214,BHLVAL=BHLST2 X BHLDIE
 S BHLFLD=.216,BHLVAL=BHLCTY X BHLDIE
 S BHLFLD=.217,BHLVAL=BHLST X BHLDIE
 S BHLFLD=.218,BHLVAL=BHLZIP X BHLDIE
 S BHLFLD=.219,BHLVAL=BHLPH X BHLDIE
 S BHLFL=9000001,BHLFLD=2802,BHLVAL=BHLREL X BHLDIE
 Q
 ;
SELF ;-- file self
 S BHLFL=9000001,BHLFLD=.19,BHLVAL=BHLEN,BHLX=BHLPAT X BHLDIE
 S BHLFLD=.21,BHLVAL=BHLES X BHLDIE
 Q
 ;
SPOUSE ;-- file spouse
 S BHLFL=9000001,BHLFLD=.22,BHLVAL=BHLEN,BHLX=BHLPAT X BHLDIE
 Q
 ;
EOJ ;-- kill variables and quit
 K @BHLTMP
 K BHLDA,BHLNM,BHLCR,BHLREL,BHLST1,BHLST2,BHLCTY,BHLST,BHLZIP,BHLPH
 K BHLEN,BHLES,BHLFL,BHLFLD,BHLVAL,BHLX
 Q
 ;
