BHLOBRI ; cmi/sitka/maw - File Inbound OBR Segment ; 
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound OBR segment it will then call
 ;the corresponding entry point to file the OBX segment (result)
 ;
 ;
MAIN ;-- this is the main routine driver
 S BHLODA=0 F  S BHLODA=$O(@BHLTMP@(BHLODA)) Q:BHLODA=""  D
 . S BHLVFL=$P($G(@BHLTMP@(BHLODA,3)),CS,3)
 . Q:BHLVFL=""
 . Q:'$L($T(@BHLVFL))
 . S BHLPAR=BHLODA
 . D @BHLVFL
 D EOJ
 Q
 ;
MSR ;-- this is the v measurement file           
 X BHLKW
 K BHLMTCH
 S BHLTP=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLTP=""
 I '$O(^AUTTMSR("B",BHLTP,0)) S BHLERCD="NOMSR" X BHLERR Q
 S BHLTPI=$O(^AUTTMSR("B",BHLTP,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVMSR("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVMSR(BHLDA,0),U)=BHLTPI S BHLMTCH=1
 Q:$D(BHLMTCH)
 D @BHLVFL^BHLOBXI
 Q
 ;
EYE ;-- this is the v eye glass file
 X BHLKW
 D @BHLVFL^BHLOBXI
 Q
 ;
LAB ;-- this is the v lab file
 X BHLKW
 K BHLMTCH
 S BHLLB=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLLB=""
 I '$O(^LAB(60,"B",BHLLB,0)) S BHLERCD="NOLAB" X BHLERR Q
 S BHLLBI=$O(^LAB(60,"B",BHLLB,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVLAB("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVLAB(BHLDA,0),U)=BHLLBI S BHLMTCH=1
 Q:$D(BHLMTCH)
 S BHLSITE=$G(@BHLTMP@(BHLODA,15))
 I BHLSITE'="" D
 . I '$O(^LAB(61,"B",BHLSITE,0)) S BHLERCD="NOLSITE" X BHLERR Q
 . S BHLSITEI=$O(^LAB(61,"B",BHLSITE,0))
 S BHLOP=$P($G(@BHLTMP@(BHLODA,16)),CS,2)
 X BHLKSV
 S APCDALVR("APCDTSTE")=BHLSITE
 S APCDALVR("APCDTPRV")=BHLOP
 D @BHLVFL^BHLOBXI
 Q
 ;
SK ;-- this is the v skin test file
 X BHLKW
 K BHLMTCH
 S BHLSK=$P($G(@BHLTMP@(BHLODA,4)),CS,5)
 Q:BHLSK=""
 I '$O(^AUTTSK("B",BHLSK,0)) S BHLERCD="NOSK" X BHLERR Q
 S BHLSKI=$O(^AUTTSK("B",BHLSK,0))
 S BHLOSD=$G(@BHLTMP@(BHLODA,7))
 S BHLOED=$G(@BHLTMP@(BHLODA,8))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVSK("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!$D(BHLERR("WARNING"))  D
 . I $P(^AUPNVSK(BHLDA,0),U)=BHLSKI S BHLMTCH=1
 I '$D(BHLMTCH) D
 . X BHLKSV
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.12 (ADD)]"
 . S APCDALVR("APCDTSK")=BHLSK
 . S APCDALVR("APCDTCDT")=BHLOSD
 . S APCDALVR("APCDTDR")=BHLOED
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVSK" X BHLERR Q
 D @BHLVFL^BHLOBXI
 Q
 ;
XAM ;-- this is the v exam file
 X BHLKW
 K BHLMTCH
 S BHLXM=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLXM=""
 I '$O(^AUTTEXAM("B",BHLXM,0)) S BHLERCD="NOXAM" X BHLERR Q
 S BHLXMI=$O(^AUTTEXAM("B",BHLXM,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVXAM("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVXAM(BHLDA,0),U)=BHLXMI S BHLMTCH=1
 Q:$D(BHLMTCH)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.13 (ADD)]"
 S APCDALVR("APCDTEX")=BHLXM
 D @BHLVFL^BHLOBXI
 Q
 ;
TRT ;-- this is the v treatment file
 X BHLKW
 K BHLMTCH
 S BHLTRT=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLTRT=""
 I '$O(^AUTTTRT("B",BHLTRT,0)) S BHLERCD="NOTRT" X BHLERR Q
 S BHLTRTI=$O(^AUTTTRT("B",BHLTRT,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVTRT("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVTRT(BHLDA,0),U)=BHLTRTI S BHLMTCH=1
 Q:$D(BHLMTCH)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.15 (ADD)]"
 S APCDALVR("APCDTTRT")="`"_BHLTRTI
 D @BHLVFL^BHLOBXI
 Q
 ;
PED ;-- this is the v patient ed file
 X BHLKW
 K BHLMTCH
 S BHLED=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 S BHLEDC=$P($G(@BHLTMP@(BHLODA,4)),CS,5)
 Q:BHLED=""
 I '$O(^AUTTEDT("B",BHLED,0)) S BHLERCD="NOPED" X BHLERR Q
 S BHLEDI=$O(^AUTTEDT("B",BHLED,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVPED("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVPED(BHLDA,0),U)=BHLEDI S BHLMTCH=1
 Q:$D(BHLMTCH)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.16 (ADD)]"
 S APCDALVR("APCDTTOP")=BHLED
 D @BHLVFL^BHLOBXI
 Q
 ;
PT ;-- this is the v physical therapy file
 X BHLKW
 K BHLMTCH
 S BHLPT=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLPT=""
 I '$O(^AUTTPHTH("B",BHLPT,0)) S BHLERCD="NOPT" X BHLERR Q
 S BHLPTI=$O(^AUTTPHTH("B",BHLPT,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVPT("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVPT(BHLDA,0),U)=BHLPTI S BHLMTCH=1
 Q:$D(BHLMTCH)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.17 (ADD)]"
 S APCDALVR("APCDTCOD")=BHLPT
 D @BHLVFL^BHLOBXI
 Q
 ;
CPT ;-- this is the v cpt file
 X BHLKW
 K BHLMTCH
 S BHLC=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLC=""
 I '$O(^ICPT("B",BHLC,0)) S BHLERCD="NOCPT" X BHLERR Q
 S BHLCI=$O(^ICPT("B",BHLC,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVCPT("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVCPT(BHLDA,0),U)=BHLCI S BHLMTCH=1
 Q:$D(BHLMTCH)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.18 (ADD)]"
 S APCDALVR("APCDTCPT")=BHLC
 D @BHLVFL^BHLOBXI
 Q
 ;
DXP ;-- this is the v diagnostic procedure file
 X BHLKW
 K BHLMTCH
 S BHLDXP=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLDXP=""
 I '$O(^AUTTDXPR("B",BHLDXP,0)) S BHLERCD="NODXP" X BHLERR Q
 S BHLDXPI=$O(^AUTTDXPR("B",BHLDXP,0))
 S BHLPRV=$P($G(@BHLTMP@(BHLODA,16)),CS,2)
 S BHLDA=0 F  S BHLDA=$O(^AUPNVDXP("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!$D(BHLERR("WARNING"))  D
 . I $P(^AUPNVDXP(BHLDA,0),U)=BHLDXPI S BHLMTCH=1
 I '$D(BHLMTCH) D
 . X BHLKSV
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.21 (ADD)]"
 . S APCDALVR("APCDTDXR")=BHLDXP
 . S APCDALVR("APCDTPRV")=BHLPRV
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVDXP" X BHLERR Q
 D @BHLVFL^BHLOBXI
 Q
 ;
RAD ;-- this is the v radiology file
 X BHLKW
 K BHLMTCH
 S BHLIMP=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLIMP=""
 I '$O(^RAMIS(71,"B",BHLIMP,0)) S BHLERCD="NORAD" X BHLERR Q
 S BHLIMPI=$O(^RAMIS(71,"B",BHLIMP,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVRAD("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!($D(BHLMTCH))  D
 . I $P(^AUPNVRAD(BHLDA,0),U)=BHLIMPI S BHLMTCH=1
 Q:$D(BHLMTCH)
 S BHLPRV=$P($G(@BHLTMP@(BHLODA,16)),CS,2)
 X BHLKSV
 S APCDALVR("APCDATMP")="[APCDALVR 9000010.22 (ADD)]"
 S APCDALVR("APCDTRAD")=BHLIMP
 D @BHLVFL^BHLOBXI
 Q
 ;
HF ;-- this is the v health factors file
 X BHLKW
 K BHLMTCH
 S BHLHF=$P($G(@BHLTMP@(BHLODA,4)),CS,4)
 Q:BHLHF=""
 I '$O(^AUTTHF("B",BHLHF,0)) S BHLERCD="NOHF" X BHLERR Q
 S BHLHFI=$O(^AUTTHF("B",BHLHF,0))
 S BHLDA=0 F  S BHLDA=$O(^AUPNVHF("AD",BHLVSIT,BHLDA)) Q:BHLDA=""!$D(BHLERR("WARNING"))  D
 . I $P(^AUPNVHF(BHLDA,0),U)=BHLHFI S BHLMTCH=1
 I '$D(BHLMTCH) D
 . X BHLKSV
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.23 (ADD)]"
 . S APCDALVR("APCDTHF")=BHLHF
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVHF" X BHLERR Q
 D @BHLVFL^BHLOBXI
 Q
 ;
EOJ ;-- kill variables
 K @BHLTMP
 K BHLDA,BHLVFL,BHLMTCH,BHLTP,BHLTPI,BHLLB,BHLLBI,BHLSITE,BHLOP
 K BHLSK,BHLOSD,BHLOED,BHLXM,BHLTRT,BHLED,BHLPT,BHLC,BHLDXP,BHLPRV
 K BHLIMP,BHLHF,BHLVL,BHLVIEN,BHLFL,BHLFLD,BHLX,BHLUT,BHLRL,BHLRH
 K BHLSITEI,BHLABN,BHLUP,BHLEDT,BHLVAL,BHLTPI,BHLLBI,BHLSKI,BHLXMI
 K BHLTRTI,BHLEDI,BHLPTI,BHLCI,BHLDXPI,BHLIMPI,BHLHFI
 Q
 ;
