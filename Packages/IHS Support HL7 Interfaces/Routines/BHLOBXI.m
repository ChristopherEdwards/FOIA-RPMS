BHLOBXI ; cmi/sitka/maw - BHL File Inbound OBX Segment ;  
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound OBX segment
 ;
 Q
 ;
MSR ;-- this is the v measurement file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLTP
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . X BHLKSV
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.01 (ADD)]"
 . S APCDALVR("APCDTTYP")=BHLTP
 . S APCDALVR("APCDTVAL")=BHLVL
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVMSR" X BHLERR Q
 Q
 ;
EYE ;-- this is the v eye glass file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . I '$O(^AUPNVEYE("AD",BHLVSIT,0)) D  Q:$D(BHLERR("WARNING"))
 .. X BHLKSV
 .. S APCDALVR("APCDATMP")="[APCDALVR 9000010.04 (ADD)]"
 .. D ^APCDALVR
 .. I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVEYE" X BHLERR Q
 . S BHLVIEN=$O(^AUPNVEYE("AD",BHLVSIT,0))
 . S BHLFL=9000010.04
 . S BHLFLD=$P($G(@BHLSTMP@(BHLXDA,3)),CS)
 . I '$D(^DD(9000010.04,BHLFLD,0)) S BHLERCD="NOVFLD" X BHLERR Q
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . I $$VAL^XBDIQ1(9000010.04,BHLVIEN,BHLFLD)'="" Q
 . S BHLVAL=BHLVL
 . S BHLX=BHLVIEN X BHLDIE
 Q
 ;
LAB ;-- this is the v lab file 
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLLB
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S BHLUT=$G(@BHLSTMP@(BHLXDA,6))
 . S BHLRL=""
 . S BHLRH=""
 . I $O(^LAB(60,BHLLBI,1,"B",BHLSITEI,0)) D
 .. S BHLRL=$P(^LAB(60,BHLLBI,1,BHLSITEI,0),U,2)
 .. S BHLRH=$P(^LAB(60,BHLLBI,1,BHLSITEI,0),U,3)
 . S BHLABN=$G(@BHLSTMP@(BHLXDA,8))
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.09 (ADD)]"
 . S APCDALVR("APCDTLAB")=BHLLB
 . S APCDALVR("APCDTRES")=BHLVL
 . S APCDALVR("APCDTABN")=BHLABN
 . S APCDALVR("APCDTUNI")=BHLUT
 . S APCDALVR("APCDTRFL")=BHLRL
 . S APCDALVR("APCDTRFH")=BHLRH
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVLAB" X BHLERR Q
 Q
 ;
SK ;-- this is the v skin test file
 K BHLSKC
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLSK
 . S BHLUP=$P($G(@BHLSTMP@(BHLXDA,3)),CS)
 . I '$D(^DD(9000010.12,BHLUP,0)) S BHLERCD="NOVFLD" X BHLERR Q
 . S BHLVL=$P($G(@BHLSTMP@(BHLXDA,5)),CS)
 . S BHLDA=0 F  S BHLDA=$O(^AUPNVSK("AD",BHLVSIT,BHLDA)) Q:BHLDA=""  D
 .. Q:$P(^AUPNVSK(BHLDA,0),U)=BHLSKI
 .. I $$VAL^XBDIQ1(9000010.12,BHLDA,BHLUP)'="" Q
 .. S BHLFL=9000010.12,BHLFLD=BHLUP,BHLX=BHLDA,BHLVAL=BHLVL X BHLDIE
 Q
 ;
XAM ;-- this is the v exam file      
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLXM
 . S BHLVL=$P($G(@BHLSTMP@(BHLXDA,5)),CS,2)
 . S APCDALVR("APCDTRES")=BHLVL
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVXAM" X BHLERR Q
 Q
 ;
TRT ;-- this is the v treatment file      
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLTRT
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S APCDALVR("APCDTHM")=BHLVL
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVTRT" X BHLERR Q
 Q
 ;
PED ;-- this is the v patient education file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLED
 . S BHLVL=$P($G(@BHLSTMP@(BHLXDA,5)),CS,2)
 . S BHLPRV=$P($G(@BHLSTMP@(BHLODA,16)),CS,2)
 . S APCDALVR("APCDTPRO")=BHLPRV
 . S APCDALVR("APCDTLOU")=BHLVL
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVPED" X BHLERR Q
 Q
 ;
PT ;-- this is the v physical therapy file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLPT
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S APCDALVR("APCDTQTY")=BHLVL
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVPT" X BHLERR Q
 Q
 ;
CPT ;-- this is the v cpt file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLC
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S APCDALVR("APCDTUN")=BHLVL
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVCPT" X BHLERR Q
 Q
 ;
DXP ;-- this is the v diagnostic procedure file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""  D
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S BHLABN=$G(@BHLSTMP@(BHLXDA,8))
 . S BHLUP=$P($G(@BHLSTMP@(BHLXDA,3)),CS)
 . I '$D(^DD(9000010.21,BHLUP,0)) S BHLERCD="NOVFLD" X BHLERR Q
 . S BHLEDT=$G(@BHLSTMP@(BHLODA,14))
 . S BHLDA=0 F  S BHLDA=$O(^AUPNVDXP("AD",BHLVSIT,BHLDA)) Q:BHLDA=""  D
 .. I $$VAL^XBDIQ1(9000010.21,BHLDA,BHLUP)'="" Q
 .. I $P(^AUPNVDXP(BHLDA,0),U)=BHLDXPI S BHLFL=9000010.21,BHLFLD=BHLUP,BHLX=BHLDA,BHLVAL=BHLVL X BHLDIE
 Q
 ;
RAD ;-- this is the v radiology file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""!($D(BHLERR("WARNING")))  D
 . Q:$G(@BHLSTMP@(BHLXDA,4))'=BHLIMP
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S BHLABN=$G(@BHLSTMP@(BHLXDA,8))
 . S BHLEDT=$G(@BHLSTMP@(BHLXDA,14))
 . S APCDALVR("APCDTIMP")=BHLVL
 . S APCDALVR("APCDTABN")=BHLABN
 . S APCDALVR("APCDTCDT")=BHLEDT
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVRAD" X BHLERR Q
 Q
 ;
HF ;-- this is the v health factors file
 N BHLR
 S BHLR="OBX"
 S BHLXDA=0 F  S BHLXDA=$O(@BHLSTMP@(BHLXDA)) Q:BHLXDA=""  D
 . S BHLVL=$G(@BHLSTMP@(BHLXDA,5))
 . S BHLUP=$P($G(@BHLSTMP@(BHLXDA,3)),CS)
 . I '$D(^DD(9000010.23,BHLUP,0)) S BHLERCD="NOVFLD" X BHLERR Q
 . S BHLDA=0 F  S BHLDA=$O(^AUPNVHF("AD",BHLVSIT,BHLDA)) Q:BHLDA=""  D
 .. I $$VAL^XBDIQ1(9000010.23,BHLDA,BHLUP)'="" Q
 .. I $P(^AUPNVHF(BHLDA,0),U)=BHLHFI S BHLFL=9000010.23,BHLFLD=BHLUP,BHLX=BHLDA,BHLVAL=BHLVL X BHLDIE
 Q
 ;
