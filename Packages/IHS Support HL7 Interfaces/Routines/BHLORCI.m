BHLORCI ; cmi/sitka/maw - BHL File Inbound ORC Segment ;
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound ORC segment
 ;
MAIN ;-- get the event type and file accordingly    
 I BHLET="O01" D VMED,EOJ Q
 D VIMM,EOJ
 Q
 ;
VIMM ;-- V immunization record
 S BHLDA=0 F  S BHLDA=$O(@BHLTMP@(BHLDA)) Q:BHLDA=""  D
 . S BHLOC=$G(@BHLTMP@(BHLDA,1))
 . S BHLVIEN=$P($G(@BHLTMP@(BHLDA,3)),CS)
 . S BHLVSDT=$P($G(@BHLTMP@(BHLDA,3)),CS,2)
 . ;I $D(^AUPNVIMM(BHLVIEN,0)) S BHLERCD="EXVIMM" X BHLERR Q future
 . I BHLVIEN'="" Q:$D(^AUPNVIMM(BHLVIEN,0))
 . X BHLKSV
 . D VIMM^BHLRXAI
 . Q:$D(BHLMTCH)
 . I BHLIMM="" S BHLERCD="NOIMM" X BHLERR Q
 . D VIMM^BHLZRAI
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.11 (ADD)]"
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVIMM" X BHLERR Q
 Q
 ;
VMED ;-- V medication record
 S BHLDA=0 F  S BHLDA=$O(@BHLTMP@(BHLDA)) Q:BHLDA=""  D
 . S BHLPT=$G(@BHLTMP@(BHLDA,1))
 . S BHLVIEN=$P($G(@BHLTMP@(BHLDA,3)),CS)
 . S BHLVSDT=$P($G(@BHLTMP@(BHLDA,3)),CS,2)
 . S BHLOED=$G(@BHLTMP@(BHLDA,15))
 . I BHLPT="DC" S BHLFL=9000010.14,BHLFLD=.08,BHLX=BHLVIEN,BHLVAL=BHLOED X BHLDIE Q
 . I BHLVIEN'="" Q:$D(^AUPNVMED(BHLVIEN,0))
 . S BHLQTY=$P($G(@BHLTMP@(BHLDA,7)),CS)
 . S BHLSIG=$P($G(@BHLTMP@(BHLDA,7)),CS,2)
 . S BHLDAY=$P($G(@BHLTMP@(BHLDA,7)),CS,3)
 . S BHLOP=$P($G(@BHLTMP@(BHLDA,12)),CS,2)
 . S BHLOP=$S($$DIC^BHLU(200,BHLOP)'<0:BHLOP,1:BHLDPRV)
 . X BHLKSV
 . D VMED^BHLRXDI
 . Q:$D(BHLMTCH)
 . I BHLMED="" S BHLERCD="NOMED" X BHLERR Q
 . S APCDALVR("APCDATMP")="[APCDALVR 9000010.14 (ADD)]"
 . S APCDALVR("APCDTSIG")=BHLSIG
 . S APCDALVR("APCDTQTY")=BHLQTY
 . S APCDALVR("APCDTDAY")=BHLDAY
 . S APCDALVR("APCDTPRV")=BHLOP
 . D ^APCDALVR
 . I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVMED" X BHLERR Q
 Q
 ;
EOJ ;-- kill variables and quit
 K @BHLTMP
 K BHLDA,BHLOC,BHLVIEN,BHLVSDT,BHLIMM,BHLLOT,BHLMED,BHLDTD,BHLNTD,BHLSER
 K BHLREA,BHLCON,BHLPT,BHLOED,BHLQTY,BHLSIG,BHLDAY,BHLOP
 Q
 ;
