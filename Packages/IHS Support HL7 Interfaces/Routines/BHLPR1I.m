BHLPR1I ; cmi/sitka/maw - BHL File Inbound PR1 Segment ;
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound PR1 segment
 ;
MAIN ;-- this is the main routine driver
 D FILE,EOJ
 Q
 ;
FILE ;-- get the data and file it
 S BHLDA=0 F  S BHLDA=$O(@BHLTMP@(BHLDA)) Q:BHLDA=""  D
 . S BHLPCT=$P($G(@BHLTMP@(BHLDA,3)),CS,3)
 . S BHLPC=$P($G(@BHLTMP@(BHLDA,3)),CS)
 . I BHLPCT="99IHS" S BHLPCT="ADA"
 . D @BHLPCT
 Q
 ;
C4 ;-- file this as an evaluation and management visit
 S BHLFL=9000010,BHLFLD=.17,BHLX=APCDALVR("APCDVSIT"),BHLVAL=BHLPC
 X BHLDIE
 Q
 ;
ADA ;-- file this as a dental visit
 I '$O(BHL("ZPR",0)) S BHLERCD="NODEN" X BHLERR Q
 D DEN^BHLZPRI
 S APCDALVR("APCDTSC")=BHLPC
 D ^APCDALVR
 I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVDEN" X BHLERR
 Q
 ;
I9 ;-- file this in the v procedure file
 S BHLPCDT=$G(@BHLTMP@(BHLDA,5))
 S BHLANS=$P($G(@BHLTMP@(BHLDA,8)),CS,2)
 S BHLANSM=$G(@BHLTMP@(BHLDA,10))
 S BHLSUR=$P($G(@BHLTMP@(BHLDA,11)),CS,2)
 S BHLPP=$G(@BHLTMP@(BHLDA,14))
 S BHLADC=$P($G(@BHLTMP@(BHLDA,15)),CS)
 S APCDALVR("APCDTPRC")=BHLPC
 S APCDALVR("APCDTDX")=BHLADC
 S APCDALVR("APCDTPD")=BHLPCDT
 S APCDALVR("APCDTPP")=BHLPP
 S APCDALVR("APCDTOP")=BHLSUR
 S APCDALVR("APCDTAN")=BHLANS
 S APCDALVR("APCDTET")=BHLANSM
 I $O(BHL("ZPR",0)) D PRC^BHLZPRI
 D ^APCDALVR
 I $D(APCDALVR("APCDAFLG")) S BHLERCD="NOVPRC" X BHLERR
 Q
 ;
EOJ ;-- kill variables
 K @BHLTMP
 K BHLPC,BHLPCT,BHLNOU,BHLOPS,BHLTS,BHLPCDT,BHLANS,BHLANSM,BHLSUR,BHLPP
 K BHLADC,BHLPVN,BHLINF,BHLAA,BHLASA,BHLPRC,BHLX
 Q
 ;
