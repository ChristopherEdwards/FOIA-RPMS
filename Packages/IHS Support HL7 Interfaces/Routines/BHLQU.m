BHLQU ; cmi/sitka/maw - BHL Query Utilities ;  
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;
 ;this routine will contain a number of utilities to support queries
 ;
PTLK ;EP - patient lookup for queries
 Q:BHLWHO=""
 S BHLPHR=+$E($P(BHLWHO,U),7,12)
 Q:BHLPHR=""
 S BHLDOB=$$HDATE^INHUT($P($G(BHLWHOM),RS,2))
 S BHLSSN=$P($G(BHLWHOM),RS)
 S BHLLNM=$P($G(BHLWHO),CS,2)
 S BHLFNM=$P($G(BHLWHO),CS,3)
 S BHLASU=$E($P(BHLWHO,U),1,6)
 I BHLASU="" D DOB
 I BHLASU=""&('$O(BHLPAT(0))) D SSN  Q
 Q:$O(BHLPAT(0))
 S BHLLOC=$O(^AUTTLOC("C",BHLASU,0))
 Q:BHLLOC=""
 S BHLDUZ=BHLLOC
 S BHLXDA=0 F  S BHLXDA=$O(^AUPNPAT("D",BHLPHR,BHLXDA)) Q:'BHLXDA!($G(BHLPAT))  D
 . S BHLYDA=0 F  S BHLYDA=$O(^AUPNPAT("D",BHLPHR,BHLXDA,BHLYDA)) Q:'BHLYDA!($G(BHLPAT))  I BHLYDA=BHLDUZ S BHLPAT=BHLXDA
 I BHLPAT="" D DOB
 I BHLPAT="" D SSN Q
 I $P(^DPT(BHLPAT,0),U,3)'=BHLDOB S BHLERCD="NODOBM",BHLPAT="" X BHLERR Q
 I BHLSSN'="" D  Q:$G(BHLERR("FATAL"))
 . I $P(^DPT(BHLPAT,0),U,9)'=BHLSSN S BHLERCD="NOSSNM",BHLPAT="" X BHLERR Q
 Q
 ;
DOB ;-- lookup the query by dob
 S BHLDCNT=0
 Q:BHLDOB=""
 S BHLDDA=0 F  S BHLDDA=$O(^DPT("ADOB",BHLDOB,BHLDDA)) Q:'BHLDDA  D
 . S BHLDCNT=BHLDCNT+1
 . S BHLPAT(BHLDCNT)=BHLDDA
 Q
 ;
SSN ;-- lookup the query by ssn
 S BHLSCNT=0
 Q:BHLSSN=""
 S BHLDDA=0 F  S BHLDDA=$O(^DPT("SSN",BHLSSN,BHLDDA)) Q:'BHLDDA  D
 . S BHLSCNT=BHLSCNT+1
 . S BHLPAT(BHLSCNT)=BHLDDA
 Q
 ;
