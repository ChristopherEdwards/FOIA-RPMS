BHLZV1I ; cmi/sitka/maw - BHL File Inbound ZV1 segment ; 
 ;;3.01;BHL IHS Interfaces with GIS;;JUL 01, 2001
 ;
 ;this routine will file the inbound ZV1 segment it is called from
 ;BHLPV1I
 ;
 Q
 ;
 ;
FILE ;EP - get the data and file it
 N BHLR
 S BHLR="ZV1"
 S BHLTPB=$P($G(@BHLTMP@(BHLDA,2)),CS,2)
 S BHLCL=$G(@BHLTMP@(BHLDA,3))
 S BHLCL=$S($$DIC^BHLU(40.7,BHLCL)'<0:BHLCL,1:"")
 S BHLWIA=$P($G(@BHLTMP@(BHLDA,5)),CS,2)
 S BHLLVOS=$P($G(@BHLTMP@(BHLDA,6)),CS,2)
 S BHLELG=$P($G(@BHLTMP@(BHLDA,7)),CS,2)
 S BHLELG=$S($$DIC^BHLU(8,BHLELG):BHLELG,1:"")
 S BHLAB=$G(@BHLTMP@(BHLDA,8))
 S BHLOL=$G(@BHLTMP@(BHLDA,9))
 S BHLADS=$P($G(@BHLTMP@(BHLDA,10)),CS,2)
 S BHLADS=$S($$DIC^BHLU(45.7,BHLADS):BHLADS,1:BHLDADS)
 S BHLDDS=$P($G(@BHLTMP@(BHLDA,11)),CS,2)
 S BHLDDS=$S($$DIC^BHLU(45.7,BHLDDS):BHLDDS,1:BHLDDDS)
 S BHLNOC=$G(@BHLTMP@(BHLDA,12))
 S BHLMREL=$G(@BHLTMP@(BHLDA,13))
 S BHLAOB=$G(@BHLTMP@(BHLDA,14))
 S BHLAN=$P($G(@BHLTMP@(BHLDA,15)),CS)
 S BHLAF=$P($G(@BHLTMP@(BHLDA,15)),CS,2)
 S BHLPYS=$P($G(@BHLTMP@(BHLDA,16)),CS,2)
 S BHLTC=$G(@BHLTMP@(BHLDA,17))
 S BHLSB=$P($G(@BHLTMP@(BHLDA,18)),CS,2)
 S BHLNOV=$G(@BHLTMP@(BHLDA,19))
 S BHLHVN=$G(@BHLTMP@(BHLDA,20))
 S BHLVNDR=$G(@BHLTMP@(BHLDA,21))
 S BHLVNDR=$S($$DIC^BHLU(9999999.11,BHLVNDR):BHLVNDR,1:"")
 S BHLACT=$G(@BHLTMP@(BHLDA,22))
 S BHLTM=$G(@BHLTMP@(BHLDA,23))
 S APCDALVR("APCDTPB")=BHLTPB
 S APCDALVR("APCDCLN")=BHLCL
 Q
 ;
VF ;EP - called from BHLPV1I if this segment exists
 D VCHS,VACT
 Q
 ;
VCHS ;-- v chs
 Q:BHLAF=""
 S APCDALVR("APCDTFAC")=BHLAF
 S APCDALVR("APCDTAUT")=BHLAN
 S APCDALVR("APCDTPAY")=BHLPYS
 S APCDALVR("APCDTTC")=BHLTC
 S APCDALVR("APCDTSB")=BHLSB
 S APCDALVR("APCDTNV")=BHLNOV
 S APCDALVR("APCDTHV")=BHLHVN
 S APCDALVR("APCDTVDR")=BHLVNDR
 D ^APCDALVR
 I $D(APCDALVR("APCDAFLG")) S BHLERR="NOVCHS" X BHLERR Q
 Q
 ;
VACT ;-- v activity time
 Q:BHLACT=""
 S APCDALVR("APCDTACT")=BHLACT
 S APCDALVR("APCDDTTM")=BHLTM
 D ^APCDALVR
 I $D(APCDALVR("APCDAFLG")) S BHLERR="NOVACT" X BHLERR Q
 Q
 ;
