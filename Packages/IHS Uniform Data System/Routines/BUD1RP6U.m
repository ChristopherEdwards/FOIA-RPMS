BUD1RP6U ; IHS/CMI/LAB - UDS REPORT PROCESSOR 01 Dec 2011 5:11 PM ;
 ;;6.0;IHS/RPMS UNIFORM DATA SYSTEM;;JAN 23, 2012;Build 25
 ;
 ;
G2 ;EP
 S BUDDOB=$P(^DPT(DFN,0),U,3)
 S BUD18RB=($E(BUDBD,1,3)-18)_"1231"
 Q:BUDDOB>BUD18RB
 Q:BUDMEDV<1
 S BUD18TH=$E(BUDDOB,1,3)+18_$E(BUDDOB,4,7)
 I '$$VBBD^BUD1RP6V(DFN,$$FMADD^XLFDT(BUD18TH,1),BUDED) Q  ;quit if no visiT before 17TH birthday
 S X=$$GETV^BUD1RP6U(DFN,BUDDOB,BUDED,BUDSITE)
 Q:X<2
 K BUDTOBS,BUDTOBD,BUDTOBC
 S BUDTOBDD=$E(BUDBD,1,3)-1_$E(BUDBD,4,7)
 S BUDTOBS=$$TOBACCO(DFN,BUDTOBDD,BUDED)
 I BUDTOBS="" S BUDTOBC=$$TOBCESS(DFN,BUDTOBDD,BUDED) I BUDTOBC]"" S ^XTMP("BUD1RP6B",BUDJ,BUDH,"TCI2",BUDAGE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDTOBS,U,1)_U_$P(BUDTOBC,U,1) Q
 I BUDTOBS="" Q  ;COUNSELING??
 S BUDTOBC=$$TOBCESS(DFN,BUDTOBDD,BUDED)
 I BUDTOBC]"" S BUDSECG2("ATOB")=$G(BUDSECG2("ATOB"))+1
 ;put the rest in demoninator
 S BUDSECG2("PTS")=$G(BUDSECG2("PTS"))+1 D
 .I $G(BUDTCI2L) D
 ..I BUDTOBC="" S ^XTMP("BUD1RP6B",BUDJ,BUDH,"TCI2",BUDAGE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDTOBS,U,1)_U_$P(BUDTOBC,U,1)
 .I $G(BUDTCI1L) D
 ..I BUDTOBC]"" S ^XTMP("BUD1RP6B",BUDJ,BUDH,"TCI1",BUDAGE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDTOBS,U,1)_U_$P(BUDTOBC,U,1)
 Q
 ;
TOBACCO(P,BDATE,EDATE) ;
 NEW H,D,O,X,Y,V,F,C
 S X=0,Y="" F  S X=$O(^AUPNVHF("AC",P,X)) Q:X'=+X!(Y]"")  D
 .Q:'$D(^AUPNVHF(X,0))
 .S V=$P(^AUPNVHF(X,0),U,3)
 .S D=$$VD^APCLV(V)
 .Q:D>EDATE  ;after time frame
 .Q:D<BDATE  ;before time frame
 .S H=$P(^AUPNVHF(X,0),U,1)
 .S F=$P($G(^AUTTHF(H,0)),U,1)
 .S G=0
 .I F="CURRENT SMOKELESS" S G=1
 .I F="CESSATION-SMOKER" S G=1
 .I F="CESSATION-SMOKELESS" S G=1
 .I F="CURRENT SMOKER, STATUS UNKNOWN" S G=1
 .I F="CURRENT SMOKER, EVERY DAY" S G=1
 .I F="CURRENT SMOKER, SOME DAY" S G=1
 .Q:'G
 .S Y=F_U_D
 .Q
 I Y]"" Q Y
 ;NOW CHECK DX
 S Y=$$LASTDX^BUD1UTL1(P,"BUD TOBACCO SCREEN DXS",BDATE,EDATE)
 I Y S Y=$P(Y,U,2)_U_$P(Y,U,3) Q Y
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("1034F"),U,1))
 I Y S Y="1034F"_U_$P(Y,U,2) Q Y
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("1035F"),U,1))
 I Y S Y="1035F"_U_$P(Y,U,2) Q Y
 Q ""
 ;
TOBCESS(P,BDATE,EDATE,PDATE) ;EP
 NEW Y,X,E,D,T,%,BUDG
 S Y="BUDG("
 S X=P_"^ALL EDUC;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 ;I '$D(BUDG) Q ""
 S (X,D)=0,%="",T="" F  S X=$O(BUDG(X)) Q:X'=+X!(%]"")  D
 .S T=$P(^AUPNVPED(+$P(BUDG(X),U,4),0),U)
 .Q:'T
 .Q:'$D(^AUTTEDT(T,0))
 .S T=$P(^AUTTEDT(T,0),U,2)
 .I $P(T,"-")="TO" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 .I $P(T,"-",2)="TO" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 .I $P(T,"-",2)="SHS" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 .I $P(T,"-")="99406" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 .I $P(T,"-")="99407" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 .I $P(T,"-")="4000F" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 .I $P(T,"-")="4001F" S %=T_U_$$FMTE^XLFDT($P(BUDG(X),U))_U_$P(BUDG(X),U) Q
 I %]"" Q %
 S Y=""
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("99406"),U,1))
 I Y S Y="99406"_U_$P(Y,U,2) Q Y
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("99407"),U,1))
 I Y S Y="99407"_U_$P(Y,U,2) Q Y
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("4000F"),U,1))
 I Y S Y="4000F"_U_$P(Y,U,2) Q Y
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("4001F"),U,1))
 I Y S Y="4001F"_U_$P(Y,U,2) Q Y
 NEW BUDMEDS1,T1,M,E,G,Z,N,C,BUDLPED
 S BUDLPED=""
 D GETMEDS^BUD1UTL2(P,BDATE,EDATE,,,,,.BUDMEDS1)
 ;I '$D(BUDMEDS1) G PEDREF
 S T=$O(^ATXAX("B","BGP CMS SMOKING CESSATION MEDS",0))
 S T1=$O(^ATXAX("B","BGP CMS SMOKING CESSATION NDC",0))
 S (X,G,M,E)=0,D="" F  S X=$O(BUDMEDS1(X)) Q:X'=+X  S V=$P(BUDMEDS1(X),U,5),Y=+$P(BUDMEDS1(X),U,4) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:$$UP^XLFSTR($P($G(^AUPNVMED(Y,11)),U))["RETURNED TO STOCK"
 .S Z=$P($G(^AUPNVMED(Y,0)),U) ;get drug ien
 .Q:'Z
 .S N=$P($G(^PSDRUG(Z,0)),U)
 .I $D(^ATXAX(T,21,"B",Z)) I $P(BUDLPED,U)<$P($P(^AUPNVSIT(V,0),U),".") S BUDLPED=$P($P(^AUPNVSIT(V,0),U),".")_U_"CESSATION MED - "_N Q
 .I $D(^ATXAX(T,21,"B",Z))!(N["NICOTINE PATCH")!(N["NICOTINE POLACRILEX")!(N["NICOTINE INHALER")!(N["NICOTINE NASAL SPRAY") D
 ..I $P(BUDLPED,U)<$P($P(^AUPNVSIT(V,0),U),".") S BUDLPED=$P($P(^AUPNVSIT(V,0),U),".")_U_"CESSATION MED - "_N
 .S C=$P($G(^PSDRUG(Z,2)),U,4)
 .I C]"",$D(^ATXAX(T1,21,"B",C)) I $P(BUDLPED,U)<$P($P(^AUPNVSIT(V,0),U),".") S BUDLPED=$P($P(^AUPNVSIT(V,0),U),".")_U_"CESSATION MED - "_N
 I BUDLPED]"" Q $P(BUDLPED,U,2)_U_$P(BUDLPED,U,1)  ;BUDLPED
 Q ""
NDC(A,B) ;
 ;a is drug ien
 ;b is taxonomy ien
 NEW BUDNDC
 S BUDNDC=$P($G(^PSDRUG(A,2)),U,4)
 I BUDNDC]"",B,$D(^ATXAX(B,21,"B",BUDNDC)) Q 1
 Q 0
H ;EP ; ASTHMA
 S BUDDOB=$P(^DPT(DFN,0),U,3)
 S BUD40RB=($E(BUDBD,1,3)-40)_"0101"
 S BUD5RB=($E(BUDED,1,3)-5)_"1231"
 Q:BUDDOB>BUD5RB
 Q:BUDDOB<BUD40RB
 S Z=($E(BUDDOB,1,3)+5)_$E(BUDDOB,4,7)
 Q:$$VD^APCLV(BUDLASTV)<Z
 S Z=($E(BUDDOB,1,3)+40)_$E(BUDDOB,4,7)
 Q:$$VD^APCLV(BUDLASTV)>Z
 ;S B=$$FMADD^XLFDT($$VD^APCLV(BUDLASTV),1)
 ;S A=$$AGE^AUPNPAT(DFN,B)
 ;I A<5 Q
 ;I A>40 Q
 Q:BUDMEDV<1
 S X=$$GETV(DFN,BUDDOB,BUDED,BUDSITE)
 Q:X<2
 Q:$$PROBAS1(DFN,BUDDOB,BUDED)=1  ;eliminate anyone with severity 1 on PL
 Q:$$ASTALG(DFN,BUDED)   ;eliminate those with an allergy to a controller
 Q:$$SABA(DFN,BUDBD,BUDED)  ;if only SABA, quit
 ;eliminate those with an allergy to a drug in the BUD PQA CONTROLLERS or NDC
 S BUDAST=$$ASTHMA(DFN,BUDBD,BUDED)  ;no diagnosis of asthma during time period
 I BUDAST="" S X="",X=$$ASTHTHER(DFN,BUDBD,BUDED) I X]"" S ^XTMP("BUD1RP6B",BUDJ,BUDH,"APT2",BUDAGE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDAST,U)_U_$P(X,U,2) Q
 Q:$$AST1039(DFN,$P(BUDAST,U,2),BUDED)]""  ;had a 1039f after the asthma dx/1038f
 S BUDASTT=$$ASTHTHER(DFN,BUDBD,BUDED)
 I BUDASTT]"" S BUDSECTH("APT")=$G(BUDSECTH("APT"))+1
 ;put the rest in demoninator
 S BUDSECTH("PTS")=$G(BUDSECTH("PTS"))+1 D
 .I $G(BUDAPT2L) D
 ..I BUDASTT="" S ^XTMP("BUD1RP6B",BUDJ,BUDH,"APT2",BUDAGE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDAST,U)_U_$P(BUDASTT,U,2)
 .I $G(BUDAPT1L) D
 ..I BUDASTT]"" S ^XTMP("BUD1RP6B",BUDJ,BUDH,"APT1",BUDAGE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDAST,U)_U_$P(BUDASTT,U,2)
 Q
SABA(P,BD,ED) ;
 NEW BUDMEDS1,G,A,C,M,V,V1D,BGPHSABA
 S G="",BGPHSABA=""
 D GETMEDS^BUD1UTL2(P,BD,ED,"BGP PQA SABA MEDS","BGP PQA SABA NDC",,,.BUDMEDS1)
 I '$D(BUDMEDS1) Q G  ; no SABA meds
 S BUDISD=""
 S A=0,C="" F  S A=$O(BUDMEDS1(A)) Q:A'=+A!(C)  D
 .S M=$P(BUDMEDS1(A),U,4)  ;IEN OF V MED
 .Q:'$D(^AUPNVMED(M,0))
 .I $$UP^XLFSTR($P($G(^AUPNVMED(M,11)),U))["RETURNED TO STOCK" K BUDMEDS1(A) Q
 .I $$STATDC(M) K BUDMEDS1(A) Q  ;d/c'ed BY PROVIDER OR EDIT
 .S V=$P(BUDMEDS1(A),U,5)
 .S V1D=$$VD^APCLV(V)
 .S C=1_U_$$VAL^XBDIQ1(9000010.14,M,.01)_" on "_$$FMTE^XLFDT(V1D)
 I C S BGPHSABA=1
 I 'BGPHSABA Q ""
 S G=""
 D GETMEDS^BUD1UTL2(P,BD,ED,"BGP PQA CONTROLLER MEDS","BGP PQA CONTROLLER NDC",,,.BUDMEDS1)
 I '$D(BUDMEDS1) Q 1  ; no CONTROLLER meds BUT HAS SABA
 S BUDISD=""
 S A=0,C="" F  S A=$O(BUDMEDS1(A)) Q:A'=+A!(C)  D
 .S M=$P(BUDMEDS1(A),U,4)  ;IEN OF V MED
 .Q:'$D(^AUPNVMED(M,0))
 .I $$UP^XLFSTR($P($G(^AUPNVMED(M,11)),U))["RETURNED TO STOCK" K BUDMEDS1(A) Q
 .I $$STATDC(M) K BUDMEDS1(A) Q  ;d/c'ed BY PROVIDER OR EDIT
 .S V=$P(BUDMEDS1(A),U,5)
 .S V1D=$$VD^APCLV(V)
 .S C=1_U_$$VAL^XBDIQ1(9000010.14,M,.01)_" on "_$$FMTE^XLFDT(V1D)
 I C Q 0
 Q 1
ASTALG(P,ED) ;
 ;allergy tracking
 NEW BUDC,X,N,G,Y,T,T1,S,A,B,C
 S T=$O(^ATXAX("B","BGP PQA CONTROLLER MEDS",0))
 S T1=$O(^ATXAX("B","BGP PQA CONTROLLER NDC",0))
 S BUDC=0
 S X=0 F  S X=$O(^GMR(120.8,"B",P,X)) Q:X'=+X  D
 .Q:$P($P($G(^GMR(120.8,X,0)),U,4),".")>ED  ;entered after end date
 .S N=$P($G(^GMR(120.8,X,0)),U,3)
 .;IF PSDRUG CHECK AGAINST MEDS TAXONOMY
 .I N["PSDRUG" D  I BUDC Q 1
 ..S Y=+N
 ..I T,$D(^ATXAX(T,21,"AA",Y)) S BUDC=1
 ..S D=$P($G(^PSDRUG(Y,2)),U,4)
 ..I D,$D(^ATXAX(T1,21,"AA",D)) S BUDC=1
 .;check name for the heck of it
 .S S=$P(^GMR(120.8,X,0),U,2)   ;NAME OF THING THEY ARE ALLERGIC TO
 .S A=0 F  S A=$O(^ATXAX(T,21,A)) Q:A'=+A  D
 ..S B=$P($G(^ATXAX(T,21,A,0)),U,1)
 ..I $P($G(^PSDRUG(B,0)),U,1)=S S BUDC=1 Q
 Q BUDC
 ;
PROBAS1(P,BDATE,EDATE) ;EP
 NEW S,A,B,T,X,G,V,Y,Z
 S G=""
 S T=$O(^ATXAX("B","BUD ASTHMA DXS",0))
 S X=0,G="" F  S X=$O(^AUPNPROB("AC",P,X)) Q:X'=+X  D
 .S Z=$P(^AUPNPROB(X,0),U,13)
 .I Z="" S Z=$P(^AUPNPROB(X,0),U,8)
 .Q:Z>EDATE
 .S Y=$P(^AUPNPROB(X,0),U)
 .Q:$P(^AUPNPROB(X,0),U,12)'="A"
 .Q:'$$ICD^ATXCHK(Y,T,9)
 .Q:$P(^AUPNPROB(X,0),U,15)=""
 .S G(9999999-$P(^AUPNPROB(X,0),U,3))=$P(^AUPNPROB(X,0),U,15)
 .Q
 S X=$O(G(0)) I X Q G(X)
 S EDATE1=9999999-EDATE-1
 S D=$O(^AUPNVAST("AS",P,EDATE1))
 I 'D Q ""
 ;I D>(9999999-BDATE) Q ""
 S LAST="",E=0 F  S E=$O(^AUPNVAST("AS",P,D,E)) Q:E'=+E  S LAST=E
 I 'LAST Q ""
 S S=^AUPNVAST("AS",P,D,LAST)
 Q S
 ;
ASTHMA(P,BDATE,EDATE) ;EP
 I '$D(^AUPNVSIT("AC",P)) Q ""
 NEW A,B,E,T,G,X,V,Y
 K G
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S T=$O(^ATXAX("B","BUD ASTHMA DXS",0))
 I 'T Q ""
 S X=0,G="" F  S X=$O(^TMP($J,"A",X)) Q:X'=+X  S V=$P(^TMP($J,"A",X),U,5) D
 .Q:'$D(^AUPNVSIT(V,0))
 .Q:'$P(^AUPNVSIT(V,0),U,9)
 .Q:$P(^AUPNVSIT(V,0),U,11)
 .Q:"SAHO"'[$P(^AUPNVSIT(V,0),U,7)
 .S (D,Y)=0 F  S Y=$O(^AUPNVPOV("AD",V,Y)) Q:Y'=+Y  D
 ..S D=0
 ..I $D(^AUPNVPOV(Y,0)) S %=$P(^AUPNVPOV(Y,0),U)
 ..I $$ICD^ATXCHK(%,T,9) S D=1
 ..Q:'D
 ..S G($$VD^APCLV(V))=$$VAL^XBDIQ1(9000010.07,Y,.01)
 ..Q
 ;NOW CHECK FOR A CPT OF 1038F
 S Y=""
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("1038F"),U,1))
 I Y S G($P(Y,U,2))="CPT: 1038F"
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("4015F"),U,1))
 I Y S G($P(Y,U,2))="CPT: 4015F"
 S Y=$O(G(""),-1)
 ;W !,P," ",Y,"  " ZW G
 I Y="" Q ""
 S X=G(Y)
 Q X_" on "_$$FMTE^XLFDT(Y)_U_Y
 ;
AST1039(P,BDATE,EDATE) ;EP
 I '$D(^AUPNVSIT("AC",P)) Q ""
 NEW A,B,E,T,G,X,V,Y
 K G
 S A="^TMP($J,""A"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE),E=$$START1^APCLDF(B,A)
 I '$D(^TMP($J,"A",1)) Q ""
 S Y=""
 S Y=$$CPTI^BUD1DU(P,BDATE,EDATE,$P($$CPT^ICPTCOD("1039F"),U,1))
 I Y S G($P(Y,U,2))="CPT: 1039F"
 S Y=$O(G(""),-1) I Y="" Q ""
 S X=G(Y)
 Q X_" on "_$$FMTE^XLFDT(Y)_U_Y
ASTHTHER(P,BD,ED) ;
 NEW BUDMEDS1,G,A,C,M,V,V1D
 S G=""
 D GETMEDS^BUD1UTL2(P,BD,ED,"BGP PQA CONTROLLER MEDS","BGP PQA CONTROLLER NDC",,,.BUDMEDS1)
 I '$D(BUDMEDS1) Q G  ; no CONTROLLER meds
 S BUDISD=""
 S A=0,C="" F  S A=$O(BUDMEDS1(A)) Q:A'=+A!(C)  D
 .S M=$P(BUDMEDS1(A),U,4)  ;IEN OF V MED
 .Q:'$D(^AUPNVMED(M,0))
 .I $$UP^XLFSTR($P($G(^AUPNVMED(M,11)),U))["RETURNED TO STOCK" K BUDMEDS1(A) Q
 .I $$STATDC(M) K BUDMEDS1(A) Q  ;d/c'ed BY PROVIDER OR EDIT
 .S V=$P(BUDMEDS1(A),U,5)
 .S V1D=$$VD^APCLV(V)
 .S C=1_U_$$VAL^XBDIQ1(9000010.14,M,.01)_" on "_$$FMTE^XLFDT(V1D)
 I C Q C
 Q ""
 ;
STATDC(V) ;EP - is the prescription associated with this V MED discontinued?
 I '$G(V) Q ""
 I '$D(^AUPNVMED(V,0)) Q 0
 NEW P,S,X
 S P=$S($D(^PSRX("APCC",V)):$O(^(V,0)),1:0)
 I 'P Q 0
 S X=$P($G(^PSRX(P,0)),U,15)
 I X=12 Q 1
 I X=13 Q 1
 I X=14 Q 1
 I X=15 Q 1
 S X=$P($G(^PSRX(P,"STA")),U,1)
 I X=12 Q 1
 I X=13 Q 1
 I X=14 Q 1
 I X=15 Q 1
 Q 0
GETV(P,BD,ED,SITE) ;EP - get all visits for this patient and COUNT MEDICAL VISITS
 NEW TV,T35V,T6V,MEDV,MEDVI,LASTV,A,X,VLOC,CLINC,TIEN,VSIT,VDATE,PP,S,LINE,D
 S TV=0,T35V=0,T6V=0,MEDV=0,MEDVI="",LASTV=""
 S A="A(""VISITS"",",B=P_"^ALL VISITS;DURING "_$$FMTE^XLFDT(BD)_"-"_$$FMTE^XLFDT(ED),E=$$START1^APCLDF(B,A)
 S X=0 F  S X=$O(A("VISITS",X)) Q:X'=+X!(MEDV>1)  S VSIT=$P(A("VISITS",X),U,5) D
 .Q:'$D(^AUPNVSIT(VSIT,0))
 .Q:'$P(^AUPNVSIT(VSIT,0),U,9)
 .Q:$P(^AUPNVSIT(VSIT,0),U,11)
 .S VLOC=$P(^AUPNVSIT(VSIT,0),U,6)
 .Q:VLOC=""
 .Q:'$D(^BUDLSITE(SITE,11,VLOC))  ;not valid location
 .Q:"AHSORMEI"'[$P(^AUPNVSIT(VSIT,0),U,7)
 .S CLINC=$$CLINIC^APCLV(VSIT,"C")
 .S TIEN=$O(^BUDLCNTL("B","FIRST LEVEL CLINIC EXCLUSIONS",0))
 .I CLINC]"",$D(^BUDLCNTL(TIEN,11,"B",CLINC)) Q  ;not a clinic code we want in any table
 .;now eliminate subsequent visits to same provider on same day  = item 4 in SRD visit definition
 .S VDATE=$$VD^APCLV(VSIT)
 .S PP=$$PRIMPROV^APCLV(VSIT,"I")
 .I $P(^AUPNVSIT(VSIT,0),U,7)="I" Q  ;don't count I visits
 .I '$D(^AUPNVPOV("AD",VSIT)) Q
 .S S=0
 .I PP]"" D
 ..S D=$P($G(A("SAMEPROV",P,VDATE,PP)),U,1)
 ..I D]"",D'>$P(^AUPNVSIT(VSIT,0),U) S S=1 Q  ;already had a visit to this provider on this date
 ..S A("SAMEPROV",P,VDATE,PP)=$P(^AUPNVSIT(VSIT,0),U)_U_VSIT
 .Q:S  ;quit if already had a visit to this provider
 .S PP=$$PRIMPROV^APCLV(VSIT,"D")
 .I PP="" Q
MEDC .;NOW CHECK FOR MEDICAL CARE, CAN ONLY HAVE 1 PER LOCATION OF ENCOUNTER
 .S S=0
 .S TIEN=$O(^BUDLCNTL("B","MEDICAL CARE LINE NUMBERS",0))
 .;S PP=$$PRIMPROV^APCLV(VSIT,"D")
 .I $E($$VAL^XBDIQ1(9000010,VSIT,.06),1,3)="CHS",PP=15 S LINE=2 G MEDC1
 .S Y=$O(^BUDLTFIV("C",PP,0)) I Y="" S LINE=35 G MEDC1
 .S LINE=$O(^BUDLTFIV("AA",PP,""))
MEDC1 .S S=0
 .I $D(^BUDLCNTL(TIEN,11,"B",LINE)) D
 ..S D=$P($G(A("MEDCARE",P,VDATE,VLOC,TIEN)),U,1)
 ..I D]"",D'>$P(^AUPNVSIT(VSIT,0),U) S S=1 Q  ;already have a medical care visit on this date
 ..S A("MEDCARE",P,VDATE,VLOC,TIEN)=$P(^AUPNVSIT(VSIT,0),U)_U_VSIT
 ..S MEDV=MEDV+1,MEDVI=VSIT
 ..Q
 Q MEDV
