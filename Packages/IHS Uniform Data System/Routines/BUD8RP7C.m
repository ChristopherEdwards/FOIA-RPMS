BUD8RP7C ; IHS/CMI/LAB - UDS REPORT PROCESSOR 01 Dec 2008 5:11 PM ;
 ;;7.0;IHS/RPMS UNIFORM DATA SYSTEM;;JAN 23, 2013;Build 31
 ;
DM ;EP - list of DM
 S BUDDOB=$P(^DPT(DFN,0),U,3)
 Q:BUDDOB>2901231
 Q:BUDDOB>2851231  ;2003 TESTING
 Q:BUDMEDV<2
 ;
 S BUDP=$$DMDX(DFN,$$DOB^AUPNPAT(DFN),BUDED)
 I '$P(BUDP,U) Q  ;not dx'ed before end of report period
 S BUDOVAR=$$OVAR(DFN,BUDBD,BUDED)
 S BUD2DM=$$DM2(DFN,BUDBD,BUDED)
 I $P(BUDOVAR,U),$P(BUD2DM,U) Q  ;had 2 dx of dm during report period and an ovary dx
 I $$GESTDX(DFN,BUDBD,BUDED) Q  ;had gestational dx during report period
 S BUDRACE=$$RACE^BUD8RPTC(DFN),BUDRACE=$P(BUDRACE,U,2)
 S BUDRACE=$$RACE^BUD8RP7I(BUDRACE),BUDR=$$RACE(BUDRACE)
 S BUDETHN=$P($$HISP^BUD8RPTC(DFN),U,1)
 I +BUDETHN=1 S BUDETHNN="Hispanic or Latino"
 I +BUDETHN=2 S BUDETHNN="All Others"
 S $P(BUDSECTC(9),U,BUDR)=$P($G(BUDSECTC(9)),U,BUDR)+1
 S $P(BUDSECTC(10),U,BUDR)=$P($G(BUDSECTC(10)),U,BUDR)+1
 S $P(BUDSECTC(9),U,9)=$P($G(BUDSECTC(9)),U,9)+1
 S $P(BUDSECTC(10),U,9)=$P($G(BUDSECTC(10)),U,9)+1
 S $P(BUDSECTF(9),U,BUDETHN)=$P($G(BUDSECTF(9)),U,BUDETHN)+1
 S $P(BUDSECTF(10),U,BUDETHN)=$P($G(BUDSECTF(10)),U,BUDETHN)+1
 S $P(BUDSECTF(9),U,3)=$P($G(BUDSECTF(9)),U,3)+1
 S $P(BUDSECTF(10),U,3)=$P($G(BUDSECTF(10)),U,3)+1
 I $G(BUDDMRL) S ^XTMP("BUD8RP7",BUDJ,BUDH,"DMR",BUDRACE,BUDCOM,BUDAGE,$P(^DPT(DFN,0),U),DFN)=$P(BUDP,"^",2)
 I $G(BUDDMEL) S ^XTMP("BUD8RP7",BUDJ,BUDH,"DME",BUDETHNN,BUDCOM,BUDAGE,$P(^DPT(DFN,0),U),DFN)=$P(BUDP,"^",2)
 ;get last hgba1c value and set counters/lists
 S BUDHGB=$$HGBA1C(DFN,BUDBD,BUDED)
SET ;
 S BUDLINE=$P(BUDHGB,U)
 S $P(BUDSECTC(BUDLINE),U,BUDR)=$P($G(BUDSECTC(BUDLINE)),U,BUDR)+1,$P(BUDSECTC(BUDLINE),U,9)=$P($G(BUDSECTC(BUDLINE)),U,9)+1
 S $P(BUDSECTF(BUDLINE),U,BUDETHN)=$P($G(BUDSECTF(BUDLINE)),U,BUDETHN)+1,$P(BUDSECTF(BUDLINE),U,9)=$P($G(BUDSECTF(BUDLINE)),U,9)+1
 I $G(BUDDMR1L),BUDLINE=11 S ^XTMP("BUD8RP7",BUDJ,BUDH,"DMR1",BUDRACE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDP,"^",2)_"^"_$P(BUDHGB,U,2)
 I $G(BUDDMR2L),BUDLINE=12 S ^XTMP("BUD8RP7",BUDJ,BUDH,"DMR2",BUDRACE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDP,"^",2)_"^"_$P(BUDHGB,U,2)
 I $G(BUDDMR3L),BUDLINE=13 S ^XTMP("BUD8RP7",BUDJ,BUDH,"DMR3",BUDRACE,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDP,"^",2)_"^"_$P(BUDHGB,U,2)
 I $G(BUDDME1L),BUDLINE=11 S ^XTMP("BUD8RP7",BUDJ,BUDH,"DME1",BUDETHNN,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDP,"^",2)_"^"_$P(BUDHGB,U,2)
 I $G(BUDDME2L),BUDLINE=12 S ^XTMP("BUD8RP7",BUDJ,BUDH,"DME2",BUDETHNN,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDP,"^",2)_"^"_$P(BUDHGB,U,2)
 I $G(BUDDME3L),BUDLINE=13 S ^XTMP("BUD8RP7",BUDJ,BUDH,"DME3",BUDETHNN,$P(^DPT(DFN,0),U),BUDCOM,DFN)=$P(BUDP,"^",2)_"^"_$P(BUDHGB,U,2)
 Q
 ;
HGBA1C(P,BDATE,EDATE) ;EP
 NEW BUDG,BUDT,BUDC,E,%,L,T,BUDLT,D,X,J,C,G
 S BUDC=0
 S G=$$CPT^BUD8DU(P,BDATE,EDATE,$O(^ATXAX("B","BUD HGBA1C CPTS",0)),5)
 I G]"" S BUDC=BUDC+1,BUDT((9999999-$P(G,U,1)),BUDC)=U_"CPT "_$P(G,U,2)
 ;now get all loinc/taxonomy tests
 S T=$O(^ATXAX("B","BGP HGBA1C LOINC CODES",0))
 S BUDLT=$O(^ATXLAB("B","DM AUDIT HGB A1C TAX",0))
 S B=9999999-BDATE,E=9999999-EDATE S D=E-1 F  S D=$O(^AUPNVLAB("AE",P,D)) Q:D'=+D!(D>B)  D
 .S L=0 F  S L=$O(^AUPNVLAB("AE",P,D,L)) Q:L'=+L  D
 ..S X=0 F  S X=$O(^AUPNVLAB("AE",P,D,L,X)) Q:X'=+X  D
 ...Q:'$D(^AUPNVLAB(X,0))
 ...I BUDLT,$P(^AUPNVLAB(X,0),U),$D(^ATXLAB(BUDLT,21,"B",$P(^AUPNVLAB(X,0),U))) S BUDC=BUDC+1,BUDT(D,BUDC)=$P(^AUPNVLAB(X,0),U,4)_U_"LAB: "_$$VAL^XBDIQ1(9000010.09,X,.01) Q
 ...Q:'T
 ...S J=$P($G(^AUPNVLAB(X,11)),U,13) Q:J=""
 ...Q:'$$LOINC(J,T)
 ...S BUDC=BUDC+1,BUDT(D,BUDC)=$P(^AUPNVLAB(X,0),U,4)_U_"LAB LOINC: "_$$VAL^XBDIQ1(9000010.09,X,.01)_" "_$P(^AUPNVLAB(X,11),U,13)
 ...Q
 ; now got though and set return value of done 1 or 0^numerator 2-7^date^value
 I '$D(BUDT) Q 13_U_"NO A1C TEST DURING THE REPORT PERIOD"  ;no tests so is hit in numerator
 ; now get rid of all on same day where 1 has a result and the other doesn't
 S D=0,BUDC=0 F  S D=$O(BUDT(D)) Q:D'=+D  S C=0,G=0 F  S C=$O(BUDT(D,C)) Q:C'=+C  D
 .I $P(BUDT(D,C),U,1)]"" S BUDC=BUDC+1
 .I BUDC>0,$P(BUDT(D,C),U,1)="" K BUDT(D,C)
 S D=0,G=""
 S D=$O(BUDT(D))
 S C=0,C=$O(BUDT(D,C))
 S X=$P(BUDT(D,C),U,1)
 I $$UP^XLFSTR(X)="COMMENT" Q 13_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D)_" (no result) "_X
 I X="" D  Q G
 .S G=""
 .I $P(BUDT(D,C),U,2)="CPT 3046F" S G=13_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D) Q
 .I $P(BUDT(D,C),U,2)="CPT 3045F" S G=12_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D) Q
 .I $P(BUDT(D,C),U,2)="CPT 3044F" S G=11_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D) Q
 .I $P(BUDT(D,C),U,2)="CPT 83036" S G=13_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D) Q
 .I $P(BUDT(D,C),U,2)="CPT 83037" S G=13_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D) Q
 .S G=13_U_$P(BUDT(D,C),U,2)_" DID NOT HAVE A RESULT "_$$DATE^BUD8UTL1(9999999-D) Q
 S X=$$STRIP^XLFSTR(X," ")  ;strip spaces
 I X[">9" Q 13_U_$P(BUDT(D,C),U,2)_" "_X_" "_X_$$DATE^BUD8UTL1(9999999-D)
 S X=$$STV(X)
 I X="" Q 13_U_$P(BUDT(D,C),U,2)_" DID NOT HAVE A RESULT "_$$DATE^BUD8UTL1(9999999-D)
 I +X>9 Q 13_U_$P(BUDT(D,C),U,2)_" "_X_" "_$$DATE^BUD8UTL1(9999999-D)
 I +X<7 Q 11_U_$P(BUDT(D,C),U,2)_" "_X_" "_$$DATE^BUD8UTL1(9999999-D)
 Q 12_U_$P(BUDT(D,C),U,2)_" "_X_" "_$$DATE^BUD8UTL1(9999999-D)
 ;Q 0_U_$P(BUDT(D,C),U,2)_" "_$$DATE^BUD8UTL1(9999999-D)_" result: "_X
 ;
RACE(R) ;EP
 I R="Unreported" Q 8
 I R="Asian" Q 1
 I R="Native Hawaiian" Q 2
 I R="Other Pacific Islander" Q 3
 I R="Black/African American" Q 4
 I R="American Indian/Alaska Native" Q 5
 I R="White" Q 6
 Q ""
DMDX(P,BDATE,EDATE,STOP) ;EP
 NEW BUDDX,B,CNT,BUDD,BUDG,BUDALL
 S STOP=$G(STOP)
 S B=0,CNT=0,BUDD="",BUDALL=""  ;if there is one before time frame set this to 1
 K BUDG
 S Y="BUDG("
 S X=P_"^LAST DX [SURVEILLANCE DIABETES;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BUDG(1)) Q 1_"^"_$P(BUDG(1),U,5)_"|"_$P(BUDG(1),U,2)
 I STOP Q ""
PROBDX S T=$O(^ATXAX("B","SURVEILLANCE DIABETES",0))
 S (X,G)=0,Z="" F  S X=$O(^AUPNPROB("AC",P,X)) Q:X'=+X!(G)  D
 .Q:$P(^AUPNPROB(X,0),U,12)'="A"
 .Q:$P(^AUPNPROB(X,0),U,8)>EDATE
 .Q:$P(^AUPNPROB(X,0),U,8)<BDATE
 .S Y=$P(^AUPNPROB(X,0),U)
 .Q:'$$ICD^ATXCHK(Y,T,9)
 .S G=$P(^AUPNPROB(X,0),U,8),Z=X
 .Q
 I G Q 1_U_"Problem List: "_$$VAL^XBDIQ1(9000011,Z,.01)_" on "_$$FMTE^XLFDT($P(^AUPNPROB(Z,0),U,8))
 ;now check for med
 K BUDG
 S Y="BUDG("
 S X=P_"^LAST RX [BUD DIABETES MEDS TAX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BUDG(1)) Q 1_"^"_$P(BUDG(1),U,5)_"|"_$E($P(BUDG(1),U,2),1,15)
 Q ""
 ;
OVAR(P,BDATE,EDATE,STOP) ;EP
 NEW BUDDX,B,CNT,BUDD,BUDG,BUDALL
 S STOP=$G(STOP)
 S B=0,CNT=0,BUDD="",BUDALL=""  ;if there is one before time frame set this to 1
 K BUDG
 S Y="BUDG("
 S X=P_"^LAST DX 256.4;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BUDG(1)) Q 1_"^"_$P(BUDG(1),U,5)_"|"_$P(BUDG(1),U,2)
 I STOP Q ""
PROBOV ;
 S (X,G)=0,Z="" F  S X=$O(^AUPNPROB("AC",P,X)) Q:X'=+X!(G)  D
 .Q:$P(^AUPNPROB(X,0),U,12)'="A"
 .Q:$P(^AUPNPROB(X,0),U,8)>EDATE
 .Q:$P(^AUPNPROB(X,0),U,8)<BDATE
 .S Y=$$VAL^XBDIQ1(9000011,X,.01)
 .Q:Y'="256.4"
 .S G=$P(^AUPNPROB(X,0),U,8),Z=X
 .Q
 I G Q 1
 Q ""
DM2(P,BDATE,EDATE,STOP) ;EP
 NEW BUDDX,B,CNT,BUDD,BUDG,BUDALL
 S STOP=$G(STOP)
 S B=0,CNT=0,BUDD="",BUDALL=""  ;if there is one before time frame set this to 1
 K BUDG
 S Y="BUDG("
 S X=P_"^LAST 2 DX [SURVEILLANCE DIABETES;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BUDG(2)) Q 1_"^"_$P(BUDG(1),U,5)_"|"_$P(BUDG(1),U,2)
 Q ""
GESTDX(P,BDATE,EDATE,STOP) ;EP
 NEW BUDDX,B,CNT,BUDD,BUDG,BUDALL
 S STOP=$G(STOP)
 S B=0,CNT=0,BUDD="",BUDALL=""  ;if there is one before time frame set this to 1
 K BUDG
 S Y="BUDG("
 S X=P_"^LAST DX [BUD GEST/STEROID DM DX;DURING "_$$FMTE^XLFDT(BDATE)_"-"_$$FMTE^XLFDT(EDATE) S E=$$START1^APCLDF(X,Y)
 I $D(BUDG(1)) Q 1_"^"_$P(BUDG(1),U,5)_"|"_$P(BUDG(1),U,2)
 I STOP Q ""
GESTPL S T=$O(^ATXAX("B","BUD GEST/STEROID DM DX",0))
 S (X,G)=0,Z="" F  S X=$O(^AUPNPROB("AC",P,X)) Q:X'=+X!(G)  D
 .Q:$P(^AUPNPROB(X,0),U,12)'="A"
 .Q:$P(^AUPNPROB(X,0),U,8)>EDATE
 .Q:$P(^AUPNPROB(X,0),U,8)<BDATE
 .S Y=$P(^AUPNPROB(X,0),U)
 .Q:'$$ICD^ATXCHK(Y,T,9)
 .S G=$P(^AUPNPROB(X,0),U,8),Z=X
 .Q
 I G Q 1
 Q ""
 ;
LOINC(A,B) ;EP
 NEW %
 S %=$P($G(^LAB(95.3,A,9999999)),U,2)
 I %]"",$D(^ATXAX(B,21,"B",%)) Q 1
 S %=$P($G(^LAB(95.3,A,0)),U)_"-"_$P($G(^LAB(95.3,A,0)),U,15)
 I $D(^ATXAX(B,21,"B",%)) Q 1
 Q ""
STV(X) ;EP - strip all characters besides numbers and a "."
 I X="" Q X
 NEW A,B,L
 S L=$L(X)
 F B=1:1:L S A=$E(X,B) Q:A=""  I A'?1N,A'?1"." S X=$$STRIP^XLFSTR(X,A) S B=B-1
 Q X
 ;
