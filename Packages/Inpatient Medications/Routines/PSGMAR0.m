PSGMAR0 ;BIR/CML3-GATHERS INFO FOR 24 HOUR MAR ;14 Oct 98 / 4:28 PM
 ;;5.0; INPATIENT MEDICATIONS ;**8,15,20**;16 DEC 97
 ;
 ; Reference to ^PS(55 supported by DBIA #2191.
 ; Reference to ^PS(59.7 supported by DBIA #2181.
 ; Reference to CUR^FHORD7 supported by DBIA #2019.
ENQ ;
 S PSGMSORT=$P($G(^PS(59.7,1,26)),U,4)
 K ^TMP($J) D NOW^%DTC S PSGDT=%,PSGMARWN="",PSJACNWP=1 D @("G"_PSGSS) I $D(^TMP($J))<10 U IO W:$Y @IOF W !!,"(No data found or 24 hour MAR run.)"
 ;
DONE ;
 K PSGMFOR
 Q
 ;
GG ; find individual wards in this ward group
 F PSGMARWD=0:0 S PSGMARWD=$O(^PS(57.5,"AC",PSGMARWG,PSGMARWD)) Q:'PSGMARWD  D GW
 Q
 ;
GW ; find patients in each ward
 I $D(^DIC(42,PSGMARWD,0)),$P(^(0),"^")]"" S PSGMARWN=$P(^(0),"^")
 E  Q
 I 'PSGMARWG S PSGMARWG=+$O(^PS(57.5,"AB",PSGMARWD,0))
 F PSGP=0:0 S PSGP=$O(^DPT("CN",PSGMARWN,PSGP)) Q:'PSGP  D PSJAC2^PSJAC(1),DTSET:'$P(PSGMARDT,".",2) D GPI
 Q
 ;
GP ; go thru selected patients
 F PSGP=0:0 S PSGP=$O(PSGPAT(PSGP)) Q:'PSGP  D PSJAC2^PSJAC(1),DTSET:'$P(PSGMARDT,".",2) D GPI
 Q
 ;
GPI ; get patient info
 ; PSGTMALL=1(sort by all team), PSGTM=1(individual team(S) selected).
 S TM="" S:PSGSS="P" PSGMARWN=$S(PSJPWDN]"":PSJPWDN,1:"NOT FOUND")
 S:PSJPRB="" PSJPRB="zz"
 S:"GP"[PSGSS!('$G(PSGTM)&'$G(PSGTMALL)) TM="zz"
 S:$G(TM)="" TM=$S(PSJPRB="zz":0,1:+$O(^PS(57.7,"AWRT",PSGMARWD,PSJPRB,0))),TM=$S('TM:"zz",'$D(^PS(57.7,PSGMARWD,1,TM,0)):TM,$P(^(0),"^")]"":$P(^(0),"^"),1:TM)
 Q:'$G(PSGTMALL)&$G(PSGTM)&'$D(PSGTM(TM))
 S PPN=$E($P(PSGP(0),"^"),1,15)_"^"_PSGP
 N SUB1,SUB2 S:PSGRBPPN="P" SUB1=PPN,SUB2=PSJPRB S:PSGRBPPN="R" SUB1=PSJPRB,SUB2=PPN
 I PSGMARB=1 D SPN Q
 I PSGMTYPE[1 F XTYPE=2:1:6 D @XTYPE
 I PSGMTYPE'[1 F XTYPE=2:1:6 D:PSGMTYPE[XTYPE @XTYPE
 D ^PSGMMAR5
 D:$S(PSGSS["P":$D(^TMP($J,PPN)),1:$D(^TMP($J,TM,PSGMARWN,SUB1,SUB2))) SPN
 Q
 ;
2 ;Loop thru UD orders       
 F PST="C","O","OC","P","R" F PSGMARED=PSGPLS-.0001:0 S PSGMARED=$O(^PS(55,PSGP,5,"AU",PST,PSGMARED)) Q:'PSGMARED  F PSGMARO=0:0 S PSGMARO=$O(^PS(55,PSGP,5,"AU",PST,PSGMARED,PSGMARO)) Q:'PSGMARO  D ORSET
 S PST="S" D ^PSGMIV
 Q
3 ;Loop thru IV orders that are Piggy back and Syringes types. 
 F PST="P","S" D ^PSGMIV
 Q
4 ;Loop thru IV orders(Additives).
 S PST="A" D ^PSGMIV
 Q
5 ;Loop thru IV orders(Hyperal).
 S PST="H" D ^PSGMIV
 Q
6 ;Loop thru IV order(Chemo).
 S PST="C" D ^PSGMIV
 Q
 ;
 ; PSGMFOR is set to bypass "fill on request" when call ^PSGPL0.
ORSET ; order record set
 S PSGMFOR="",ND2=$G(^PS(55,PSGP,5,PSGMARO,2)),(SD,X)=$P($P(ND2,"^",2),".") Q:X>PSGPLF  S FD=$P($P(ND2,"^",4),"."),T=$P(ND2,"^",6)
 NEW MARX D DRGDISP^PSJLMUT1(PSGP,+PSGMARO_"U",20,0,.MARX,1)
 S DRG=MARX(1)_U_PSGMARO_"U",QST=$S(PST="C"!(PST="O"):PST,PST="OC":"OA",PST="P":"OP",$P(ND2,"^")["PRN":"OR",1:"CR")
 S X="" I "OB"]QST,$P(ND2,U)'["@",$P(ND2,U,2)'>PSGPLS,$P(ND2,U,4)'<PSGPLF,$P(ND2,U,5),$P(ND2,U,6)<1441,$P(ND2,U,6)'="D" S X=$P(ND2,U,5),PSGPLC=1
 E  I "OB"]QST S PSGPLO=PSGMARO K PSGMAR D ^PSGPL0 S (Q,X)="" F QX=0:0 S Q=$O(PSGMAR(Q)) Q:Q=""  S X=X_$E("0",2-$L(Q))_Q_"-"
 S X=$S(QST["C"!(QST["O"):$P(ND2,"^",5),1:"")_"^"_X
 I PSGSS="P" S ^TMP($J,PPN,$S(+PSGMSORT:$E(QST,1),1:QST),DRG)=X Q
 S ^TMP($J,TM,PSGMARWN,SUB1,SUB2,$S(+PSGMSORT:$E(QST,1),1:QST),DRG)=X
 Q
 ;
SPN ; set patient node
 D DIET
 S X=$P(PSGP(0),U)_U_$E($P(PSJPDOB,U,2),1,10)_";"_PSJPAGE_U_VA("PID")_U_PSJPDX_U_PSJPWT_U_PSJPWTD_U_PSJPHT_U_PSJPHTD_U_$P(PSJPAD,U,2)_U_$P(PSJPTD,U,2)_U_$P(PSJPSEX,U,2)_U_PSJPWD_U_PSGPLS_U_PSGPLF_U_PSGMARSD_U_PSGMARFD_U_PSGMARSP_U_PSGMARFP
 I PSGSS="P" S ^TMP($J,PPN)=X_U_PSGMARWN_U_PSJPRB Q
 S ^TMP($J,TM,PSGMARWN,SUB1,SUB2)=X
 Q
DIET ; Include abbr. diet label if indicated in the Site par.
 NEW ADM,DFN,PSJMPAR K PSJDIET
 S PSJMPAR=$G(^PS(59.7,1,26))
 Q:'$P(PSJMPAR,U,3)
 S DFN=PSGP,ADM=$G(^DPT("CN",PSGMARWN,DFN))
 I +ADM D CUR^FHORD7 S PSJDIET=Y
 Q
 ;
DTSET ;
 S (PSGPLS,PSGPLF)=PSGMARDT
 S PSJSYSW=$O(^PS(59.6,"B",+$G(PSJPWD),0))
 S:PSJSYSW PSJSYSW0=$G(^PS(59.6,PSJSYSW,0))
 I $D(PSJSYSW0),$P(PSJSYSW0,"^",8) S ST=$P(PSJSYSW0,"^",8),FT=$P(PSJSYSW0,"^",9)
 E  S ST="0001",FT=24
SET S PSGMARSD=$E(ST,1,2),PSGMARFD=$E(FT,1,2) S:'PSGMARSD PSGMARSD="01" S PSGMARFD=$S(+PSGMARSD=1:24,PSGMARSD=PSGMARFD:PSGMARSD-1,1:PSGMARFD) S:$L(PSGMARFD)<2 PSGMARFD=0_PSGMARFD
 I ST>1 S X1=$P(PSGPLF,"."),X2=1 D C^%DTC S PSGPLF=X
 S PSGPLS=+(PSGPLS_"."_ST),PSGPLF=+(PSGPLF_"."_FT)
 S PSGMARSP=$$ENDTC2^PSGMI(PSGPLS),PSGMARFP=$$ENDTC2^PSGMI(PSGPLF)
 Q
