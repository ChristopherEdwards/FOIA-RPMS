PSIVCAL ;BIR/RGY,PR-CALCULATES START AND STOP DATES ;12 Mar 99 / 12:42 PM
 ;;5.0; INPATIENT MEDICATIONS ;**4,26,41,47,63,67,69,58,94,80**;16 DEC 97
 ;
 ; Reference to ^PS(52.6 is supported by DBIA #1231.
 ; Reference to ^PS(55 is supported by DBIA #2191.
 ; Reference to ^DD is supported by DBIA #10017.
 ; Reference to ^%DT is supported by DBIA #10003.
 ; Reference to ^%DTC is supported by DBIA 10000.
 ; Reference to ^XLFDT is supported by DBIA #10103.
 ; Reference to ^PS(50.7 is supported by DBIA #2180.
 ;
ENT ;NEEDS PSIVTYPE (P(4))
 I $G(PSJREN) D  Q:P(2)
 . I $G(P(9))="" S P(2)=$$DATE^PSJUTL2() Q
 . N X1
 . S X1=$$DSTART^PSJDCU(DFN,$S(PSJORD'["P":PSJORD,1:P("OLDON"))) I X1]"" S P(2)=X1 Q
 I $G(PSJSYSW0)=""!($P(PSJSYSW0,U,5)=2) S START=+$E(P("LOG"),1,12) G Q
 I $G(P("RES"))="R" N PSIVAC S PSIVAC="PR" D ENAD I PSIVADM S P(2)=PSIVADM Q
 S PSIVSN=+P("IVRM"),START="",PSIVTYPE=$G(P(4)) Q:PSIVTYPE=""
 N PSIV X $S($E(PSIVAC)="C":"S X=+$E(P(""LOG""),1,12) D H^%DTC S PSIV=%T",1:"S PSIV=$P($H,"","",2)") G T2:PSIVTYPE'["P"&('P(5))
 I P(11)']"" X $S($E(PSIVAC)="C":"S Y=+$E(P(""LOG""),1,12)",1:"D NOW^%DTC S Y=%") S Y=Y+.007\.01/100 S:'$P(Y,".",2) Y=$$MDNGHT(Y) X ^DD("DD") S START=Y G Q
 S X=P(11) D CHK S PX=Y,X1=PSIV\3600,X2=PSIV#3600\60,X=$E(".0",1,$L(X1)#2+1)_X1_$E("0",X2<10)_X2,START=$S($E(PSIVAC)="C":$P(P("LOG"),"."),1:"T")
 S X1=$P(PX,"-"),X1=$E(".0",1,$L(X1)#2+1)_X1,X2=$P(PX,"-",PSGCNT),X2=$E(".0",1,$L(X2)#2+1)_X2
 S NAT=+$P($G(^PS(59.6,+$O(^PS(59.6,"B",+VAIN(4),0)),0)),U,5)
 I '$D(PSGDT) S PSGDT=$$DATE^PSJUTL2()
 I X<X1,'NAT S START=$$ENSD^PSGNE3(P(9),P(11),+$E(P("LOG"),1,12),PSGDT) G Q
 I X>X2 S START=$$ENSD^PSGNE3(P(9),P(11),+$E(P("LOG"),1,12),PSGDT) G Q
T6 F I=2:1:PSGCNT S X1="."_$P(PX,"-",I-1),X2="."_$P(PX,"-",I) Q:+X1<X&(+X2>X)
 S X1=X-X1,X2=$S(NAT:0,1:X2-X),START=$S(X1<X2:$P(PX,"-",I-1),1:$P(PX,"-",I)) S:START="" START=$P(PX,"-") X $S($E(PSIVAC)="C":"S Y=$P(P(""LOG""),""."") X ^DD(""DD"") S PSIV=Y",1:"S PSIV=""TODAY""") S START=PSIV_"@"_$E("0",$L(START)=3)_START G Q
T2 S X=+("."_$E(10000+(PSIV\3600*100)+(PSIV#3600\60),2,5)),START=$O(^PS(59.5,PSIVSN,3,"AT",X)) S:'START START=$O(^(0)),PSIVTOM=1 I 'START S START=X K PSIVTOM
 S START=$S($E(PSIVAC)="C":$P(P("LOG"),"."),1:DT)_START I $D(PSIVTOM) S X1=$S($E(PSIVAC)="C":$P(P("LOG"),"."),1:DT),X2=1 D C^%DTC S Y=$P(X,".")_START K PSIVTOM
 S X=START,%DT="XRTX" D ^%DT
Q ;
 I START["@" S X=START,%DT="RTX" D ^%DT S START=+Y
 S P(2)=START
 I $G(PSJORD)["P" D:'$G(PSGRDTX(+PSJORD,"PSGSD")) REQDT^PSJLIVMD(PSJORD) S START=$G(PSGRDTX(+PSJORD,"PSGSD")) S P(2)=$S(START:START,1:P(2))
 K NAT,START,PSIVTYPE,PSIVSTRT,PSGCNT,X1,X2,PX
 Q
CHK F Y=1:1 Q:$L(X)>240!($P(X,"-",Y)="")  S $P(X,"-",Y)=$P(X,"-",Y)_$E("0000",1,4-$L($P(X,"-",Y)))
 S Y=X,PSGCNT=$L(X,"-") S:X]""&(PSGCNT<1) PSGCNT=1 Q
 ;
ENSTOP ; WILL CALCULATE STOP DATE FOR ORDER
 ;NEEDS (DFN) & ON
 N WALL,P3,ADX,DDLX,OIX,DRGT,PSIDAY S (WALL,P3,PSIDAY)=0
 D:'$G(PSIVSITE) ^PSIVSET  Q:'P(2)
 I P(23)'="" S PSIVTYPE="C"
 S STOP="",X="",PSIVSTRT=P(2),PSIVTYPE=$G(P(4))
 I $S("^NOW^STAT^ONCE^ONE-TIME^ONE TIME^ONETIME^1TIME^1-TIME^1 TIME^"[(U_P(9)_U):1,1:0),PSIVTYPE="P"!P(5)!(P(23)="P") S X=$$ENOSD^PSJDCU(PSJSYSW0,PSIVSTRT,DFN) I X]"" S:P(11)=""&($G(ON)["P") PSIVCAL=1 G END
 N DUR,DURMIN I $G(PSJORD)["V" S DUR=$$GETDUR^PSJLIVMD(DFN,+PSJORD,"IV",1) I DUR]"" S DURMIN=$$DURMIN^PSJLIVMD(DUR) I DURMIN D  G END
 . S X=$$FMADD^XLFDT(PSIVSTRT,,,DURMIN)
 I $P(PSIVSITE,"^",5) D
 . N Z S Y=0
 . F  S Y=$O(^PS(55,DFN,"IV",Y)) Q:'Y  S Z=^(Y,0) D  Q:X]""
 .. I $P(Z,"^",17)="A",$$ONE^PSJBCMA(DFN,Y_"V",$P(Z,"^",9))'="O" S X=$P(Z,"^",3) Q
 ;G:X]"" END
 S:X WALL=X
 S PSIDAY=$S(PSIVTYPE="A":$P(PSIVSITE,"^",4),PSIVTYPE="H":$P(PSIVSITE,"^",17),PSIVTYPE="P":$P(PSIVSITE,"^",18),PSIVTYPE="S":$P(PSIVSITE,"^",20),1:$P(PSIVSITE,"^",21))
 F X=0:0 S X=$O(DRG("AD",X)) Q:'X  I $P(^PS(52.6,+$P(DRG("AD",+X),U),0),"^",4),($P(^(0),"^",4))<+PSIDAY S PSIDAY=$P(^(0),"^",4)
 I WALL,($$FMADD^XLFDT(PSIVSTRT,PSIDAY,"D"))>WALL S PSIDAY=$$FMDIFF^XLFDT(WALL,PSIVSTRT,1) S:PSIDAY=0 PSIDAY=""
 S DRGT=$S($D(DRG("AD")):"AD",1:"SOL") F ADX=0:0 S ADX=$O(DRG(DRGT,ADX)) Q:'ADX!($G(DRGTMP)&($G(DRGTN)["AD")&(DRGT="SOL"))  D
 . I DRGT="AD",+$P(^PS(52.6,$P(DRG(DRGT,+ADX),U),0),"^",4),(+$P(^(0),"^",4))<+Y S Y=$P(^(0),"^",4)
 . S OIX=+$P(DRG(DRGT,ADX),"^",6),DDLX=$P(^PS(50.7,OIX,0),"^",5) Q:'DDLX  D DDLIM(.PSIDAY,.P3)
 I '$G(DRG("AD",0)),$G(DRGTMP),($G(DRGTN)["SOL") S OIX=$P($G(DRGTMP),"^",6) I OIX S DDLX=$P(^PS(50.7,OIX,0),"^",5) I DDLX  D DDLIM(.PSIDAY,.P3)
 I $G(P3),$G(P(2)) I P3>P(2) S X=P3 G END
 S:'PSIDAY PSIDAY=1
TIME S X2=PSIDAY,X1=PSIVSTRT D C^%DTC S X=$P(X,"."),X=X_$S($P(PSIVSITE,"^",14)="":.2359,1:"."_$P(PSIVSITE,"^",14))
END ;
 S P(3)=+X
 I $G(PSJORD)["P" D:'$G(PSGRDTX(+PSJORD,"PSGFD")) REQDT^PSJLIVMD(PSJORD) S P(3)=$S($G(PSGRDTX(+PSJORD,"PSGFD")):PSGRDTX(+PSJORD,"PSGFD"),1:P(3))
 S P(3)=$$DATE2^PSJUTL2(P(3)),P(2)=$$DATE2^PSJUTL2(P(2))
 Q
 ;
ENAD ;Will get last admin. time for order (needs dfn and on)
 N P4,PSIVX,PSIVY
 I $P(PSJSYSW0,U,5)=2 S PSIVADM=$$DATE^PSJUTL2() Q
 I $S($G(PSIVAC)["R":1,P(9)="QOD":1,1:P(9)?1"Q".N1"D") S PSIVADM=$$ENSD^PSGNE3(P(9),P(11),+$E(P("LOG"),1,12),+$P($G(^PS(55,DFN,"IV",+P("OLDON"),0)),U,2)) Q:PSIVADM
 S PSIVX=X,PSIVY=Y,P4=P(4) S:P(4)="C" P4=P(23) S:P4="S" P4=$S(P(5):"P",1:"A") D NOW^%DTC S Y=%,PSIVNOW=Y I (P4="P"&(P(11)="")&'P(15))!("HA"[P4&'P(15)) S Y=Y+.007\.01/100 G QAD
 D P:P4="P"&('P(15)),AH:P(15)
QAD ;
 S:'$D(PSGSA) PSGSA=""
 S PSIVSD=Y I Y S OD=$L(PSGSA," ") I OD>2 S X=+PSGSA\1 F OD1=2:1:OD-1 I $P(PSGSA," ",OD1)'>$S(OD1>2:$P(PSGSA," ",OD1-1),1:PSGSA#1) S X1=X,X2=1 D C^%DTC
 I PSIVSD,OD>2 S Y=X_PSIVSD
 S PSIVADM=+Y,X=PSIVX,Y=PSIVY K PSGSA,PSIVSD,OD,OD1,PSIVMI,PSIVNOW S:PSIVADM<P(2) PSIVADM=P(2) Q
 ;
P S CD=PSIVNOW,PSGSA="",(PSIVSD,OD)=DT_.0001,X=P(11) D CHK S P(11)=X D ENP4^PSIVWL
 I PSGSA="" S PSIVSD=DT_.0001,PSIVMIN=-1440 D ENT^PSIVWL S $P(Y,".",2)=$P(P(11),"-",$L(P(11),"-")) Q
 S Y=$P(PSGSA," ",$L(PSGSA," ")-1) Q
AH F PSIVADM=0:-1 S CD=PSIVNOW,(X,X1)=DT,X2=PSIVADM D:X2 C^%DTC S X=$P(X,".") S (OD1,PSIVSD,OD)=X_.0001,PSIVMIN=P(15) D ENP3^PSIVWL Q:PSIVADM<-4!(PSGSA]"")
 S Y=$P(PSGSA," ",$L(PSGSA," ")-1) Q
MDNGHT(Y)          ;Sets Start Date/Time on orders placed between midnight and 12:30
 S Y=$$FMADD^XLFDT(Y,-1,0,0,0),Y=$P(Y,".")_".24" Q Y
 ;
DDLIM(PSIVDUR,STPDT) ;  
 N PSGS0Y,PSGS0XT,P3 S PSGS0Y=$G(P(11)),PSGS0XT=$G(P(9))
 I DDLX["D",+DDLX<+PSIVDUR S PSIVDUR=+DDLX
 I DDLX["L",$G(PSGS0Y),($G(PSGS0XT)'=""),"AH"'[PSIVTYPE D
 . S PSGNEDFD=$P(^PS(50.7,OIX,0),"^",5)_"^"_+$G(P("MR"))_"^"_$G(PSJSCHT)_"^"_$G(P(9))
 . N PSGNEW,PSGNESD,PSGNEFD
 . S PSGNEW=$S('$P(PSJSYSW0,U,4):0,+$G(^PS(55,PSGP,5.1))'>PSGDT:0,1:+$G(^PS(55,PSGP,5.1)))
 . S PSGNESD=$S($G(PSGNESD)="":P(2),1:$G(PSGNESD))
 . D DFD^PSGNE3 I PSGNEFD,(PSGNEFD-PSGNESD)<PSIVDUR,(PSGNEFD'<PSGNESD) S STPDT=$S('STPDT:+PSGNEFD,PSGNEFD<STPDT:+PSGNEFD,1:STPDT)
 ;I DDLX["L",$G(P(2)),$G(P(15)) N PSGCNT,OD,CD,PSIVEC D S
 S P(3)=$$DATE2^PSJUTL2(P(3)),P(2)=$$DATE2^PSJUTL2(P(2))
 Q
S ;
 Q:+PSJSYSU'=3!($G(P(23))="P")  N P7,PSIVDNM,PSIVSD
 I PSIVDUR,(($$FMADD^XLFDT(P(2),PSIVDUR)<P(3))!'P(3)) S P(3)=$$FMADD^XLFDT(P(2),PSIVDUR)
 I 'P(3) S P(3)=30
 D NOW^%DTC S Y=% Q:P(4)=""!(P(2)="")  S:'$D(OD) OD=P(2) S PSGCNT=0,PNOW=DT
 S P7=P(7),P(7)=1 S CD=P(3) S:OD=CD CD=CD+.0001 D ENP3^PSIVWL S P(7)=P7 D:$G(PSGSA)  Q
 .F Y=1:1:+DDLX S X=$P(PSGSA," ",Y) Q:X=""  S:$E(X)="." X=$$CONVER^PSIVORE2(X,Y) D
 .. S:X=""!(X>P(3)) (X,PSGSA)="" S:X STPDT=X
 S CD=P(3),PSIVSD=P(2) S:'$G(OD) OD=P(2) S:OD=CD CD=CD+.0001 D
 . F Y=1:1 S (PSIVMI,MI)=$P(P(11),"-",Y),PSIVSD=+(PSIVSD\1_"."_MI) Q:PSIVSD>CD  D
 .. X:MI="" "S X1=PSIVSD,X2=1 D C^%DTC S PSIVSD=X,Y=0" I MI,PSIVSD'<OD,PSIVSD'>CD,PSIVSD'=P(3),'P7 S PSIVDNM=$G(PSIVDNM)+1 S PSGSA(PSIVDNM)=PSIVSD
 . F Y=1:1:+DDLX S X=$G(PSGSA(Y)) S:X=""!(X>P(3)) (X,PSGSA)="" S:X STPDT=X
 Q
