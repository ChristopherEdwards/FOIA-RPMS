BKMIMRP3 ;PRXM/HC/BWF - Print Visit Report; [ 1/12/2005  7:16 PM ] ; 13 Jun 2005  1:39 PM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 Q
PAUSE ;
 ; Utility to pause the print screen.
 D ^XBFMK
 S (BKMRTN,ENTER,QUITALL)=""
 I $E(IOST,1,2)="C-",$$PAUSE^BKMIXX3 S (BKMRTN,ENTER,QUITALL)="^"
 Q
 ;
PRINT(SRT1,SRT2) ;EP - Print the report data.
 N NODE1,DFN,VSIT,VSDATA,PTNM,PROV,HRN,CLIN,DOB,VSDT,NEXT,NXTDFN,NXTVSIT,QUITALL
 S QUITALL=0
 ; if there is no data in the temp global, inform user of no data and quit
 I '$D(^TMP("BKMIMRP2",$J,"PRINT")) D  Q
 .W !! D HDR3^BKMIMRP1 W "No Data to Display for Search Criteria",! D HDR3^BKMIMRP2,PAUSE,XIT
 ; initialize page number
 S PAGE=1
 K %ZIS,IOP,IOC,ZTIO S %ZIS="MQ" D ^%ZIS G:POP XIT
 I $D(IO("Q")) D
 . S ZTRTN="PRINT1^BKMIMRP3",ZTSAVE("SRT*")="",ZTSAVE("PAGE")=""
 . S ZTSAVE("OPT1TXT")="",ZTSAVE("OPT2TXT")="",ZTDESC="HMS PATIENT VISIT REPORT"
 . S ZTSAVE("^TMP(""BKMIMRP2"",$J,")=""
 I $D(IO("Q")) K IO("Q") D ^%ZTLOAD W !,"REQUEST QUEUED" G XIT^BKMIMRP
 D PRINT1
 Q
 ;
PRINT1 ;
 U IO
 W @IOF
 S QUITALL=0
 D HDRINFO^BKMIMRP1(PAGE)
 ; depending on selections made by user, sort data
 ; single sort selection by user
 G:QUITALL XIT
 I SRT2="" D
 .S NODE1=""
 .F  S NODE1=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1)) Q:NODE1=""!(QUITALL)  D
 ..I SRT1TXT="GENDER" W !?1,SRT1TXT_": "_$S(NODE1="F":"FEMALES",NODE1="M":"MALES"),!?1,"--------",!?1
 ..I SRT1TXT="REGISTER STATUS" W !?1,SRT1TXT_": "_$S(NODE1="A":"ACTIVE",NODE1="I":"INACTIVE",NODE1="D":"DECEASED",NODE1="T":"TRANSIENT",1:"UNKNOWN"),!?1,"--------",!?1
 ..I SRT1TXT="REGISTER DIAGNOSIS" W !,?1,SRT1TXT_": "_$S(NODE1="~":"NOT DEFINED",1:NODE1),!?1,"--------",!?1
 ..I SRT1TXT="CLINICAL CLASSIFICATION" W !,?1,SRT1TXT_": "_$S(NODE1="~":"NOT DEFINED",1:NODE1),!?1,"--------",!?1
 ..S VISIT="",LPTNM="",DFN=""
 ..F  S DFN=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1,DFN)) Q:DFN=""  D
 ...F  S VISIT=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1,DFN,VISIT)) Q:VISIT=""!(QUITALL)  D
 ....;W !?1,$E(PAT,1,19)
 ....; get all applicable print data
 ....S VSDATA=$G(^TMP("BKMIMRP2",$J,"PRINT",NODE1,DFN,VISIT))
 ....S PTNM=$P(VSDATA,U,1),PROV=$P(VSDATA,U,6),HRN=$P(VSDATA,U,2)
 ....S CLIN=$P(VSDATA,U,5),DOB=$P(VSDATA,U,3),VSDT=$P(VSDATA,U,4)
 ....I PTNM'=LPTNM W !?1,$E(PTNM,1,19),?21,HRN,?28,DOB S LPTNM=PTNM
 ....W ?39,$P(VSDT,"@"),?50,$E(CLIN,1,12),?63,$E(PROV,1,16),!?1
 ....I ($Y=(IOSL-2))!($Y>(IOSL-2)) D PAGEA I BKMRTN="^" S QUITALL=1
 ....Q:QUITALL
 ;-----------------------------------------------------------------------------
 I SRT2'="" D
 .S NODE1=""
 .F  S NODE1=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1)) Q:NODE1=""!(QUITALL)  D
 ..I SRT1TXT="GENDER" W !?1,SRT1TXT_": "_$S(NODE1="F":"FEMALES",NODE1="M":"MALES")
 ..I SRT1TXT="REGISTER STATUS" W !?1,SRT1TXT_": "_$S(NODE1="A":"ACTIVE",NODE1="I":"INACTIVE",NODE1="D":"DECEASED",NODE1="T":"TRANSIENT",1:"UNKNOWN")
 ..I SRT1TXT="REGISTER DIAGNOSIS" W !,?1,SRT1TXT_": "_$S(NODE1="~":"NOT DEFINED",1:NODE1)
 ..I SRT1TXT="CLINICAL CLASSIFICATION" W !,?1,SRT1TXT_": "_$S(NODE1="~":"NOT DEFINED",1:NODE1)
 ..S NODE2=""
 ..F  S NODE2=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1,NODE2)) Q:NODE2=""!(QUITALL)  D
 ...I SRT2TXT="GENDER" W !?5,SRT2TXT_": "_$S(NODE2="F":"FEMALES",NODE2="M":"MALES"),!?1,"--------",!?1
 ...I SRT2TXT="REGISTER STATUS" W !?5,SRT2TXT_": "_$S(NODE2="A":"ACTIVE",NODE2="I":"INACTIVE",NODE2="D":"DECEASED",NODE2="T":"TRANSIENT",1:"UNKNOWN"),!?1,"--------",!?1
 ...I SRT2TXT="REGISTER DIAGNOSIS" W !,?5,SRT2TXT_": "_$S(NODE2="~":"NOT DEFINED",1:NODE2),!?1,"--------",!?1
 ...I SRT2TXT="CLINICAL CLASSIFICATION" W !,?5,SRT2TXT_": "_$S(NODE2="~":"NOT DEFINED",1:NODE2),!?1,"--------",!?1
 ...S VISIT="",LPTNM="",DFN=""
 ...F  S DFN=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1,NODE2,DFN)) Q:DFN=""  D
 ....F  S VISIT=$O(^TMP("BKMIMRP2",$J,"PRINT",NODE1,NODE2,DFN,VISIT)) Q:VISIT=""!(QUITALL)  D
 .....S VSDATA=$G(^TMP("BKMIMRP2",$J,"PRINT",NODE1,NODE2,DFN,VISIT))
 .....S PTNM=$P(VSDATA,U,1),PROV=$P(VSDATA,U,6),HRN=$P(VSDATA,U,2)
 .....S CLIN=$P(VSDATA,U,5),DOB=$P(VSDATA,U,3),VSDT=$P(VSDATA,U,4)
 .....I PTNM'=LPTNM W !?1,$E(PTNM,1,19),?21,HRN,?28,DOB S LPTNM=PTNM
 .....W ?39,$P(VSDT,"@"),?50,$E(CLIN,1,12),?63,$E(PROV,1,16),!?1
 .....I ($Y=(IOSL-2))!($Y>(IOSL-2)) D PAGEA G:BKMRTN="^" XIT
 D FOOT ;Display end confidentiality message
 D PAUSE
 I IOST["C-" W @IOF
 D ^%ZISC
 K ^TMP("BKMIMRP2",$J)
 Q
 ;
FOOT ;Print confidential message footer
 W !!,?16,"****  END CONFIDENTIAL PATIENT INFORMATION  ****",!
 Q
 ;
PAGEA ;
 D PAUSE I BKMRTN="^" S QUITALL=1
 ;I ENTER="^" S QUITALL=1 K ENTER Q
 W @IOF
 S PAGE=PAGE+1
 D HDRINFO^BKMIMRP2(PAGE)
 Q
DISPINFO ;
 ; Call BLDPTLST^BKMIRMP1 to get the information for
 ; each patient that needs to be displayed
 ; Items to be displayed are Clinic,Patient Name,HRN,DOB,Visit Date,
 ; and Provider 
 ; 
 ; REG must be defined upon entry into this module
 ; 
 N DFN,VSIT,VPRV,FOUND,VPRVIEN
 D BLDPTLST^BKMIMRP1 ; setting up ^TMP("BKMIMRP2,$J,"PTFLTR",DFN,VSIT) global
 S DFN=0
 F  S DFN=$O(^TMP("BKMIMRP2",$J,"PTFLTR",DFN)) Q:DFN=""!QUITALL=1  D
 .;GET PATIENT NAME AND DOB
 .D GETS^DIQ(2,DFN_",",".01;.02;.03","IE","DATA","")
 .S PTNM=$G(DATA(2,DFN_",",.01,"E"))
 .S SEX=$G(DATA(2,DFN_",",.02,"I"))
 .S INDOB=$G(DATA(2,DFN_",",.03,"I"))
 .S EXDOB=$G(DATA(2,DFN_",",.03,"E"))
 .;GET INFORMATION FROM iCARE REGISTRY FILE
 .S REGIEN=$O(^BKM(90451,"B",DFN,0))
 .S REGLOC=$O(^BKM(90451,"D",REG,REGIEN,0))
 .S IEN=REGLOC_","_REGIEN_","
 .D GETS^DIQ(90451.01,IEN,".5;2;2.3;2.5;3","IE","REGDAT","")
 .S STAT=$G(REGDAT(90451.01,IEN,.5,"E"))
 .S ICDXIEN=$G(REGDAT(90451.01,IEN,2,"I"))
 .I $T(ICDDX^ICDCODE)'="" S ICDX=$$ICD9^BKMUL3(ICDXIEN,$G(REGDAT(90451.01,IEN,2.5,"I")),4) ; csv
 .I $T(ICDDX^ICDCODE)="" S ICDX=$$GET1^DIQ(80,ICDXIEN_",",3,"E","","")
 .S RGDX=$G(REGDAT(90451.01,IEN,2.3,"E"))
 .S CCLAS=$G(REGDAT(90451.01,IEN,3,"E"))
 .;GET PROVIDER
 .S VSIT=0,PCP=""
 .F  S VSIT=$O(^TMP("BKMIMRP2",$J,"PTFLTR",DFN,VSIT)) Q:VSIT=""!QUITALL=1  D
 ..S VPRVIEN=0,FOUND=0,PCP=""
 ..F  S VPRVIEN=$O(^AUPNVPRV("AD",VSIT,VPRVIEN)) Q:VPRVIEN=""!(FOUND)  D
 ...S VPRV=$$GET1^DIQ(9000010.06,VPRVIEN,.01,"I")
 ...S PCP=$$GET1^DIQ(200,VPRV,.01,"E"),FOUND=1
 ..;GET VISIT DATE (.01) AND CLINIC (.08)
 ..D GETS^DIQ(9000010,VSIT_",",".01;.08","IE","DATA3","")
 ..S HRNIEN=$G(DUZ(2))
 ..S INVSDT=$G(DATA3(9000010,VSIT_",",.01,"I"))
 ..S EXVSDT=$$DATE^BKMIDTF(INVSDT)
 ..S CLINIC=$G(DATA3(9000010,VSIT_",",.08,"E"))
 ..;GET HEALTH RECORD NUMBER
 ..D GETS^DIQ(9000001.41,HRNIEN_","_DFN_",",".02","I","DATA4","")
 ..S HRN=$G(DATA4(9000001.41,HRNIEN_","_DFN_",",.02,"I"))
 ..S VSTYP="NULL" ;THIS STILL NEEDS TO BE DEFINED BASED ON QUESTIONS
 ..S DATASTR=PTNM_U_PCP_U_CLINIC_U_STAT_U_ICDX_U_RGDX_U_CCLAS_U_VSTYP_U_SEX_U_HRN_U_EXDOB_U_EXVSDT
 .. ;THIS MAY NEED TO BE SET UP DIFFERENTLY FOR SORTING PURPOSES
 ..S ^TMP("BKMIMRP2",$J,"DATA",DFN,VSIT)=DATASTR
 ..K DATA,DATA2,DATA3,DATA4,REGDAT
 ;
 K ^TMP("BKMIMRP2",$J,"PTFLTR")
 Q
 ;
DTRNG() ;Prompt for beginning date
 N %DT,X,Y,BDATE
 S BDATE=""
 S %DT="AE"
 S %DT("A")="Select beginning date: "
 D ^%DT
 I X="^" S BDATE="E"
 I BDATE="",Y=-1 S BDATE=0
 I Y'=-1 S BDATE=Y
 Q BDATE
DTRNG2(EXTDT,BDATE) ; Prompt for ending date
 N %DT,X,Y,EDATE
 S EDATE=""
 S %DT="AE",%DT(0)=BDATE
 S %DT("A")="Select ending date: "
 S %DT("B")=EXTDT
 D ^%DT
 I X="^" S EDATE="E"
 I EDATE="",Y=-1 S EDATE=0
 I Y'=-1 S EDATE=Y
 Q EDATE
FORGETIT() ;Prompt user to end program
 K DIR,X,Y
 S DIR("A")="Would you like to quit this option (Y/N) "
 S DIR("B")="NO"
 S DIR(0)="YO"
 D ^DIR
 ;PRXM/HC/BHS - 10/31/2005 - Added logic to treat timeout as '^'
 I $D(DTOUT)!$D(DUOUT) Q "^"
 Q Y
AA(TYPE) ;
 ; Input  TYPE - used to tell the programmer whether this is being asked during
 ;a search or sort
 ;1 = Search
 ;2 = Sort
 K DIR
 I TYPE=1 S DIR("A")="Select Another Search Parameter (Y/N)"
 I TYPE=2 S DIR("A")="Select Another Sort Parameter (Y/N)"
 S DIR("B")="NO"
 S DIR(0)="YO"
 D ^DIR
 I $D(DTOUT)!$D(DUOUT) Q "^"
 Q Y
XIT ; EP - Kill variables and quit.
 K BDATE,CCLAS,CLINIC,DATASTR,DIR,EDATE,EXDOB,EXTDT,EXVSDT,HRNIEN,ICDX,TAGERR
 K ICDXIEN,IEN,INDOB,INVSDT,NEXT1,NEXT2,NODE2,PAGE,PCP,REG,REGIEN,REGLOC,RGDX
 K SCRATCH,SEX,STAT,VSTYP,BDT,CATEGRY,CENTER,CLIN,CLINE,CS1RES,CS2RES,CS2STOP
 K DX,DTS1,DTS2,EDT,I,LEN,OPT1,OPT1TXT,OPT2TXT,PAT,POP,PRINT,PRV,PRVPAT,QUITALL
 K QUITTAG,REGDX,REGLOC,SEX,SRT1TXT,SRT2TXT,START,STAT,TODAY,XECUTE,X,Y,PROV,PRV
 K ^TMP("BKMIMRP2",$J)
 K BKDFN,BKMRIEN,BKMRTN,CHKDT,CLASS,DAYS,DTOUT,DUOUT,ECLIN,ENDT,ENTER
 K ENTRY,GENDER,LPTNM,PTNAME,RDX,REGSTAT,RGSTAT,SEL,VSDATE,VSDTM,VTXT
 Q
