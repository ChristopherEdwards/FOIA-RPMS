BKMSUPP2 ;PRXM/HC/WOM - Continuation of BKMSUPP, HIV SUPPLEMENT; [ 1/19/2005 7:16 PM ] ; 10 Jun 2005  12:33 PM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 Q
 ;Beginning of Immunizations
IMM(DFN) ;EP - Retrieve HAART Appropriate and Compliance Data
 I $Y>(MAXCT-2) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 W !!?1,"LAST DOCUMENTED IMMUNIZATIONS: ",!!
 D IMFIND(DFN)
 Q:QUIT
 W ! I $Y>(MAXCT-4) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 N BKMIEN1,BKMIEN2,B1,HAAR,HAART,HAARS,HAARDT,BKMIENS,BKMDT,OK
 S HAART="",BKMIEN1="B"
 W !?1,"RECENT MEDICATIONS (past 6 months): ",!
 W !?1,"HAART Status: ",!
 ;Build HAAR array to capture HAART Appropriate and Compliance Data
 F  S BKMIEN1=$O(^BKM(90451,BKMIEN,1,BKMIEN1),-1) Q:'BKMIEN1  D  Q:QUIT
 .S BKMDT=$$FMADD^XLFDT(DT,-183) ;Look at last 6 months
 .F  S BKMDT=$O(^BKM(90451,BKMIEN,1,BKMIEN1,40,"B",BKMDT)) Q:'BKMDT  D  Q:QUIT
 ..S BKMIEN2="" F  S BKMIEN2=$O(^BKM(90451,BKMIEN,1,BKMIEN1,40,"B",BKMDT,BKMIEN2)) Q:'BKMIEN2  D  Q:QUIT
 ...S HAART=HAART+1
 ...D GETS^DIQ(90451.03,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",".01;1;2;3","IE","HAAR("_HAART_")")
 ...;Confirm that there is something to be printed
 ...S OK=0 D  Q:'OK
 .... I $G(HAAR(HAART,90451.03,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",1,"I"))]"" S OK=1 Q
 .... I $G(HAAR(HAART,90451.03,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",2,"E"))]"" S OK=1 Q
 .... I $G(HAAR(HAART,90451.03,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",3,"E"))]"" S OK=1 Q
 ...S HAARS(HAAR(HAART,90451.03,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",.01,"I"),HAART,BKMIEN2_","_BKMIEN1_","_BKMIEN_",")=""
 ;Sort data by appropriate date and print in reverse date order
 I $D(HAARS) D  K HAARS
 .S HAARDT=""
 .F  S HAARDT=$O(HAARS(HAARDT),-1) Q:HAARDT=""  D
 ..S HAART=""
 ..F  S HAART=$O(HAARS(HAARDT,HAART)) Q:HAART=""  D
 ...S BKMIENS=""
 ...F  S BKMIENS=$O(HAARS(HAARDT,HAART,BKMIENS)) Q:BKMIENS=""  D
 ....I $Y>(MAXCT-2) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 ....W ?5,"Appropriate: ",$$FMTE^XLFDT(HAARDT,"5Z"),"  "
 ....W HAAR(HAART,90451.03,BKMIENS,1,"E"),"  ",HAAR(HAART,90451.03,BKMIENS,2,"E"),!
 ....W ?5,"Comment: ",HAAR(HAART,90451.03,BKMIENS,3,"I"),! Q:QUIT
 I QUIT K HAAR Q
 I $D(HAAR) W !
 S BKMIEN1="B"
 F  S BKMIEN1=$O(^BKM(90451,BKMIEN,1,BKMIEN1),-1) Q:'BKMIEN1  D  Q:QUIT
 .S BKMDT=$$FMADD^XLFDT(DT,-183) ;Look at last 6 months
 .F  S BKMDT=$O(^BKM(90451,BKMIEN,1,BKMIEN1,50,"B",BKMDT)) Q:'BKMDT  D  Q:QUIT
 ..S BKMIEN2="" F  S BKMIEN2=$O(^BKM(90451,BKMIEN,1,BKMIEN1,50,"B",BKMDT,BKMIEN2)) Q:'BKMIEN2  D  Q:QUIT
 ...S HAART=HAART+1
 ...D GETS^DIQ(90451.07,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",".01;1;2","IE","HAAR("_HAART_")")
 ...;Confirm that there is something to be printed
 ...S OK=0 D  Q:'OK
 .... I $G(HAAR(HAART,90451.07,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",1,"E"))]"" S OK=1 Q
 .... I $G(HAAR(HAART,90451.07,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",2,"I"))]"" S OK=1 Q
 ...S HAARS(HAAR(HAART,90451.07,BKMIEN2_","_BKMIEN1_","_BKMIEN_",",.01,"I"),HAART,BKMIEN2_","_BKMIEN1_","_BKMIEN_",")=""
 ;Sort data by appropriate date and print in reverse date order
 I $D(HAARS) D  K HAARS
 .S HAARDT=""
 .F  S HAARDT=$O(HAARS(HAARDT),-1) Q:HAARDT=""  D
 ..S HAART=""
 ..F  S HAART=$O(HAARS(HAARDT,HAART)) Q:HAART=""  D
 ...S BKMIENS=""
 ...F  S BKMIENS=$O(HAARS(HAARDT,HAART,BKMIENS)) Q:BKMIENS=""  D
 ....I $Y>(MAXCT-2) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 ....W ?5,"Compliance: ",$$FMTE^XLFDT(HAARDT,"5Z")
 ....W "  ",HAAR(HAART,90451.07,BKMIENS,1,"E"),!
 ....W ?5,"Comment: ",HAAR(HAART,90451.07,BKMIENS,2,"I"),! Q:QUIT
 I QUIT K HAAR Q
 I '$D(HAAR) D  Q:QUIT
 .I $Y>(MAXCT-2) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 .W !?5,"Appropriate:",!?5,"Compliance:"
 K HAAR
 W !! I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 Q
ED(DFN) ;EP - Retrieve Education data.
 N BKMCKDT
 S BKMCKDT=$$FMADD^XLFDT(DT,-360)
 W !!?1,"Last HIV-related education given (past 12 months): ",!
 I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 Q:QUIT
 K BKMT("ED")
 S GLOBAL="BKMT(""ED"",VSTDT,TEST,""ICD"")"
 D ICDTAX^BKMIXX1(DFN,"BKM FAMILY PLANNING POV","",BKMCKDT,GLOBAL)
 D ICDTAX^BKMIXX1(DFN,"BKMV HIV ED DXS","",BKMCKDT,GLOBAL)
 D ICDTAX^BKMIXX1(DFN,"BKMV STD ED DXS","",BKMCKDT,GLOBAL)
 S GLOBAL="BKMT(""ED"",VSTDT,TEST,""ED"")"
 ; Patient Education Codes can use two different formats
 D PTEDTAX^BKMIXX(DFN,"FP-","",BKMCKDT,GLOBAL) ; Family Planning
 D PTEDTAX^BKMIXX(DFN,"HIV-,-HIV,*BGP HIV/AIDS DXS","",BKMCKDT,GLOBAL) ; HIV Counseling/Education
 D PTEDTAX^BKMIXX1(DFN,"BKM STD ED CODES","",BKMCKDT,GLOBAL)
 D PTEDTAX^BKMIXX1(DFN,"BKM SAFE SEX ED CODES","",BKMCKDT,GLOBAL)
 I '$D(BKMT("ED")) D EDREF Q  ;Get refusals if no data found
 I $Y>(MAXCT-2) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 Q:QUIT
 W ?3,"[Topic]",?35,"[Date]",?47,"[Provider initials]"
 N EDDT,EDTST,DISPDT,PROV
 S EDDT="" F  S EDDT=$O(BKMT("ED",EDDT),-1) Q:EDDT=""  D  Q:QUIT
 . S EDTST="" F  S EDTST=$O(BKMT("ED",EDDT,EDTST)) Q:EDTST=""  D  Q:QUIT
 .. S DISPDT=$P($$FMTE^XLFDT(EDDT,"5Z"),"@")
 .. I $D(BKMT("ED",EDDT,EDTST,"ICD")) D  Q:QUIT
 ... W !?3,$E($$GET1^DIQ(9000010.07,EDTST,.01,"E"),1,30),?35,DISPDT
 ... W ?47,$E($$GET1^DIQ(9000010.07,EDTST,.04,"E"),1,30)
 ... I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 .. I $D(BKMT("ED",EDDT,EDTST,"ED")) D  Q:QUIT
 ... W !?1,$E($$GET1^DIQ(9000010.16,EDTST,.01,"E"),1,30),?35,DISPDT
 ... S PROV=$$GET1^DIQ(9000010.16,EDTST,.05,"I") Q:PROV=""
 ... W ?47,$$GET1^DIQ(200,PROV,1,"E")
 ... I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 K BKMT("ED")
 Q
EDREF ;Check refusals for education
 S GLOBAL="BKMT(""ED"",VSTDT,TEST,""ED"")"
 ; Patient Education Codes can use two different formats
 D REFUSAL^BKMIXX2(DFN,9999999.09,"FP-","","",GLOBAL) ; Family Planning
 D REFUSAL^BKMIXX2(DFN,9999999.09,"HIV-,-HIV,*BGP HIV/AIDS DXS","","",GLOBAL) ; HIV Counseling/Education
 D REFUSAL^BKMIXX2(DFN,9999999.09,"BKM STD ED CODES","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,9999999.09,"BKM SAFE SEX ED CODES","","",GLOBAL)
 D LTAXPRT^BKMSUPP1("ED",1,1,1)
 K BKMT("ED")
 Q
IMFIND(DFN) ;Return recent immunizations in ^TMP("BKMSUPP",$J,1)
 N BKMIM,IMARR,IMIEN
 D PNE(DFN) Q:QUIT
 D FLU(DFN) Q:QUIT
 D HEPA(DFN) Q:QUIT
 D HEPB(DFN) Q:QUIT
 D CMV(DFN) Q:QUIT
 D TET(DFN)
 Q
PNE(DFN) ;Retrieve Pneumococcal taxonomies (IZ.6)
 W ?1,"Pneumococcal: "
 K BKMT("PNE")
 S GLOBAL="BKMT(""PNE"",VSTDT,TEST,""CPT"")"
 D CPTTAX^BKMIXX(DFN,"BGP PNEUMO IZ CPTS","","",GLOBAL)
 S GLOBAL="BKMT(""PNE"",VSTDT,TEST,""ICD"")"
 D ICDTAX^BKMIXX1(DFN,"BGP PNEUMO IZ DXS","","",GLOBAL)
 S GLOBAL="BKMT(""PNE"",VSTDT,TEST,""PROC"")"
 D PRCTAX^BKMIXX1(DFN,"BGP PNEUMO IZ PROCEDURES","","",GLOBAL)
 S GLOBAL="BKMT(""PNE"",VSTDT,TEST,""CVX"")"
 D CVXTAX^BKMIXX1(DFN,"BKM PNEUMO IZ CVX CODES","","",GLOBAL) ;***
 I $D(BKMT("PNE")) D LTAXPRT^BKMSUPP1("PNE",1) K BKMT("PNE") Q
 ;Check refusals
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BKM PPD CVX CODES","","",GLOBAL)
 ;Print results
 D LTAXPRT^BKMSUPP1("PNE",1,1,1)
 K BKMT("PNE")
 Q
FLU(DFN) ;Retrieve Influenza taxonomies (IZ.5)
 W !?1,"Influenza: "
 K BKMT("FLU")
 S GLOBAL="BKMT(""FLU"",VSTDT,TEST,""CPT"")"
 D CPTTAX^BKMIXX(DFN,"BGP CPT FLU","","",GLOBAL)
 S GLOBAL="BKMT(""FLU"",VSTDT,TEST,""ICD"")"
 D ICDTAX^BKMIXX1(DFN,"BGP FLU IZ DXS","","",GLOBAL)
 S GLOBAL="BKMT(""FLU"",VSTDT,TEST,""PROC"")"
 D PRCTAX^BKMIXX1(DFN,"BGP FLU IZ PROCEDURES","","",GLOBAL)
 S GLOBAL="BKMT(""FLU"",VSTDT,TEST,""CVX"")"
 D CVXTAX^BKMIXX1(DFN,"BGP FLU IZ CVX CODES","","",GLOBAL)
 I $D(BKMT("FLU")) D LTAXPRT^BKMSUPP1("FLU",1) K BKMT("FLU") Q
 ;Check refusals
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BGP FLU IZ CVX CODES","","",GLOBAL)
 ;Print results
 D LTAXPRT^BKMSUPP1("FLU",1,1,1)
 K BKMT("FLU")
 Q
HEPA(DFN) ;Retrieve Hepatitis A taxonomies (IZ.3)
 W !?1,"Hepatitis A (last 2): ",?24,"Dx Date: "
 K BKMT("HEPADX") N HEPDT
 S GLOBAL="BKMT(""HEPADX"",VSTDT,TEST,""ICD"")"
 D ICDTAX^BKMIXX1(DFN,"BKM HEP A DXS","","",GLOBAL)
 D PRBTAX^BKMIXX(DFN,"BKM HEP A DXS","","",GLOBAL)
 I $D(BKMT("HEPADX")) D  K BKMT("HEPADX")
 . S HEPDT=$O(BKMT("HEPADX",""),-1)
 . I HEPDT W $$FMTE^XLFDT(HEPDT\1,"5Z"),"  "
 . W $P(@$Q(BKMT("HEPADX")),U,2)
 K BKMT("HEPA")
 S GLOBAL="BKMT(""HEPA"",VSTDT,TEST,""CPT"")"
 D CPTTAX^BKMIXX(DFN,"BKM HEP A IZ CPTS","","",GLOBAL)
 S GLOBAL="BKMT(""HEPA"",VSTDT,TEST,""CVX"")"
 D CVXTAX^BKMIXX1(DFN,"BKM HEP A IZ CVX CODES","","",GLOBAL)
 W !
 I $D(BKMT("HEPA")) D LTAXPRT^BKMSUPP1("HEPA",2,"","","","",1) K BKMT("HEPA") Q
 ;Check refusals
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BKM HEP A IZ CVX CODES","","",GLOBAL)
 ;Print results
 D LTAXPRT^BKMSUPP1("HEPA",2,1,1)
 K BKMT("HEPA")
 Q
HEPB(DFN) ;Retrieve Hepatitis B taxonomies (IZ.4)
 W !?1,"Hepatitis B (last 3): ",?24,"Dx Date: "
 K BKMT("HEPBDX") N HEPDT
 S GLOBAL="BKMT(""HEPBDX"",VSTDT,TEST,""ICD"")"
 D ICDTAX^BKMIXX1(DFN,"BKM HEP B DXS","","",GLOBAL)
 D PRBTAX^BKMIXX(DFN,"BKM HEP B DXS","","",GLOBAL)
 I $D(BKMT("HEPBDX")) D  K BKMT("HEPBDX")
 . S HEPDT=$O(BKMT("HEPBDX",""),-1)
 . I HEPDT W $$FMTE^XLFDT(HEPDT\1,"5Z"),"  "
 . W $P(@$Q(BKMT("HEPBDX")),U,2)
 K BKMT("HEPB")
 S GLOBAL="BKMT(""HEPB"",VSTDT,TEST,""CPT"")"
 D CPTTAX^BKMIXX(DFN,"BKM HEP B IZ CPTS","","",GLOBAL)
 S GLOBAL="BKMT(""HEPB"",VSTDT,TEST,""CVX"")"
 D CVXTAX^BKMIXX1(DFN,"BKM HEP B IZ CVX CODES","","",GLOBAL)
 W !
 I $D(BKMT("HEPB")) D LTAXPRT^BKMSUPP1("HEPB",3,"","","","",1) K BKMT("HEPB") Q
 ;Check refusals
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BKM HEP B IZ CVX CODES","","",GLOBAL)
 ;Print results
 D LTAXPRT^BKMSUPP1("HEPB",3,1,1)
 K BKMT("HEPB")
 Q
CMV(DFN) ;Retrieve CMV(IgG) taxonomies (T.6)
 W !?1,"CMV (IgG): "
 K BKMT("CMV")
 S GLOBAL="BKMT(""CMV"",VSTDT,TEST,""CPT"")"
 D CPTTAX^BKMIXX(DFN,"BKM CMV IZ CPTS","","",GLOBAL)
 S GLOBAL="BKMT(""CMV"",VSTDT,TEST,""CVX"")"
 D CVXTAX^BKMIXX1(DFN,"BKM CMV IZ CVX CODES","","",GLOBAL)
 I $D(BKMT("CMV")) D LTAXPRT^BKMSUPP1("CMV",1) K BKMT("CMV") Q
 ;Check refusals
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BKM CMV IZ CVX CODES","","",GLOBAL)
 ;Print results
 D LTAXPRT^BKMSUPP1("CMV",1,1,1)
 K BKMT("CMV")
 Q
TET(DFN) ;Retrieve Tetanus taxonomies (IZ.7)
 W !?1,"Tetanus: "
 K BKMT("TET")
 S GLOBAL="BKMT(""TET"",VSTDT,TEST,""CPT"")"
 D CPTTAX^BKMIXX(DFN,"BKM TETANUS IZ CPTS","","",GLOBAL)
 S GLOBAL="BKMT(""TET"",VSTDT,TEST,""ICD"")"
 D ICDTAX^BKMIXX1(DFN,"BKM TETANUS IZ DXS","","",GLOBAL)
 S GLOBAL="BKMT(""TET"",VSTDT,TEST,""PROC"")"
 D PRCTAX^BKMIXX1(DFN,"BKM TETANUS IZ PROCEDURES","","",GLOBAL)
 S GLOBAL="BKMT(""TET"",VSTDT,TEST,""CVX"")"
 D CVXTAX^BKMIXX1(DFN,"BKM TETANUS IZ CVX CODES","","",GLOBAL)
 I $D(BKMT("TET")) D LTAXPRT^BKMSUPP1("TET",1) K BKMT("TET") Q
 ;Check refusals
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BKM TETANUS IZ CVX CODES","","",GLOBAL)
 ;Print results
 D LTAXPRT^BKMSUPP1("CMV",1,1,1)
 K BKMT("TET")
 Q
SCREENS(DFN) ;EP - Get screens from taxonomies
 ; Several calls below used to use $H-360.
 ; Replaced with BKMCKDT (FileMan format date).
 N BKMCKDT
 S BKMCKDT=$$FMADD^XLFDT(DT,-360)
 ;Variable MAXCT is set by PRINT^BKMSUPP to IOSL-4
 W !
 I $Y>(MAXCT-2) S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 Q:QUIT
 W !?1,"IN THE PAST 12 MONTHS:",!
 N GLOBAL,CODE,Y,BKMDT ;K ^TMP("BKMSUPP",$J)
DEP ;Depression Screening (S.2)
 K BKMT("DSC")
 S GLOBAL="BKMT(""DSC"",DFN,VSTDT,TEST)"
 ; ** The following line was removed from S.2 and replaced with BGP MOOD DISORDERS below.
 ; D ICDTAX^BKMIXX1(DFN,"BGP DEPRESSION SCREEN ICDS","",BKMCKDT,GLOBAL)
 D ICDTAX^BKMIXX1(DFN,"BGP MOOD DISORDERS","",BKMCKDT,GLOBAL) ; Requested by IHS as part of S.2
 D EXAMTAX^BKMIXX1(DFN,"36","",BKMCKDT,GLOBAL) ; Requested by IHS as part of S.2
 S BKMDT=$O(BKMT("DSC",DFN,""),-1),Y=BKMDT,Y=$$FMTE^XLFDT(Y,"5Z")
 S CODE="" S:BKMDT'="" CODE=$O(BKMT("DSC",DFN,BKMDT,""))
 I CODE'="" S CODE=$P(BKMT("DSC",DFN,BKMDT,CODE),U,2) ;$$GET1^DIQ(80,CODE,3,"I")
 W ?1,"Depression Screening: ",?24,"Date: ",$P(Y,"@"),?42,$E(CODE,1,30),!
 I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 K BKMT("DSC")
 ;
IPV ;Intimate Partner/Domestic Violence Screening
 K BKMT("VSC")
 ;S GLOBAL="BKMT(""VSC"",DFN,VSTDT,TEST,""ICD"")"
 S GLOBAL="BKMT(""VSC"",DFN,VSTDT,TEST)"
 D ICDTAX^BKMIXX1(DFN,"BGP IPV/DV COUNSELING ICDS","",BKMCKDT,GLOBAL)
 ; ** The following line was removed and replaced with BGP DV DXS below.
 ;D ICDTAX^BKMIXX1(DFN,"BGP IPV/DV ICDS","",BKMCKDT,GLOBAL)
 D ICDTAX^BKMIXX1(DFN,"BGP DV DXS","",BKMCKDT,GLOBAL)
 ;S GLOBAL="BKMT(""VSC"",DFN,VSTDT,TEST,""ED"")"
 S GLOBAL="BKMT(""VSC"",DFN,VSTDT,TEST)"
 D PTEDTAX^BKMIXX(DFN,"DV-,-DV","",BKMCKDT,GLOBAL) ; Domestic Violence
 D EXAMTAX^BKMIXX1(DFN,"34","",BKMCKDT,GLOBAL)
 S BKMDT=$O(BKMT("VSC",DFN,""),-1),Y=BKMDT,Y=$$FMTE^XLFDT(Y,"5Z")
 S CODE="" S:BKMDT'="" CODE=$O(BKMT("VSC",DFN,BKMDT,""))
 I CODE'="" S CODE=$P(BKMT("VSC",DFN,BKMDT,CODE),U,2)
 ;I CODE D
 ;. I $D(BKMT("VSC",DFN,BKMDT,CODE,"ICD")) S CODE=$$GET1^DIQ(80,CODE,3,"I") Q  ;$P($G(^ICD9(CODE,0)),U,3) Q
 ;. S CODE=$$GET1^DIQ(9999999.09,CODE,.01,"I")
 W ?1,"IPV/DV Screening: ",?24,"Date: ",$P(Y,"@"),?42,$E(CODE,1,30),!
 K BKMT("VSC")
 I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
ALC ;Alcohol Screening (S.1)
 K BKMT("ASC")
 S GLOBAL="BKMT(""ASC"",DFN,VSTDT,TEST)"
 D ICDTAX^BKMIXX1(DFN,"BGP ALCOHOL SCREEN ICDS","",BKMCKDT,GLOBAL)
 D ICDTAX^BKMIXX1(DFN,"BGP ALCOHOL DXS","",BKMCKDT,GLOBAL)
 D HFTAX^BKMIXX(DFN,"BGP ALCOHOL HLTH FACTOR","",BKMCKDT,GLOBAL)
 D PTEDTAX^BKMIXX(DFN,"CD-,-CD,AOD-,-AOD","",BKMCKDT,GLOBAL) ; Alcohol
 D EXAMTAX^BKMIXX1(DFN,"35","",BKMCKDT,GLOBAL)
 S BKMDT=$O(BKMT("ASC",DFN,""),-1),Y=BKMDT,Y=$$FMTE^XLFDT(Y,"5Z")
 S CODE="" S:BKMDT'="" CODE=$O(BKMT("ASC",DFN,BKMDT,""))
 I CODE'="" S CODE=$P(BKMT("ASC",DFN,BKMDT,CODE),U,2) ;S CODE=$$GET1^DIQ(80,CODE,3,"I")
 W ?1,"Alcohol Screening: ",?24,"Date: ",$P(Y,"@"),?42,$E(CODE,1,30),!
 K BKMT("ASC")
 I $Y>MAXCT S QUIT=$$PAUSE^BKMSUPP3() Q:QUIT  D HEADER^BKMSUPP1(.PAGE,XNOW)
 Q
HTWT(DFN) ;EP - HEIGHT/WEIGHT
 Q:'$D(^AUPNVMSR("AC",DFN)) "^"
 N HT,WT,TYP
 S (HT,WT)=""
 S TYP=$$FIND1^DIC(9999999.07,,"X","HT")
 I TYP S HT=$$MSRVAL(TYP)
 S TYP=$$FIND1^DIC(9999999.07,,"X","WT")
 I TYP S WT=$$MSRVAL(TYP)
 Q HT_"^"_WT
 ;
MSRVAL(TYP) ;Return most recent value based on type of measurement passed
 N VAL,BKMIDT,BKMIM,DT
 S VAL=""
 S BKMIDT=$O(^AUPNVMSR("AA",DFN,TYP,"")) I BKMIDT="" Q "^"
 S BKMIM=$O(^AUPNVMSR("AA",DFN,TYP,BKMIDT,"")) I BKMIM="" Q "^"
 S TYP=$P($G(^AUPNVMSR(BKMIM,0)),U,4)
 Q TYP_"^"_$$FMTE^XLFDT(9999999-BKMIDT,"5Z")
 ;
XIT ;QUIT POINT
 Q
