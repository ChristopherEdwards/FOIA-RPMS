BKMVC0 ;PRXM/HC/JGH - Patient Ed Review; 24-JAN-2005 ; 09 Jun 2005  1:54 PM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
EN ; ENTRY POINT - ListMan template BKMV RECORD EDUC
 ; Assumes existence of DFN,DUZ
 N HIVIEN
 S HIVIEN=$$HIVIEN^BKMIXX3()
 I HIVIEN="" W !,"There is no HMS register defined." H 2 Q
 I '$$VALID^BKMIXX3(DUZ) Q
 K ^TMP("BKMVC0",$J)
 D EN^VALM("BKMV RECORD EDUC")
 Q
 ;
INIT ; called by list template BKMV RECORD EDUC
 D GETALL(DFN)
 Q
 ;
 ; This routine gathers all educations for a patient given their
 ; DFN in file 9000001 and 2. 
 ; BEGDATE must be defined and is equal to the FM beginning date
 ; ENDDATE must be defined and is equal to the FM ending date
GETALL(DFN) ;
 N PEDIEN,DA,IENS,PEDDT,VSTIEN,BDATE,EDATE,BKMVDT,DA0,TEXT,RPEDDT,PPEDDT
 S VALMCNT=0,VALMAR="^TMP(""BKMVC0"","""_$J_""")",VALM0=""
 K ^TMP("BKMVC0",$J)
 ;
 D ^XBFMK
 ;S BEGLDT=9999999-$$HTFM^XLFDT(BEGDATE)
 ;S ENDLDT=9999999-$$HTFM^XLFDT(ENDDATE)
 S BEGLDT=9999999-BEGDATE
 S ENDLDT=9999999-ENDDATE
 S RPEDDT=ENDLDT-.01
 S PPEDDT=""
 ;S PED=0
 F  S RPEDDT=$O(^AUPNVPED("AA",DFN,RPEDDT)) Q:RPEDDT=""!(RPEDDT>BEGLDT)  D
 . S PEDDT=9999999-RPEDDT
 . S PEDIEN=0
 . F  S PEDIEN=$O(^AUPNVPED("AA",DFN,RPEDDT,PEDIEN)) Q:PEDIEN=""  D
 . . ; Only display unique dates
 . . S TEXT=""
 . . S TEXT=$$SETFLD^VALM1($S(PEDDT'=PPEDDT:$$FMTE^XLFDT(PEDDT\1,"5Z"),1:""),TEXT,"Visit")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".01","E"),TEXT,"Class")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".06","E"),TEXT,"Level")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".05","E"),TEXT,"Provider")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".14","E"),TEXT,"Objs Met")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".07","E"),TEXT,"I/G")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".08","E"),TEXT,"Len")
 . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.16,PEDIEN,".13","I"),TEXT,"Beh")
 . . S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 . . S PPEDDT=PEDDT
 D ^XBFMK
 Q
 ;
PROMPT ; ENTRY POINT -- from Protocol BKMV UPD1 EDUC REVIEW/ORDER
 N STOP,OPTS,OPTA,PAR,QT,BEGDT,ENDDT,ERR,OPTTEXT,BEGDATE,ENDDATE
 S STOP=0 ;F  Q:$G(STOP)=1  D  Q:$G(STOP)=1
 D CLEAR^VALM1
 D FULL^VALM1
 S (PAR,OPTS,OPTA)=""
 S QT=$$PROMPT^BKMIXX4(PAR,.OPTS,OPTA,0,1)
 I $P(QT,U) S STOP=1 Q
 D EN^BKMVC0
 I '$$GETALL^BKMVA1(DFN) Q
 D INIT^BKMVA1
 Q
HDR ;
 N BKMDTRNG,DA,IENS,SITE,BKMBDT,BKMEDT,RCRDHDR,BKMDOD
 S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 S VALMHDR(1)=$$PAD^BKMIXX4("",">"," ",(80-($L(SITE)+2))\2)_"["_$G(SITE)_"]"
 S BKMBDT=$$FMTE^XLFDT(BEGDATE,"5Z"),BKMEDT=$$FMTE^XLFDT(ENDDATE,"5Z")
 S BKMDTRNG="Date Range: "_BKMBDT_" - "_BKMEDT
 S VALMHDR(2)=$$PAD^BKMIXX4("",">"," ",(80-$L(BKMDTRNG)\2))_BKMDTRNG
 S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,"5Z"),">"," ",15)
 S VALMHDR(3)=RCRDHDR
 Q
 ;
HELP ; -- help code
 N X
 S X="?" D DISP^XQORM1 W !
 Q
 ;
EXIT ; -- exit code
 K ^TMP("BKMVC0",$J)
 K VALM0,VALMAR,VALMHDR,VALMCNT
 Q
 ;
EXPND ;
 Q
 ;
 ;
