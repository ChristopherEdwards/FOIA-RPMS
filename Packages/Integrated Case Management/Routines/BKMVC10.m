BKMVC10 ;PRXM/HC/BWF - Patient Record Appointment Review; 24-JAN-2005
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 Q
 ;
APTEN ;
 N HIVIEN
 S HIVIEN=$$HIVIEN^BKMIXX3()
 I HIVIEN="" W !,"There is no HMS register defined." H 2 Q
 I '$$VALID^BKMIXX3(DUZ) W !,"You are not a valid HMS user." H 2 Q
 D EN^VALM("BKMV RECORD APPOINTMENTS")
 I '$$GETALL^BKMVA1(DFN) Q
 D INIT^BKMVA1
 D CLEAN
 Q
 ;
APTINIT ;
 D VIEWAPPT(DFN)
 Q
 ;
APTHDR ;
 ; Assumes DFN, DUZ, BDATE and EDATE exist
 N DA,IENS,SITE,BKMBDT,BKMEDT,BKMDTRNG,RCRDHDR,BKMDOD
 S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 S VALMHDR(1)=$$PAD^BKMIXX4("",">"," ",(80-$L(SITE)+2)\2)_"["_$G(SITE)_"]"
 S BKMBDT=$$FMTE^XLFDT(BDATE,"5Z"),BKMEDT=$$FMTE^XLFDT(EDATE,"5Z")
 S BKMDTRNG="Date Range: "_BKMBDT_" - "_BKMEDT
 S VALMHDR(2)=$$PAD^BKMIXX4("",">"," ",(80-$L(BKMDTRNG)\2))_BKMDTRNG
 S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,"5Z"),">"," ",15)
 S VALMHDR(3)=RCRDHDR
 Q
 ;
APTXIT ;
 K ^TMP("BKMVC10",$J)
 K VALM0,VALMAR,VALMCNT,VALMHDR
 Q
 ;
APTHLP ;
 S X="?" D DISP^XQORM1 W !
 Q
 ;
APPROMPT ;  Prompts for BDATE and EDATE
 ; Assumes DFN exists
 N DA,NAME,EXTDT,BKMDT,RCRDHDR,BKMDOD
 D CLEAR^VALM1
 D FULL^VALM1
 I '$D(^DPT(DFN,"S")) W !!,"There are no appointments for this patient.",! H 3 Q
 S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,1),">"," ",15)
 W !,RCRDHDR,!
 S DA=DFN,NAME=$$GET1^DIQ(2,DFN,.01,"E")
 S BDATE=$$DTRNG^BKMIMRP()
 I BDATE="E" Q
 I BDATE=0 K X,Y Q
 K X,Y
 ; get ending date
 S EXTDT=$$FMTE^XLFDT(DT)
 S EDATE=$$DTRNG2^BKMIMRP(EXTDT,BDATE)
 I EDATE="E" Q
 I EDATE=0 S EDATE=DT
 S BKMDT=$O(^DPT(DFN,"S",BDATE))
 I $G(BKMDT)=""!($G(BKMDT)>EDATE) D  Q
 .W !!,"No appointments for the selected date range",! H 3 Q
 D APTEN
 Q
 ;
VIEWAPPT(DFN) ;
 ; Assumes DFN, BDATE and EDATE exist
 N ORDERDT,DA,IENS,CLIEN,CLINIC,VSTYPE,SCIEN,LENGTH,TEXT
 S VALMCNT=0,VALMAR="^TMP(""BKMVC10"","""_$J_""")",VALM0=""
 K ^TMP("BKMVC10",$J)
 S ORDERDT=EDATE+1
 F  S ORDERDT=$O(^DPT(DFN,"S",ORDERDT),-1) Q:ORDERDT=""!(ORDERDT<BDATE)  D
 .K DA
 .S DA(1)=DFN,DA=ORDERDT
 .S IENS=$$IENS^DILF(.DA)
 .S CLIEN=$$GET1^DIQ(2.98,IENS,".01","I")
 .S CLINIC=$$GET1^DIQ(2.98,IENS,".01","E")
 .S VSTYPE=$$APPTYP^BSDU2(DFN,ORDERDT)
 .S SCIEN=$$SCIEN^BSDU2(DFN,CLIEN,ORDERDT)
 .K DA
 .S DA(2)=CLIEN,DA(1)=ORDERDT,DA=SCIEN
 .S IENS=$$IENS^DILF(.DA)
 .S LENGTH=$$GET1^DIQ(44.003,IENS,1,"E")
 .S VALMCNT=$G(VALMCNT)+1
 .S TEXT=""
 .S TEXT=$$SETFLD^VALM1($$FMTE^XLFDT(ORDERDT,1),TEXT,"Date")
 .S TEXT=$$SETFLD^VALM1(CLINIC,TEXT,"Clinic")
 .S TEXT=$$SETFLD^VALM1(VSTYPE,TEXT,"Type")
 .S TEXT=$$SETFLD^VALM1(LENGTH,TEXT,"Length")
 .D SET^VALM10(VALMCNT,TEXT)
 Q
 ;
CLEAN ;
 K BDATE,EDATE
 Q
 ;
 ;
