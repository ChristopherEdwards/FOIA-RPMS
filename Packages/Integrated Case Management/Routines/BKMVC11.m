BKMVC11 ;PRXM/HC/BWF - Patient Record Allergy Review; 24-JAN-2005
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 QUIT
ALEN(DFN) ;
 NEW MEDS,HIVIEN
 S HIVIEN=$$HIVIEN^BKMIXX3()
 I HIVIEN="" W !,"There is no HMS register defined." H 2 Q
 I '$D(^BKM(90450,HIVIEN,11,"B",DUZ)) Q
 D EN^VALM("BKMV RECORD ALLERGIES")
 I '$$GETALL^BKMVA1(DFN) Q
 D INIT^BKMVA1
 D CLEAN
 Q
ALINIT ;
 N TEXT
 D VIEWAL(DFN)
 Q
ALHDR ;
 S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 S VALMHDR(1)=$$PAD^BKMIXX4("",">"," ",(80-$L(SITE)+2)\2)_"["_$G(SITE)_"]"
 S VALMHDR(2)=$G(RCRDHDR)
 Q
ALXIT ;
 K GMRA,GMRAL,IENS,RCRDHDR,SITE,DA
 Q
ALHLP ;
 S X="?" D DISP^XQORM1 W !
 Q
ALPROMPT(DFN) ;
 D ALEN(DFN)
 Q
VIEWAL(DFN) ;
 N BKMDATA,ADVREAC,ADVREACT,ALLREACT,BKMLOOP,CAUSAGNT,DATE,REACTYP,REACTYPE
 S VALMCNT=0,VALMAR="^TMP(""BKMVC11"","_$J_")",VALM0=""
 K ^TMP("BKMVC11",$J)
 S TEXT=$$PAD^BKMIXX4("Orig. Date",">"," ",10)_" "_$$PAD^BKMIXX4("Causative Agent",">"," ",30)_" "_$$PAD^BKMIXX4("Allergic Reaction",">"," ",20)_" "_$$PAD^BKMIXX4("Reaction Type",">"," ",18)
 S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 S GMRA="1^1^111"
 D ^GMRADPT
 S BKMLOOP=0
 F  S BKMLOOP=$O(GMRAL(BKMLOOP)) Q:BKMLOOP=""  D
 .S DATE=$$GET1^DIQ(120.8,BKMLOOP,4,"I")
 .S DATE=$P($G(DATE),"."),DATE=$$DATE^BKMIDTF(DATE)
 .S (BKMDATA,^TMP("BKMVC11",$J,BKMLOOP))=$G(GMRAL(BKMLOOP)) Q:BKMDATA=""
 .S CAUSAGNT=$P(BKMDATA,"^",2),ADVREAC=$P(BKMDATA,"^",4)
 .S ALLREACT=$P(BKMDATA,"^",5),REACTYPE=$P(BKMDATA,"^",7)
 .S REACTYPE=$S(REACTYPE="D":"Drug",REACTYPE="DF":"Drug/Food",REACTYPE="DFO":"Drug/Food/Other",REACTYPE="F":"Food",REACTYPE="FO":"Food/Other",REACTYPE="O":"Other",1:"Unknown")
 .S TEXT=$$PAD^BKMIXX4(DATE,">"," ",10)_" "_$$PAD^BKMIXX4(CAUSAGNT,">"," ",30)_" "_$$PAD^BKMIXX4($S(ALLREACT=0:"No",ALLREACT=1:"Yes",1:"Unk"),">"," ",20)_" "_$$PAD^BKMIXX4(REACTYPE,">"," ",20)
 .S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 Q
CLEAN ;
 K BDATE,BKMDT,EDATE,EXTDT,NAME
 Q
