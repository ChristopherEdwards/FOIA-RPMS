BKMVC12 ;PRXM/HC/BWF - Patient Record Problem List Review; 24-JAN-2005
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 QUIT
EN(DFN) ;
 NEW MEDS,HIVIEN
 S HIVIEN=$$HIVIEN^BKMIXX3()
 I HIVIEN="" W !,"There is no HMS register defined." H 2 Q
 I '$D(^BKM(90450,HIVIEN,11,"B",DUZ)) Q
 D EN^VALM("BKMV RECORD PROB LIST")
 I '$$GETALL^BKMVA1(DFN) Q
 D INIT^BKMVA1
 D CLEAN
 Q
INIT ;
 N TEXT
 D VIEW(DFN)
 Q
HDR ;
 S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 S VALMHDR(1)=$$PAD^BKMIXX4("",">"," ",(80-$L(SITE)+2)\2)_"["_$G(SITE)_"]"
 S VALMHDR(2)=$G(RCRDHDR)
 Q
XIT ;
 K DA,SITE,IENS,RCRDHDR
 Q
HLP ;
 S X="?" D DISP^XQORM1 W !
 Q
PROMPT(DFN) ;
 ;PRXM/HC/BHS - 9/26/2005 - Updated per IHS to request interactive Problem List functionality
 ;D EN(DFN)
 ; This is a published entry point in the PCC Visit APIs documentation from IHS.
 ; Interactive problem list updating
 D EN1^APCDPL
 ; Set flag to recalculate reminders (in the EXIT ACTION of protocol BKMV UPD1 PROB LIST VIEW/EDIT
 S CALCREM=1
 Q
VIEW(DFN) ;
 N BKMDATA,ICDIEN,ICDNAME,ONSTDT,ENTDT,PROBDATA,PROBIEN,RESDATE
 S VALMCNT=0,VALMAR="^TMP(""BKMVC12"","_$J_")",VALM0=""
 K ^TMP("BKMVC12",$J)
 S TEXT=$$PAD^BKMIXX4("Problem",">"," ",30)_" "_$$PAD^BKMIXX4("Date Entered",">"," ",12)_" "_$$PAD^BKMIXX4("Onset Date",">"," ",12)_" "_$$PAD^BKMIXX4("Resolved/Unresolved",">"," ",20)
 S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 S PROBIEN=0
 F  S PROBIEN=$O(^AUPNPROB("AC",DFN,PROBIEN)) Q:PROBIEN=""  D
 .D GETS^DIQ(9000011,PROBIEN,".01;.08;.13;1.07;","I","PROBDATA")
 .S ICDIEN=$G(PROBDATA(9000011,PROBIEN_",",.01,"I"))
 .I $T(ICDDX^ICDCODE)'="" S ICDNAME=$$ICD9^BKMUL3(ICDIEN,DT,4) ; csv - for problem file use today's date
 .I $T(ICDDX^ICDCODE)="" S ICDNAME=$$GET1^DIQ(80,ICDIEN,3,"E")
 .S ONSTDT=$G(PROBDATA(9000011,PROBIEN_",",.13,"I"))
 .S ONSTDT=$P(ONSTDT,"."),ONSTDT=$$DATE^BKMIDTF(ONSTDT)
 .S ONSTDT=$S(ONSTDT'="":ONSTDT,1:"Unknown")
 .S ENTDT=$G(PROBDATA(9000011,PROBIEN_",",.08,"I"))
 .S ENTDT=$P(ENTDT,"."),ENTDT=$$DATE^BKMIDTF(ENTDT)
 .S ENTDT=$S(ENTDT'="":ENTDT,1:"Unknown")
 .S RESDATE=$G(PROBDATA(9000011,PROBIEN_",",1.07,"I"))
 .S RESDATE=$P(RESDATE,"."),RESDATE=$$DATE^BKMIDTF(RESDATE)
 .S RESDATE=$S(RESDATE'="":"Resolved",1:"Unresolved")
 .S TEXT=$$PAD^BKMIXX4(ICDNAME,">"," ",30)_" "_$$PAD^BKMIXX4(ENTDT,">"," ",12)_" "_$$PAD^BKMIXX4(ONSTDT,">"," ",12)_" "_$$PAD^BKMIXX4(RESDATE,">"," ",20)
 .S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 ;S GMRA="1^1^111"
 ;D ^GMRADPT
 ;S BKMLOOP=0
 ;F  S BKMLOOP=$O(GMRAL(BKMLOOP)) Q:BKMLOOP=""  D
 ;.S DATE=$$GET1^DIQ(120.8,BKMLOOP,4,"I")
 ;.S DATE=$P($G(DATE),"."),DATE=$$DATE^BKMIDTF(DATE)
 ;;.S (BKMDATA,^TMP("BKMVC12",$J,BKMLOOP))=$G(GMRAL(BKMLOOP)) Q:BKMDATA=""
 ;.S CAUSAGNT=$P(BKMDATA,"^",2),ADVREAC=$P(BKMDATA,"^",4)
 ;.S ALLREACT=$P(BKMDATA,"^",5),REACTYPE=$P(BKMDATA,"^",7)
 ;.S REACTYPE=$S(REACTYPE="D":"Drug",REACTYPE="DF":"Drug/Food",REACTYPE="DFO":"Drug/Food/Other",REACTYPE="F":"Food",REACTYP="FO":"Food/Other",REACTYP="O":"Other",1:"Unknown")
 ;.S TEXT=$$PAD^BKMIXX4(DATE,">"," ",10)_" "_$$PAD^BKMIXX4(CAUSAGNT,">"," ",30)_" "_$$PAD^BKMIXX4($S(ALLREACT=0:"No",ALLREACT=1:"Yes",1:"Unk"),">"," ",20)_" "_$$PAD^BKMIXX4(REACTYPE,">"," ",20)
 ;.S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 ;Q
CLEAN ;
 K BDATE,BKMDT,EDATE,EXTDT,NAME
 Q
