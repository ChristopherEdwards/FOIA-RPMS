BKMVC2 ;PRXM/HC/JGH - Patient Lab Review; 24-JAN-2005 ; 25 May 2005  5:10 PM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
EN ; ENTRY POINT - ListMan template BKMV RECORD LABS
 N HIVIEN
 S HIVIEN=$$HIVIEN^BKMIXX3()
 I HIVIEN="" W !,"There is no HMS register defined." H 2 Q
 I '$$VALID^BKMIXX3(DUZ) W !,"You are not a valid HMS user." H 2 Q
 D EN^VALM("BKMV RECORD LABS")
 Q
 ;
INIT ; EP; called from list template BKMV Record Labs
 D GETALL(DFN)
 Q
 ;
 ; This routine gathers all labs for a patient given their
 ; DFN in to 9000001 and 2. 
 ; HIVTAX must be defined and is equal to 1 for HIV labs or 0 for all labs.
 ; BEGDATE must be defined and is equal to the FM beginning date
 ; ENDDATE must be defined and is equal to the FM ending date
GETALL(DFN) ; LABS BKMVC2
 N BEGLDT,ENDLDT,RLABDT,PLABDT,LABTYP,LABIEN,CPTPTR,LOINC,FND,CPT,ATAX
 N LABDT,REFLOW,REFHIGH,BKMRNG,TEXT,GLOBAL
 S VALMCNT=0,VALMAR="^TMP(""BKMVC2"","""_$J_""")",VALM0=""
 K ^TMP("BKMVC2",$J)
 ;
 D ^XBFMK
 S BEGLDT=9999999-BEGDATE
 S ENDLDT=9999999-ENDDATE
 S RLABDT=ENDLDT-.01
 S PLABDT=""
 S LABTYP=0
 I HIVTAX D  Q
 .D CD4(DFN),VIRAL(DFN),RPR(DFN),PAP(DFN),CHL(DFN),GON(DFN)
 .D HEP(DFN),HEPB(DFN),HEPC(DFN),CMV(DFN),TOX(DFN),COC(DFN),PPD(DFN)
 .D FTA(DFN),HIVG(DFN),HIVP(DFN),HIVT(DFN),TRICH(DFN)
 .D SETUP
 F  S LABTYP=$O(^AUPNVLAB("AA",DFN,LABTYP)) Q:LABTYP=""  D
 .S RLABDT=ENDLDT-.01
 .F  S RLABDT=$O(^AUPNVLAB("AA",DFN,LABTYP,RLABDT)) Q:RLABDT=""!(RLABDT>BEGLDT)  D
 ..S LABIEN=0
 ..F  S LABIEN=$O(^AUPNVLAB("AA",DFN,LABTYP,RLABDT,LABIEN)) Q:LABIEN=""  D
 ...;PRXM/HC/BHS - 06/14/2006 - Sort lab test by date, acc #, parent lab, child lab
 ...S BKMACC=$E($$GET1^DIQ(9000010.09,LABIEN,".06","E"),1,2)
 ...I BKMACC="" S BKMACC="ZZ"
 ...S BKMPAR=$S($$GET1^DIQ(9000010.09,LABIEN,"1208","I")]"":$$GET1^DIQ(9000010.09,LABIEN,"1208","I"),1:LABIEN)
 ...S BKMCHI=$S($$GET1^DIQ(9000010.09,LABIEN,"1208","I")="":0,1:LABIEN)
 ...S ^TMP("BKMVC2",$J,"NEW_SORT",RLABDT,BKMACC,BKMPAR,BKMCHI)=""
 D SETUP
 Q
SETUP ;
 N RLABDT,LABIEN,LABDT,REFLOW,REFHIGH,PLABDT,REF,BKMACC,BKMPAR,BKMCHI
 S RLABDT="",PLABDT=""
 F  S RLABDT=$O(^TMP("BKMVC2",$J,"NEW_SORT",RLABDT)) Q:RLABDT=""  D
 . S BKMACC="" F  S BKMACC=$O(^TMP("BKMVC2",$J,"NEW_SORT",RLABDT,BKMACC)) Q:BKMACC=""  D
 . . S BKMPAR="" F  S BKMPAR=$O(^TMP("BKMVC2",$J,"NEW_SORT",RLABDT,BKMACC,BKMPAR)) Q:BKMPAR=""  D
 . . . S BKMCHI="" F  S BKMCHI=$O(^TMP("BKMVC2",$J,"NEW_SORT",RLABDT,BKMACC,BKMPAR,BKMCHI)) Q:BKMCHI=""  D
 . . . . S REF=0
 . . . . I $G(^TMP("BKMVC2",$J,"NEW_SORT",RLABDT,BKMACC,BKMPAR,BKMCHI))="REFUSED" S REF=1
 . . . . S LABDT=9999999-RLABDT
 . . . . S LABIEN=$S(BKMCHI>0:BKMCHI,1:BKMPAR)
 . . . . S VALMCNT=$G(VALMCNT)+1
 . . . . S TEXT=""
 . . . . ; Display only date and only when not a duplicate of previous entry
 . . . . S TEXT=$$SETFLD^VALM1($S(LABDT'=PLABDT:$$FMTE^XLFDT(LABDT\1,"5Z"),1:""),TEXT,"Visit")
 . . . . S TEXT=$$SETFLD^VALM1($S('REF:$S(BKMCHI>0:"  ",1:"")_$$GET1^DIQ(9000010.09,LABIEN,".01","E"),1:$$GET1^DIQ(9000022,LABIEN,".04","E")),TEXT,"Lab Test")
 . . . . S TEXT=$$SETFLD^VALM1($S('REF:$$GET1^DIQ(9000010.09,LABIEN,".04","E"),1:"REFUSED"),TEXT,"Results")
 . . . . S TEXT=$$SETFLD^VALM1($$GET1^DIQ(9000010.09,LABIEN,"1101","E"),TEXT,"Units")
 . . . . S REFLOW=$$GET1^DIQ(9000010.09,LABIEN,"1104","E")
 . . . . S REFHIGH=$$GET1^DIQ(9000010.09,LABIEN,"1105","E")
 . . . . S BKMRNG=REFLOW_"-"_REFHIGH
 . . . . I BKMRNG="-" S BKMRNG=""
 . . . . S TEXT=$$SETFLD^VALM1(BKMRNG,TEXT,"RefRange")
 . . . . D SET^VALM10(VALMCNT,TEXT)
 . . . . S PLABDT=LABDT
 K ^TMP("BKMVC2",$J,"NEW_SORT")
 D ^XBFMK
 Q
 ;
PROMPT ;
 N STOP,OPTS,OPTA,PAR,QT,HIVTAX,BEGDT,ENDDT,ERR,OPTTEXT,BEGDATE,ENDDATE,BKMDOD,RCRDHDR,VALMHDR
 S STOP=0 F  Q:$G(STOP)=1  D  Q:$G(STOP)=1
 .D CLEAR^VALM1
 .D FULL^VALM1
 .S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 .S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 .S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 .I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,1),">"," ",15)
 .S VALMHDR=RCRDHDR
 .W !,VALMHDR
 .S OPTS(1)="HMS Related Labs"
 .S OPTS(2)="All Labs"
 .S OPTA="Lab Option",PAR="SO"
 .S QT=$$PROMPT^BKMIXX4(PAR,.OPTS,OPTA,1,1)
 .;PRXM/HC/BHS - 05/08/2006 - Modified exit logic so user can return to the Meds prompt
 .;                           when '^' is entered at a date prompt
 .I $P(QT,"^",2)=-1 S STOP=1 Q
 .S HIVTAX=$S($P(QT,"^",2)=1:1,1:0)
 .;I $P(QT,U) S STOP=1 Q
 .I $P(QT,U) Q
 .D EN^BKMVC2
 .K BEGDT,ENDDT,ERR,OPTTEXT
 I '$$GETALL^BKMVA1(DFN) Q
 ; get patient info for screen display
 D INIT^BKMVA1
 Q
HDR ;
 N BKMDTRNG,DA,IENS,SITE,BKMBDT,BKMEDT,RCRDHDR,BKMDOD,BKMTYPE
 S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 S VALMHDR(1)=$$PAD^BKMIXX4("",">"," ",(80-($L(SITE)+2))\2)_"["_$G(SITE)_"]"
 S BKMTYPE=$S($G(HIVTAX)=1:"HMS Related Labs",1:"All Labs")
 S VALMHDR(2)=$$PAD^BKMIXX4("",">"," ",(80-$L(BKMTYPE)\2))_BKMTYPE
 S BKMBDT=$$FMTE^XLFDT(BEGDATE,"5Z"),BKMEDT=$$FMTE^XLFDT(ENDDATE,"5Z")
 S BKMDTRNG="Date Range: "_BKMBDT_" - "_BKMEDT
 S VALMHDR(3)=$$PAD^BKMIXX4("",">"," ",(80-$L(BKMDTRNG)\2))_BKMDTRNG
 S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,"5Z"),">"," ",15)
 S VALMHDR(4)=RCRDHDR
 Q
HELP ; -- help code
 N X
 S X="?" D DISP^XQORM1 W !
 Q
 ;
EXIT ;
 K VALM0,VALMAR,VALMHDR,VALMCNT
 K ^TMP("BKMVC2",$J),^TMP("BKMVC2A",$J)
 Q
 ;
EXPND ;
 Q
 ;
CLEAN ;
 K CPT,%DT,DA,DIR,LABDT,LIND,LOINC,RCRDHDR,Y,SITE
 Q
CD4(DFN) ;Get CD4 taxonomies
 K ^TMP("BKMVC2A",$J,"CD4")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""CD4"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BGP CD4 TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOINC^BKMIXX(DFN,"BGP CD4 LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("CD4")
 Q
VIRAL(DFN) ;Get Viral taxonomies
 K ^TMP("BKMVC2A",$J,"VL")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""VL"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BGP HIV VIRAL LOAD TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOINC^BKMIXX(DFN,"BGP VIRAL LOAD LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("VL")
 Q
 ;
RPR(DFN) ;Get RPR taxonomies
 K ^TMP("BKMVC2A",$J,"RPR")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""RPR"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM RPR TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM RPR LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""RPR"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM RPR TAX",ENDDATE,BEGDATE,GLOBAL) ;taxonomy not loaded yet
 D REFUSAL^BKMIXX2(DFN,60,"BKM RPR LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("RPR")
 Q
PAP(DFN) ;Get PAP taxonomies
 K ^TMP("BKMVC2A",$J,"PAP")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""PAP"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BGP PAP SMEAR TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOINC^BKMIXX(DFN,"BGP PAP LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""PAP"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BGP PAP SMEAR TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BGP PAP LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("PAP")
 Q
CHL(DFN) ;Get Chlamydia taxonomies
 K ^TMP("BKMVC2A",$J,"CHL")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""CHL"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BGP CHLAMYDIA TESTS TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOINC^BKMIXX(DFN,"BGP CHLAMYDIA LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""CHL"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BGP CHLAMYDIA TESTS TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BGP CHLAMYDIA LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("CHL")
 Q
GON(DFN) ;Get Gonorrhea taxonomies
 K ^TMP("BKMVC2A",$J,"GON")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""GON"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM GONORRHEA TEST TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM GONORRHEA LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""GON"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM GONORRHEA TEST TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM GONORRHEA LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("GON")
 Q
HEP(DFN) ;Get Hepatitis Panel taxonomies
 K ^TMP("BKMVC2A",$J,"HPNL")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HPNL"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM HEPATITIS PANEL TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM HEP PANEL LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HPNL"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEPATITIS PANEL TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP PANEL LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HPNL")
 Q
HEPB(DFN) ;Get Hep B taxonomies
 K ^TMP("BKMVC2A",$J,"HEPB")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HEPB"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM HEP B TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOINC^BKMIXX(DFN,"BKM HEP B LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HEPB"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP B TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP B LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HEPB")
 Q
HEPC(DFN) ;Get Hep C taxonomies
 K ^TMP("BKMVC2A",$J,"HEPC")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HEPC"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM HEP C SCREENING TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LABTAX^BKMIXX(DFN,"BKM HEP C CONFIRMATORY TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM HEP C SCREEN LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOINC^BKMIXX(DFN,"BKM HEP C CONFIRM LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HEPC"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP C SCREENING TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP C CONFIRMATORY TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP C SCREEN LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM HEP C CONFIRM LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HEPC")
 Q
CMV(DFN) ;Get CMV taxonomies
 K ^TMP("BKMVC2A",$J,"CMV")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""CMV"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM CMV TEST TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM CMV LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""CMV"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM CMV TEST TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM CMV LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("CMV")
 Q
TOX(DFN) ;Get Toxoplasmosis taxonomies
 K ^TMP("BKMVC2A",$J,"TOX")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""TOX"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM TOXOPLASMOSIS TESTS TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM TOXOPLASMOSIS LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""TOX"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM TOXOPLASMOSIS TESTS TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM TOXOPLASMOSIS LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("TOX")
 Q
COC(DFN) ;Get Cocci taxonomies
 K ^TMP("BKMVC2A",$J,"COC")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""COC"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM COCCI ANTIBODY TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM COCCI ANTIBODY LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""COC"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM COCCI ANTIBODY TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM COCCI ANTIBODY LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("COC")
 Q
PPD(DFN) ;Get PPD taxonomies
 K ^TMP("BKMVC2A",$J,"PPD")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""PPD"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM PPD TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM PPD LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 S GLOBAL="^TMP(""BKMVC2A"",$J,""PPD"",VSTDT,TEST,""LABREF"")"
 D REFUSAL^BKMIXX2(DFN,60,"BKM PPD LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,60,"BKM PPD TAX",ENDDATE,BEGDATE,GLOBAL)
 D REFUSAL^BKMIXX2(DFN,9999999.14,"BKM PPD CVX CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("PPD")
 Q
FTA(DFN) ;Get FTA-ABS taxonomies
 K ^TMP("BKMVC2A",$J,"FTA")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""FTA"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM FTA-ABS TEST TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM FTA-ABS LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("FTA")
 Q
HIVG(DFN) ;Get HIV Genotype taxonomies
 K ^TMP("BKMVC2A",$J,"HIVG")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HIVG"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKMV HIV GENOTYPE TESTS TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HIVG")
 Q
HIVP(DFN) ;Get HIV Phenotype taxonomies
 K ^TMP("BKMVC2A",$J,"HIVP")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HIVP"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKMV HIV PHENOTYPE TESTS TAX",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HIVP")
 Q
HIVT(DFN) ;Get HIV Test taxonomies
 K ^TMP("BKMVC2A",$J,"HIVT")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""HIVT"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BGP HIV TEST TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BGP HIV TEST LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HIVT")
 Q
TRICH(DFN) ;Get Trichomoniasis taxonomies
 K ^TMP("BKMVC2A",$J,"TRICH")
 S GLOBAL="^TMP(""BKMVC2A"",$J,""TRICH"",VSTDT,TEST,""LAB"")"
 D LABTAX^BKMIXX(DFN,"BKM TRICH TESTS TAX",ENDDATE,BEGDATE,GLOBAL) ;***
 D LOINC^BKMIXX(DFN,"BKM TRICH LOINC CODES",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("TRICH")
 Q
LOOPDAT(NODETXT) ;
 N VSTDT,LABIEN,RVSTDT,BKMACC,BKMPAR,BKMCHI
 ;
 S VSTDT=0
 F  S VSTDT=$O(^TMP("BKMVC2A",$J,NODETXT,VSTDT)) Q:VSTDT=""  D
 .S LABIEN=0
 .F  S LABIEN=$O(^TMP("BKMVC2A",$J,NODETXT,VSTDT,LABIEN)) Q:LABIEN=""  D
 ..S RVSTDT=9999999-VSTDT
 ..I $D(^TMP("BKMVC2A",$J,NODETXT,VSTDT,LABIEN,"LABREF")) D  Q
 ...S BKMACC=$E($$GET1^DIQ(9000010.09,LABIEN,".06","E"),1,2)
 ...I BKMACC="" S BKMACC="ZZ"
 ...S BKMPAR=$S($$GET1^DIQ(9000010.09,LABIEN,"1208","I")]"":$$GET1^DIQ(9000010.09,LABIEN,"1208","I"),1:LABIEN)
 ...S BKMCHI=$S($$GET1^DIQ(9000010.09,LABIEN,"1208","I")="":0,1:LABIEN)
 ...S ^TMP("BKMVC2",$J,"NEW_SORT",RVSTDT,BKMACC,BKMPAR,BKMCHI)="REFUSED" Q
 ..;PRXM/HC/BHS - 06/14/2006 - Sort lab test by date, acc #, parent lab, child lab
 ..S BKMACC=$E($$GET1^DIQ(9000010.09,LABIEN,".06","E"),1,2)
 ..I BKMACC="" S BKMACC="ZZ"
 ..S BKMPAR=$S($$GET1^DIQ(9000010.09,LABIEN,"1208","I")]"":$$GET1^DIQ(9000010.09,LABIEN,"1208","I"),1:LABIEN)
 ..S BKMCHI=$S($$GET1^DIQ(9000010.09,LABIEN,"1208","I")="":0,1:LABIEN)
 ..S ^TMP("BKMVC2",$J,"NEW_SORT",RVSTDT,BKMACC,BKMPAR,BKMCHI)=""
 Q
