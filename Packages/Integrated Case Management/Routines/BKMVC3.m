BKMVC3 ;PRXM/HC/JGH - Patient Med Review; 24-JAN-2005
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
EN ; ENTRY POINT - ListMan template BKMV RECORD MEDS
 ; Assumes existence of DFN,DUZ
 N HIVIEN
 S HIVIEN=$$HIVIEN^BKMIXX3()
 I HIVIEN="" W !,"There is no HMS register defined." H 2 Q
 I '$$VALID^BKMIXX3(DUZ) W !,"You are not a valid HMS user." H 2 Q
 K ^TMP("BKMVC3",$J)
 D EN^VALM("BKMV RECORD MEDS")
 Q
 ;
INIT ; EP; called by list template BKMV Record Meds
 D GETALL(DFN)
 Q
 ;
 ; This routine gathers all medications for a patient given their
 ; DFN in to 9000001 and 2. 
 ; HIVTAX must be defined and is equal to 1 for HIV medications or 0 for all medications.
 ; BEGDATE must be defined and is equal to the FM beginning date
 ; ENDDATE must be defined and is equal to the FM ending date
GETALL(DFN) ;
 N RMEDDT,PMEDDT,MEDDT,MEDIEN,MEDTYPE,NDC,FND,TEXT
 S VALMCNT=0,VALMAR="^TMP(""BKMVC3"","""_$J_""")",VALM0="",PMEDDT=""
 K ^TMP("BKMVC3",$J),^TMP("BKMVC3A",$J)
 ;
 D ^XBFMK
 S BEGLDT=9999999-BEGDATE
 S ENDLDT=9999999-ENDDATE
 S RMEDDT=ENDLDT-.01
 S PMEDDT=""
 I HIVTAX D  Q
 . D HAART(DFN)
 . D MAC(DFN)
 . D PCP(DFN)
 . D OTHER(DFN)
 . D GATHER
 F  S RMEDDT=$O(^AUPNVMED("AA",DFN,RMEDDT)) Q:RMEDDT=""!(RMEDDT>BEGLDT)  D
 . S MEDIEN=0
 . F  S MEDIEN=$O(^AUPNVMED("AA",DFN,RMEDDT,MEDIEN)) Q:MEDIEN=""  D
 . . ; Build same data string as BKMIXX* routines used in other section.
 . . N MED,SIG,QTY,DAY
 . . S MED=$$GET1^DIQ(9000010.14,MEDIEN,.01,"E")
 . . S SIG=$$GET1^DIQ(9000010.14,MEDIEN,.05,"E")
 . . S QTY=$$GET1^DIQ(9000010.14,MEDIEN,.06,"E")
 . . S DAY=$$GET1^DIQ(9000010.14,MEDIEN,.07,"E")
 . . S ^TMP("BKMVC3",$J,"SORTED",RMEDDT,MEDIEN)=MED_U_SIG_U_QTY_U_DAY
 D GATHER
 D ^XBFMK
 K BEGLDT,ENDLDT
 Q
 ;
PROMPT ; ENTRY POINT -- from Protocol BKMV UPD1 MEDS REVIEW/ORDER
 N STOP,OPTS,OPTA,PAR,QT,HIVTAX,BEGDT,ENDDT,ERR,OPTTEXT,BEGDATE,ENDDATE,BKMDOD,VALMHDR
 S STOP=0 F  Q:$G(STOP)=1  D  Q:$G(STOP)=1
 .D CLEAR^VALM1
 .D FULL^VALM1
 .S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 .S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 .I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,1),">"," ",15)
 .S VALMHDR=RCRDHDR
 .W !,VALMHDR
 .S OPTS(1)="HMS Related Meds"
 .S OPTS(2)="All Meds"
 .S OPTA="Med Option",PAR="SO"
 .S QT=$$PROMPT^BKMIXX4(PAR,.OPTS,OPTA,1,1)
 .;PRXM/HC/BHS - 05/08/2006 - Modified exit logic so user can return to the Meds prompt
 .;                           when '^' is entered at a date prompt
 .I $P(QT,"^",2)=-1 S STOP=1 Q
 .S HIVTAX=$S($P(QT,"^",2)=1:1,1:0)
 .;I $P(QT,U) S STOP=1 Q
 .I $P(QT,U) Q
 .D EN^BKMVC3
 .K BEGDT,ENDDT,ERR,OPTTEXT
 I '$$GETALL^BKMVA1(DFN) Q
 ; get patient info for screen display
 D INIT^BKMVA1
 Q
HDR ;
 N BKMDTRNG,DA,IENS,SITE,BKMBDT,BKMEDT,RCRDHDR,BKMDOD,BKMTYPE
 S DA=$G(DUZ(2)),IENS=$$IENS^DILF(.DA),SITE=$$GET1^DIQ(4,IENS,.01,"E")
 S VALMHDR(1)=$$PAD^BKMIXX4("",">"," ",(80-($L(SITE)+2))\2)_"["_$G(SITE)_"]"
 S BKMTYPE=$S($G(HIVTAX)=1:"HMS Related Medications",1:"All Medications")
 S VALMHDR(2)=$$PAD^BKMIXX4("",">"," ",(80-$L(BKMTYPE)\2))_BKMTYPE
 S BKMBDT=$$FMTE^XLFDT(BEGDATE,"5Z"),BKMEDT=$$FMTE^XLFDT(ENDDATE,"5Z")
 S BKMDTRNG="Date Range: "_BKMBDT_" - "_BKMEDT
 S VALMHDR(3)=$$PAD^BKMIXX4("",">"," ",(80-$L(BKMDTRNG)\2))_BKMDTRNG
 S RCRDHDR=$$PAD^BKMIXX4(" Patient: ",">"," ",10)_$$PAD^BKMIXX4($$GET1^DIQ(2,DFN,".01","E"),">"," ",30)_$$PAD^BKMIXX4(" HRN: ",">"," ",6)_$$PAD^BKMIXX4($$HRN^BKMVA1(DFN),">"," ",9)
 S BKMDOD=$$GET1^DIQ(2,DFN,".351","I")
 I BKMDOD'="" S RCRDHDR=RCRDHDR_$$PAD^BKMIXX4(" DOD: ",">"," ",6)_$$PAD^BKMIXX4($$FMTE^XLFDT(BKMDOD,"5Z"),">"," ",15)
 S VALMHDR(4)=RCRDHDR
 Q
HELP ; -- help code
 N X
 S X="?" D DISP^XQORM1 W !
 Q
EXIT ; -- exit code
 K ^TMP("BKMVC3",$J),^TMP("BKMVC3A",$J),TEXT,PMEDDT
 K VALM0,VALMAR,VALMHDR,VALMCNT
 Q
 ;
EXPND ;
 Q
HAART(DFN) ;GATHER HAART DRUG DATA (using Taxonomy)
 ; Assumes existence of ENDDATE and BEGDATE
 N MEDDT,MEDIEN,PMEDDT,GLOBAL
 S GLOBAL="^TMP(""BKMVC3A"",$J,""HAART"",VSTDT,TEST)"
 D NDCTAX^BKMIXX1(DFN,"BKMV EI MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKMV EI MEDS",ENDDATE,BEGDATE,GLOBAL)
 D NDCTAX^BKMIXX1(DFN,"BKMV NNRTI MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKMV NNRTI MEDS",ENDDATE,BEGDATE,GLOBAL)
 D NDCTAX^BKMIXX1(DFN,"BKMV NRTI MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKMV NRTI MEDS",ENDDATE,BEGDATE,GLOBAL)
 D NDCTAX^BKMIXX1(DFN,"BKMV PI MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKMV PI MEDS",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("HAART")
 D REFUSAL^BKMIXX2(DFN,50,"BKMV EI MED NDCS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV EI MEDS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV NNRTI MED NDCS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV NNRTI MEDS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV NRTI MED NDCS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV NRTI MEDS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV PI MED NDCS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV PI MEDS","","",GLOBAL)
 D REFUSAL("HAART")
 D LOOPDAT("HAART")
 Q
MAC(DFN) ;GATHER MAC Prophylaxis DRUG DATA (using Taxonomy)
 ; Assumes existence of ENDDATE and BEGDATE
 N MEDDT,PMEDDT,GLOBAL
 S GLOBAL="^TMP(""BKMVC3A"",$J,""MAC"",VSTDT,TEST)"
 D NDCTAX^BKMIXX1(DFN,"BKMV MAC PROPH MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKMV MAC PROPH MEDS",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("MAC")
 D REFUSAL^BKMIXX2(DFN,50,"BKMV MAC PROPH MED NDCS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV MAC PROPH MEDS","","",GLOBAL)
 D REFUSAL("MAC")
 D LOOPDAT("MAC")
 Q
PCP(DFN) ;GATHER PCP Prophylaxis DRUG DATA (using Taxonomy)
 ; Assumes existence of ENDDATE and BEGDATE
 N MEDDT,PMEDDT,GLOBAL
 S GLOBAL="^TMP(""BKMVC3A"",$J,""PCP"",VSTDT,TEST)"
 D NDCTAX^BKMIXX1(DFN,"BKMV PCP PROPH MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKMV PCP PROPH MEDS",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("PCP")
 D REFUSAL^BKMIXX2(DFN,50,"BKMV PCP PROPH MED NDCS","","",GLOBAL)
 D REFUSAL^BKMIXX2(DFN,50,"BKMV PCP PROPH MEDS","","",GLOBAL)
 D REFUSAL("PCP")
 D LOOPDAT("PCP")
 Q
OTHER(DFN) ;gather other medications
 ; Assumes existence of ENDDATE and BEGDATE
 N MEDDT,PMEDDT,GLOBAL
 S GLOBAL="^TMP(""BKMVC3A"",$J,""OTH"",VSTDT,TEST)"
 ; Checks for syphilis meds removed per customer request
 ;D NDCTAX^BKMIXX1(DFN,"BKM SYPHILIS MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 ;D MEDTAX^BKMIXX(DFN,"BKM SYPHILIS MEDS",ENDDATE,BEGDATE,GLOBAL)
 D NDCTAX^BKMIXX1(DFN,"BKM TB MED NDCS",ENDDATE,BEGDATE,GLOBAL)
 D MEDTAX^BKMIXX(DFN,"BKM TB MEDS",ENDDATE,BEGDATE,GLOBAL)
 D LOOPDAT("OTH")
 Q
REFUSAL(NODETXT) ; Reset data for refusals
 N VSTDT,MEDIEN
 S VSTDT=0
 F  S VSTDT=$O(^TMP("BKMVC3A",$J,NODETXT,VSTDT)) Q:VSTDT=""  D
 .S MEDIEN=0
 .F  S MEDIEN=$O(^TMP("BKMVC3A",$J,NODETXT,VSTDT,MEDIEN)) Q:MEDIEN=""  D
 ..S ^TMP("BKMVC3A",$J,NODETXT,VSTDT,MEDIEN)=$$GET1^DIQ(9000022,MEDIEN,".04","E")_U_"REFUSED"
 Q
LOOPDAT(NODETXT) ; Re-sort in reverse date order
 N VSTDT,MEDIEN,RVSTDT
 S VSTDT=0
 F  S VSTDT=$O(^TMP("BKMVC3A",$J,NODETXT,VSTDT)) Q:VSTDT=""  D
 .S RVSTDT=9999999-VSTDT
 .S MEDIEN=0
 .F  S MEDIEN=$O(^TMP("BKMVC3A",$J,NODETXT,VSTDT,MEDIEN)) Q:MEDIEN=""  D
 ..S ^TMP("BKMVC3",$J,"SORTED",RVSTDT,MEDIEN)=^TMP("BKMVC3A",$J,NODETXT,VSTDT,MEDIEN)
 K ^TMP("BKMVC3A",$J,NODETXT)
 Q
GATHER ;
 N RMEDDT,MEDIEN,MEDDT,PMEDDT,TEMP
 S RMEDDT="",PMEDDT=""
 F  S RMEDDT=$O(^TMP("BKMVC3",$J,"SORTED",RMEDDT)) Q:RMEDDT=""  D
 . S MEDIEN=""
 . F  S MEDIEN=$O(^TMP("BKMVC3",$J,"SORTED",RMEDDT,MEDIEN)) Q:MEDIEN=""  D
 . . S TEMP=^TMP("BKMVC3",$J,"SORTED",RMEDDT,MEDIEN)
 . . S MEDDT=9999999-RMEDDT
 . . S TEXT=""
 . . S TEXT=$$SETFLD^VALM1($S(MEDDT'=PMEDDT:$$FMTE^XLFDT(MEDDT\1,"5Z"),1:""),TEXT,"Visit")
 . . S TEXT=$$SETFLD^VALM1($P(TEMP,U,1),TEXT,"Medication")
 . . S TEXT=$$SETFLD^VALM1($P(TEMP,U,2),TEXT,"Instructions")
 . . S TEXT=$$SETFLD^VALM1($P(TEMP,U,3),TEXT,"Qty")
 . . S TEXT=$$SETFLD^VALM1($P(TEMP,U,4),TEXT,"Days")
 . . S VALMCNT=$G(VALMCNT)+1 D SET^VALM10(VALMCNT,TEXT)
 . . S PMEDDT=MEDDT
 K ^TMP("BKVMC3",$J,"SORTED")
 Q
