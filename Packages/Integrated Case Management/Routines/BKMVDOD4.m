BKMVDOD4 ;PRXM/HC/BHS - DUE/OVERDUE REPORT PRINT CONTROL ; 14 Aug 2005  11:17 AM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
EN(BKMSEL) ; EP - Entry point to build report data global
 N BKMDFN,BKMX
 ;
 ; Build report for all ACTIVE HMS Register patients or one patient
 I $G(BKMSEL("PAT"))="ALL" D
 . S BKMX=0
 . F  S BKMX=$O(^BKM(90451,BKMX)) Q:BKMX'?1.N  D
 . . Q:'$O(^BKM(90451,BKMX,1,"STATUS","A",""))
 . . S BKMDFN=$$GET1^DIQ(90451,BKMX,".01","I") Q:'BKMDFN
 . . D PPR(BKMDFN,.BKMSEL)
 I $G(BKMSEL("PAT"))'="ALL" D
 . S BKMDFN=$G(BKMSEL("PAT"))
 . Q:'BKMDFN
 . D PPR(BKMDFN,.BKMSEL)
 Q
 ;
PPR(BKMDFN,BKMSEL) ; Build temporary report global
 ; Assumes BKMSEL array
 N BKMIEN,BKMREG,DA,IENS,BKMOTH,BKMOTHI,BKMQUIT,BKMNM,BKMDOB
 N BKMSEX,BKMAGE,BKMCC,BKMDIAG,BKMPR,BKMCM,BKMDPC
 N BKMPRI,BKMCMI,BKMDPCI,BKMSRT,BKMSRT1,BKMSRT2,BKMCTR,BKMCTR2
 N BKMREM,BKMBDT,BKMPAT,BKMLST,BKMX1,BKMX2,BKMDUE
 ;
 S BKMPAT=$G(BKMSEL("PAT"))
 S BKMIEN=$$BKMIEN^BKMIXX3(BKMDFN)
 I BKMPAT="ALL",BKMIEN="" Q
 S BKMREG=$S(BKMIEN'="":$$BKMREG^BKMIXX3(BKMIEN),1:"")
 I BKMPAT="ALL",BKMREG="" Q
 S DA=BKMREG,DA(1)=BKMIEN,IENS=$$IENS^DILF(.DA)
 ; Filter on ACTIVE status, if All Register Patients were selected
 I BKMPAT="ALL",$$GET1^DIQ(90451.01,IENS,".5","I")'="A" Q
 S (BKMOTH,BKMOTHI)=""
 I $D(BKMSEL("OTHER")) S BKMOTHI=$G(BKMSEL("OTHER")),BKMOTH=$P(BKMOTHI,U,2),BKMOTHI=+BKMOTHI
 I BKMOTH="PR" D  Q:BKMQUIT
 .S BKMQUIT=0
 .I BKMIEN=""!(BKMREG="") S BKMQUIT=1 Q
 .I BKMOTHI'=$$GET1^DIQ(90451.01,IENS,"6","I") S BKMQUIT=1 Q
 I BKMOTH="CM" D  Q:BKMQUIT
 .S BKMQUIT=0
 .I BKMIEN=""!(BKMREG="") S BKMQUIT=1 Q
 .I BKMOTHI'=$$GET1^DIQ(90451.01,IENS,"6.5","I") S BKMQUIT=1 Q
 I BKMOTH="DP" D  Q:BKMQUIT
 .S BKMQUIT=0
 .I BKMOTHI'=$$GET1^DIQ(9000001,BKMDFN,".14","I") S BKMQUIT=1 Q
 S BKMNM=$$GET1^DIQ(2,BKMDFN,".01","E")
 S BKMDOB=$$GET1^DIQ(2,BKMDFN,".03","I")
 S BKMSEX=$$GET1^DIQ(2,BKMDFN,".02","I")
 S BKMAGE=$$AGE^BKMIMRP1(BKMDFN)
 S (BKMCC,BKMDIAG)="",(BKMPR,BKMCM,BKMDPC)="{NONE}"
 I BKMIEN'="",BKMREG'="" D  Q:BKMQUIT
 .S BKMQUIT=0
 .S BKMCC=$$GET1^DIQ(90451.01,IENS,"3","I")
 .S BKMDIAG=$$GET1^DIQ(90451.01,IENS,"2.3","I")
 .S BKMPRI=$$GET1^DIQ(90451.01,IENS,"6","I")
 .S BKMPR=$$GET1^DIQ(90451.01,IENS,"6","E") I BKMPR="" S BKMPR="{NONE}"
 .S BKMCMI=$$GET1^DIQ(90451.01,IENS,"6.5","I")
 .S BKMCM=$$GET1^DIQ(90451.01,IENS,"6.5","E") I BKMCM="" S BKMCM="{NONE}"
 S BKMDPCI=$$GET1^DIQ(9000001,BKMDFN,".14","I")
 S BKMDPC=$$GET1^DIQ(9000001,BKMDFN,".14","E") I BKMDPC="" S BKMDPC="{NONE}"
 S BKMSRT=$G(BKMSEL("SORT"))
 S BKMSRT1=$S(BKMSRT="PR":BKMPR,BKMSRT="DP":BKMDPC,BKMSRT="CM":BKMCM,1:"{NONE}")
 S BKMSRT2=$S(BKMNM="":"{NONE}",1:BKMNM)
 K BKMLST
 ; Call reminder logic here
 D REMIND^BKMVF3(BKMDFN,BKMSEL("BLDATE"),.BKMLST)
 ; Walk reminders to build tests to list here
 S BKMREM=$G(BKMSEL("REM"))
 S BKMBDT=$G(BKMSEL("BLDATE"))
 I $O(BKMLST("")) D
 .S BKMCTR2=0
 .S BKMX1="" F  S BKMX1=$O(BKMLST(BKMX1)) Q:BKMX1=""  D
 ..I BKMX1[" REM.T.",BKMREM'="A",BKMREM'="T" Q
 ..I BKMX1[" REM.IZ.",BKMREM'="A",BKMREM'="IZ" Q
 ..I BKMX1[" REM.P.",BKMREM'="A",BKMREM'="E" Q
 ..I BKMX1[" REM.ED.",BKMREM'="A",BKMREM'="P" Q
 ..S BKMX2="" F  S BKMX2=$O(BKMLST(BKMX1,BKMX2)) Q:BKMX2=""  D
 ...S BKMDUE=$G(BKMLST(BKMX1,BKMX2,"DUE"))
 ...I 'BKMDUE Q
 ...; Check due date against baseline date - if the date is beyond one month
 ...;  it is not due or overdue so it should not be displayed in the report
 ...I BKMDUE>+$$SCH^XLFDT("1M",BKMBDT) Q
 ...I BKMCTR2=0 D
 ....S BKMCTR=+$G(^TMP("BKMVDOD",$J,"SORTED",BKMSRT1,BKMSRT2,0))+1
 ....S ^TMP("BKMVDOD",$J,"SORTED",BKMSRT1,BKMSRT2,0)=BKMCTR
 ....S ^TMP("BKMVDOD",$J,"SORTED",BKMSRT1,BKMSRT2,BKMCTR)=BKMIEN_U_BKMDFN_U_BKMDOB_U_BKMAGE_U_BKMSEX_U_U_BKMCC_U_BKMDIAG_U_BKMPR_U_BKMDPC_U_BKMCM
 ...S BKMCTR2=+$G(^TMP("BKMVDOD",$J,"SORTED",BKMSRT1,BKMSRT2,BKMCTR,0))+1
 ...S ^TMP("BKMVDOD",$J,"SORTED",BKMSRT1,BKMSRT2,BKMCTR,0)=BKMCTR2
 ...S ^TMP("BKMVDOD",$J,"SORTED",BKMSRT1,BKMSRT2,BKMCTR,BKMCTR2)=BKMLST(BKMX1,BKMX2,0)_U_BKMLST(BKMX1,BKMX2,"LAST")_U_BKMDUE
 Q
 ;
 ;
