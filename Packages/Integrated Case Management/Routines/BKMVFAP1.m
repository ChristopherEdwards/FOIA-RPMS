BKMVFAP1 ;PRXM/HC/ALA - Autopopulate BKM HIV Candidate File ; 19 Aug 2005  1:13 PM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 ;**Program Description**
 ;  This program will auto-populate the BKM Registry File (#90451)
 ;  based on criteria using taxonomies
 ;
 Q
 ;
EN ; Entry Point - Candidate File autopopulate.
 NEW REGISTER
 S REGISTER=$$HIVIEN^BKMIXX3()
 I REGISTER="" D EN^DDIOL("There is no HMS register defined.") H 2 Q
 I '$$VALID^BKMIXX3(DUZ) D EN^DDIOL("You are not a valid HMS user.") H 2 Q
 ;
 NEW GLOBAL,FLAG,DFLAG
 S GLOBAL=$NA(^BKM(90450,REGISTER,50))
 D EN^DDIOL("",GLOBAL)
 ; Check taxonomies
 S FLAG=1,DFLAG=1 D EN^BKMVC1
 ;
 ; Ask for Deceased patients
 D DEC
 I $D(DTOUT)!$D(DUOUT) Q
 ;
 S DIR(0)="Y",DIR("B")="YES",DIR("A")="Queue the auto populate"
 D ^DIR K DIR
 I $G(DUOUT)!$G(DTOUT) G XIT
 I Y=0 G FORE
 ;
 D MSG I $G(BKMCONT)'=1 G XIT
 S BKMUPD(90450,REGISTER_",",22.7)="Q",BKMUPD(90450,REGISTER_",",22.9)=DUZ
 D FILE^DIE("I","BKMUPD") K BKMUPD
 ;
QUE ;QUE REPORT FOR PRINT/DISPLAY
 K %ZIS,IOP,IOC,ZTIO
 S ZTRTN="BACK^BKMVFAP1(REGISTER)",ZTIO=""
 S ZTSAVE("REGISTER")="",ZTSAVE("BKM*")="",ZTSAVE("BKMDEC")=""
 S ZTDESC="HMS AUTOPOPULATE REGISTER"
 D ^%ZTLOAD W:$D(ZTSK) !,"REQUEST QUEUED"
 S DIR("A")="Press <Enter> to continue",DIR(0)="E" D ^DIR
 G XIT
 ;
FORE ; Foreground entry point
 D MSG I $G(BKMCONT)'=1 G XIT
 D EN^DDIOL("Please wait. ","")
 S BKMUPD(90450,REGISTER_",",22.7)="Q",BKMUPD(90450,REGISTER_",",22.9)=DUZ
 D FILE^DIE("I","BKMUPD")
 D BACK(REGISTER)
 S DIR("A")="Auto populate is complete. Press <Enter> to continue",DIR(0)="E" D ^DIR
 G XIT
 ;
BACK(REGISTER) ; background entry point
 K DIR
 S BKMPOPDT=$E($$NOW^XLFDT(),1,12),CNT=0
 ;
 S BKMUPD(90450,REGISTER_",",22)=BKMPOPDT
 S BKMUPD(90450,REGISTER_",",22.5)=""
 S BKMUPD(90450,REGISTER_",",22.7)="R"
 S BKMUPD(90450,REGISTER_",",22.8)=$J
 S BKMUPD(90450,REGISTER_",",22.9)=DUZ
 D FILE^DIE("","BKMUPD")
 K BKMUPD
 ;
 ;  Check for deceased patients if running as a scheduled TaskMan
 I $G(BKMDEC)="" S BKMDEC=+$$GET1^DIQ(90450,REGISTER,22.1,"I")
 ;
INIT ;  Initialize some Taxonomies
 D ITAX^BKMVA1U
 ;
DXN ; Check the ICD9 Diagnosis taxonomies
 K ^TMP("BKMARRAY",$J),^TMP("BKMPOP",$J),^TMP("BKMREM",$J)
 D BLDTAX^BKMIXX5("BGP HIV/AIDS DXS","^TMP(""BKMARRAY"",$J)")
 S DXN="" F  S DXN=$O(^TMP("BKMARRAY",$J,DXN)) Q:DXN=""  D
 . S LIEN="" F  S LIEN=$O(^AUPNVPOV("B",DXN,LIEN)),IGNORE=0 Q:LIEN=""  D
 .. S DFN=$P(^AUPNVPOV(LIEN,0),U,2) Q:DFN=""
 .. I 'BKMDEC,$$GET1^DIQ(2,DFN,.351,"I")'="" Q
 .. I '$$HRN^BKMVUTL(DFN) Q
 .. D NPAT(DFN) Q:IGNORE
 .. S VISIT=$$GET1^DIQ(9000010.07,LIEN,.03,"I") Q:VISIT=""
 .. D NREC(DXN,2,VISIT,"BGP HIV/AIDS DXS-POV")
 . ;
 . S LIEN="" F  S LIEN=$O(^AUPNPROB("B",DXN,LIEN)) Q:LIEN=""  D
 .. S DFN=$P(^AUPNPROB(LIEN,0),U,2) Q:DFN=""
 .. I 'BKMDEC,$$GET1^DIQ(2,DFN,.351,"I")'="" Q
 .. I '$$HRN^BKMVUTL(DFN) Q
 .. D NPAT(DFN) Q:IGNORE
 .. D NREC(DXN,5,LIEN,"BGP HIV/AIDS DXS-PROB")
 ;
 K DA,DIC,DFN,DLAYGO,DXN,IENS,LAB,LIEN,SBFILE,VISDTM,VISIT,X,Y
 ;
MED ; Check the medication taxonomies
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV NRTI MED NDCS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV NRTI MED NDCS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV NRTI MEDS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV NRTI MEDS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV NNRTI MED NDCS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV NNRTI MED NDCS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV NNRTI MEDS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV NNRTI MEDS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV PI MED NDCS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV PI MED NDCS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV PI MEDS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV PI MEDS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV EI MED NDCS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV EI MED NDCS")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BKMV EI MEDS","^TMP(""BKMARRAY"",$J)")
 S MED="" F  S MED=$O(^TMP("BKMARRAY",$J,MED)) Q:MED=""  D FMED("BKMV EI MEDS")
 K ^TMP("BKMARRAY",$J)
 ;
 ; Determine if at least 2 different meds on same day (MDCT) or same med on 2 different days (MCT)
 K ^TMP($J,"BKMMEDV")
 S DFN=""
 F  S DFN=$O(^TMP($J,"BKMVMEDS1",DFN)) Q:DFN=""  D
 . S VDTM=""
 . F  S VDTM=$O(^TMP($J,"BKMVMEDS1",DFN,VDTM)),MDCT=0 Q:VDTM=""  D  D:MDCT>1 CMD
 .. S MED=""
 .. F  S MED=$O(^TMP($J,"BKMVMEDS1",DFN,VDTM,MED)) Q:MED=""  D
 ... S MDCT=MDCT+1
 S DFN=""
 F  S DFN=$O(^TMP($J,"BKMVMEDS2",DFN)) Q:DFN=""  D
 . S MED="",MCT=0
 . F  S MED=$O(^TMP($J,"BKMVMEDS2",DFN,MED)) Q:MED=""  S MCT=MCT+1
 . I MCT>1 D CMD1
 ;
 D FMD
 ;
 K ^TMP($J,"BKMVMEDS1"),^TMP($J,"BKMVMEDS2"),^TMP($J,"BKMMEDV")
 ;  
LAB ;  Check the lab, CPT, and LOINC codes in the Lab V-File.
 ;
 ;  Build the lab tests from taxonomies to check
 K ^TMP("BKMARRAY",$J),^TMP("BKMCPT",$J)
 D BLDTAX^BKMIXX5("BGP HIV TEST TAX","^TMP(""BKMARRAY"",$J)")
 S LAB="" F  S LAB=$O(^TMP("BKMARRAY",$J,LAB)) Q:LAB=""  D FLB("BGP HIV TEST TAX")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BGP HIV TEST LOINC CODES","^TMP(""BKMARRAY"",$J)")
 S LAB="" F  S LAB=$O(^TMP("BKMARRAY",$J,LAB)) Q:LAB=""  D FLB("BGP HIV TEST LOINC CODES")
 K ^TMP("BKMARRAY",$J)
 D BLDTAX^BKMIXX5("BGP CPT HIV TESTS","^TMP(""BKMCPT"",$J)")
 S CPT="" F  S CPT=$O(^TMP("BKMCPT",$J,CPT)) Q:CPT=""  D
 . S BCPTR=0 F  S BCPTR=$O(^BLRCPT(BCPTR)) Q:'BCPTR  D
 .. I $D(^BLRCPT(BCPTR,11,"B",CPT)) D
 ... S LAB=$P($G(^BLRCPT(BCPTR,1)),U,1)
 ... Q:LAB=""
 ... I $G(^LAB(60,LAB,0))'="" S ^TMP("BKMARRAY",$J,LAB)=$P(^LAB(60,LAB,0),U,1)
 K ^TMP("BKMCPT",$J),CPT,BCPTR
 S LAB="" F  S LAB=$O(^TMP("BKMARRAY",$J,LAB)) Q:LAB=""  D FLB("BGP CPT HIV TESTS")
 K ^TMP("BKMARRAY",$J)
 ;  
FIN D STAMP
 ;
 S DFN="",CNT=0 F  S DFN=$O(^TMP("BKMPOP",$J,DFN)) Q:DFN=""  S CNT=CNT+1
 S BKCIEN="" F  S BKCIEN=$O(^TMP("BKMREM",$J,BKCIEN)) Q:BKCIEN=""  D
 . S BKUP(90451.2,BKCIEN_",",.04)=BKMPOPDT
 . D FILE^DIE("","BKUP","ERROR")
 . K BKUP
 ;
 ; Notify someone when there are new potential patients.
 D:CNT NOTIFY(DUZ)
 ;
XIT ;KILL LOCALS AND EXIT
 D ^XBFMK
 K DIR,REGISTER,BKM,FOUND,LOINC,MED,VMEDIEN,Y,ZTDESC,ZTIO,ZTSAVE
 K STATUS,ZTRTN,ZTSK,AIDX,AIEN,AOK,DXN,HIDX,HIEN,LIEN,QFL,VISDTM,VISIT
 K ^TMP("BKMPOP",$J),^TMP("BKMARRAY",$J),^TMP("BKMTST",$J)
 K ^TMP("BKMAIDS",$J),^TMP("BKMHIV",$J),^TMP("BKMCD4",$J)
 K DCT,DIAGCAT,HAIDSDT,IAIDSDT,RDATE,LAB,RDA,SBFILE,SUBFIL,XCNP,IGNORE
 K BKCIEN,BKMIEN,BKMPOPDT,DFN,ERR,ERROR,BCPTR,CPT,ENTRY,BKMDEC,BKMCONT
 K ^TMP("BKMREM",$J),MCT,MDCT,VDTM
 Q
 ;
STAMP ;
 S BKMUPD(90450,REGISTER_",",21)=$E($$NOW^XLFDT(),1,12)
 S BKMUPD(90450,REGISTER_",",22.5)=$E($$NOW^XLFDT(),1,12)
 S BKMUPD(90450,REGISTER_",",22.7)="N"
 D FILE^DIE("","BKMUPD")
 Q
 ;
NOTIFY(ADUZ) ; automatically notify case file managers
 ;
 S XMTEXT="TEXT("
 S TEXT(1,0)=CNT_" new potential patients found for the HIV Management System."
 S NDUZ=""
 F  S NDUZ=$O(^BKM(90450,1,11,"B",NDUZ)) Q:NDUZ=""  D
 . S UIHIV=$O(^BKM(90450,1,11,"B",NDUZ,""))
 . Q:UIHIV=""
 . S OCCUP=$$GET1^DIQ(90450.011,UIHIV_","_REGISTER,.5,"I")
 . I OCCUP'="M" QUIT  ; If they are not the Case File Manager don't e-mail them. 
 . S XMY(NDUZ)=""
 S XMTEXT="TEXT("
 S XMSUB="HMS AUTOPOPULATE RUN"
 S XMDUZ=.5
 I '$D(XMY) S XMY(ADUZ)=""
 D ^XMD
 K XMSUB,XMY,XMDUZ,DFL,TEXT,CNT,NDUZ,UIHIV,OCCUP,XMZ,XMTEXT
 Q
 ;
AIDS(DFN) ;  EP - Check if patient has an AIDS diagnosis
 ;
 S AOK=0,DIAGCAT="",IAIDSDT="",HAIDSDT=""
 S AIDX=""
 F   S AIDX=$O(^TMP("BKMAIDS",$J,AIDX)) Q:AIDX=""  D
 . S AIEN=""
 . F  S AIEN=$O(^AUPNVPOV("B",AIDX,AIEN)) Q:AIEN=""  D  Q:AOK
 .. I $P(^AUPNVPOV(AIEN,0),U,2)=DFN S AOK=1 D
 ... S IAIDSDT=""
 ... S VISIT=$$GET1^DIQ(9000010.07,AIEN,.03,"I") Q:VISIT=""
 ... S IAIDSDT=$P($$GET1^DIQ(9000010,VISIT,.01,"I"),".",1)
 . ;
 . S AIEN=""
 . I 'AOK F  S AIEN=$O(^AUPNPROB("B",AIDX,AIEN)) Q:AIEN=""  D  Q:AOK
 .. I $P(^AUPNPROB(AIEN,0),U,2)=DFN S AOK=1 D
 ... S IAIDSDT=$$PROB^BKMVUTL(AIEN)
 ;
 ;  If an AIDS diagnosis, set the Register Diagnosis field to AIDS
 I AOK S DIAGCAT="A"
 ;
 K ^TMP("BKMHIVP",$J)
 S HIDX=""
 F  S HIDX=$O(^TMP("BKMHIV",$J,HIDX)) Q:HIDX=""  D
 . S HIEN="",DCT=0
 . F  S HIEN=$O(^AUPNVPOV("B",HIDX,HIEN)) Q:HIEN=""  D
 .. I $P(^AUPNVPOV(HIEN,0),U,2)=DFN D
 ... S ^TMP("BKMHIVP",$J,DFN)=$G(^TMP("BKMHIVP",$J,DFN))+1,DCT=DCT+1
 ... I DCT=1 D
 .... S VISIT=$$GET1^DIQ(9000010.07,HIEN,.03,"I") Q:VISIT=""
 .... S HAIDSDT=$P($$GET1^DIQ(9000010,VISIT,.01,"I"),".",1)
 .... S DIAGCAT="H"
 . ;
 . S HIEN=""
 . F  S HIEN=$O(^AUPNPROB("B",HIDX,HIEN)) Q:HIEN=""  D
 .. I $P(^AUPNPROB(HIEN,0),U,2)=DFN D
 ... S ^TMP("BKMHIVP",$J,DFN)=$G(^TMP("BKMHIVP",$J,DFN))+1,DCT=DCT+1
 ... I DCT=1 D
 .... S HAIDSDT=$$PROB^BKMVUTL(HIEN),DIAGCAT="H"
 ;
 I $G(^TMP("BKMHIVP",$J,DFN))>1 S DIAGCAT="A",IAIDSDT=$P(HAIDSDT,".",1)
 ;
DLB ;  Check for lab result to determine register diagnosis
 NEW LAB,LIEN
 S LAB="",DCT=0
 F  S LAB=$O(^TMP("BKMCD4",$J,LAB)) Q:LAB=""  D
 . S LIEN=""
 . F  S LIEN=$O(^AUPNVLAB("B",LAB,LIEN)) Q:LIEN=""  D  Q:AOK
 .. I $P($G(^AUPNVLAB(LIEN,0)),U,2)'=DFN Q
 .. S RESULT=$$GET1^DIQ(9000010.09,LIEN,.04,"E")
 .. S RDATE=$P($$GET1^DIQ(9000010.09,LIEN,1212,"I"),".",1)
 .. I 'RDATE S VISIT=$$GET1^DIQ(9000010.14,LIEN,.03,"I") Q:VISIT=""  D
 ... S VISDTM=$P($$GET1^DIQ(9000010,VISIT,.01,"I"),".",1)
 .. I RESULT'="",RESULT<200 S DIAGCAT="A",AOK=1 D
 ... S DCT=DCT+1
 ... I DCT=1 S IAIDSDT=$S($G(RDATE):RDATE,1:$G(VISDTM))
 Q
 ;
FMD ; Find Medications
 S DFN="" F  S DFN=$O(^TMP($J,"BKMMEDV",DFN)) Q:DFN=""  D
 . I 'BKMDEC,$$GET1^DIQ(2,DFN,.351,"I")'="" Q
 . I '$$HRN^BKMVUTL(DFN) Q
 . D NPAT(DFN) Q:IGNORE
 . S LIEN="" F  S LIEN=$O(^TMP($J,"BKMMEDV",DFN,LIEN)) Q:LIEN=""  D
 .. S VISIT=$$GET1^DIQ(9000010.14,LIEN,.03,"I") Q:VISIT=""
 .. S VISDTM=$P($$GET1^DIQ(9000010,VISIT,.01,"I"),".",1)
 .. S MTAX=^TMP($J,"BKMMEDV",DFN,LIEN)
 .. S MED=$$GET1^DIQ(9000010.14,LIEN,.01,"I")
 .. D NREC(MED,3,VISIT,MTAX)
 K DA,DIC,X,Y,LIEN,IENS,DFN,VISIT,VISDTM,MTAX
 Q
 ;
FMED(MTAX) ; Find Medications
 S LIEN="" F  S LIEN=$O(^AUPNVMED("B",MED,LIEN)) Q:LIEN=""  D
 . S DFN=$P($G(^AUPNVMED(LIEN,0)),U,2)
 . I DFN="" Q
 . S VISIT=$P(^AUPNVMED(LIEN,0),U,3) Q:VISIT=""
 . S VISDTM=$P($$GET1^DIQ(9000010,VISIT,.01,"I"),".",1)
 . S ^TMP($J,"BKMVMEDS1",DFN,VISDTM,MED,LIEN)=MTAX
 . S ^TMP($J,"BKMVMEDS2",DFN,MED,LIEN)=MTAX
 Q
 ;
CMD ;  Compile meds
 S MED=""
 F  S MED=$O(^TMP($J,"BKMVMEDS1",DFN,VDTM,MED)) Q:MED=""  D
 . S LIEN="" F  S LIEN=$O(^TMP($J,"BKMVMEDS1",DFN,VDTM,MED,LIEN)) Q:LIEN=""  S ^TMP($J,"BKMMEDV",DFN,LIEN)=^TMP($J,"BKMVMEDS1",DFN,VDTM,MED,LIEN)
 Q
 ;
CMD1 ; Compile meds
 S MED=""
 F  S MED=$O(^TMP($J,"BKMVMEDS2",DFN,MED)) Q:MED=""  D
 . S LIEN="" F  S LIEN=$O(^TMP($J,"BKMVMEDS2",DFN,MED,LIEN)) Q:LIEN=""  S ^TMP($J,"BKMMEDV",DFN,LIEN)=^TMP($J,"BKMVMEDS2",DFN,MED,LIEN)
 Q
 ;
FLB(LTAX) ; Find Lab tests
 S LIEN="" F  S LIEN=$O(^AUPNVLAB("B",LAB,LIEN)) Q:LIEN=""  D
 . S DFN=$P($G(^AUPNVLAB(LIEN,0)),U,2)
 . I DFN="" Q
 . I 'BKMDEC,$$GET1^DIQ(2,DFN,.351,"I")'="" Q
 . I '$$HRN^BKMVUTL(DFN) Q
 . S RESULT=$$GET1^DIQ(9000010.09,LIEN,.04,"E") Q:RESULT=""
 . I $$POSITIVE^BKMVF32(RESULT) D
 .. D NPAT(DFN) Q:IGNORE
 .. S VISIT=$$GET1^DIQ(9000010.09,LIEN,.03,"I") Q:VISIT=""
 .. S VISDTM=$P($$GET1^DIQ(9000010,VISIT,.01,"I"),".",1)
 .. S RESULT=$$GET1^DIQ(9000010.09,LIEN,.04,"E")
 .. D NREC(LAB,4,VISIT,LTAX)
 K DA,DIC,X,Y,LIEN,VISIT,LTAX,RESULT,VISDTM,DFN,IENS
 Q
 ;
NPAT(DFN) ; EP - Registry update.
 S IGNORE=0
 S BKMIEN=$$BKMIEN^BKMIXX3(DFN)
 I $$BKMREG^BKMIXX3(BKMIEN)'="" S IGNORE=1 Q
 S BKCIEN=+$$FIND1^DIC(90451.2,"","Q",DFN,"B","","ERROR")
 K ERROR
 I BKCIEN=0 D
 . S BKMVA(90451.2,"+1,",.01)=DFN
 . S BKMVA(90451.2,"+1,",.04)=BKMPOPDT
 . ; PRXM/HC/BHS - 05/10/2006 - Updated UNREVIEWED code to UNR
 . S BKMVA(90451.2,"+1,",.03)="UNR"
 . D UPDATE^DIE("","BKMVA","IENARRAY","ERROR")
 . I '$D(ERROR) S BKCIEN=$G(IENARRAY(1))
 . K BKMVA,IENARRAY
 S STATUS=$$GET1^DIQ(90451.2,BKCIEN,.03,"I")
 Q
 ;
NREC(VALUE,MULT,VISIT,TAX) ; File data record
 I $G(VALUE)="" Q
 I $G(MULT)="" Q
 S SBFILE=$S(MULT=2:"90451.26PA",MULT=3:"90451.27PA",MULT=4:"90451.28PA",1:"90451.29P")
 S SUBFIL=$S(MULT=2:90451.26,MULT=3:90451.27,MULT=4:90451.28,1:90451.29)
 K VAL S VAL(1)=VALUE,VAL(2)=VISIT
 S RDA=+$$FIND1^DIC(SUBFIL,","_BKCIEN_",","Q",.VAL,"C","","ERR")
 K ERR
 I RDA=0 D
 . S IENS="+1,"_BKCIEN_","
 . S BKUP(SUBFIL,IENS,.01)=VALUE
 . S BKUP(SUBFIL,IENS,.02)=VISIT
 . S BKUP(SUBFIL,IENS,.03)=TAX
 . S BKUP(SUBFIL,IENS,.04)=$$NOW^XLFDT()
 . D UPDATE^DIE("","BKUP","IENSARRY","ERROR")
 . S ^TMP("BKMPOP",$J,DFN)=""
 . K BKUP,IENSARRY,IENS,ERROR
 . I $G(STATUS)'="UNR" D
 .. I $G(STATUS)="REM",$$GET1^DIQ(90451.2,BKCIEN_",",.04,"I")="" S ^TMP("BKMREM",$J,BKCIEN)="" Q
 .. S BKUPD(90451.2,BKCIEN_",",.03)="NEW"
 .. D FILE^DIE("","BKUPD","ERROR")
 .. K BKUPD,ERROR
 .. S STATUS=$$GET1^DIQ(90451.2,BKCIEN,.03,"I")
 K VAL,DA,BKUPD
 Q
 ;
DEC ; Include deceased patients?
 S DIR(0)="Y",BKMDEC=0
 S DIR("A")="Do you want to include deceased patients"
 D ^DIR K DIR
 I $D(DTOUT)!$D(DUOUT) Q
 S BKMDEC=Y
 Q
 ;
MSG ;  Ask to continue
 NEW DIR
 S DIR(0)="Y",BKMCONT=0,DIR("B")="YES"
 S DIR("A")="This process may take several minutes to run.  Do you want to continue"
 D ^DIR K DIR
 I $D(DTOUT)!$D(DUOUT) Q
 S BKMCONT=Y
 Q
