BKMVQCR3 ;PRXM/HC/BWF - BKMV Quality of Care Report; [ 1/19/2005  7:16 PM ]
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ; Quality of Care Audit Report
 Q
TETSTAT ; EP - Tetanus Status
 N TETDT,TETDT1,CVXTAX,ICDTAX,PRCTAX,CPTTAX,GLOBAL,GLOBAL2,REFGLOB,TOTPTS,DFN
 S TETDT=$$FMADD^XLFDT(EDATE,-3650)
 S TETDT1=$$FMADD^XLFDT(EDATE,-365)
 S CVXTAX="BKM TETANUS IZ CVX CODES"
 S ICDTAX="BKM TETANUS IZ DXS"
 S PRCTAX="BKM TETANUS IZ PROCEDURES"
 S CPTTAX="BKM TETANUS IZ CPTS"
 S GLOBAL="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""TET"",VSTDT,TEST)"
 S REFGLOB="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""TETREF"",VSTDT,TEST)"
 S TOTPTS="^TMP(""BKMVQCR"",$J,""HIVCHK"",""TETPTCNT"")"
 S DFN=0,@TOTPTS=0
 F  S DFN=$O(^TMP("BKMVQCR",$J,"HIVCHK",DFN)) Q:'DFN  D
 .D ICDTAX^BKMIXX1(DFN,ICDTAX,EDATE,TETDT,GLOBAL)
 .D CPTTAX^BKMIXX(DFN,CPTTAX,EDATE,TETDT,GLOBAL)
 .D PRCTAX^BKMIXX1(DFN,PRCTAX,EDATE,TETDT,GLOBAL)
 .D CVXTAX^BKMIXX1(DFN,CVXTAX,EDATE,TETDT,GLOBAL)
 .; Refusals are only counted from last year.
 .D REFUSAL^BKMIXX2(DFN,9999999.14,CVXTAX,EDATE,TETDT1,REFGLOB)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"TET"))!$D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"TETREF")) S @TOTPTS=@TOTPTS+1 Q
 Q
EYEEXAM ; EP - Eye Exam Status
 N EEXAMDT,CPTTAX,GLOBAL,REFGLOB,TOTPTS,DFN,CLINIC,PROVIDER
 S EEXAMDT=$$FMADD^XLFDT(EDATE,-365)
 S CPTTAX="BGP EYE EXAM CPTS"
 S GLOBAL="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""EYE"",VSTDT,TEST)"
 S REFGLOB="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""EYEREF"",VSTDT,TEST)"
 S TOTPTS="^TMP(""BKMVQCR"",$J,""HIVCHK"",""EYEPTCNT"")"
 S DFN=0,@TOTPTS=0
 F  S DFN=$O(^TMP("BKMVQCR",$J,"HIVCHK",DFN)) Q:'DFN  D
 .D CPTTAX^BKMIXX(DFN,CPTTAX,EDATE,EEXAMDT,GLOBAL)
 .F CLINIC=17,18,64,"A2" D CLNTAX^BKMIXX2(DFN,CLINIC,EDATE,EEXAMDT,GLOBAL)
 .F PROVIDER=79,24,"08" D PRVTAX^BKMIXX2(DFN,PROVIDER,EDATE,EEXAMDT,GLOBAL)
 .D EXAMTAX^BKMIXX1(DFN,"03",EDATE,EEXAMDT,GLOBAL)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"EYE")) S @TOTPTS=@TOTPTS+1 Q
 .D REFUSAL^BKMIXX2(DFN,9999999.15,"03",EDATE,EEXAMDT,REFGLOB)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"EYEREF")) S @TOTPTS=@TOTPTS+1
 Q
DENTEXAM ; EP - Dental Exam Status
 N DEXAMDT,ICDTAX,ADATAX,GLOBAL,REFGLOB,TOTPTS,DFN
 S DEXAMDT=$$FMADD^XLFDT(EDATE,-365)
 S ICDTAX="BKM DENTAL EXAMINATION"
 S ADATAX="BGP DENTAL EXAM DENTAL CODE"
 S GLOBAL="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""DENT"",VSTDT,TEST)"
 S REFGLOB="^TMP(""BKMVQCT"",$J,""HIVCHK"",DFN,""DENTREF"",VSTDT,TEST)"
 S TOTPTS="^TMP(""BKMVQCR"",$J,""HIVCHK"",""DENTPTCNT"")"
 S DFN=0,@TOTPTS=0
 F  S DFN=$O(^TMP("BKMVQCR",$J,"HIVCHK",DFN)) Q:'DFN  D
 .D ICDTAX^BKMIXX1(DFN,ICDTAX,EDATE,DEXAMDT,GLOBAL)
 .D ADATAX^BKMIXX(DFN,ADATAX,EDATE,DEXAMDT,GLOBAL)
 .D EXAMTAX^BKMIXX1(DFN,"30",EDATE,DEXAMDT,GLOBAL)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"DENT")) S @TOTPTS=@TOTPTS+1 Q
 .D REFUSAL^BKMIXX2(DFN,9999999.15,"30",EDATE,DEXAMDT,REFGLOB)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"DENTREF")) S @TOTPTS=@TOTPTS+1
 Q
PAP ; EP - Pap Smear Status
 N PAPDT,HISTDT,CPTTAX,ICDTAX,PRCTAX,LOINTAX,SITETAX,CPTTAX1,ICDTAX1
 N GLOBAL,GLOBAL1,REFGLOB,TOTPTS,SEX,AGE,DFN
 S HISTDT=""
 S CPTTAX1="BGP HYSTERECTOMY CPTS"
 S ICDTAX1="BGP HYSTERECTOMY PROCEDURES"
 S GLOBAL1="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""HIST"",VSTDT,TEST)"
 S PAPDT=$$FMADD^XLFDT(EDATE,-365)
 S CPTTAX="BGP CPT PAP"
 S ICDTAX="BGP PAP ICDS"
 S PRCTAX="BGP PAP PROCEDURES"
 S LOINTAX="BGP PAP LOINC CODES"
 S SITETAX="BGP PAP SMEAR TAX"
 S GLOBAL="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""PAP"",VSTDT,TEST)"
 S REFGLOB="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""PAPREF"",VSTDT,TEST)"
 S TOTPTS="^TMP(""BKMVQCR"",$J,""HIVCHK"",""PAPPTCNT"")"
 S DFN=0,@TOTPTS=0
 F  S DFN=$O(^TMP("BKMVQCR",$J,"HIVCHK",DFN)) Q:'DFN  D
 .S SEX=$$GET1^DIQ(2,DFN,.02,"I")
 .;Only interested in female patients
 .I SEX'="F" Q
 .S AGE=$$AGE^BKMIMRP1(DFN)
 .;Only interested in patients between 19 and 64
 .I AGE<19!(AGE>64)!(AGE'?1.N) Q
 .;Only interested in patients with no hysterectomy
 .D ICDTAX^BKMIXX1(DFN,ICDTAX1,EDATE,HISTDT,GLOBAL1)
 .D CPTTAX^BKMIXX(DFN,CPTTAX1,EDATE,HISTDT,GLOBAL1)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"HIST")) Q
 .D ICDTAX^BKMIXX1(DFN,ICDTAX,EDATE,PAPDT,GLOBAL)
 .D CPTTAX^BKMIXX(DFN,CPTTAX,EDATE,PAPDT,GLOBAL)
 .D PRCTAX^BKMIXX1(DFN,PRCTAX,EDATE,PAPDT,GLOBAL)
 .D LABTAX^BKMIXX(DFN,SITETAX,EDATE,PAPDT,GLOBAL)
 .D LOINC^BKMIXX(DFN,LOINTAX,EDATE,PAPDT,GLOBAL)
 .D WHTAX^BKMIXX2(DFN,"PAP SMEAR",EDATE,PAPDT,GLOBAL)
 .D REFUSAL^BKMIXX2(DFN,60,LOINTAX,EDATE,PAPDT,REFGLOB)
 .D REFUSAL^BKMIXX2(DFN,60,SITETAX,EDATE,PAPDT,REFGLOB)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"PAP"))!$D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"PAPREF")) S @TOTPTS=@TOTPTS+1
 Q
