BKMVQCR5 ;PRXM/HC/BWF - BKMV Quality of Care Report; [ 1/19/2005  7:16 PM ]
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ; Quality of Care Audit Report
 Q
TOBACCO ; EP - Tobacco Use
 N TOBDT,ICDTAX,ADATAX,HFTAX,HFTAX1,HFTAX2,PTEDTAX
 N CLINIC,GLOBAL,GLOBAL1,GLOBAL2,GLOBAL3,TOTPTS,DFN,TOB
 S TOBDT=$$FMADD^XLFDT(EDATE,-365)
 ;S.4 Tobacco Use Screen
 S ICDTAX="BGP GPRA SMOKING DXS"
 S HFTAX="BGP TOBACCO SCREEN HLTH FACTOR"
 S ADATAX="BGP TOBACCO CESS DENTAL CODE"
 S PTEDTAX="TO-,-TO,-SHS" ; Tobacco
 S CLINIC=94
 ;ED.5 Tobacco Cessation Counseling (includes ADATAX, PTEDTAX, and CLINIC above)
 ;DX.12 Tobacco Users (also includes ICDTAX above)
 S HFTAX1="BGP TOBACCO USER HLTH FACTORS"
 ;DX.7 Non Tobacco Users
 S HFTAX2="BGP NON TOBACCO USER FACTORS"
 S GLOBAL="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""TOB"",VSTDT,TEST)" ; S.4 Tobacco Use Screen
 S GLOBAL1="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""TOBUSER"",VSTDT,TEST)" ; DX.12 Tobacco Users
 S GLOBAL2="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""TOBED"",VSTDT,TEST)" ; ED.5 Tobacco Cessation Counseling
 S GLOBAL3="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""TOBNONUSER"",VSTDT,TEST)" ; DX.7 Non Tobacco Users
 S TOTPTS="^TMP(""BKMVQCR"",$J,""HIVCHK"",""TOBTOT"")"
 S @TOTPTS=0,DFN=0
 F  S DFN=$O(^TMP("BKMVQCR",$J,"HIVCHK",DFN)) Q:'DFN  D
 .;S.4 and DX.12
 .D ICDTAX^BKMIXX1(DFN,ICDTAX,EDATE,TOBDT,GLOBAL)
 .D ICDTAX^BKMIXX1(DFN,ICDTAX,EDATE,TOBDT,GLOBAL1)
 .; DX.12 only. Include problem list.
 .D PRBTAX^BKMIXX(DFN,ICDTAX,EDATE,TOBDT,GLOBAL1)
 .;S.4 and ED.5
 .D ADATAX^BKMIXX(DFN,ADATAX,EDATE,TOBDT,GLOBAL)
 .D ADATAX^BKMIXX(DFN,ADATAX,EDATE,TOBDT,GLOBAL2)
 .;S.4 and ED.5
 .D PTEDTAX^BKMIXX(DFN,PTEDTAX,EDATE,TOBDT,GLOBAL)
 .D PTEDTAX^BKMIXX(DFN,PTEDTAX,EDATE,TOBDT,GLOBAL2)
 .;S.4 and ED.5
 .D CLNTAX^BKMIXX2(DFN,CLINIC,EDATE,TOBDT,GLOBAL)
 .D CLNTAX^BKMIXX2(DFN,CLINIC,EDATE,TOBDT,GLOBAL2)
 .;S.4
 .D HFTAX^BKMIXX(DFN,HFTAX,EDATE,TOBDT,GLOBAL)
 .;DX.12
 .D HFTAX^BKMIXX(DFN,HFTAX1,EDATE,TOBDT,GLOBAL1)
 .;DX.7
 .D HFTAX^BKMIXX(DFN,HFTAX2,EDATE,TOBDT,GLOBAL3)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"TOB")) S @TOTPTS=@TOTPTS+1
 Q
SUBS01 ; EP - Substance Abuse
 N SUBS01DT,ICDTAX,ICDTAX2,ICDTAX3,HFTAX,PTEDTAX,GLOBAL,GLOBAL1,TOTPTS,DFN
 S SUBS01DT=$$FMADD^XLFDT(EDATE,-365)
 ;S.1
 S ICDTAX="BGP ALCOHOL SCREEN ICDS"
 S ICDTAX2="BGP ALCOHOL DXS"
 S HFTAX="BGP ALCOHOL HLTH FACTOR"
 S PTEDTAX="CD-,-CD,AOD-,-AOD"
 ;S.3
 S ICDTAX3="BKM OTHER SUBSTANCE ABUSE DXS"
 S GLOBAL="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""SUBS01"",VSTDT,TEST)"
 S GLOBAL1="^TMP(""BKMVQCR"",$J,""HIVCHK"",DFN,""SUBS01CURR"",VSTDT,TEST)"
 S TOTPTS="^TMP(""BKMVQCR"",$J,""HIVCHK"",""SUBS01TOT"")"
 S DFN=0,@TOTPTS=0
 F  S DFN=$O(^TMP("BKMVQCR",$J,"HIVCHK",DFN)) Q:'DFN  D
 .;S.1
 .D ICDTAX^BKMIXX1(DFN,ICDTAX,EDATE,SUBS01DT,GLOBAL)
 .D ICDTAX^BKMIXX1(DFN,ICDTAX2,EDATE,SUBS01DT,GLOBAL)
 .D ICDTAX^BKMIXX1(DFN,ICDTAX2,EDATE,SUBS01DT,GLOBAL1)
 .D HFTAX^BKMIXX(DFN,HFTAX,EDATE,SUBS01DT,GLOBAL)
 .D PTEDTAX^BKMIXX(DFN,PTEDTAX,EDATE,SUBS01DT,GLOBAL)
 .D EXAMTAX^BKMIXX1(DFN,"35",EDATE,SUBS01DT,GLOBAL)
 .;S.3
 .D ICDTAX^BKMIXX1(DFN,ICDTAX3,EDATE,SUBS01DT,GLOBAL1)
 .D ICDTAX^BKMIXX1(DFN,ICDTAX3,EDATE,SUBS01DT,GLOBAL)
 .I $D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"SUBS01"))!$D(^TMP("BKMVQCR",$J,"HIVCHK",DFN,"SUBS01CURR")) S @TOTPTS=@TOTPTS+1
 Q
