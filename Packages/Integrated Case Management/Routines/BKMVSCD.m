BKMVSCD ;PRXM/HC/ALA - BACKGROUND AUTOPOPULATE ; Dec 7, 2005
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 NEW REGISTER
 S REGISTER=$$HIVIEN^BKMIXX3()
 I REGISTER="" Q
 S BKMUPD(90450,REGISTER_",",22.7)="Q",BKMUPD(90450,REGISTER_",",22.9)=DUZ
 D FILE^DIE("I","BKMUPD") K BKMUPD
 S BKMDEC=+$$GET1^DIQ(90450,REGISTER,22.1,"I")
 D BACK^BKMVFAP1(REGISTER)
 K BKMDEC
 Q
 ;
EDIT ;EP - User edit of option schedule file.
 N DP,X,DC,DI,DL,DR,DIE,DA,DIC,DDSFILE
 N DIEL,DG,DI,DK,DL,DM,DP,DU,D0,D1,DA,DIC,DIE,DR,DQ,X,Y,DV,DOV
 ;S BKMPGHD="HMS REGISTER AUTOPOPULATE SCHEDULE" W @IOF,!!,?IOM-$L(BKMPGHD)\2,BKMPGHD,!!
 S OPTION="BKMV BACKGROUND AUTO POP"
 I OPTION?1A.ANP S OPTION=$$FIND(OPTION) Q:OPTION'>0
 I '$D(^DIC(19.2,"B",OPTION,0)) D
 . S X=$P(^DIC(19,OPTION,0),U,1),DIC(0)="NL",DIC="^DIC(19.2,",DLAYGO=19.2
 . D ^DIC
 . S DA=+Y
 . S DR="6Schedule Frequency//"_"7D"_";2Select a Start Date and Time",DIE="^DIC(19.2," D ^DIE
 W !! H 1
 Q
FIND(X,F) ;EP - Find an option
 N DIC,Y,DLAYGO S DLAYGO=19
 S X=$O(^DIC(19,"B",X,0)) I X'>0 Q -1
 Q X
