BKMVSSR5 ;PRXM/HC/CLT - STATE SURV. REPORT PRINT CONTINUED ; 31 Mar 2005  3:52 PM
 ;;2.1;HIV MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 ;
EN ;EP - PRIMARY ROUTINE ENTRY POINT
 D HDR^BKMVSSR3 Q:BKMX
 D HDR1^BKMVSSR3
 D SECTV Q:BKMX
 D HDR^BKMVSSR3 Q:BKMX
 D HDR1^BKMVSSR3
 D SECTVI Q:BKMX
 Q
SECTV ;SECTION V PRINT
 Q:$P(^BKM(90456,1,2,5,0),U,4)'="Y"
 W !?1,"SECTION V: ",$P(^BKM(90456,1,2,5,0),U,2),!?1
 F BKM=2,1 S BKMLINE=$P(^BKM(90456,1,2,5,1,BKM,0),U,1) D
 .I $Y>(IOSL-6) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 .W:$P(^BKM(90456,1,2,5,1,BKM,0),U,2)="Y" !?1,BKMLINE D
 ..I BKM=1 D SECT5 Q
 ..I BKM=2 D:$G(BKMVETO)]""  Q
 ...W !?5,BKMVETO
 Q
SECT5 ;SECTION 5 WORD PROCESSING FIELDS
 I $D(^BKM(90456,1,2,5,1,BKM,1)) D  Q:BKMX
 .;Following line causes a nearly blank page on the screen display. It is not really needed because a header is printed just prior to this call.
 .;I $Y>(IOSL-$P(^BKM(90456,1,2,5,1,BKM,1,0),U,4)-2) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 .S BKM1=0 F  S BKM1=$O(^BKM(90456,1,2,5,1,BKM,1,BKM1)) Q:'BKM1  D  Q:BKMX
 ..I $Y>(IOSL-4) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 ..S BKMLINE1=$P(^BKM(90456,1,2,5,1,BKM,1,BKM1,0),U,1)
 ..W !?1,BKMLINE1
 Q
SECTVI ;SECTION VI PRINT
 Q:$P(^BKM(90456,1,2,6,0),U,4)'="Y"
 W !?1,"SECTION VI: ",$P(^BKM(90456,1,2,6,0),U,2),!?1,$P(^BKM(90456,1,2,6,0),U,3),!?1
 S BKM0="" F  S BKM0=$O(BKMVLABS(BKM0),-1) Q:BKM0=""  D  Q:BKMX
 .S BKMDT=$$FMTE^XLFDT(BKM0\1,"5Z")
 .S BKM1="" F  S BKM1=$O(BKMVLABS(BKM0,BKM1)) Q:BKM1=""  D  Q:BKMX
 ..S BKM2="" F  S BKM2=$O(BKMVLABS(BKM0,BKM1,BKM2)) Q:BKM2=""  D  Q:BKMX
 ...I BKM2="LAB" S BKMVLN=$$GET1^DIQ(9000010.09,BKM1,.01,"E")
 ...I BKM2="CPT" S BKMIEN=$$GET1^DIQ(9000010.18,BKM1,.01,"I") D
 .... I $T(CPT^ICPTCOD)'="" S BKMVLN=$$ICPT^BKMUL3(BKMIEN,BKM0\1,3) Q  ; csv
 .... S BKMVLN=$$GET1^DIQ(81,BKMIEN,2,"E")
 ...I $Y>(IOSL-4) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 ...W !?5,$E(BKMVLN,1,30),?40,BKMDT,?55,BKMVLABS(BKM0,BKM1,BKM2)
 Q:BKMX
 F BKM=1:1:6 S BKMLINE=$P(^BKM(90456,1,2,6,1,BKM,0),U,1) D  Q:BKMX
 .I $Y>(IOSL-4) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 .W:$P(^BKM(90456,1,2,6,1,BKM,0),U,2)="Y" !?1,BKMLINE
 .D SECT6
 Q
SECT6 ;SECTION 6 WORD PROCESSING FIELDS
 I $D(^BKM(90456,1,2,6,1,BKM,1)) D  Q:BKMX
 .I $Y>(IOSL-$P(^BKM(90456,1,2,6,1,BKM,1,0),U,4)-2) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 .S BKM1=0 F  S BKM1=$O(^BKM(90456,1,2,6,1,BKM,1,BKM1)) Q:'BKM1  D  Q:BKMX
 ..I $Y>(IOSL-4) D HDR^BKMVSSR3 Q:BKMX  D HDR1^BKMVSSR3
 ..S BKMLINE1=$P(^BKM(90456,1,2,6,1,BKM,1,BKM1,0),U,1)
 ..W !?1,BKMLINE1
 Q
