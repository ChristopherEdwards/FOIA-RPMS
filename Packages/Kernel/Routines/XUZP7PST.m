XUZP7PST ;IHS/OIRM/DSD/AEF - PATCH XU*8.0*1007 POST-INSTALL ROUTINE [ 04/02/2003   8:29 AM ]
 ;;8.0;KERNEL;**1007**;APR 1, 2003
 ;
EN ;EP -- MAIN ENTRY POINT
 ;
 D ^XBKVAR
 D ZMU
 D AVAP15
 D DIU9
 D XUYDEV
 D PREFIX
 D COMPILE
 Q
ZMU ;----- KILL OLD %MU GLOBAL
 ;USED BY ROUTINE XUBU^XMD
 ;
 I $$VERSION^%ZOSV(1)["Cache" D
 . W $ZU(68,28,0) ;allow kill of %zmu global on Cache system
 ;
 S ^%zmu=$G(^%MU)
 K ^%MU
 Q
AVAP15 ;----- RUN AVAP15 ROUTINE TO RESET CROSSREFERENCES IN FILES 200 AND 4
 ;
 D EN^AVAP15
 Q
XUYDEV ;----- POST INSTALL FROM PATCH XU*8*135
 ;      RUN ^XUYDEV TO ADD DEVICE TO FILE 8989.518
 ;
 D ^XUYDEV
 Q
PREFIX ;----- MAKE SURE PREFIX FIELD IN PACKAGE FILE IS POPULATED
 ;
 S PKG("RPC BROKER")="XWB"
 S PKG("SAGG PROJECT")="KMPS"
 S PKG("CAPACITY MANAGEMENT - RUM")="KMPR"
 S PKG("NEW PERSON")="A4A7"
 S PKG("RESOURCE USAGE MONITOR")="KMPR"
 ;
 S PKG=""
 F  S PKG=$O(PKG(PKG)) Q:PKG']""  D
 . S IEN=0
 . F  S IEN=$O(^DIC(9.4,"B",PKG,IEN)) Q:'IEN  D
 . . I $D(^DIC(9.4,IEN,0)) D
 . . . S $P(^DIC(9.4,IEN,0),U,2)=PKG(PKG)
 . . . S ^DIC(9.4,"C",PKG(PKG),IEN)=""
 Q
COMPILE ;----- COMPILE FILE 200 PRINT TEMPLATES
 ;
 N DMAX,TEMP,X,Y
 ;
 S TEMP("XUFILEINQ")="XUFILE0"
 S TEMP("XUSERINQ")="XUCT01"
 ;
 S TEMP=""
 F  S TEMP=$O(TEMP(TEMP)) Q:TEMP']""  D
 . S X=TEMP(TEMP)
 . S Y=$O(^DIPT("B",TEMP,0))
 . Q:'Y
 . D BMES^XPDUTL("Compiling print template "_TEMP)
 . S DMAX=$$ROUSIZE^DILF
 . D EN^DIPZ
 Q
DIU9 ;----- DELETE ZISH SEND PARAMETERS FILE #9999999.93
 ;
 ;      FOR ALPHA SITES ONLY!
 ;
 N DIU,X,Y
 Q:$P($G(^DIC(9999999.93,0)),U)'="ZISH SEND PARAMETERS"
 S DIU="^%ZISH(9999999.93,"
 S DIU(0)="DT"
 D EN^DIU2
 Q
