LRLABLIO ;SLC/TGA- TESTS LABEL PRINTER ; 8/22/88  11:53 ;8/19/88  2:09 PM
 ;;V~3.051~
1 Q:$D(LRLABLIO)
 S U="^"
 S IOP=$I,%IS="N" D ^%ZIS S X=$S('POP:+IOS,1:0),X=$S($D(^%ZIS(1,+X,101)):+^(101),1:0),X=$S($D(^%ZIS(1,+X,0)):$P(^(0),U,1),1:"LABLABEL") ;AC/SFISC
 S %IS("A")="PRINT LABELS ON: ",%IS="NQ",%IS("B")=X
2 D ^%ZIS G BD:POP
 I $E(IOST,1)'="P" W !,*7,"NOT printing on a printer. ARE YOU SURE" S %=2 D YN^DICN G:%'=1 2
 S LRLABLIO=ION K %IS I ^%ZOSF("VOL")'=$P(^%ZIS(1,IOS,0),"^",9),$P(^%ZIS(1,IOS,0),"^",9)]"" G K ;AC/SFISC
3 G K:$D(IO("Q")) U IO(0) S %=2 W !,"DO YOU WISH TO TEST THE LABEL PRINTER" D YN^DICN W:%=0 !?5,"Enter 'Y' if you want to test the printer, 'N' if you do not.",! G 3:%=0,K:%'=1
 K %IS S IOP=LRLABLIO D ^%ZIS K IOP I POP W !,"DEVICE IN USE - TRY LATER" K LRLABLIO G 1
 S LABEL="^LRLABEL"_$P(^LAB(69.9,1,3),U,3)
T U IO(0) W !,"ALIGN THE PRINT HEAD ON THE FIRST LINE OF THE LABEL" R !,"PRESS RETURN WHEN READY: ",X:DTIME I X=U G BD
 S:'$D(LRLLOC) LRLLOC="" S L9=LRLLOC,PNM="TEST, LABEL",LRDAT="XX/XX/XX",SSN="000-00-0000",LRLLOC="LAB",LRACC="TEST LABEL",LRCE="000",LRRB=1,LRPREF="SMALL ",LRTOP="TEST TUBE",LRTS(1)="DON'T USE",LRTS(2)="This label"
 S LRINFW="",I=1,N=1,LRXL=0 D @LABEL S LRLLOC=L9 K L9
 U IO(0) H 1 W !," TEST COMPLETED",!," LABELS OK" S %=1 D YN^DICN I %=1 X ^%ZIS("C") G K
T1 W !,"TEST PRINTER AGAIN" S %=2 D YN^DICN G T:%=1,K:%=2!(%=-1) W !,"ANSWER 'YES' OR 'NO'" G T1
BD K LRLABLIO
K K %IS,POP,X,IOP,LABEL,LRTS Q
 ;*** NOTE ***
 ;PATCHES HAVE BEEN INSERTED INTO THIS ROUTINE IN ORDER TO RUN WITH KERNEL V6.
 ;INSERTED BY AC/SFISC 10-30-88.  UNVERIFIED PATCHES.
 ;Line> 1+2
 ;replace> K...X=$S
 ;with> S IOP=$I,%IS="N" D ^%ZIS S X=$S('POP:+IOS,1:0),X=$S
 ;Line> 2+2
 ;replace> G K:$P
 ;with> I ^%ZOSF("VOL")'=$P(^%ZIS(1,IOS,0),"^",9),$P
 ;replace> ]""
 ;with> ]"" G K
