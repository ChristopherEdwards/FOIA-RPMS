AZHLSC50 ; IHS/ADC/GTH:KEU:JN - SAC CHAPTER 2: M LANGUAGE PROGRAMMING STANDARDS & CONVENTIONS) ;  [ 08/26/97  1:56 PM ]
 ;;5.0;AZHLSC;;JUL 10, 1996
 ;
 NEW AZHLB,AZHLFLD,AZHL0,BFN,DIF,EFN,F,G,XCNP,Z
 D END S AZHL0=""
 F  S AZHL0=$O(^UTILITY($J,AZHL0)) Q:AZHL0=""  W:AZHLTERM "." S X=AZHL0,DIF="^TMP($J,""Z"",",XCNP=0 X "X ^%ZOSF(""LOAD""),AZHLGFCX" I  S XCNP=XCNP-1 D
 .F Z=3:1:XCNP S (^TMP($J,"Z"),Z(Z))=^TMP($J,"Z",Z,0) D
 ..I ^TMP($J,"Z")["S ^"!(^TMP($J,"Z")["D ^DIE") D EN^AZHLSC47 Q:AZHLSC4I=1  D EN^AZHLSC46 Q:AZHLSC4I=1  D AZHLSC55 Q:AZHLSC4I=1  S ^TMP($J,"2.4.7.2A",AZHL0,Z)=^TMP($J,"Z")
 ..Q
 .K ^TMP($J,"Z") ;K Z
 .Q
2210 D TTL^AZHLSC("2.4.7.2A    Global Update")
 I $O(^UTILITY($J,""))="" D NRTN^AZHLSC G END
 I $L($O(^TMP($J,"2.4.7.2A",""))) W !?10,"SUSPECT Global UPDATE, CHECK FOR LOCK INCREMENTAL/DECREMENTAL." S %="" F  S %=$O(^TMP($J,"2.4.7.2A",%)) Q:%=""  S A=0 F  S A=$O(^TMP($J,"2.4.7.2A",%,A)) Q:'A  W !?10,"+",A,"^",%," :  ",^(A)
END K ^("2.4.7.2A"),^("Z"),Z Q  
AZHLSC55 ;   SEARCH FOR ^TMP ^% ^UTILITY GLOBALS
 S AZHLSC4I=0
 I ^TMP($J,"Z")["S ^TMP"!(^TMP($J,"Z")["S ^%")!(^TMP($J,"Z")["S ^UTILITY") S AZHLSC4I=1
 Q
