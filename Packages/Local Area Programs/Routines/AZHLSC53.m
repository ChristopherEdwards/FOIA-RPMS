AZHLSC53 ; IHS/ADC/GTH:KEU:JN - SAC CHAPTER 2: M LANGUAGE PROGRAMMING STANDARDS & CONVENTIONS ;  [ 06/05/1998  7:25 AM ]
 ;;5.0;AZHLSC;;JUL 10, 1996
 ;
 NEW AZHLFILE,AZHLFLD,A,B
526 D TTL^AZHLSC("2.3.2.6,  (10.1)  Executable Fields.")
 I 'AZHLPIEN D NPKG^AZHLSC Q
 S AZHLFILE=0
 F  S AZHLFILE=$O(^DIC(9.4,AZHLPIEN,4,"B",AZHLFILE)) Q:'AZHLFILE  D FLD
 Q
FLD S AZHLFLD=0 F  S AZHLFLD=$O(^DD(AZHLFILE,AZHLFLD)) Q:'AZHLFLD  D CHK
 Q
CHK I '$P(^DD(AZHLFILE,AZHLFLD,0),U,2) W:($P(^(0),U,2)["K")&($S('$D(^(9)):1,(^(9)'="@"):1,1:0)) !?10,"File ",AZHLFILE,", Field ",AZHLFLD," is MUMPS field and Write access is not '@'." Q
 I +$P(^DD(AZHLFILE,AZHLFLD,0),U,2) S AZHL=+$P(^(0),U,2) D
 .NEW AZHLFILE,AZHLFLD S AZHLFILE=AZHL D FLD
 .Q
 Q
