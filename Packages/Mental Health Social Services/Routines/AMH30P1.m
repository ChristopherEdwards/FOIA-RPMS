AMH30P1 ; IHS/CMI/LAB - POST INIT BH ;   [ 04/27/04  2:31 PM ]
 ;;3.0;IHS BEHAVIORAL HEALTH;**1**;JAN 27, 2003
 ;re-index all cross references on Designated provider fields
 ;
ENV ;EP 
 I $G(XPDENV)=1 S (XPDDIQ("XPZ1"),XPDDIQ("XPZ2"))=0
 Q
 ;
PRE ;
 S DIK="^DD(9002011.55,",DA=.13,DA(1)=9002011.55 D ^DIK
 S DIK="^DD(9002011.51,",DA=.06,DA(1)=9002011.51 D ^DIK
 S DIK="^DD(9002011,",DA=.17,DA(1)=9002011 D ^DIK
 S DA=$O(^AMHTACT("B",32,0)) I DA S DIE="^AMHTACT(",DR=".02///CLINICAL SUPERVISION PROVIDED" D ^DIE K DIE,DA,DR
 S DA=$O(^AMHPROB("B",19,0))
 I DA S DIE="^AMHPROB(",DR=".02///PERSONALITY DISORDER" D ^DIE
 S DA=$O(^AMHPROBC("B",19,0))
 I DA S DIE="^AMHPROBC(",DR=".02///PERSONALITY DISORDER" D ^DIE
 D ^XBFMK
 S DA=$O(^AMHTSDRG("B","TYLENOL (ACETOMINOPHEN)",0))
 I DA S DIE="^AMHTSDRG(",DR=".01///TYLENOL (ACETAMINOPHEN)" D ^DIE K DA,DR,DIE
 S DA=$O(^AMHTSDRG("B","TRICYLCLIC ANTIDEPRESSANTS (TC",0))
 I DA S DIE="^AMHTSDRG(",DR=".01///TRICYCLIC ANTIDEPRESSANT (TCA)" D ^DIE
 D ^XBFMK
 S X=0 F  S X=$O(^AMHTPLT(X)) Q:X'=+X  S $P(^AMHTPLT(X,0),U,2)=""
 F DA=1:1:200 S DIK="^AMHSORT(" D ^DIK
 Q
POST ;EP
 NEW X S X=$$ADD^XPDMENU("AMH M PROBLEM SPECIFIC","AMH P SUICIDE DELIMITED","SDEL")
 I 'X W "Attempt to add suicide delimited option failed.." H 3
 K ^AMHPATR("AOP")
 ;convert .13 to free text
 S X=0 F  S X=$O(^AMHPATR(X)) Q:X'=+X  D
 .Q:$P(^AMHPATR(X,0),U,13)=""
 .S Y=$P(^AMHPATR(X,0),U,13)
 .Q:'Y
 .S $P(^AMHPATR(X,0),U,13)=$P(^VA(200,Y,0),U)
 .Q
 S X=0 F  S X=$O(^AMHPCASE(X)) Q:X'=+X  I $P(^AMHPCASE(X,0),U,3)="A" S $P(^AMHPCASE(X,0),U,3)="C"
 ;REINDEX AVISIT
 S DIK="^AMHREC(",DIK(1)=".16" D ENALL^DIK
 Q
 ;;
 ;
 ;;
 ;
