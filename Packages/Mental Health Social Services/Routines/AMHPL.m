AMHPL ; IHS/CMI/LAB - PROBLEM LIST UPDATE ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;; ;
GETLOC ;
 S AMHLOC="",DIC="^AUTTLOC(",DIC(0)="AEMQ",DIC("B")=$P(^DIC(4,$S($G(AMHLOC):AMHLOC,1:DUZ(2)),0),U),DIC("A")="Location where Problem List update occurred: " D ^DIC K DIC
 Q:Y<0
 S AMHLOC=+Y
 Q
GETDATE ;
 S AMHDATE=""
 W !!,"Date Problem List Updated: " R X:$S($D(DTIME):DTIME,1:300) S:'$T X=""
 Q:X=""!(X="^")
 S %DT="ET" D ^%DT G:Y<0 GETDATE
 I Y>DT W "  <Future dates not allowed>",$C(7),$C(7) K X G GETDATE
 S AMHDATE=Y
 Q
EOJ ;End of job cleanup
 D:$D(VALMWD) CLEAR^VALM1 ;clears out all list man stuff
 K ^TMP($J,"AMHPL")
 K XQORNEST,VALMKEY,VALM,VALMAR,VALMBCK,VALMBG,VALMCAP,VALMCNT,VALMOFF,VALMMCON,VALMDN,VALMEVL,VALMIOXY,VALMKEY,VALMLFT,VALMLST,VALMMENU,VALMSGR,VALMUP,VALMWD,VALMY,XQORS,XQORSPEW
 K AMHPLPT,AMHLOC,AMHPAT,AMHDATE,AMHPIEN,AMHAF,AMHPRB,APCDOVRR,AMHLOOK,AMHPDFN
 Q
EN1 ;EP - requires DFN to be set to patient
 K ^TMP($J,"AMHPL")
 Q:'$G(DFN)
 S AMHPLPT=DFN
 Q:'$G(AMHPLPT)
 Q:'$D(^AUPNPAT(AMHPLPT))
 Q:'$D(^DPT(AMHPLPT))
 S Y=AMHPLPT D ^AUPNPAT
 D GETLOC
 I '$G(AMHLOC) D EXIT Q
 D GETDATE
 I '$G(AMHDATE) D EXIT Q
 S APCDOVRR=1
 D EN
 K AMHPLPT
 D FULL^VALM1
 D EXIT
 Q
EN ;EP  main entry point for AMH PL PROBLEM LIST
 S VALMCC=1 ;1 means screen mode, 0 means scrolling mode
 D EN^VALM("AMH PCC PROBLEM LIST")
 D CLEAR^VALM1
 Q
 ;
HDR ;EP -- header code
 S VALMHDR(1)=$TR($J(" ",80)," ","-")
 S VALMHDR(2)="Patient Name: "_IORVON_$P(^DPT(AMHPLPT,0),U)_IOINORM_"   DOB: "_$$FTIME^VALM1(AUPNDOB)_"   Sex: "_$P(^DPT(AMHPLPT,0),U,2)_"   HRN: "_$S($D(^AUPNPAT(AMHPLPT,41,DUZ(2),0)):$P(^AUPNPAT(AMHPLPT,41,DUZ(2),0),U,2),1:"????")
 S VALMHDR(3)=$TR($J(" ",80)," ","-")
 Q
 ;
INIT ; -- init variables and list array
 D GATHER ;gather up all problems
 S VALMCNT=AMHLINE ;this variable must be the total number of lines in list
 S APCDOVRR="" ;for provider narrative lookup
 Q
 ;
GATHER ;EP
 ;set up array containing list of problems
 ;**** see page 7 of List Manager Manual for info on how to
 ;**** set up the array that contains the list
 K ^TMP($J,"AMHPL")
 K AMHQUIT,AMHPL S AMHRCNT=0,AMHLINE=0
 I '$D(^AUPNPROB("AC",AMHPLPT)) S ^TMP($J,"AMHPL",1,0)="No Problems currently on file",^TMP($J,"AMHPL","IDX",1,1)="" S AMHRCNT=1 Q
 S AMHAF="A" D GATHER1 S AMHAF="I" D GATHER1
 Q
GATHER1 ;
 S AMHF=0 F  S AMHF=$O(^AUPNPROB("AA",AMHPLPT,AMHF)) Q:AMHF'=+AMHF  D
 .S AMHPRB="" F  S AMHPRB=$O(^AUPNPROB("AA",AMHPLPT,AMHF,AMHPRB)) Q:AMHPRB=""  S AMHPIEN=$O(^(AMHPRB,"")),AMHP0=^AUPNPROB(AMHPIEN,0) I $P(^AUPNPROB(AMHPIEN,0),U,12)=AMHAF D
 ..S AMHRCNT=AMHRCNT+1,AMHLINE=AMHLINE+1,^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN,AMHX=""
 ..S AMHX=$$SETSTR^VALM1($J(AMHRCNT,2),AMHX,3,2),AMHX=$$SETSTR^VALM1(") Problem ID:",AMHX,5,14),X=$S($P(^AUTTLOC(AMHF,0),U,7)]"":$J($P(^(0),U,7),4),1:"??")_$P(AMHP0,U,7),AMHX=$$SETSTR^VALM1(X,AMHX,19,8)
 ..S AMHX=$$SETSTR^VALM1("DX:",AMHX,28,3),AMHX=$$SETSTR^VALM1($P($$ICDDX^ICDCODE($P(AMHP0,U)),U,2),AMHX,32,6),X="Status: "_IOUON_$$EXTSET^XBFUNC(9000011,.12,$P(AMHP0,U,12))_IOUOFF,AMHX=$$SETSTR^VALM1(X,AMHX,40,25)
 ..S AMHX=$$SETSTR^VALM1("Onset:",AMHX,64,6) I $P(AMHP0,U,13)]"" S AMHX=$$SETSTR^VALM1($$FMTE^XLFDT($P(AMHP0,U,13),"5D"),AMHX,71,17)
 ..S ^TMP($J,"AMHPL",AMHLINE,0)=AMHX,AMHX="",^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN
 ..S AMHLINE=AMHLINE+1,AMHX=$P($G(^AUTNPOV(+$P(AMHP0,U,5),0)),U),^TMP($J,"AMHPL",AMHLINE,0)="      Provider Narrative:  "_IOINHI_AMHX_IOINORM,^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN
 ..;S AMHLINE=AMHLINE+1,^TMP($J,"AMHPL",AMHLINE,0)=IOINORM_"  ",^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN
NOTE ..S AMHC=0 I $O(^AUPNPROB(AMHPIEN,11,0)) D
 ...S (AMHC,AMHL)=0 F  S AMHL=$O(^AUPNPROB(AMHPIEN,11,AMHL)) Q:AMHL'=+AMHL  I $O(^AUPNPROB(AMHPIEN,11,AMHL,11,0)) S AMHLR=$P(^AUTTLOC($P(^AUPNPROB(AMHPIEN,11,AMHL,0),U),0),U,7) D
 ....S AMHX=0 F  S AMHX=$O(^AUPNPROB(AMHPIEN,11,AMHL,11,AMHX)) Q:AMHX'=+AMHX  D
 .....S AMHC=AMHC+1 I AMHC=1 S X=IOINORM_"        "_IORVON_"Notes:"_IORVOFF S AMHLINE=AMHLINE+1,^TMP($J,"AMHPL",AMHLINE,0)=X,^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN
 .....S X="           "_AMHLR_" Note#"_$P(^AUPNPROB(AMHPIEN,11,AMHL,11,AMHX,0),U)_" "_$S($P(^(0),U,5)]"":$$FMTE^XLFDT($P(^(0),U,5),5),1:"        ")_"  "_$P(^AUPNPROB(AMHPIEN,11,AMHL,11,AMHX,0),U,3)
 .....S AMHLINE=AMHLINE+1,^TMP($J,"AMHPL",AMHLINE,0)=X,^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN
 ..S AMHLINE=AMHLINE+1,^TMP($J,"AMHPL",AMHLINE,0)=IOINORM_"  ",^TMP($J,"AMHPL","IDX",AMHLINE,AMHRCNT)=AMHPIEN
 ..Q
 .Q
 K AMHLR,AMHL,AMHX,AMHF
 Q
TEXT ;
 ;;Patient Care Component (PCC)
 ;;
 ;;***********************************
 ;;* Update PCC Patient Problem List *
 ;;***********************************
 ;;
 Q
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 K ^TMP($J,"AMHPL")
 K AMHRCNT,AMHPL,AMHLINE,AMHX,AMHP0,AMHC,AMHL,AMHLR,AMHPIEN,AMHAF,AMHPRB,APCDOVRR,AMHLOOK,AMHPDFN,AMHLOC,AMHDATE
 K X,Y
 K VALMHDR
 Q
 ;
EXPND ; -- expand code
 Q
 ;
