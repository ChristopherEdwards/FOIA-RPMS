AMHRBV2 ; IHS/CMI/LAB - gather billable visits 03 Jun 2009 1:01 PM ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
 ;CMI/TUCSON/LAB - 09/22/97 - modified activity data to not bomb if null
 ;SEARCH VISIT FILE FOR DATE RANGE AND GENERATE CLINIC COUNTS
 ;
 S AMHJOB=$J,AMHBT=$H
 K ^XTMP("AMHRBV",AMHJOB,AMHBT)
 D XTMP^AMHUTIL("AMHRBV","BH - BILLABLE VISITS")
 S AMHS=AMHSD-.000001
 D @AMHPROC
 S AMHET=$H
 Q
1 F X="03","04","30","31" S Y=$O(^AUTTBEN("C",X,"")) S AMHCOAR(Y)="" S AMHCOPN(Y)=$P(^AUTTBEN(Y,0),U)
 D V
 ;
 Q
V ;
 F I=0:0 S AMHS=$O(^AMHREC("B",AMHS)) Q:AMHS=""!($P(AMHS,".")>AMHED)  D V1
 Q
V1 ;
 S AMHVDFN="" F J=0:0 S AMHVDFN=$O(^AMHREC("B",AMHS,AMHVDFN)) Q:AMHVDFN=""  I $$ALLOWVI^AMHUTIL(DUZ,AMHVDFN) S AMHVN0=^AMHREC(AMHVDFN,0) S DFN=$P(AMHVN0,U,8) I DFN]"" D @(AMHPROC_"2")
 Q
12 ;
 Q:'$D(^AUPNPAT(DFN,41,AMHSU,0))
 Q:'$D(^AUPNPAT(DFN,11))
 S AMHCOP=$P(^AUPNPAT(DFN,11),U,11) Q:AMHCOP=""
 Q:'$D(AMHCOAR(AMHCOP))
VC ;
 S AMHACT=$P(AMHVN0,U,6) Q:'AMHACT  Q:'$P(^AMHTACT(AMHACT,0),U,6)  ;do not use non patient activities CMI/TUCSON/LAB - added Q:'AMHACT to not bomb if activity null
 S AMHVISIT=$P(AMHVN0,U,16)
 Q:'$D(^AMHRPROV("AD",AMHVDFN))
 Q:'$D(^AMHRPRO("AD",AMHVDFN))
 Q:$P(AMHVN0,U,4)'=AMHSU
 S AMHPN=$P(^DPT(DFN,0),U)
 S ^XTMP("AMHRBV",AMHJOB,AMHBT,AMHPN,DFN,AMHVDFN)=""
 Q
2 ;
 S AMHVAL=$S(AMHPROC=2:"A",1:"B")
 S AMHPROC=2
 D V
 Q
22 ;
 Q:'$D(^DPT(DFN,0))
 Q:'$D(^AUPNMCR(DFN,11))
 Q:'$D(^AUPNPAT(DFN,41,AMHSU,0))
 I $D(^DPT(DFN,.35)),$P(^(.35),U)]"",$P(^(.35),U)<$P(AMHS,".") Q
 K AMHGOT S AMHMDFN=0 F  S AMHMDFN=$O(^AUPNMCR(DFN,11,AMHMDFN)) Q:AMHMDFN'=+AMHMDFN!($D(AMHGOT))  D 23
 Q:'$D(AMHGOT)
 S AMHPN=$P(^DPT(DFN,0),U)
 D VC
 Q
 ;
23 ;
 Q:AMHVAL'[$P(^AUPNMCR(DFN,11,AMHMDFN,0),U,3)
 Q:$P(^AUPNMCR(DFN,11,AMHMDFN,0),U)>$P(AMHS,".")
 I $P(^AUPNMCR(DFN,11,AMHMDFN,0),U,2)]"",$P(^(0),U,2)<$P(AMHS,".") Q
 S AMHGOT=""
 Q
 ;
3 ;
 D 2
 Q
 ;
5 ;
 D V
 Q
52 ;
 Q:'$D(^AUPNPRVT(DFN,11))
 Q:'$D(^AUPNPAT(DFN,41,AMHSU))
 I $D(^DPT(DFN,.35)),$P(^(.35),U)]"",$P(^(.35),U)<$P(AMHS,".") Q
 S AMHPN=$P(^DPT(DFN,0),U)
 K AMHGOT S AMHMDFN=0 F  S AMHMDFN=$O(^AUPNPRVT(DFN,11,AMHMDFN)) Q:AMHMDFN'=+AMHMDFN  D 53
 Q:'$D(AMHGOT)
 D VC
 Q
53 ;
 Q:$P(^AUPNPRVT(DFN,11,AMHMDFN,0),U)=""
 S AMHNAME=$P(^AUPNPRVT(DFN,11,AMHMDFN,0),U) Q:AMHNAME=""
 S AMHNAME=$P(^AUTNINS(AMHNAME,0),U) I AMHNAME["AHCCCS" Q
 Q:$P(^AUPNPRVT(DFN,11,AMHMDFN,0),U,6)=""
 Q:$P(^AUPNPRVT(DFN,11,AMHMDFN,0),U,6)>$P(AMHS,".")
 I $P(^AUPNPRVT(DFN,11,AMHMDFN,0),U,7)]"",$P(^(0),U,7)<$P(AMHS,".") Q
 S AMHGOT=""
 Q
 ;
4 ;
 D V
 Q
42 ;
 Q:'$D(^AUPNPAT(DFN,41,AMHSU))
 I $D(^DPT(DFN,.35)),$P(^(.35),U)]"",$P(^(.35),U)<$P(AMHS,".") Q
 S AMHPN=$P(^DPT(DFN,0),U)
 K AMHGOT S AMHMDFN=0 S AMHMDFN=$O(^AUPNMCD("B",DFN,AMHMDFN)) Q:AMHMDFN'=+AMHMDFN!($D(AMHGOT))  D 43
 Q:'$D(AMHGOT)
 D VC
 Q
43 ;
 Q:'$D(^AUPNMCD(AMHMDFN,11))
 K AMHGOT S AMHNDFN=0 F  S AMHNDFN=$O(^AUPNMCD(AMHMDFN,11,AMHNDFN)) Q:AMHNDFN'=+AMHNDFN!($D(AMHGOT))  S AMHREC=^AUPNMCD(AMHMDFN,11,AMHNDFN,0) D 44
 Q
44 ;
 Q:AMHNDFN>$P(AMHS,".")
 I $P(AMHREC,U,2)]"",$P(AMHREC,U,2)<$P(AMHS,".") Q
 S AMHGOT=""
 Q
 ;
6 ;NON INDIANS
 D V
 Q
62 ;
 Q:'$D(^AUPNPAT(DFN,41,AMHSU))
 I $D(^DPT(DFN,.35)),$P(^(.35),U)]"",$P(^(.35),U)<$P(AMHS,".") Q
 Q:'$D(^AUPNPAT(DFN,11))
 Q:$P(^AUPNPAT(DFN,11),U,8)=""
 S AMHTRI=$P(^AUPNPAT(DFN,11),U,8)
 Q:'$D(^AUTTTRI(AMHTRI))
 S AMHTRIC=$P(^AUTTTRI(AMHTRI,0),U,2)
 Q:(+AMHTRIC&(AMHTRIC<969))
 D VC
 Q
