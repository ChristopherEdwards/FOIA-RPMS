AMHRLU ; IHS/CMI/LAB - TUCSON-OHPRD/LAB - GEN RETR UTILITIES 03 Jun 2009 12:08 PM ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
CASE ;EP - called from BH REPORT SORT
 K X
 Q:'$G(P)
 S Y=0 F  S Y=$O(^AMHPCASE("C",DFN,Y)) Q:Y'=+Y  S D=$P(^AMHPCASE(Y,0),U,P) D
 .Q:'$$ALLOWCD^AMHLCD(DUZ,Y)
 .Q:D=""
 .Q:$P(^AMHTRPT(AMHRPT,11,AMHI,11,1,0),U)]D
 .Q:D]$P(^AMHTRPT(AMHRPT,11,AMHI,11,1,0),U,2)
 .S X(D)=""
 .Q
 K P,D,Y
 Q
MCR(P,D) ;EP is patient medicare eligible on this date
 NEW AMHMIFN,AMHFLG
 S AMHFLG=0
 I '$D(^DPT(P,0)) G MCRX
 I $P(^DPT(P,0),U,19) G MCRX
 I '$D(^AUPNPAT(P,0)) G MCRX
 I '$D(^AUPNMCR(P,11)) G MCRX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCRX
 S AMHMIFN=0 F  S AMHMIFN=$O(^AUPNMCR(P,11,AMHMIFN)) Q:AMHMIFN'=+AMHMIFN  D
 .Q:$P(^AUPNMCR(P,11,AMHMIFN,0),U)>D
 .I $P(^AUPNMCR(P,11,AMHMIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 .S AMHFLG=1
 .Q
MCRX ;
 Q AMHFLG
 ;
MCD(P,D) ;EP
 NEW AMHMIFN,AMHNIFN,AMHFLG
 S AMHFLG=0
 I '$D(^DPT(P,0)) G MCRX
 I $P(^DPT(P,0),U,19) G MCRX
 I '$D(^AUPNPAT(P,0)) G MCDX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCRX
 S AMHMIFN=0 F  S AMHMIFN=$O(^AUPNMCD("B",P,AMHMIFN)) Q:AMHMIFN'=+AMHMIFN  D
 .Q:'$D(^AUPNMCD(AMHMIFN,11))
 .S AMHNIFN=0 F  S AMHNIFN=$O(^AUPNMCD(AMHMIFN,11,AMHNIFN)) Q:AMHNIFN'=+AMHNIFN  D
 ..Q:AMHNIFN>D
 ..I $P(^AUPNMCD(AMHMIFN,11,AMHNIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 ..S AMHFLG=1
 ..Q
 .Q
 ;
MCDX ;
 Q AMHFLG
 ;
PI(P,D) ;EP
 NEW AMHMIFN,AMHFLG
 S AMHFLG=0
 I '$D(^DPT(P,0)) G PIX
 I $P(^DPT(P,0),U,19) G PIX
 I '$D(^AUPNPAT(P,0)) G PIX
 I '$D(^AUPNPRVT(P,11)) G PIX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G PIX
 S AMHMIFN=0 F  S AMHMIFN=$O(^AUPNPRVT(P,11,AMHMIFN)) Q:AMHMIFN'=+AMHMIFN  D
 .Q:$P(^AUPNPRVT(P,11,AMHMIFN,0),U)=""
 .S AMHNAME=$P(^AUPNPRVT(P,11,AMHMIFN,0),U) Q:AMHNAME=""
 .Q:$P(^AUTNINS(AMHNAME,0),U)["AHCCCS"
 .Q:$P(^AUPNPRVT(P,11,AMHMIFN,0),U,6)>D
 .I $P(^AUPNPRVT(P,11,AMHMIFN,0),U,7)]"",$P(^(0),U,7)<D Q
 .S AMHFLG=1
 .Q
PIX ;
 Q AMHFLG
