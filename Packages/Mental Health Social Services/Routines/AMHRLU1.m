AMHRLU1 ; IHS/CMI/LAB - GEN RET UTIL ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
 ;
MCR ;display all current medicare data
 NEW AMHMIFN
 I '$D(^DPT(P,0)) G MCRX
 I $P(^DPT(P,0),U,19) G MCRX
 I '$D(^AUPNPAT(P,0)) G MCRX
 I '$D(^AUPNMCR(P,11)) G MCRX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCRX
 S AMHMIFN=0 F  S AMHMIFN=$O(^AUPNMCR(P,11,AMHMIFN)) Q:AMHMIFN'=+AMHMIFN  D
 .Q:$P(^AUPNMCR(P,11,AMHMIFN,0),U)>D
 .I $P(^AUPNMCR(P,11,AMHMIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 .S AMHPCNT=AMHPCNT+1,AMHPRNM(AMHPCNT)=$P(^AUPNMCR(DFN,0),U,3)_" ["_$S($P(^(0),U,4)]"":$P(^AUTTMCS($P(^(0),U,4),0),U),1:"-")_"]"
 .S AMHPCNT=AMHPCNT+1,Y=$P(^AUPNMCR(DFN,11,AMHMIFN,0),U),Z=$P(^(0),U,2),AMHPRNM(AMHPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3)_"-" I Z]"" S AMHPRNM(AMHPCNT)=AMHPRNM(AMHPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_$E(Y,2,3)
 .Q
MCRX ;
 K Y,Z
 Q
 ;
MCD ;
 NEW AMHMIFN,AMHNIFN
 I '$D(^DPT(P,0)) G MCDX
 I $P(^DPT(P,0),U,19) G MCDX
 I '$D(^AUPNPAT(P,0)) G MCDX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCDX
 S AMHMIFN=0 F  S AMHMIFN=$O(^AUPNMCD("B",P,AMHMIFN)) Q:AMHMIFN'=+AMHMIFN  D
 .Q:'$D(^AUPNMCD(AMHMIFN,11))
 .S AMHNIFN=0 F  S AMHNIFN=$O(^AUPNMCD(AMHMIFN,11,AMHNIFN)) Q:AMHNIFN'=+AMHNIFN  D
 ..Q:AMHNIFN>D
 ..I $P(^AUPNMCD(AMHMIFN,11,AMHNIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 ..S AMHPCNT=AMHPCNT+1,AMHPRNM(AMHPCNT)=$P(^AUPNMCD(AMHMIFN,0),U,3)_"/"_$S($P(^AUPNMCD(AMHMIFN,0),U,2)]"":$P(^AUTNINS($P(^AUPNMCD(AMHMIFN,0),U,2),0),U),1:"<>")
 ..S AMHPCNT=AMHPCNT+1,Y=$P(^AUPNMCD(AMHMIFN,11,AMHNIFN,0),U),Z=$P(^(0),U,2),AMHPRNM(AMHPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3)_"-" I Z]"" S AMHPRNM(AMHPCNT)=AMHPRNM(AMHPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_$E(Y,2,3)
 ..Q
 .Q
 ;
MCDX ;
 Q
 ;
PI ;
 NEW AMHMIFN,AMHFLG
 I '$D(^DPT(P,0)) G PIX
 I $P(^DPT(P,0),U,19) G PIX
 I '$D(^AUPNPAT(P,0)) G PIX
 I '$D(^AUPNPRVT(P,11)) G PIX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G PIX
 S AMHMIFN=0 F  S AMHMIFN=$O(^AUPNPRVT(P,11,AMHMIFN)) Q:AMHMIFN'=+AMHMIFN  D
 .Q:$P(^AUPNPRVT(P,11,AMHMIFN,0),U)=""
 .S AMHNAME=$P(^AUPNPRVT(DFN,11,AMHMIFN,0),U) Q:AMHNAME=""
 .Q:$P(^AUTNINS(AMHNAME,0),U)["AHCCCS"
 .Q:$P(^AUPNPRVT(P,11,AMHMIFN,0),U,6)>D
 .I $P(^AUPNPRVT(P,11,AMHMIFN,0),U,7)]"",$P(^(0),U,7)<D Q
 .S AMHPCNT=AMHPCNT+1,AMHPRNM(AMHPCNT)=$P(^AUTNINS($P(^AUPNPRVT(P,11,AMHMIFN,0),U),0),U)
 .S AMHPCNT=AMHPCNT+1,Y=$P(^AUPNPRVT(DFN,11,AMHMIFN,0),U,6),Z=$P(^(0),U,7),AMHPRNM(AMHPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_$E(Y,2,3)_"-" I Z]"" S AMHPRNM(AMHPCNT)=AMHPRNM(AMHPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_$E(Z,2,3)
 .Q
PIX ;
 Q
