AMHRP1 ; IHS/CMI/LAB - DETAILED/BRIEF LISTING OF RECORDS, REPORT 1 ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
 ;
BDRL ;type of report
 W !!?5,"Report Print Selection."
 S DIR(0)="S^D:Detailed (132 column print);B:Brief (80 column print)",DIR("A")="Type of Report to Print",DIR("B")="D" D ^DIR K DIR S:$D(DUOUT) DIRUT=1
 I $D(DIRUT) S AMHQUIT=1 Q
 S AMHRTYPE=Y
 Q
PRINT ;EP
 S AMHCW=$S(AMHRTYPE="B":80,1:132)
 ;
 D COVPAGE^AMHRPTCP
 I '$D(^XTMP("AMHRPT",AMHJOB,AMHBTH,"RECORDS")) G DONE
 S (AMHRSRT,AMHFRST)="",(AMHPG,AMHRCNT)=0 K AMHQUIT
 F  S AMHRSRT=$O(^XTMP("AMHRPT",AMHJOB,AMHBTH,"RECORDS",AMHRSRT)) Q:AMHRSRT=""!($D(AMHQUIT))  D PRINT1
 G:$D(AMHQUIT) DONE
 I $Y>(IOSL-6) D HEADER G:$D(AMHQUIT) DONE
DONE ;
 D DONE^AMHLEIN,^AMHEKL
 K ^XTMP("AMHRPT",AMHJOB,AMHBT)
 K AMHBT,AMHBTH,AMHJOB,AMHET
 Q
PRINT1 ;
 ;get readable sort variable
 S AMHSRTR="<NONE AVAILABLE>",AMHR=$O(^XTMP("AMHRPT",AMHJOB,AMHBTH,"RECORDS",AMHRSRT,"")) I AMHR]"" S AMHCRIT=AMHSORT D
 .S AMHR0=^AMHREC(AMHR,0),DFN=$P(AMHR0,U,8) X:$D(^AMHSORT(AMHSORT,3)) ^AMHSORT(AMHSORT,3)
 .Q
 S (AMHSCNT,AMHR)=0 I $G(AMHSPAG)!($D(AMHFRST)) D HEADER Q:$D(AMHQUIT)
 K AMHFRST
 F  S AMHR=$O(^XTMP("AMHRPT",AMHJOB,AMHBTH,"RECORDS",AMHRSRT,AMHR)) Q:AMHR=""!($D(AMHQUIT))  S AMHR0=^AMHREC(AMHR,0) D @("PRINT"_AMHRTYPE)
 I $Y>(IOSL-3) D HEADER Q:$D(AMHQUIT)
 W:$G(AMHSPAG) !!!,"SUB-TOTAL for ",AMHSORV," ",AMHRSRT,":  ",AMHSCNT
 Q
PRINTB ;
 S:$G(AMHSPAG) AMHSCNT=AMHSCNT+1
 I $Y>(IOSL-6) D HEADER Q:$D(AMHQUIT)
 S AMHRCNT=AMHRCNT+1
 W !,$E($P(AMHR0,U),4,5),"/",$E($P(AMHR0,U),6,7),"/",$E($P(AMHR0,U),2,3),?10,$$PPINI^AMHUTIL(AMHR)
 W ?15,$S($P(^AUTTLOC($P(AMHR0,U,4),0),U,7)]"":$P(^(0),U,7),1:$E($P(^AUTTLOC($P(AMHR0,U,4),0),U),1,4)) W:$P(AMHR0,U,8) ?20,$E($P(^DPT($P(AMHR0,U,8),0),U),1,12)
 W ?34,$S($P(AMHR0,U,6)]"":$P(^AMHTACT($P(AMHR0,U,6),0),U),1:""),?37,$S($P(AMHR0,U,7)]"":$E($P(^AMHTSET($P(AMHR0,U,7),0),U),1,4),1:""),?42,$P(AMHR0,U,12)
 I $P(AMHR0,U,8)]""  D
 .I $D(^AUPNPAT($P(AMHR0,U,8),41,$P(AMHR0,U,4))) W ?46,$P(^AUTTLOC($P(AMHR0,U,4),0),U,7),$P(^AUPNPAT($P(AMHR0,U,8),41,$P(AMHR0,U,4),0),U,2) Q
 .I $D(^AUPNPAT($P(AMHR0,U,8),41,DUZ(2))) W ?46,$P(^AUTTLOC(DUZ(2),0),U,7),$P(^AUPNPAT($P(AMHR0,U,8),41,DUZ(2),0),U,2) Q
 .W ?47,"<*****>"
 E  W ?46,"-----"
 S AMHP=$O(^AMHRPRO("AD",AMHR,0)) I AMHP="" W ?57,"No Problems recorded" Q
 W ?57,$P(^AMHPROB($P(^AMHRPRO(AMHP,0),U),0),U) W:$P(^AMHRPRO(AMHP,0),U,4) ?64,$E($P(^AUTNPOV($P(^AMHRPRO(AMHP,0),U,4),0),U),1,15) D
 .F  S AMHP=$O(^AMHRPRO("AD",AMHR,AMHP)) Q:AMHP'=+AMHP   W !?57,$P(^AMHPROB($P(^AMHRPRO(AMHP,0),U),0),U) W:$P(^AMHRPRO(AMHP,0),U,4) ?64,$E($P(^AUTNPOV($P(^AMHRPRO(AMHP,0),U,4),0),U),1,15)
 .Q
 Q
 ;
PRINTD ;detailed print
 S:$G(AMHSPAG) AMHSCNT=AMHSCNT+1
 I $Y>(IOSL-6) D HEADER Q:$D(AMHQUIT)
 S AMHRCNT=AMHRCNT+1
 W !,$E($P(AMHR0,U),4,5),"/",$E($P(AMHR0,U),6,7),"/",$E($P(AMHR0,U),2,3) I $P($P(AMHR0,U),".")]"" W ?9,"@" S Y=$P(AMHR0,U) D DD^%DT W $P(Y,"@",2)
 W ?16,$E($$PPNAME^AMHUTIL(AMHR),1,17),?34,$$PPCLSC^AMHUTIL(AMHR)
 W ?39,$P(AMHR0,U,2),?42,$S($P(^AUTTLOC($P(AMHR0,U,4),0),U,7)]"":$P(^(0),U,7),1:$E($P(^AUTTLOC($P(AMHR0,U,4),0),U),1,4)) W:$P(AMHR0,U,8) ?47,$E($P(^DPT($P(AMHR0,U,8),0),U),1,12)
 W ?60,$S($P(AMHR0,U,6)]"":$P(^AMHTACT($P(AMHR0,U,6),0),U),1:""),?64,$P(AMHR0,U,12),?68,$S($P(AMHR0,U,7)]"":$E($P(^AMHTSET($P(AMHR0,U,7),0),U),1,4),1:"")
 I $P(AMHR0,U,8)]""  D
 .I $D(^AUPNPAT($P(AMHR0,U,8),41,$P(AMHR0,U,4))) W ?74,$P(^AUTTLOC($P(AMHR0,U,4),0),U,7),$P(^AUPNPAT($P(AMHR0,U,8),41,$P(AMHR0,U,4),0),U,2) G SEX
 .I $D(^AUPNPAT($P(AMHR0,U,8),41,DUZ(2))) W ?74,$P(^AUTTLOC(DUZ(2),0),U,7),$P(^AUPNPAT($P(AMHR0,U,8),41,DUZ(2),0),U,2) G SEX
  .W ?74,"<******>"
SEX .W ?86,$P(^DPT($P(AMHR0,U,8),0),U,2)
 .K ^UTILITY("DIQ1",$J) S DA=$P(AMHR0,U,8),DR=1102.99,DIC="^AUPNPAT(",DIQ(0)="E" D EN^DIQ1
 .I $D(^UTILITY("DIQ1",$J)) W ?90,^UTILITY("DIQ1",$J,9000001,$P(AMHR0,U,8),1102.99,"E") K ^UTILITY("DIQ1",$J)
 .Q
 E  W ?74,"-----"
 S AMHP=$O(^AMHRPRO("AD",AMHR,0)) I AMHP="" W ?100,"No Problems recorded" Q
 W ?93,$P(^AMHPROB($P(^AMHRPRO(AMHP,0),U),0),U) W:$P(^AMHRPRO(AMHP,0),U,4) ?100,$E($P(^AUTNPOV($P(^AMHRPRO(AMHP,0),U,4),0),U),1,30) D
 .F  S AMHP=$O(^AMHRPRO("AD",AMHR,AMHP)) Q:AMHP'=+AMHP   W !?93,$P(^AMHPROB($P(^AMHRPRO(AMHP,0),U),0),U) W:$P(^AMHRPRO(AMHP,0),U,4) ?100,$E($P(^AUTNPOV($P(^AMHRPRO(AMHP,0),U,4),0),U),1,30)
 .Q
 Q
HEADER ;
 D HEADER^AMHRP11
 Q
