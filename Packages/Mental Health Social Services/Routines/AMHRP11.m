AMHRP11 ; IHS/CMI/LAB - DETAILED/BRIEF LISTING OF RECORDS, REPORT 1 ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
 ;
HEADER ;EP
 I 'AMHPG G HEADER1
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S AMHQUIT="" Q
HEADER1 ;
 W:$D(IOF) @IOF S AMHPG=AMHPG+1
 W !?13,"********** CONFIDENTIAL PATIENT INFORMATION **********"
 W !?(AMHCW-$L($P(^DIC(4,DUZ(2),0),U))/2),$P(^DIC(4,DUZ(2),0),U),?($S(AMHCW=132:120,1:72)),"Page ",AMHPG,!
 S AMHTEXT="BEHAVIORAL HEALTH RECORD LISTING"
 W !?(AMHCW-$L(AMHTEXT)/2),AMHTEXT,!
 S AMHTEXT="Visit Dates:  "_AMHBDD_" and "_AMHEDD
 W ?(AMHCW-$L(AMHTEXT)/2),AMHTEXT,!
 I $G(AMHSPAG) S AMHLENG=$L(AMHSRTR)+$L(AMHSORV)+2 S:AMHCW<AMHLENG AMHLENG=AMHCW W !?((AMHCW-AMHLENG)/2),AMHSORV,":  ",AMHRSRT,!
 W $TR($J(" ",AMHCW)," ","=")
 I AMHRTYPE="B" W !," DATE",?10,"PROV",?15,"LOC",?20,"PATIENT NAME",?33,"ACT",?37,"CONT",?42,"AT",?46,"HRN",?57,"PROB",?64,"NARRATIVE"
 E  W !,"DATE/TIME",?16,"PROVIDER",?34,"PD",?38,"PRG",?42,"LOC",?47,"PATIENT",?59,"ACT",?64,"AT",?68,"CONT",?75,"HRN",?84,"SEX",?89,"AGE",?93,"PROB",?100,"   NARRATIVE"
 W !,$TR($J(" ",AMHCW)," ","-")
 Q
