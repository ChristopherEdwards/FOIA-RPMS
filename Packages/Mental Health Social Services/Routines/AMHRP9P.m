AMHRP9P ; IHS/CMI/LAB - print active client list ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
 ;
PRINT ;
START ;
 S AMH80D="-------------------------------------------------------------------------------"
 S Y=AMHBD D DD^%DT S AMHBDD=Y S Y=AMHED D DD^%DT S AMHEDD=Y
 S AMHPG=0 D HEAD
 I '$D(^XTMP("AMHRP9",AMHJOB,AMHBTH)) W !!,"NO PATIENTS TO REPORT" G DONE
 S DFN="" K AMHQ
 S AMHNAME="" F  S AMHNAME=$O(^XTMP("AMHRP9",AMHJOB,AMHBTH,AMHNAME)) Q:AMHNAME=""!($D(AMHQ))  D
 .S DFN=0 F  S DFN=$O(^XTMP("AMHRP9",AMHJOB,AMHBTH,AMHNAME,DFN)) Q:DFN=""!($D(AMHQ))  D DFN
 G:$D(AMHQ) DONE
DONE D DONE^AMHLEIN,^AMHEKL
 K ^XTMP("AMHRP9",AMHJOB,AMHBTH),AMHJOB,AMHBTH
 Q
DFN ;
 I $Y>(IOSL-4) D HEAD Q:$D(AMHQ)
 S AMHHRCN=$S($D(^AUPNPAT(DFN,41,DUZ(2),0)):$P(^(0),U,2),1:"<none>")
 W !,$E($P(^DPT(DFN,0),U),1,15),?18,AMHHRCN
 W ?27,$P(^DPT(DFN,0),U,2) S Y=$P(^DPT(DFN,0),U,3) W ?31,$E(Y,4,5),"/",$E(Y,6,7),"/",$E(Y,2,3)
VSTS ; process visits
 K AMHRLOC,AMHPRV,AMHPROB
 S AMHR=0,AMHBDO=9999999-AMHBD,AMHEDO=9999999-AMHED,AMHSD=AMHED-1,AMHSD=AMHSD_".9999",AMHRCNT=0
 F  S AMHSD=$O(^AMHREC("AE",DFN,AMHSD)) Q:$P(AMHSD,".")>AMHBDO!(AMHSD="")  D
 .S AMHR=0 F  S AMHR=$O(^AMHREC("AE",DFN,AMHSD,AMHR)) Q:AMHR'=+AMHR  D
 ..Q:'$$ALLOWVI^AMHUTIL(DUZ,AMHR)
 ..S AMHRCNT=AMHRCNT+1 ;COUNT # VISITS
 ..;TABLE LOC SEEN
 ..I $P(^AMHREC(AMHR,0),U,4)]"",'$D(AMHRLOC($P(^DIC(4,$P(^(0),U,4),0),U))) S AMHRLOC($P(^DIC(4,$P(^AMHREC(AMHR,0),U,4),0),U))=""
 ..;TABLE PROVIDERS
 ..S AMHP=0 F  S AMHP=$O(^AMHRPROV("AD",AMHR,AMHP)) Q:AMHP'=+AMHP  S P=$P(^AMHRPROV(AMHP,0),U),AMHPRV($P(^VA(200,P,0),U))=""
 ..;TABLE PROBLEMS
 ..S AMHP=0 F  S AMHP=$O(^AMHRPRO("AD",AMHR,AMHP)) Q:AMHP'=+AMHP  S P=$P(^AMHRPRO(AMHP,0),U),AMHPROB($P(^AMHPROB(P,0),U))=""
 ..Q
 .Q
 K AMHLINE,AMHPRNT,AMHPCNT,AMHPRNM
 S AMHLINE(1)=""
 S X="",C=0,K=11 F  S X=$O(AMHRLOC(X)) Q:X=""  S C=C+1,AMHPRNM(C)=X
 D LINE
 K AMHPRNM S X="",C=0,K=11 F  S X=$O(AMHPRV(X)) Q:X=""  S C=C+1,AMHPRNM(C)=X
 D LINE
 K AMHPRNM S X="",C=0,K=9 F  S X=$O(AMHPROB(X)) Q:X=""  S C=C+1,AMHPRNM(C)=X
 D LINE
 S AMHRCNT=$J(AMHRCNT,4),AMHLINE(1)=AMHLINE(1)_AMHRCNT,X=0 F  S X=$O(AMHLINE(X)) Q:X'=+X  W ?41,AMHLINE(X),!
 Q
LINE ;
 I '$D(AMHPRNM) S AMHPRNT="--" D
 .S AMHPRNT=$E(AMHPRNT,1,10) D
 ..S J=$L(AMHPRNT),AMHLINE(1)=AMHLINE(1)_AMHPRNT F I=J:1:K S AMHLINE(1)=AMHLINE(1)_" "
 S X=0 F  S X=$O(AMHPRNM(X)) Q:X'=+X  D
 .I X=1 D  Q
 ..S AMHPRNT=$E(AMHPRNM(1),1,10) D
 ...S J=$L(AMHPRNT),AMHLINE(1)=AMHLINE(1)_AMHPRNT F I=J:1:K S AMHLINE(1)=AMHLINE(1)_" "
 .S AMHPRNT=$E(AMHPRNM(X),1,10) D
 ..I '$D(AMHLINE(X)) S AMHLINE(X)="",$P(AMHLINE(X)," ",($L(AMHLINE(1))-K))=""
 ..S J=$L(AMHPRNT),AMHLINE(X)=AMHLINE(X)_AMHPRNT F I=J:1:K S AMHLINE(X)=AMHLINE(X)_" "
 S X=1 F  S X=$O(AMHLINE(X)) Q:X'=+X  I $L(AMHLINE(X))<$L(AMHLINE(1)) S K=$L(AMHLINE(X))+1,J=$L(AMHLINE(1)) F I=K:1:J S AMHLINE(X)=AMHLINE(X)_" "
 Q
HEAD I 'AMHPG G HEAD1
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S AMHQ="" Q
HEAD1 ;
 W:$D(IOF) @IOF S AMHPG=AMHPG+1
 W !?13,"********** CONFIDENTIAL PATIENT INFORMATION **********"
 W !,$P(^VA(200,DUZ,0),U,2),?72,"Page ",AMHPG,!
 W ?(80-$L($P(^DIC(4,DUZ(2),0),U))/2),$P(^DIC(4,DUZ(2),0),U),!
 W ?22,"PATIENTS SEEN AT LEAST ",AMHNUM," TIMES",!
 W ?17,"RECORD DATES:  ",AMHBDD,"  TO  ",AMHEDD,!
PIH W !!,"PATIENT NAME",?18,"CHART #",?27,"SEX",?31,"DOB",?41,"LOCATION",?53,"PROVIDER",?65,"PROBLEM",?74,"#",!
 W ?41,"SEEN",?53,"SEEN",?65,"CODES",?74,"VISITS",!,AMH80D
 Q
