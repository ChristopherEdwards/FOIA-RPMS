AMHRPT4 ; IHS/CMI/LAB - PROCESS VISIT LIST ;
 ;;4.0;IHS BEHAVIORAL HEALTH;;MAY 14, 2010
 ;
 ;
 ;
START ;
 S (AMHBT,AMHBTH)=$H,AMHJOB=$J
 D XTMP^AMHUTIL("AMHRPT","BH REPORT")
 I $P(^AMHRCNT(AMHRPTC,0),U,11)]"" S AMHRPREP=$P(^(0),U,11) S AMHRPREP=$TR(AMHRPREP,"~","^") D @AMHRPREP
 D D,END
 Q
 ;
S ;run by search template
 S AMHR=0 F  S AMHR=$O(^DIBT(AMHSEAT,1,AMHR)) Q:AMHR'=+AMHR  D PROC,EOJ
 Q
D ; Run by visit date
 S X1=AMHBD,X2=-1 D C^%DTC S AMHSD=X
 S AMHODAT=AMHSD_".9999" F  S AMHODAT=$O(^AMHREC("B",AMHODAT)) Q:AMHODAT=""!((AMHODAT\1)>AMHED)  D V1
 Q
 ;
END ;
 I $P(^AMHRCNT(AMHRPTC,0),U,9)]"" S AMHRPOSP=$P(^(0),U,9) S AMHRPOSP=$TR(AMHRPOSP,"~","^") D @AMHRPOSP
 S AMHET=$H
 D EOJ
 Q
EOJ ;
 K AMHB,AMHI,AMHR,AMHRCNT
 Q
V1 ;
 S (AMHR,AMHRCNT)=0 F  S AMHR=$O(^AMHREC("B",AMHODAT,AMHR)) Q:AMHR'=+AMHR  I $D(^AMHREC(AMHR,0)),$P(^(0),U,2)]"",$P(^(0),U,3)]"" S AMHR0=^(0) D PROC
 Q
PROC ;
 Q:'$$ALLOWVI^AMHUTIL(DUZ,AMHR)
 I $P(AMHR0,U,8) Q:$$DEMO^AMHUTIL1($P(AMHR0,U,8),$G(AMHDEMO))
 I $P(AMHR0,U,8) Q:'$$ALLOWP^AMHUTIL(DUZ,$P(AMHR0,U,8))
 Q:$P(AMHR0,U,4)=""
 Q:$P(AMHR0,U,5)=""
 Q:$P(AMHR0,U,6)=""
 Q:$P(AMHR0,U,7)=""
 D SCREENS
 Q:$D(AMHSKIP)
 I $P(^AMHRCNT(AMHRPTC,0),U,8) K AMHSRT,AMHPRNT S AMHCRIT=AMHSORT,AMHX=0 X:$D(^AMHSORT(AMHSORT,5)) ^AMHSORT(AMHSORT,5) S:'$D(AMHPRNT) AMHPRNT="<NONE AVAILABLE>" S AMHSRT=AMHPRNT
 I $G(AMHRPTST)]"" D @(AMHRPTST) Q
 S ^XTMP("AMHRPT",AMHJOB,AMHBTH,"RECORDS",AMHSRT,AMHR)=""
 Q
SCREENS ;
 S DFN=$P(AMHR0,U,8)
 K AMHSKIP
 S AMHI=0 F  S AMHI=$O(^AMHTRPT(AMHRPT,11,AMHI)) Q:AMHI'=+AMHI!($D(AMHSKIP))  D
 .I '$P(^AMHSORT(AMHI,0),U,8) D SINGLE Q
 .D MULT
 .Q
 Q
SINGLE ;
 K AMHSPEC
 S X="",AMHX=0
 X:$D(^AMHSORT(AMHI,1)) ^(1)
 I X="" S AMHSKIP="" Q
 I '$D(AMHSPEC),'$D(^AMHTRPT(AMHRPT,11,AMHI,11,"B",X)) S AMHSKIP="" Q
 Q
MULT ;
 K AMHFOUN,AMHSKIP,X S AMHX=0,X=""
 X:$D(^AMHSORT(AMHI,1)) ^(1)
 I '$L($O(X)) S AMHSKIP="" Q
 S Y="" F  S Y=$O(X(Y)) Q:Y=""  I $D(^AMHTRPT(AMHRPT,11,AMHI,11,"B",Y)) S AMHFOUN="" Q
 S:'$D(AMHFOUN) AMHSKIP=""
 Q
