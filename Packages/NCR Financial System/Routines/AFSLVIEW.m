AFSLVIEW ;IHS/OIRM/DSD/HJT - C.O. DISPLAY LISTINGS;  [ 09/27/2005  4:34 PM ]
 ;;3.0t1;1166 APPROVALS FOR PAYMENT;**19**;AUG 31, 2005
 ;Modified for Y2K compliance 1/5/1999HJT
 ;Modified further for Y2K IHS/DSD/JLG 3/8/99
 ;Lists open schedules by Certifying Office for technician/export-by
 ;date, Certifying Officer/export-by date, closed schedules to be
 ;certified, etc. - part 1
 W !,"NOT AN ENTRY POINT" H 2 Q
PMTDEL ;EP ;DELETE PMT FM PMT BATCH ONLY
 D ^XBCLS
 D CRTSETUP^AFSLCRTS
 W !,?20,"PAYMENT TRANSACTION DELETE"
 W !!!,"NOTE:  This option deletes a selected payment from a payment BATCH"
 W !,"       ONLY and DOES NOT delete the payment record from the obligation on file."
 W !!,"       Therefore, " W @AFSLRVON,"IF YOU ARE WANTING TO DELETE A PAYMENT FROM THE DOCUMENT",@AFSLRVOF
 W !,"                  ",@AFSLRVON,"AND PAYMENT FILES SIMULTANEOUSLY, DON'T USE THIS OPTION!",@AFSLRVOF
 W !!,"       Instead, use 'PD -  DEL PMT FM BOTH PMT & DOC FILES' found in"
 W !,"       the UTILITIES MENU."
 W !!,"PRESS RETURN"
 S AFSLCHRS=1
 D READCHRS^AFSLSRDR
PMTY ;
 K DIR
 ;Begin Y2K modifications
 ;Changed references to 2 digit years to 4 digit year in code that follows.
 S DIR(0)="N^1900:2699:0"   ;Y2000
 S DIR("A")="ENTER 4 DIGIT YEAR OF BATCH (i.e., '1997')"    ;Y2000
 D ^DIR
 K DIR
 I $D(DTOUT)!($D(DUOUT)) Q
 I X'?4N W !,"ENTER AS PER EXAMPLE!" G PMTY  ;Y2000
 ;End Y2k modifications
 S AFSLFYR=X
 D ^AFSLYRLU
 I AFSLYFND="XX" W !,"YEAR NOT FOUND!" G PMTY
PMTB ;
 K DIR
 S DIR(0)="F^6:6"
 S DIR("A")="ENTER BATCH NUMBER OF PMT(i.e.,'510001')"
 D ^DIR
 K DIR
 I $D(DTOUT)!($D(DUOUT)) Q
 I $L(X)'=6 W !,"ENTER AS PER EXAMPLE!" G PMTB
 S AFSLSCHD=X
 D ^AFSLSCLU
 I AFSLSFND="XX" W !,"BATCH# NOT FOUND!" G PMTB
PMTS ;
 K DIR
 S DIR(0)="F^4:4"
 S DIR("A")="ENTER SEQUENCE NO. OF PMT (i.e., '0001')"
 D ^DIR
 I $D(DTOUT)!($D(DUOUT)) Q
 I X'?4N W !,"ENTER AS PER EXAMPLE!" G PMTS
 S AFSLSQNO=X
 D ^AFSLSQLU
 I AFSLNFND="XX" W !,"SEQUENCE# NOT FOUND!" G PMTS
PMTD ;
 W !!,"ENTER '@' WHEN SHOWN SEQ# BELOW TO DELETE THE PAYMENT SEQUENCE.",!
 S DIE="^AFSLAFP(AFSLYNOD,1,AFSLSNOD,1,"
 S DIC(0)="AQEM"
 S DA(2)=AFSLYNOD
 S DA(1)=AFSLSNOD
 S DA=AFSLNNOD
 S DR=".01//"
 D ^DIE
 K DIE
 Q
OPENSC1 ;EP; LST OPN SCHDS BY C.O. FOR TECH&EXPORT-BY DT
 D ^XBCLS
 S L=0
 S DIC="^AFSLAFP("
 S FLDS="[AFSL.OPENSC]"
 S BY="1,2,1,6,1,9"
 D ^AFSLCTLU
 ;S AFSLCERT=$P(^VA(200,AFSLCOFF,0),U,1)  ;ACR*2.1*19.02 IM16848
 S AFSLCERT=$$NAME2^ACRFUTL1(AFSLCOFF)  ;ACR*2.1*19.02 IM16848
 S AFSLCERT=$P(AFSLCERT,",",1)
 I AFSLCOFF=$P(^AFSLPRM(1,0),U,3) S FR="A,O,",TO="Z,O," D EN1^DIP
 I AFSLCOFF'=$P(^AFSLPRM(1,0),U,3) S FR=AFSLCERT_",O,",TO=AFSLCERT_"Z,O," D EN1^DIP
 D PRESS
 D CERT1^AFSLOPKL
 Q
OPENSC2 ;EP; LST OPN SCHDS BY C.O.&EXPORT-BY DT
 D ^XBCLS
 S L=0
 S DIC="^AFSLAFP("
 S FLDS="[AFSL.OPENSC]"
 S BY="1,2;""CERT. OFFICER"",1,6,1,9"
 S FR="?,O,"
 S TO="?,O,"
 D EN1^DIP
 D PRESS
 D CERT1^AFSLOPKL
 Q
BTEXTYP ;EP; SET EXPORT TYPE (T/A/B/C/G) FOR A BATCH
 D ^XBCLS
 K DIR
 ;Begin Y2K modifications
 S DIR(0)="F^4:4"    ;Y2000
 S DIR("A")="BATCH YEAR"
 S DIR("T")=60
 S DIR("?")="Enter the 4 digit year under which the BATCH is filed"   ;Y2000
 D ^DIR
 I X["^" Q
 I X'?4N W !,"Must enter a 4 digit year" H 2 G BTEXTYP   ;Y2000
 ;End Y2K modifications
 S AFSLFYR=X
 K DIR
 S DIR(0)="F^6:6"
 S DIR("A")="BATCH#"
 S DIR("T")=60
 S DIR("?")="Enter 6 characters"
 D ^DIR
 I X["^" Q
 I $L(X)'=6 W !,"Must enter 6 characters" H 2 G BTEXTYP
 S AFSLSCHD=X
 D ^AFSLYRLU
 I AFSLYFND="XX"!('$D(AFSLYNOD)) W !,"THAT BATCH YEAR NOT FOUND ON FILE" H 5 Q
 D ^AFSLSCLU
 I AFSLSFND="XX"!('$D(AFSLSNOD)) S AFSLSNOD="" W !,"BATCH NOT FOUND ON FILE UNDER THAT YEAR" H 5 Q
 S DA=AFSLSNOD
 S DA(1)=AFSLYNOD
 S DIE="^AFSLAFP(AFSLYNOD,1,"
 S DR="3;10;22"
 D ^DIE
 K DIE
 I $D(Y) W !,"COULD NOT CHANGE THE EXPORT TYPE!  NOTIFY SUPERVISOR." H 3
 I '$D(Y) W !,"THE EXPORT TYPE HAS BEEN CHANGED" H 3
 Q
ACHTINFO ;EP; EDIT ACH/EFT INFO FOR TRAVELERS [GLB ^ACRAU( ]
 I '$D(^ACRAPL("AC",DUZ,38)) D  Q
 .W !!,"You do not have authority to ADD or EDIT bank account data."
 .H 5
 S DIC="^ACRAU("
 S DIC(0)="AEMQZ"
 S DIC("A")="Person's Name (LAST,FIRST): "
 W !
 D ^DIC
 K DIC
 I +Y<1 Q
 S DA=+Y
 S DIE="^ACRAU("
 S DR="1901T;1902T;1903T;1904T;1905T"
 D ^DIE
 K DA,DIE,DR
 Q
ACPT1 ;
 W !!,"CONFIRM ACH INFORMATION."
 W !!,"YOU MAY DELETE INCORRECT INFO NOW & THEN ADD CORRECT INFO LATER."
 W !,"YOU MAY NOT CHANGE THE INFO AT THIS PROMPT.",!!
 W !,"ACCOUNT TYPE: ",AFSLTYP,"//"
 R AFSLACPT:300
 I AFSLACPT'=""&(AFSLACPT'="@") G ACPT1
 I AFSLACPT="@" S DR="1901///@" D ^DIE
 W !,"RTN NUMBER: ",AFSLRTN,"//"
 R AFSLACPT:300
 I AFSLACPT="@" S DR="1902///@" D ^DIE
 S DR="1903//@" D ^DIE
 Q
ACHVINFO ;EP ;EDIT ACH/EFT INFO FOR VENDORS [GLB ^AUTTVNDR( ]
 I '$D(^ACRAPL("AC",DUZ,38)) D  Q
 .W !!,"You do not have authority to ADD or EDIT vendor account data."
 .H 5
 S DIC="^AUTTVNDR("
 S DIC(0)="AEMQZ"
 S DIC("A")="Vendor/Contractor Name: "
 W !
 D ^DIC
 K DIC
 I +Y<1 Q
 S DA=+Y
 S DIE="^AUTTVNDR("
 S DR="1901T;1902T;1903T;1904T;1905T"
 D ^DIE
 K DA,DIE,DR
 Q
ACPT2 ;
 W !!,"CONFIRM ACH INFORMATION."
 W !!,"YOU MAY DELETE INCORRECT INFO NOW & THEN ADD CORRECT INFO LATER."
 W !,"YOU MAY NOT CHANGE THE INFO AT THIS PROMPT.",!!
 W !,"ACCOUNT TYPE: ",AFSLTYP,"//"
 R AFSLACPT:300
 I AFSLACPT'=""&(AFSLACPT'="@") G ACPT2
 I AFSLACPT="@" S DR="1901///@" D ^DIE
 W !,"RTN NUMBER: ",AFSLRTN,"//"
 R AFSLACPT:300
 I AFSLACPT="@" S DR="1902///@" D ^DIE
 S DR="1903//@" D ^DIE
 Q
CLOSC ;EP; LST CLOSED SCHDS TO BE CERT
 D ^XBCLS
 S L=0
 S DIC="^AFSLAFP("
 S FLDS="[AFSL.CLOSC]"
 S BY="1,2;""CERTIFYING OFFICER: "",1,6,1,@9,1,@4"
 D ^AFSLCTLU
 ;S AFSLCERT=$P(^VA(200,AFSLCOFF,0),U,1)  ;ACR*2.1*19.02 IM16848
 S AFSLCERT=$$NAME2^ACRFUTL1(AFSLCOFF)  ;ACR*2.1*19.02 IM16848
 S AFSLCERT=$P(AFSLCERT,",",1)
 S FR="A,C,,@"
 S TO="Z,C,,@"
 D EN1^DIP
 D PRESS
 D CERT1^AFSLOPKL
 Q
SUPCERT ;EP; SUPV CERT A SCHED
 D ^XBCLS
 S L=0
 S DIC="^AFSLAFP("
 S FLDS="[AFSL.CLOSC]"
 S BY="1,2;""CERTIFYING OFFICER: "",1,6,1,@9,1,@4"
 W !!?17,"1166 APPROVALS FOR PAYMENT SYSTEM"
 W !?10,"LISTING OF BATCHES AVAILABLE TO BE CERTIFIED",!!
 S AFSLABRT=""
 S FR="A,C,,@"
 S TO="Z,C,,@"
 S IOP="HOME"
 D EN1^DIP
 S AFSLSPV="1"
 G CERTSCX
 I X'>1 W *7,!,"THERE ARE NO BATCHES TO BE CERTIFIED" S AFSLABRT="A"
 D CERT3^AFSLOPKL
 Q
CERTSC ;EP; CERT A SCHD
 D ^XBCLS
 S L=0
 S DIC="^AFSLAFP("
 S FLDS="[AFSL.CLOSC]"
 S BY="1,2;""CERTIFYING OFFICER: "",1,6,1,@9,1,@4"
 W !!?17,"1166 APPROVALS FOR PAYMENT SYSTEM"
 W !?10,"LISTING OF BATCHES AVAILABLE TO BE CERTIFIED",!!
 D ^AFSLCTLU
 ;S AFSLCERT=$P(^VA(200,AFSLCOFF,0),U,1)  ;ACR*2.1*19.02 IM16848
 S AFSLCERT=$$NAME2^ACRFUTL1(AFSLCOFF)  ;ACR*2.1*19.02 IM16848
 S AFSLCERT=$P(AFSLCERT,",",1)
 S AFSLABRT=""
 I AFSLCOFF=$P(^AFSLPRM(1,0),U,3) D  G CERTSCX
 .S FR="A,C,,@"
 .S TO="Z,C,,@"
 .S IOP="HOME"
 .D EN1^DIP
 I AFSLCOFF'=$P(^AFSLPRM(1,0),U,3) D
 .S FR=AFSLCERT_",C,,@"
 .S TO=AFSLCERT_"Z,C,,@"
 .S IOP="HOME"
 .D EN1^DIP
 I '$D(X) S X=0
 I X'>1 W *7,!,"YOU HAVE NO BATCHES TO BE CERTIFIED" S AFSLABRT="A"
CERTSCX ;
 D PRESS
 I AFSLABRT="A" G CERTEND
CERTSC2 ;
 D ^XBCLS
 W !!?10,"1166 APPROVALS FOR PAYMENT SYSTEM"
 W !?18,"CERTIFY A BATCH",!!
 I $D(AFSLFINC) D
 .K AFSLFINC
 .;Begin Y2K modifications  ;ACR*2.1*19.05 IM16848 - ADDED PERIOD
 .W *7,!!,"ENTER 4 DIGIT BATCH YEAR & 6 DIGIT BATCH NUMBER.",!   ;Y2000
 S AFSLABRT=""
 W !!,"   BATCH YR:"
 S AFSLCHRS=4   ;Y2000
 D READCHRS^AFSLSRDR
 S AFSLFYR=AFSLVOUT
 I AFSLFYR["^"!(AFSLFYR="") S AFSLABRT="A" G CERTEND
 I AFSLFYR["?"!(AFSLFYR'?4N) S AFSLFINC="1" G CERTSC2     ;Y2000
 ;End Y2K modifications
 W "     BATCH NO.:"
 S AFSLCHRS=6
 D READCHRS^AFSLSRDR
 S AFSLSCHD=AFSLVOUT
 I AFSLSCHD["^"!(AFSLSCHD="") S AFSLABRT="A" G CERTEND
 I AFSLSCHD["?"!(AFSLSCHD'?6N) S AFSLFINC="!" G CERTSC2
 D ^AFSLYRLU
 D ^AFSLSCLU
 S AFSLSPV=1
 I '$D(AFSLSPV) S AFSLSPV="0"
 I AFSLSPV="1" G CERTIFY
 I AFSLCOFF=$P(^AFSLPRM(1,0),U,3) G CERTIFY
 I AFSLCOFF'=DUZ D ^XBCLS W *7,!!,"THAT IS NOT YOUR BATCH!" S AFSLABRT="A" G CERTEND
CERTIFY ;EP
 I '$D(AFSLYRUX) G SKYRUX
 W !,"SCHED FY: ",AFSLYRUX
 I '$D(AFSLBTUX) G SKYRUX
 W !,"BATCH NO: ",AFSLBTUX
 K DIR
 S DIR(0)="Y"
 S DIR("A")="CORRECT TO CERTIFY? (Y/N)",DIR("B")="Y"
 D ^DIR
 I $E(X)'="Y" Q
SKYRUX ;
 I '$D(AFSLYNOD) W !,"PROBLEM WITH YEAR ENTERED" H 3 Q
 I '$D(AFSLSNOD) W !,"PROBLEM WITH BATCH ENTERED" H 3 Q
 I '$D(^AFSLAFP(AFSLYNOD,1,AFSLSNOD,0)) W !,"PROBLEM WITH YEAR+BATCH COMBINATION ENTERED" H 3 Q
 S AFSLCTP=$O(^AFSLCERT("B",DUZ,0))
 S DA=AFSLSNOD
 S DA(1)=AFSLYNOD
 S DIE="^AFSLAFP("_AFSLYNOD_",1,"
 S DR="4///NOW"
 L +^AFSLAFP(AFSLYNOD,1,AFSLSNOD,0):3
 I $T D ^DIE
 L -^AFSLAFP(AFSLYNOD,1,AFSLSNOD,0):0
CERTEND ;
 I $D(Y)&(AFSLABRT'="A") W *7,!!,"A PROBLEM OCCURRED WHILE CERTIFYING.  NOTIFY SUPERVISOR."
 I '$D(Y)&(AFSLABRT'="A") W !!,"THE BATCH HAS BEEN CERTIFIED WITH A CERTIFY DATE OF TODAY."
 D PRESS
 K AFSLABRT,AFSLSPV
 D CERT1^AFSLOPKL
 D CERT2^AFSLOPKL
 D CERT3^AFSLOPKL
 W !!,"CERTIFY ANOTHER BATCH? (Y/N) N//"
 S AFSLCHRS=1
 D READCHRS^AFSLSRDR
 Q:AFSLVOUT["^"
 I AFSLVOUT["?" W !,"ENTER 'Y' OR 'N'.  ENTER '^' TO QUIT" G CERTEND
 I AFSLVOUT="Y" G CERTSC2
 Q
VIOLA ;EP; LST ACC VIOL
 D VIOLA^AFSLVUE2
 Q
CERTA ;EP; LST COs & ACCT TECHS
 D CERTA^AFSLVUE2
 Q
SQVU ;EP; DISP BATCH ENTRIES FROM A BATCH
 D SQVU^AFSLVUE2
 Q
 K DIR S DIR(0)="F^1:1",DIR("A")="      ",DIR("A",1)="ENTER:  V .... VENDOR RELATED BATCH",DIR("A",2)="        P .... PERSON RELATED BATCH" D ^DIR
 I $D(DTOUT)!($D(DIROUT))!($D(DUOUT))!($D(DIRUT)) Q
 I '$D(X) S X=""
 I X'="V"&(X'="P") G SQVU
 I X="V" D SQVU^AFSLVUE2 Q
 I X="P" D SQVUP^AFSLVUE2
 Q
SQVUP ;EP; DISP BATCH ENTRIES FROM A 'PERSON' BATCH
 D SQVUP^AFSLVUE2
 Q
SQVUX ;EP; DISP BATCH PAYMENT ENTRIES
 D SQVUX^AFSLVUE2
 Q
DOCVU ;EP; DISP DHR DOC
 D DOCVU^AFSLVUE2
 Q
DOCLKUP ;EP; LKUP FOR MULT OCCUR
 D DOCLKUP^AFSLVUE2
 Q
VNDLST ;EP; LIST VEND INFO
 D VNDLST^AFSLVUE2
 Q
EMPLST ;EP; LIST EMPL
 D EMPLST^AFSLVUE2
 Q
PAYTYPS ;EP; LIST PMT TYPES
 D PAYTYPS^AFSLVUE2
 Q
CANNOS ;EP; LIST CAN#'S & INFO
 D CANNOS^AFSLVUE2
 Q
OBJCLS ;EP; LIST CL CODES & INFO
 D OBJCLS^AFSLVUE2
 Q
PRESS ;EP
 W !,"PRESS RETURN"
 S AFSLCHRS=1
 D READCHRS^AFSLSRDR
 S AFSLRTNX=AFSLVOUT
 K AFSLRTNX,AFSLVOUT,AFSLCHRS,FR,BY,TO,DIC,DA,DA(1),DHD
 Q
