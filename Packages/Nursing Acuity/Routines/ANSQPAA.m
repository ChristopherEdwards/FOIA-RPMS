ANSQPAA ;IHS/OIRM/DSD/CSC - SETUP FOR ASSESSMENT HISTORY PRINTOUT; [ 07/07/1999  2:52 PM ]
 ;;3.0;NURSING PATIENT ACUITY;**1**;APR 01, 1996
 ;;SETUP FOR ASSESSMENT HISTORY PRINTOUT
A1 Q:'$D(ANSDFN)
 Q:'$D(^DPT(ANSDFN,0))
 S X=$P(^DPT(ANSDFN,0),U)
 D PREP
 Q
PREP ;PRINT REPORT
 S ANSINPT=ANSPT
 D NAME
 Q
NAME ;PRINT PATIENT NAME
 ;S ANSY=ANSADM Q:ANSY=""
 S ANSPUNIT=$P(^ANSR(ANSADM,"DX"),U,2),ANSUNIT=$S(ANSPUNIT]"":^ANSD(59.1,ANSPUNIT,0),1:"NOT SPECIFIED")
 W:ANSINPT]"" !,?4,ANSINPT,?36,ANSUNIT
 S ANSCA=""
 F I=0:0 S ANSCA=$O(^ANSR(ANSADM,"AT",ANSCA)) Q:ANSCA=""  D DATA
 D PAUSE^ANSDIC
 Q
DATA ;PRINT DATA
 D COUNT
 W ?53,$E(ANSCA,4,5)_"-"_$E(ANSCA,6,7)_"-"_($E(ANSCA,1,3)+1700),?65,$E(ANSCA,9),!    ;Y2000
 Q
HEAD ;EP
 S ANSHEAD=""
 D HISTORY^ANSQ
 K ANSHEAD
REPHEAD ;REPORT HEADING
 W !!,?4,"LIST OF ALL DATES AND SHIFTS FOR WHICH",!,?4,"ASSESSMENT WAS COMPLETED FOR SELECTED PATIENTS"
 W !,?0,"================================================================================"
 W !,?53,"* ASSESSMENTS *"
 W !,?4,"NAME",?36,"UNIT",?53,"DATE",?63,"SHIFT"
 W !,?0,"--------------------------------------------------------------------------------"
 Q
COUNT ;CONTROL SCREEN DISPLAY
 I ($D(IOSL)),($Y>(IOSL-4)) D
 .D PAUSE^ANSDIC
 .D HEAD
 .W !,?4,ANSINPT,?36,ANSUNIT
 Q
