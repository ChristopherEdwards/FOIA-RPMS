ANSUEU ;IHS/OIRM/DSD/CSC - ENTER/EDIT ROOM/BED; [ 02/25/98  10:32 AM ]
 ;;3.0;NURSING PATIENT ACUITY;;APR 01, 1996
 ;;ADD/EDIT ROOM AND BED
ROOM ;EP;TO ENTER/EDIT PATIENT'S ROOM
 S ANSX="",ANSR=$P(ANSDX,U,3),ANSB=$P(ANSDX,U,4)
 S DIC="^ANSD(59.1,"_ANSUN_",""R"",",DIC(0)="AQZEM",DIC("A")="Room...........: "
 S:ANSR DIC("B")=ANSR
 D DIC^ANSDIC
 Q:$D(DTOUT)!$D(DUOUT)
 S ANSR=$S(+Y>0:+Y,1:"@")
 S DIE="^ANSR(",DA=ANSADM,DR="4///"_ANSR_$S(ANSR'="@":"",1:";5///@")
 D DIE^ANSDIC
 I ANSR="@" D DIAG Q
BED ;ADD/EDIT BED ASSIGNMENT
 S DIC="^ANSD(59.1,"_ANSUN_",""R"","_ANSR_",""B"",",DIC(0)="AQZEM",DIC("A")="Bed............: "
 S:ANSB]"" DIC("B")=ANSB
 D DIC^ANSDIC
 Q:$D(DTOUT)!$D(DUOUT)
 S ANSB=$S(+Y>0:+Y,1:"@")
 S DIE="^ANSR(",DA=ANSADM,DR="5///"_ANSB
 D DIE^ANSDIC
DIAG ;EP;ENTER/EDIT DIAGNOSIS
 S DA=ANSADM,DR="2Diagnosis",DIE="^ANSR("
 D DIE^ANSDIC
 Q
