ORDD100A ;slc/dcm-DD entries for file 100 ;3/12/02  10:30
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**24,138**;Dec 17, 1997
ACT1(ORIFN,ORDA,ORADT,ORVP,ORDG) ; -- set "ACT" x-ref
 Q:'$G(ORIFN)  Q:'$G(ORDA)  N OR0
 S OR0=$G(^OR(100,ORIFN,0)) S:'$G(ORADT) ORADT=$P($G(^(8,ORDA,0)),U)
 S:'$G(ORVP) ORVP=$P(OR0,U,2) S:'$G(ORDG) ORDG=$P(OR0,U,11)
 I ORVP,ORADT,ORDG S ^OR(100,"ACT",ORVP,9999999-ORADT,ORDG,ORIFN,ORDA)=""
 Q
 ;
ACT2(ORIFN,ORDA,ORADT,ORVP,ORDG) ; -- kill "ACT" x-ref
 Q:'$G(ORIFN)  Q:'$G(ORDA)  N OR0
 S OR0=$G(^OR(100,ORIFN,0)) S:'$G(ORADT) ORADT=$P($G(^(8,ORDA,0)),U)
 S:'$G(ORVP) ORVP=$P(OR0,U,2) S:'$G(ORDG) ORDG=$P(OR0,U,11)
 I ORVP,ORADT,ORDG K ^OR(100,"ACT",ORVP,9999999-ORADT,ORDG,ORIFN,ORDA)
 Q
 ;
ES ; -- set "AE" x-ref
 N ORSTOP S ORSTOP=+$P(^OR(100,DA,0),U,9) ;138
 I ORSTOP,ORSTOP'<DT S ^OR(100,"AE",ORSTOP,DA)="" ;138
 Q
EK ; -- kill "AE" x-ref
 N ORSTOP S ORSTOP=$P(^OR(100,DA,0),U,9)
 I ORSTOP K ^OR(100,"AE",ORSTOP,DA)
 Q
 ;
OI1(ORIFN) ; -- set "AOI" x-ref
 N OR0,ORVP,ORSTRT,ORIT,I
 Q:'$D(^OR(100,ORIFN,.1))  S OR0=$G(^(0))
 S ORVP=$P(OR0,U,2),ORSTRT=$P(OR0,U,8) Q:'ORVP  Q:'ORSTRT
 S I=0 F  S I=$O(^OR(100,ORIFN,.1,I)) Q:I'>0  S ORIT=+$G(^(I,0)) S:ORIT ^OR(100,"AOI",ORIT,ORVP,9999999-ORSTRT,ORIFN)=""
 Q
 ;
OI2(ORIFN) ; -- kill "AOI" x-ref
 N OR0,ORVP,ORSTRT,ORIT,I
 Q:'$D(^OR(100,ORIFN,.1))  S OR0=$G(^(0))
 S ORVP=$P(OR0,U,2),ORSTRT=$P(OR0,U,8) Q:'ORVP  Q:'ORSTRT
 S I=0 F  S I=$O(^OR(100,ORIFN,.1,I)) Q:I'>0  S ORIT=+$G(^(I,0)) K:ORIT ^OR(100,"AOI",ORIT,ORVP,9999999-ORSTRT,ORIFN)
 Q
