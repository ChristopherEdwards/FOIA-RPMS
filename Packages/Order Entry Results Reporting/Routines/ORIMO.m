ORIMO ;SLC/JDL - Inpatient medication on outpatient
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**187,190**;Dec 17, 1997
IMOLOC(ORY,ORLOC,ORDFN) ;ORY>=0: LOC is an IMO authorized location
 S ORY=-1
 N PACH
 S PACH=$$PATCH^XPDUTL("PSJ*5.0*111")
 Q:'PACH
 I $L($TEXT(SDIMO^SDAMA203)) D
 . ;#DBIA 4133
 . S ORY=$$SDIMO^SDAMA203(ORLOC,ORDFN)
 . K SDIMO(1)
 Q
 ; 
IMOOD(ORY,ORDERID) ;Is it an IMO order?
 Q:'$D(^OR(100,+ORDERID,0))
 N PIMO,DGRP,IPGRP
 S (PIMO,DGRP,IPGRP)=0
 I $P($G(^OR(100,+ORDERID,0)),U,18)>0 S PIMO=1
 S DGRP=$P($G(^OR(100,+ORDERID,0)),U,11)
 S IPGRP=$O(^ORD(100.98,"B","UD RX",0))
 I (DGRP=IPGRP),PIMO S ORY=1
 Q
 ;
