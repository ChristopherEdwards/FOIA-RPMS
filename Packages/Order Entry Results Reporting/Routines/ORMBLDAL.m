ORMBLDAL ;SLC/MKB,JFR-Build outgoing GMRA ORM msgs ;11/17/00  11:05
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**33,39,97**;Dec 17, 1997
HL7DATE(DATE) ; -- FM -> HL7 format
 Q $$FMTHL7^XLFDT(DATE)  ;**97
 ;
PTR(NAME) ; -- Returns ptr value of prompt in Dialog file
 Q $O(^ORD(101.41,"AB",$E("OR GTX "_NAME,1,63),0))
 ;
ALG ; -- Segments for new Allergy order
 N NKA,TYPE,AGENT,SYMP,OBSERVED,ENTERED,WP,I,J,K,DATE,SEV,SYMPDATE,PROV
 S NKA=$G(ORDIALOG($$PTR("NKA"),1)) I $L(NKA) D  Q
 . S ORMSG(4)="AL1|1|||||"_$$HL7DATE($P(OR0,U,7))
 . S ORMSG(5)="ZAL||YES|"_$P(OR0,U,4)
 S AGENT="^^^"_$$CODE(ORDIALOG($$PTR("FREE TEXT OI"),1))
 S TYPE=ORDIALOG($$PTR("ALLERGY TYPE"),1) S:TYPE="O" TYPE="M"
 S OBSERVED=(ORDIALOG($$PTR("OBSERVED/HISTORICAL"),1)="o")
 S DATE=$G(ORDIALOG($$PTR("OBSERVATION DATE/TIME"),1))
 S SEV=$G(ORDIALOG($$PTR("SEVERITY"),1))
 S PROV=$G(ORDIALOG($$PTR("PROVIDER"),1))
 S WP=$$PTR("WORD PROCESSING 1"),SYMP=$$PTR("REACTION")
 S SYMPDATE=$$PTR("REACTION DATE/TIME") K ORMSG(4)
 S ORMSG(4)="AL1|1|"_$E(TYPE_"A",1,2)_"|"_AGENT_"|||"_$$HL7DATE($P(OR0,U,7))
 S ORMSG(5)="ZAL||NO|"_$P(OR0,U,4)_"|"_$S(OBSERVED:"OB",1:"HI"),I=5
 I DATE S I=I+1,ORMSG(I)="ZAO||"_$$HL7DATE(DATE)_"|"_$S(SEV=1:"SV",SEV=2:"MO",1:"MI")_"|"_PROV
 S J=0 F  S J=$O(ORDIALOG(SYMP,J)) Q:J'>0  S I=I+1,ORMSG(I)="ZAS|"_J_"|^^^"_ORDIALOG(SYMP,J)_"^^99ALS|"_$$HL7DATE($G(ORDIALOG(SYMPDATE,J)))
 S J=$O(^TMP("ORWORD",$J,WP,1,0)) Q:'J  S I=I+1,ORMSG(I)="NTE||P|"_^(J,0)
 S K=0 F  S J=$O(^TMP("ORWORD",$J,WP,1,J)) Q:J'>0  S K=K+1,ORMSG(I,K)=^(J,0)
 Q
 ;
CODE(X) ; -- Returns coded element form of Allergen - NDF aware
 N D,DIC,TRD,Y
 S Y=$O(^GMRD(120.82,"B",X,0)) I Y S Y=Y_U_X_"^99ALL" G CQ
 S Y=$O(^PS(50.605,"C",X,0)) I Y S Y=Y_U_X_"^99PSC" G CQ
 S DIC=50.6,DIC(0)="X",D="B" D IX^DIC I Y>0 S Y=+Y_U_X_"^99NDF" G CQ
 S TRD=$$TTOG^PSNAPIS(X,.TRD) I TRD S Y=$O(TRD(0))_U_X_"^99NDF" G CQ
 S Y="NOS^"_X_"^99OTH"
CQ Q Y
