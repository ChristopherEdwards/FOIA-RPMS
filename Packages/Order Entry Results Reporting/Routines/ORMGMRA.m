ORMGMRA ;SLC/JFR - ALLERGY ORDER UTILITIES ;11/19/98 11:25
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**39*;Dec 17, 1997
EN(ORD) ;release code for allergy orders
 Q:'$G(ORD)
 N OLDIFN,ORALDG,ORIFN,STDT
 ; make order active & set start date
 D STATUS^ORCSAVE2(ORD,6)
 I '$P(^OR(100,ORD,0),"^",8) D DATES^ORCSAVE2(ORD,$$NOW^XLFDT)
 ; find and complete prior NKA orders
 S ORALDG=$O(^ORD(100.98,"B","ALLERGIES",0)) Q:'ORALDG
 S STDT=0
 F  S STDT=$O(^OR(100,"AW",ORVP,ORALDG,STDT)) Q:'STDT  D
 . S OLDIFN=0
 . F  S OLDIFN=$O(^OR(100,"AW",ORVP,ORALDG,STDT,OLDIFN)) Q:'OLDIFN  D
 .. Q:OLDIFN=ORD
 .. I $L($$VALUE^ORX8(OLDIFN,"NKA")) D STATUS^ORCSAVE2(OLDIFN,2)
 .. ; complete NKA order
 .Q
 Q
