PSOLLL1 ;BIR/BHW - LASER LABELS ;03-May-2010 08:47;SM
 ;;7.0;OUTPATIENT PHARMACY;**120,141,135,162,1001,1005,1006,1008,1009**;DEC 1997
 ;
 ;Reference to ^PSDRUG supported by DBIA 221
 ;Reference ^VA(200,D0,"PS" supported by DBIA 224
 ;Reference to ^PS(54 supported by DBIA 2227
 ; Modified - IHS/CIA/PLS - 03/01/04
 ;                          10/15/04 - Line ADDRESS+6
 ;                          10/27/04 - Line CONT+4
 ;            IHS/MSC/PLS   09/11/07 - Line L1+40
 ;                          10/25/07 - Line ST+7
 ;                          05/01/09 - Line L1+53
 ;                          04/30/10 - Line W
ST I $P($G(^PSRX(RX,3)),"^",3) S PSOPROV=+$P(^(0),"^",4),PSOPROV=$S($G(RXP):+$P($G(RXP),"^",17),$G(RXF):+$P($G(^PSRX(RX,1,RXF,0)),"^",17),1:PSOPROV) S:'$G(PSOPROV) PSOPROV=+$P(^PSRX(RX,0),"^",4) D
 .I +$P($G(^VA(200,PSOPROV,"PS")),"^",7) S:'$P($G(PHYS),"/",2) PHYS=$G(PHYS)_"/"_+$P($G(^PSRX(RX,3)),"^",3)
 S $P(ULN,"_",34)="",PSOTRAIL=1
 S (Y,X1)=EXPDT X ^DD("DD") S EXPDT=Y,Y=$P(^PSRX(RX,0),"^",13) X ^DD("DD") S ISD=Y,X2=DT D ^%DTC S DIFF=X
 S Y=DATE X ^DD("DD") S DATE=Y
 ; IHS/CIA/PLS - 03/06/04 - Set TECH to initials and not New Person IEN
 ;S TECH="("_$S($P($G(^PSRX(+$G(RX),"OR1")),"^",5):$P($G(^PSRX(+$G(RX),"OR1")),"^",5),1:$P(RXY,"^",16))_"/"_$S($G(VRPH)&($P(PSOPAR,"^",32)):VRPH,1:" ")_")"
 ; IHS/MSC/PLS - 10/25/07 - Changed logic to call $$LBLINI^APSPLBL
 ;S TECH="("_$$USRINI($S($P($G(^PSRX(+$G(RX),"OR1")),"^",5):$P($G(^PSRX(+$G(RX),"OR1")),"^",5),1:$P(RXY,"^",16)))_"/"_$$USRINI($S($G(VRPH)&($P(PSOPAR,"^",32)):VRPH,1:" "))_")"
 S TECH="("_$$LBLINI^APSPLBL(RX,$S($G(RXP):"P",$G(RXF):"R",1:""),$S($G(RXP):RXP,$G(RXF):RXF,1:""))_"/"_$$USRINI($S($G(VRPH)&($P(PSOPAR,"^",32)):VRPH,1:" "))_")"
 S PSZIP=$P(PS,"^",5),PSOHZIP=$S(PSZIP["-":PSZIP,1:$E(PSZIP,1,5)_$S($E(PSZIP,6,9)]"":"-"_$E(PSZIP,6,9),1:""))
L1 I $G(PSOIO("BLH"))]"" X PSOIO("BLH")
 K PSOSTLK,ZTKDRUG I $L($T(PSOSTALK^PSOTALK1)) D PSOSTALK^PSOTALK1 S PSOSTLK=1 ; PRINT SCRIPTALK LABEL IF APPLICABLE
 ; IHS/CIA/PLS - 03/31/04 - Change VAMC to IHS
 ;S T="VAMC "_$P(PS,"^",7)_", "_STATE_"  "_$G(PSOHZIP) D PRINT(T)
 D ADDRESS
 ;S T=$P(PS2,"^",2)_"  "_TECH_"  Ph: "_$P(PS,"^",3)_"-"_$P(PS,"^",4) D PRINT(T)  ; IHS/PLS/CIA - 03/31/04 - Suppress - no room
 S PSDU=$P($G(^PSDRUG($P($G(^PSRX(RX,0)),"^",6),660)),"^",8)
 I $G(PSOIO("BLB"))]"" X PSOIO("BLB")
 S XFONT=$E(PSOFONT,2,99)
 S T="Rx# "_RXN_"  " D PRINT(T,1)
 D STRT^PSOLLU1("RX#",T,.L) S PSOY=PSOY-PSOYI,OPSOX=PSOX,PSOX=L(XFONT)*300+PSOX
 S T="  "_DATE_"  "_$S('SIGF:"Fill "_(RXF+1)_" of "_(1+$P(RXY,"^",9)),1:"(label continued)") D PRINT(T)
 S PSOX=OPSOX,T=PNM D PRINT(T,1) D STRT^PSOLLU1("SIG",T,.L)
 S OPSOX=PSOX,PSOX=L(XFONT)*300+PSOX,PSOY=PSOY-PSOYI,T="  "_$G(SSNPN) D PRINT(T)
 S PSOX=OPSOX,LENGTH=0,PTEXT="",SIGF=0,XFONT=$E(PSOFONT,2,99)
 N DP,TEXTP,TEXTL,MORE
 F DR=SIGF("DR"):1 Q:$G(SGY(DR))=""  S TEXT=SGY(DR) D  Q:SIGF
 . F I=1:1 Q:$E(TEXT,I)'=" "  S TEXT=$E(TEXT,2,255)
 . S DP=$S(TEXT[" ":" ",TEXT[",":",",1:" ")
 . F I=SIGF("T"):1:$L(TEXT,DP) D  Q:SIGF
 .. S TEXTP=$P(TEXT,DP,I) Q:TEXTP=""  I $D(SIGF("J")) S TEXTP=$E(TEXTP,SIGF("J"),255) K SIGF("J")
 .. D STRT^PSOLLU1("SIG",TEXTP_" ",.L) I L(XFONT)>3.3 D
 ... S MORE=0
 ... F J=$L(TEXTP):-1:1 S TEXTL=PTEXT_$E(TEXTP,1,J) D STRT^PSOLLU1("SIG",TEXTL_" ",.L) D  Q:SIGF!MORE
 .... Q:L(XFONT)>3.3
 .... D PRINT(TEXTL) S TEXT=$E(TEXT,J+1,512),(PTEXT,TEXTL)=""
 .... I PSOY>PSOYM S SIGF=1,SIGF("DR")=DR,SIGF("T")=I,SIGF("J")=J+1 Q
 .... D STRT^PSOLLU1("SIG",TEXT_" ",.L) S TEXTP=TEXT,J=$L(TEXTP) I L(XFONT)<3.3 S MORE=1,LENGTH=0
 .. I LENGTH+L(XFONT)<3.3 S PTEXT=PTEXT_TEXTP_" ",LENGTH=LENGTH+L(XFONT) Q
 .. S LENGTH=0,I=I-1
 .. D PRINT(PTEXT) S PTEXT=""
 .. I PSOY>PSOYM S SIGF=1,SIGF("DR")=DR,SIGF("T")=I+1,PTEXT="" Q
 . I 'SIGF S SIGF("T")=1
 I PTEXT]"" D PRINT(PTEXT)
 I $G(PSOIO("BLF"))]"" X PSOIO("BLF")
 S PSOY=PSODY-PSOYI,PSOFONT=PSODFONT
 I SIGF S PSOX=PSOCX,T="(continued on next label)" D PRINT(T) G WARN:'SIGM,CONT
 D NOW^%DTC S X1=X,X2=365 D C^%DTC S Y=X X ^DD("DD")
 S DEA=$P($G(^PSDRUG($P(RXY,"^",6),0)),"^",3),T=""
 I DEA'["S",DEA'["I" S T="Discard after "_$S(DEA[0!(DEA["M"):"_________",1:Y)_"__________   "
 S T=T_$S($G(NURSE):"Mfr_________",1:"") D PRINT(T)
 I $$GET1^DIQ(9009033,PSOSITE,311,"I") S T="NDC "_$$NDCVAL^APSPFUNC(RX,+$G(RXFL(RX))) D PRINT(T)  ;IHS/MSC/PLS - 09/11/07
 S PPHYS=$G(PHYS)
 S XFONT=$E(PSOQFONT,2,99)
 S TEXT="Qty: " D STRT^PSOLLU1("SIG",TEXT,.L) S Q(1)=L(XFONT)
 S TEXT=" "_PSDU D STRT^PSOLLU1("SIG",TEXT,.L) S Q(2)=L(XFONT)
 S TEXT="       "_$G(PHYS) D STRT^PSOLLU1("SIG",TEXT,.L) S Q(3)=L(XFONT)
 S TEXT=$G(QTY) D STRT^PSOLLU1("SIG",TEXT,.L) S LENGTH=Q(1)+Q(2)+Q(3)+L(XFONT+2),Q(4)=L(XFONT+2)
 I LENGTH>3 F I=$L(PHYS)-1:-1:1 S PPHYS=$E(PHYS,1,I),TEXT="       "_PPHYS D STRT^PSOLLU1("SIG",TEXT,.L) I Q(1)+Q(2)+Q(4)+L(XFONT)<3.3 Q
 S PSOFONT=PSOTFONT,OPSOX=PSOX,PSOX=PSOX+(Q(1)*300),PSOY=PSOQY-PSOYI,T=$G(QTY) D PRINT(T)
 S PSOX=OPSOX,PSOFONT=PSOQFONT,PSOY=PSOY-PSOYI,T="Qty: " D PRINT(T)
 S PSOX=PSOX+(Q(1)+Q(4)*300),PSOY=PSOY-PSOYI,T=" "_$G(PSDU)_"       "_$G(PPHYS) D PRINT(T)
 S PSOFONT=PSOTFONT,PSOX=OPSOX,PSOY=PSOTY-PSOYI,T=DRUG D STRT^PSOLLU1("SIG",T,.L)
 ;I L($E(PSOFONT,2,99))>3 S PSOFONT=$S(PSOFONT="F12":"F10",PSOFONT="F10":"F9",PSOFONT="F9":F8,PSOFONT="F8":"F6")
 I L($E(PSOFONT,2,99))>3 S PSOFONT=$S(PSOFONT="F12":"F10",PSOFONT="F10":"F9",PSOFONT="F9":"F8",PSOFONT="F8":"F6")  ;IHS/MSC/PLS 05/01/09 - Corrected typo.
 S ZTKDRUG="XXXXXX   SCRIPTALK RX   XXXXXX"
 I $G(PSOSTLK) S T=$S($G(PSOSTALK):ZTKDRUG,1:DRUG)
 D PRINT(T,1)
 I SIGM G CONT
 S ^PSRX(RX,"TYPE")=0
WARN ;PRINT WARNING LABELS
 I $G(PSOIO("WLI"))]"" X PSOIO("WLI")
 F WWW=1:1:5 S PSOWARN=$P(WARN,",",WWW) Q:PSOWARN=""  I $D(^PS(54,PSOWARN,0)) S PTEXT="",(LENGTH,OUT)=0,LINE=1,LCNT=3 D
 . ;BHW;Modify Loop to use $O instead of 1:1
 . S T="",JJJ=0
 . F  S JJJ=$O(^PS(54,PSOWARN,1,JJJ)) Q:('JJJ)  D
 . . I $D(^PS(54,PSOWARN,1,JJJ,0)) S T=T_" "_^(0)
 . . Q
 . D STRT^PSOLLU1("WRN",T,.L,.XFONT)
 . I WWW=1 S XFONT=$S(XFONT="F12":"F10",XFONT="F10":"F9",XFONT="F9":"F8",1:"F6")
 . S PSOY=PSOY+$S(XFONT="F12":10,XFONT="F10":5,1:0),PSOYI=$S(XFONT="F12":40,XFONT="F10":35,XFONT="F9":29,1:29)
 . ;BHW;Modify Loop to use $O instead of 1:1
 . S JJJ=0
 . F  S JJJ=$O(^PS(54,PSOWARN,1,JJJ)) Q:('JJJ)  D  Q:OUT
 .. S TEXT=$G(^PS(54,PSOWARN,1,JJJ,0)) Q:'$L(TEXT)
 .. F I=1:1 Q:$E(TEXT,I)'=" "  S TEXT=$E(TEXT,2,255)
 .. F I=1:1:$L(TEXT," ") D STRT^PSOLLU1("SEC2",$P(TEXT," ",I)_" ",.L) D  Q:OUT
 ... I LENGTH+L($E(XFONT,2,99))<1.9 S PTEXT=PTEXT_$P(TEXT," ",I)_" ",LENGTH=LENGTH+L($E(XFONT,2,99)) Q
 ... S LENGTH=0,I=I-1,PSOFONT=XFONT
 ... D PRINT(PTEXT) S PTEXT="",LINE=LINE+1 I LINE>LCNT S OUT=1 Q
 . I 'OUT S PSOFONT=XFONT D PRINT(PTEXT)
 . S PSOY=WWW*115+29+(WWW-1*2)
 ;RETURN MAIL
 S PS=$S($D(^PS(59,PSOSITE,0)):^(0),1:"") I $P(PSOSYS,"^",4),$D(^PS(59,+$P($G(PSOSYS),"^",4),0)) S PS=^PS(59,$P($G(PSOSYS),"^",4),0)
 S VAADDR1=$P(PS,"^"),VASTREET=$P(PS,"^",2),STATE=$S($D(^DIC(5,+$P(PS,"^",8),0)):$P(^(0),"^",2),1:"UNKNOWN")
 S PSZIP=$P(PS,"^",5),PSOHZIP=$S(PSZIP["-":PSZIP,1:$E(PSZIP,1,5)_$S($E(PSZIP,6,9)]"":"-"_$E(PSZIP,6,9),1:""))
 I $G(PSOIO("RMI"))]"" X PSOIO("RMI")
 S PSOYI=$G(PSOHYI,40),OFONT=PSOFONT,PSOFONT=$G(PSOHFONT)
 ; IHS/CIA/PLS - 03/05/04 - Changed 119 to Pharmacy
 ;S T="Attn: (119)" D PRINT(T,0)
 S T="Attn: Pharmacy" D PRINT(T,0)
 S T=$G(VASTREET) D PRINT(T,0)
 S T=$P(PS,"^",7)_", "_$G(STATE)_"  "_$G(PSOHZIP) D PRINT(T,0)
 S PSOY=PSOY+PSOYI,PSOFONT=OFONT,T=$S(PS55=2:"***DO NOT MAIL***",PS55[0!(PS55[3)!(PS55=""):"REGULAR MAIL",1:"CERTIFIED MAIL") D PRINT(T,0)
 S PSOY=PSOY+PSOYI,PSOYI=PSORYI D PRINT(PNM,0)
 I $G(VAPA(1))=""!(PS55=2) G W
 F I=1:1:3 I $G(VAPA(I))]"" S T=$G(VAPA(I)) D
 . D STRT^PSOLLU1("ML",T,.L) I L($E(PSOFONT,2,99))<2.37 D PRINT(T,0) Q
 . F F=12,10,9,8,6 I L(F)<2.37 S OFONT=PSOFONT,PSOFONT="F"_F D PRINT(T,0) S PSOFONT=OFONT Q
 S A=+$G(VAPA(5)) I A S A=$S($D(^DIC(5,A,0)):$P(^(0),"^",2),1:"UNKNOWN")
 S T=$G(VAPA(4))_", "_A_"  "_$S($G(VAPA(11)):$P(VAPA(11),"^",2),1:$G(VAPA(6))) D PRINT(T,0)
W D:$$GET1^DIQ(9009033,PSOSITE,320,"I") PRINT("DOB:"_$$FMTE^XLFDT($$DOB^AUPNPAT(DFN),"5Z"),0)  ;IHS/MSC/PLS - 04/30/10
 I MW="WINDOW" D
 . N XFONT
 . S OFONT=PSOFONT,PSOYI=$G(PSOTYI,40),PSOFONT=PSOTFONT,XFONT=$E(PSOFONT,2,99),PSOY=PSOTY,T="WINDOW - "_$G(^PSRX(RX,"MP"))
 . D STRT^PSOLLU1("ML",T,.L)
 . I L(XFONT)<2.3 D PRINT(T,0) S PSOFONT=OFONT Q
 . F F=10,9,8,6 I L(F)<4.5 Q
 . S XFONT=F,PSOFONT="F"_F,PSOYI=$S(PSOFONT="F12":40,PSOFONT="F10":35,PSOFONT="F9":30,PSOFONT="F8":25,1:20)
 . F J=$L(T," "):-1:1 S PTEXT=$P(T," ",1,J) D STRT^PSOLLU1("ML",PTEXT,.L) D  Q:T=""
 .. I L(XFONT)<2.3 D PRINT(PTEXT) S T=$P(T," ",J+1,512),J=$L(T," ")+1,PTEXT=""
 . D:PTEXT]"" PRINT(PTEXT)
 . S PSOFONT=OFONT
CONT I $G(SIDE) G BARC:'L5,CONT2
 I $G(COPIES)>1 G BARC
 I 'L2!PFM D ^PSOLLL2 S L2=1
 I 'L3 D ^PSOLLL3 S L3=1
 ; IHS/CIA/PLS - 10/27/04 - Incorrect variable was set
 ;I 'L4!PMIM S PIMI=0 D ^PSOLLL4 S L4=1
 I 'L4!PMIM S PMIM=0 D ^PSOLLL4 S L4=1
 I L5 W @IOF G CONT2
 ; IHS/CIA/PLS - 03/08/04 - Changed to use barcode output routine
BARC ;I $G(PSOIO("BLBC"))]"" X PSOIO("BLBC") I $G(NOBARC) G BARCE
 ;I $G(PSOIO("BLBC"))]"" X PSOIO("BLBC")
 I $G(NOBARC) G BARCE
 ; IHS/CIA/PLS - 03/08/04 - Changed to use barcode output routine
 ;S X2=PSOINST_"-"_RX W X2
 ;IHS/MSC/PLS - 12/06/07 - Moved the top of barcode up by 60 points
 ;S X2=PSOINST_$S($L(PSOINST):"-",1:"")_RX W $$BC^CIAUBC28(X2,1,50,950,120)
 S X2=PSOINST_$S($L(PSOINST):"-",1:"")_RX W $$BC^CIAUBC28(X2,1,50,950,60)
 ;W $$BC^CIAUBC28($TR($$NDCVAL^APSPFUNC(RX,+$G(RXFL(RX))),"-","")_","_+$G(QTY),1,50,970,120)
 ;I $G(PSOIO("EBLBC"))]"" X PSOIO("EBLBC")
BARCE W @IOF
COPY I SIGF S SIGM=1 G L1
 I $G(COPIES)>1 D  G L1
 . S COPIES=COPIES-1
 . S (SIGM,PFM,PMIM,L2,L3,L4,L5)=0
 . K SIGF,PFF,PMIF S (SIGF,PFF,PMIF)=0 F I="DR","T" S (SIGF(I),PFF(I))=1
 . F I="A","B","I" S PMIF(I)=1
 S IR=0 F FDA=0:0 S FDA=$O(^PSRX(RX,"L",FDA)) Q:'FDA  S IR=FDA
 S IR=IR+1,^PSRX(RX,"L",0)="^52.032DA^"_IR_"^"_IR
 S ^PSRX(RX,"L",IR,0)=PSOFNOW_"^"_$S($G(RXP):99-RXPI,1:RXF)_"^"_$S($G(PCOMX)]"":$G(PCOMX),$G(PCOMH(RX))]"":PCOMH(RX),1:"From RX number "_$P(^PSRX(RX,0),"^"))_$S($G(RXP):" (Partial)",1:"")_$S($D(REPRINT):" (Reprint)",1:"")_"^"_PDUZ
 S L5=1
CONT2 I SIGF S SIGM=1 G L1
 I PMIM G CONT
 I $G(PSOBLALL)=1,$P(PPL,",",PI+1)="" D TRAIL
 Q
PRINT(T,B) ;
 S BOLD=$G(B)
 I 'BOLD,$G(PSOIO(PSOFONT))]"" X PSOIO(PSOFONT)
 I BOLD,$G(PSOIO(PSOFONT_"B"))]"" X PSOIO(PSOFONT_"B")
 I $G(PSOIO("ST"))]"" X PSOIO("ST")
 W T,!
 I $G(PSOIO("ET"))]"" X PSOIO("ET")
 I BOLD,$G(PSOIO(PSOFONT))]"" X PSOIO(PSOFONT) ;TURN OFF BOLDING
 Q
TRAIL G END  ; IHS/CIA/PLS - 03/31/04 - Suppress printing of last page
 ;I $G(SIDE) G END
 D ^PSOLLL5
 D ^PSOLLL6
 I '$P($G(^PS(59,PSOSITE,1)),"^",18) Q
 D ^PSOLLL7
END I '$P(PSOPAR,"^",31) Q
 W @IOF
 I $G(PSOIO("PMII"))]"" X PSOIO("PMII")
 I $G(PSOIO(PSOFONT))]"" X PSOIO(PSOFONT)
 S T="NEXT PATIENT"
 S PSOX=1100-(L($E(PSOFONT,2,99))*300/2)
 I $G(PSOIO("ST"))]"" X PSOIO("ST")
 W T,!
 I $G(PSOIO("ET"))]"" X PSOIO("ET")
 Q
 ; Return initials associated with user
USRINI(IEN) ;
 Q:'IEN " "
 Q $$GET1^DIQ(200,IEN,1)
 ; Output Outpatient Facility Mailing Address
ADDRESS ;
 N PS,VAADDR1,VASTREET,STATE,OFONT,PSOFONT,PSZIP,PSOHZIP
 S PSOFONT="F6",PSOYI=25
 S PSOX=0
 S PS=$S($D(^PS(59,PSOSITE,0)):^(0),1:"") I $P(PSOSYS,"^",4),$D(^PS(59,+$P($G(PSOSYS),"^",4),0)) S PS=^PS(59,$P($G(PSOSYS),"^",4),0)
 S VAADDR1=$P(PS,"^"),VASTREET=$P(PS,"^",2),STATE=$S($D(^DIC(5,+$P(PS,"^",8),0)):$P(^(0),"^",2),1:"UNKNOWN")
 ; IHS/CIA/PLS - 10/15/04 - Added tech initials to bottle label
 S VAADDR1=$$LJ^XLFSTR(VAADDR1,30)_$$RJ^XLFSTR(TECH,19)
 S PSZIP=$P(PS,"^",5),PSOHZIP=$S(PSZIP["-":PSZIP,1:$E(PSZIP,1,5)_$S($E(PSZIP,6,9)]"":"-"_$E(PSZIP,6,9),1:""))
 D PRINT(VAADDR1,0)
 S T=$G(VASTREET) D PRINT(T,0)
 S T=$P(PS,"^",7)_", "_$G(STATE)_"  "_$G(PSOHZIP)_"  Ph: "_$P(PS,"^",3)_"-"_$P(PS,"^",4) D PRINT(T,0)
 Q
