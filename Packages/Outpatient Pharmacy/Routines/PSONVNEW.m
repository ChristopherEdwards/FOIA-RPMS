PSONVNEW ;BIR/SAB - Add Non-VA Med orders ;09-Aug-2010 08:04;SM
 ;;7.0;OUTPATIENT PHARMACY;**132,1009**;DEC 1997
 ;External reference ^PS(50.606 supported by DBIA 2174
 ;External reference ^PS(50.7 supported by DBIA 2223
 ;External reference ^PS(55 supported by DBIA 2228
 ;External reference to ^PS(51.2 supported by DBIA 2226
 ;adds new non-va med to #55
 ;
 ;Modified - IHS/MSC/PLS - 08/06/2010 - Line REIN+17, Added +57
 ;
 I '$D(^PS(55,DFN,0)) D
 .K DD,DO S DIC(0)="L",(DINUM,X)=DFN,DIC("DR")="52.1////2" D FILE^DICN D:Y<1  K DIC,DA,DR,DD,DO
 ..S $P(^PS(55,DFN,0),"^")=DFN,$P(^(0),"^",6)=2
 ..K DIK S DA=DFN,DIK="^PS(55,",DIK(1)=.01 D EN^DIK K DIK
 ;
 ;
 N PSONVA,SCH,SCHED,SDL,SGL,ZSB,W,WW,SZZ,DSC K HOLD,PCOM
 F I=0:0 S I=$O(MSG(I)) Q:'I  D
 .I $P(MSG(I),"|")="NTE",$P(MSG(I),"|",2)=6 S DSC=$G(DSC)+1 S PCOM(DSC)=$P(MSG(I),"|",4) F T=0:0 S T=$O(MSG(I,T)) Q:'T  S DSC=DSC+1,PCOM(DSC)=MSG(I,T)
 .I $P(MSG(I),"|")="ORC" S STDT=$P(MSG(I),"|",8),STRDT=$$HL7TFM^XLFDT($P(STDT,"^",4))
 F IV=0:0 S IV=$O(^PS(55,DFN,"NVA",IV)) Q:'IV  S PSONVA=$G(PSONVA)+1
 K IV,DSC,STDT
 S PSONVA=$G(PSONVA)+1,^PS(55,DFN,"NVA",0)="^55.05P^"_PSONVA_"^"_PSONVA
 S ^PS(55,DFN,"NVA",PSONVA,0)=PSORDITE_"^"_PSODDRUG
 S ^PS(55,DFN,"NVA","B",$E(PSORDITE,1,30),PSONVA)=""
 S $P(^PS(55,DFN,"NVA",PSONVA,0),"^",11)=$G(ENTERED),$P(^(0),"^",10)=$G(PSOLOG),$P(^(0),"^",12)=$G(LOCATION)
 S ^PS(55,"ADCDT",$E(PSOLOG,1,30),DFN,PSONVA)="",SCH=$P($G(QTARRAY(1)),"^")
 S $P(^PS(55,DFN,"NVA",PSONVA,0),"^",8)=PLACER,$P(^(0),"^",9)=$G(STRDT)
 S $P(^PS(55,DFN,"NVA",PSONVA,0),"^",3)=$G(PSOLQ1I(1)) ;,$P(^(0),"^",5)=$P($G(QTARRAY(1)),"^")
 I $G(SCH)]"" D
 .S SGL=0 F W=0:0 S W=$O(^PS(51.1,"B",SCH,W)) Q:'W!($G(SGL))  I $P($G(^PS(51.1,W,0)),"^",8)'="" S SCHED=$P($G(^(0)),"^",8),SGL=1
 .Q:$G(SGL)
 .I $G(^PS(51,"A",SCH))'="" S SCHED=$P(^PS(51,"A",SCH),"^") Q
 .S ZSB=0 F ZZS=1:1:$L(SCH) S SZZ=$E(SCH,ZZS) I SZZ=" " S ZSB=ZSB+1
 .S ZSB=ZSB+1 K HOLD F Z=1:1:ZSB S (SDL,HOLD(Z))=$P(SCH," ",Z) D
 ..Q:$G(SDL)=""
 ..S SGL=0 F WW=0:0 S WW=$O(^PS(51.1,"B",SDL,WW)) Q:'WW!($G(SGLFLAG))  I $P($G(^PS(51.1,WW,0)),"^",8)'="" S HOLD(Z)=$P($G(^(0)),"^",8),SGL=1
 ..Q:$G(SGL)
 ..I $G(^PS(51,"A",SDL))]"" S HOLD(Z)=$P(^(SDL),"^")
 .S SCHED="",SGL=0 F WW=1:1:ZSB S SCHED=SCHED_$S($G(SGL):" ",1:"")_$G(HOLD(WW)),SGL=1
 I $G(SCHED)]"" S $P(^PS(55,DFN,"NVA",PSONVA,0),"^",5)=SCHED
 I $G(ROUTE(1)) S $P(^PS(55,DFN,"NVA",PSONVA,0),"^",4)=$S($P($G(^PS(51.2,ROUTE(1),0)),"^",2)]"":$P(^PS(51.2,ROUTE(1),0),"^",2),1:$P(^PS(51.2,ROUTE(1),0),"^"))
 I $P(PSODSC,"^",2)]"" D
 .S DSC=1,^PS(55,DFN,"NVA",PSONVA,"DSC",1,0)=$P(PSODSC,"^",2)
 .S ^PS(55,DFN,"NVA",PSONVA,"DSC",0)="^55.052^1^1^"_DT_"^^^^"
 I $O(PSODSC(0)) D
 .F T=0:0 S T=$O(PSODSC(T)) Q:'T  S DSC=$G(DSC)+1,^PS(55,DFN,"NVA",PSONVA,"DSC",DSC,0)=PSODSC(T)
 .S ^PS(55,DFN,"NVA",PSONVA,"DSC",0)="^55.052^"_DSC_"^"_DSC_"^"_DT_"^^^^"
 I $O(PCOM(0)) F T=0:0 S T=$O(PCOM(T)) Q:'T  D
 .S DSC=$G(DSC)+1,^PS(55,DFN,"NVA",PSONVA,"DSC",DSC,0)=PCOM(T)
 .S ^PS(55,DFN,"NVA",PSONVA,"DSC",0)="^55.052^"_DSC_"^"_DSC_"^"_DT_"^^^^"
 .S ^PS(55,DFN,"NVA",PSONVA,1,T,0)=PCOM(T)
 .S ^PS(55,DFN,"NVA",PSONVA,1,0)="^^"_T_"^"_T_"^"_DT_"^"
 I $G(OCOUNT) S ^PS(55,DFN,"NVA",PSONVA,"OCK",0)="^55.051^"_OCOUNT_"^"_OCOUNT F OCOUNT=1:1:OCOUNT D
 .S ^PS(55,DFN,"NVA",PSONVA,"OCK",OCOUNT,0)=$G(OBXAR(OCOUNT,1))_"^"_PROV
 .I $G(OBXAR(OCOUNT,1))]"" S ^PS(55,DFN,"NVA",PSONVA,"OCK","B",$E(OBXAR(OCOUNT,1),1,30),OCOUNT)=""
 .S PSOBCT=1 F LLL=2:1 Q:'$D(OBXAR(OCOUNT,LLL))  S ^PS(55,DFN,"NVA",PSONVA,"OCK",OCOUNT,"OVR",1,0)=OBXAR(OCOUNT,LLL)
 ;IHS/MSC/REC/PLS - 08/06/2010 - Add Med Rec fields
 I $D(^PS(55,DFN,"NVA",PSONVA,0)) D
 .S ^PS(55,DFN,"NVA",PSONVA,21400)=$G(MSCLD)_U_$G(MSCHMLST)_U_$G(MSCHMLOC)
 .I $O(MSCRSN(0)) D
 ..N T,MSCR
 ..S T=0 F  S T=$O(MSCRSN(0)) Q:'T  D
 ...S MSCR=$G(MSCR)+1
 ...S ^PS(55,DFN,"NVA",PSONVA,21401,MSCR,0)=MSCRSN(T)
 ...K MSCRSN(T)
 ..S ^PS(55,DFN,"NVA",PSONVA,21401,0)="^55.521404^"_MSCR_U_MSCR_U_DT_"^^^^"
 K MSCLD,MSCHMLST,MSCHMLOC,MSCRSN
 K HOLD,SCH,SCHED,SDL,SGL,ZSB,W,WW,SZZ,STRDT
 ;
 ;
 M PMSG=MSG
REIN K MSG S NULLFLDS="F JJ=0:1:LIMIT S FIELD(JJ)="""""
 K ^UTILITY("DIQ1",$J),DIQ S DA=$P($$SITE^VASITE(),"^")
 I $G(DA) S DIC=4,DIQ(0)="I",DR="99" D EN^DIQ1 S PSOHINST=$G(^UTILITY("DIQ1",$J,4,DA,99,"I")) K ^UTILITY("DIQ1",$J),DA,DR,DIQ,DIC
 S MSG(1)="MSH|^~\&|PHARMACY|"_$G(PSOHINST)_"|||||ORR"
 ;
 S COUNT=1,LIMIT=5 X NULLFLDS D DEM^VADPT S NAME=$G(VADM(1)) K VADM
 S FIELD(0)="PID",FIELD(3)=DFN,FIELD(5)=NAME
 D SEG^PSOHLSN1
 ;
 ;
 S LIMIT=19 X NULLFLDS
 S FIELD(0)="PV1",FIELD(2)="O",FIELD(3)=LOCATION
 D SEG^PSOHLSN1
 ;
 S LIMIT=15 X NULLFLDS
 S FIELD(0)="ORC",FIELD(1)=$S($G(REIN):"SC",1:"OK"),FIELD(2)=PLACER_"^OR",FIELD(3)=PSONVA_"N^PS",FIELD(5)="CM"
 S FIELD(10)=$P(^PS(55,DFN,"NVA",PSONVA,0),"^",11)_"^"_$P(^VA(200,$P(^PS(55,DFN,"NVA",PSONVA,0),"^",11),0),"^")
 ;IHS/MSC/REC/PLS - 08/06/2010 - Change field 5 if there is a status of Reconcile
 I +$P(^PS(55,DFN,"NVA",PSONVA,0),U,6)=97 S FIELD(5)="REC"
 D SEG^PSOHLSN1
 I $G(REIN) S MSG(COUNT)=MSG(COUNT)_"|^^^^DATE OF DEATH DELETED BY MAS.^"
 ;
 S LIMIT=1 X NULLFLDS
 S FIELD(0)="RXO",OI=$P(^PS(55,DFN,"NVA",PSONVA,0),"^")
 S FIELD(1)="^^^"_OI_"^"_$P($G(^PS(50.7,OI,0)),"^")_" "_$P($G(^PS(50.606,+$P($G(^(0)),"^",2),0)),"^")_"^99PSP"
 D SEG^PSOHLSN1
 ;
 S LIMIT=1 X NULLFLDS
 S FIELD(0)="RXE"
 S PSOPSTRT="",X=$P(^PS(55,DFN,"NVA",PSONVA,0),"^",9) I X S PSOPSTRT=$$FMTHL7^XLFDT(X)
 K X S FIELD(1)="^^^"_$G(PSOPSTRT)_"^"
 D SEG^PSOHLSN1
 ;
 D SEND^PSOHLSN1 K FIELDS,LIMIT,PSODSC,PSONVA,OI Q:$G(REIN)
 ;dc change order
DC F OO=0:0 S OO=$O(PMSG(OO)) Q:'OO  I $P(PMSG(OO),"|")="ORC",$P(PMSG(OO),"|",2)="XO" D  Q
 .F XO=0:0 S XO=$O(PMSG(XO)) Q:'XO  I $P(PMSG(XO),"|")="ZRX" S POERR("PSOFILNM")=$P(PMSG(XO),"|",2) Q
 I +$G(POERR("PSOFILNM")),$G(^PS(55,DFN,"NVA",+$G(POERR("PSOFILNM")),0)) S PDFN=DFN D XO^PSOORUTL
 K XO,OO,PMSG
 Q
APSOD ;dc non-va meds because of date of death entry called from psocan3
 N PDA,PDFN ;,POERR("PSOFILNM")
 F PDA=0:0 S PDA=$O(^PS(55,PSODFN,"NVA",PDA)) Q:'PDA  I '$P(^PS(55,PSODFN,"NVA",PDA,0),"^",6),'$P(^(0),"^",7) D
 .S POERR("PSOFILNM")=PDA,PDFN=PSODFN D XO^PSOORUTL
 ;IHS/MSC/REC/PLS - 08/09/10
 ;Update status based on the dosage form
STATU ;EP - If set to miscellaneous, then the status of the order should be RECONCILE
 N MSCPDF
 Q:$G(PSORDITE)']""  ;No pharmacy orderable item
 S MSCPDF=$P(^PS(50.606,$P(^PS(50.7,PSORDITE,0),U,2),0),U)
 I MSCPDF="MISCELLANEOUS" D
 .N FDA
 .S FDA(55.05,PSONVA_","_DFN_",",5)="RECONCILE"
 .D UPDATE^DIE("E","FDA","","")
 Q
