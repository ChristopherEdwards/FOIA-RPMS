PSOORED6 ;BHAM ISC/SAB-edit orders from backdoor ;21-Jun-2012 15:57;PLS
 ;;7.0;OUTPATIENT PHARMACY;**78,104,117,133,1005,1013,1014**;DEC 1997;Build 5
 ;External reference to ^PSDRUG supported by DBIA 221
 ;External reference to ^PS(50.7 supported by DBIA 2223
 ;External reference ^PS(50.606 supported by DBIA 2174
 ; Modified - IHS/CIA/PLS - 06/15/04 - Line DRG+20
 ;                          09/07/04 - Line UPDATE+25
 ;                          09/15/06 - Line UPDATE+7 added
 ;                          10/14/11 - Line UPDATE+11 and UPDATE+28
 ;                          05/22/12 - Line UPDATE+19, UPDATE+56
 ;                          05/31/12 - Line UPDATE+13, UPDATE+38
 ;                          06/21/12 - Line UPDATE+12, UPDATE+40
DRG ;select drug
 S PSORX("EDIT")=1,RX0HLD=RX0
 S PSODRUG("IEN")=$S($G(PSODRUG("IEN"))]"":PSODRUG("IEN"),1:$P(RX0,"^",6)),PSODRUG("NAME")=$S($G(PSODRUG("NAME"))]"":PSODRUG("NAME"),1:$P(^PSDRUG($P(RX0,"^",6),0),"^"))
 D ^PSODRG I PSODRUG("IEN")=$P(RX0,"^",6) K PSORXED("FLD",6)
 D:PSODRUG("IEN")'=$P(RX0,"^",6)  I $G(PSORX("DFLG")) K PSORXED("FLD",6) S PSORXED("DFLG")=1 Q
 .D POST^PSODRG
 .I '$O(^PSRX(PSORXED("IRXN"),1,0)) S PSORXED("FLD",17)=$G(PSODRUG("COST"))
 .I $G(PSORX("DFLG")) K PSORXED("FLD",6),PSODRUG,PSOOIFLG,PSOSIGFL,VALMSG Q
 .D KV S DIR(0)="Y",DIR("B")="NO",DIR("A",1)="You have changed the dispense drug from",DIR("A",2)=$P(^PSDRUG($P(PSORXED("RX0"),"^",6),0),"^")_" to "_$P(^PSDRUG(PSODRUG("IEN"),0),"^")_".",DIR("A")="Do You want to Edit the SIG"
 .D ^DIR K DIR I $D(DIRUT) S PSORX("DFLG")=1 D M1
 .Q:$D(DIRUT)!('Y)
 .S PSOREEDQ=1 D DOLST^PSOORED3,DOSE^PSOORED3 K PSOREEDQ
 .I '$O(PSORXED("DOSE",0)) S PSORX("DFLG")=1 Q
 .D:$G(PSOSIGFL) M2
 S RX0=RX0HLD K RX0HLD I $G(PSODRUG("OI"))=$G(PSOI) D  Q
 .D:$O(^TMP("PSORXDC",$J,0))
 ..W !!,"This edit will discontinue the duplicate Rx & change the dispensed drug!"
 ..K DIR,X,Y S DIR("A")="Do You Want to Proceed",DIR("B")="NO",DIR(0)="Y"
 ..D ^DIR K DIR S:'Y!($D(DIRUT)) PSORXED("DFLG")=1 D:Y DCORD^PSONEW2
 .Q:$G(PSORXED("DFLG"))
 .I PSODRUG("IEN")'=$P(RX0,"^",6) D
 ..S PSORXED("FLD",6)=PSODRUG("IEN"),PSORXED("FLD",39.2)=PSOI
 .S PSORXED("FLD",27)=$G(PSODRUG("NDC"))  ; IHS/CIA/PLS - 06/15/04 - Update NDC
 .S:$G(PSODRUG("TRADE NAME"))]"" PSORXED("FLD",6.5)=PSODRUG("TRADE NAME")
 W !!,"New Orderable Item selected. This edit will create a new prescription!",! D PAUSE^VALM1 S VALMSG="New Orderable Item selected. This edit will create a new prescription!" S (PSOOIFLG,PSOSIGFL)=1
 Q
PSOCOU ;patient counseling
 K DIC,DIQ S DIC=52,DA=PSORXED("IRXN"),DIQ="PSORXED",DR=41 D EN^DIQ1 K DIC,DIQ
 D KV S DIR(0)="52,41" S:$G(PSORXED(52,DA,DR))]"" DIR("B")=PSORXED(52,DA,DR) D ^DIR K DIR,PSORXED(52,DA,DR)
 I $D(DIRUT) K PSORXED("FLD",41) D KV Q
 S PSORXED("FLD",DR)=Y D  K DIRUT
 .I Y D  Q
 ..K DIC,DIQ S DIC=52,DA=PSORXED("IRXN"),DIQ="PSORXED",DR=42 D EN^DIQ1 K DIC,DIQ
 ..K DIR,DIRUT S DIR(0)="52,42" S:$G(PSORXED(52,DA,DR))]"" DIR("B")=PSORXED(52,DA,DR) D ^DIR K DIR,PSORXED(52,DA,DR)
 ..I $D(DIRUT) K PSORXED("FLD",41),DUOUT,DTOUT Q
 ..S PSORXED("FLD",42)=Y
 .S PSORXED("FLD",41)=0,PSORXED("FLD",42)="@"
 Q
PSOI ;select orderable item
 W !!,"Current Orderable Item: "_$P(^PS(50.7,PSOI,0),"^")_" "_$P(^PS(50.606,$P(^(0),"^",2),0),"^")
 S DIC("B")=$P(^PS(50.7,PSOI,0),"^"),DIC="^PS(50.7,",DIC(0)="AEMQZ"
 S DIC("S")="I '$P(^PS(50.7,+Y,0),""^"",4)!($P(^(0),""^"",4)'<DT) N PSOF,PSOL S (PSOF,PSOL)=0 F  S PSOL=$O(^PSDRUG(""ASP"",+Y,PSOL)) Q:PSOF!'PSOL  "
 S DIC("S")=DIC("S")_"I $P($G(^PSDRUG(PSOL,2)),U,3)[""O"",'$G(^(""I""))!($G(^(""I""))'<DT) S PSOF=1" D ^DIC I "^"[X S PSORXED("DFLG")=1 Q
 G:Y<1 PSOI Q:PSOI=+Y
 S PSODRUG("OI")=+Y,PSODRUG("OIN")=Y(0,0) K DIC
 I PSOI'=PSODRUG("OI") W !!,"New Orderable Item selected. This edit will create a new prescription!",! D  K PSHOLDD Q
 .D PAUSE^VALM1,M2
 .S PSHOLDD=$G(PSODRUG("IEN")) K PSODRUG("IEN"),PSODRUG("NAME") S PSODRUG("DEA")="",(PSOOIFLG,PSOSIGFL)=1
 .D DREN^PSOORNW2
 .I $G(PSHOLDD),$G(PSODRUG("IEN")),$G(PSHOLDD)'=$G(PSODRUG("IEN")) D  Q:$G(PSORX("DFLG"))
 ..D FULL^VALM1,POST^PSODRG S VALMBCK="R"
 ..I $G(PSORX("DFLG")) K PSODRUG S PSODRUG("IEN")=$G(PSHOLDD),PSODRUG("NAME")=$P($G(^PSDRUG(PSODRUG("IEN"),0)),"^") K PSOOIFLG,PSOSIGFL S VALMSG=""
 .I '$G(PSODRUG("IEN")) W !!,"DRUG NAME REQUIRED!" D 2^PSOORNW1
 .I '$G(PSODRUG("IEN")) K PSORXED("FLD"),INDEL,^TMP($J,"INS1"),PSOSIGFL,VALMSG S PSORXED("DFLG")=1,VALMSG="Dispense Drug NOT Selected!" Q
 .D KV S DIR(0)="Y",DIR("B")="NO",DIR("A",1)="You have changed the Orderable Item from",DIR("A",2)=$P(^PS(50.7,PSOI,0),"^")_" to "_PSODRUG("OIN")_".",DIR("A")="Do You want to Edit the SIG"
 .D ^DIR K DIR I $D(DIRUT) K PSODRUG("OIN"),PSOOIFLG,PSOSIGFL S PSODRUG("OI")=PSOI,VALMSG="",PSORX("DFLG")=1 Q
 .I 'Y S PSORX("DFLG")=1 Q
 .S PSOREEDQ=1 D DOLST^PSOORED3,DOSE^PSOORED3 K PSOREEDQ
 .I '$O(PSORXED("DOSE",0)) S PSORX("DFLG")=1 Q
 .D:$G(PSOSIGFL) M2
 S PSORXED("FLD",39.2)=PSOI
 Q
UPDATE ;add new data to file
 N APSPRCHK,APSPRFLG S APSPRCHK=0,APSPRFLG=0
 Q:'$G(PSORXED("IRXN"))
 I $O(PSORXED("FLD",0))!($G(^TMP($J,"INS1",0))]"")!($G(INSDEL))!($O(PSORXED("ODOSE",0))) D  G:'Y UPDX
 .K DIR,DIRUT,DTOUT,DUOUT
 .S DIR(0)="Y",DIR("A")="Are You Sure You Want to Update Rx "_$P(^PSRX(PSORXED("IRXN"),0),"^"),DIR("B")="Yes"
 .D ^DIR K DIR I 'Y D M1 Q
 .K X,DIRUT,DUOUT,DTOUT
 S:$O(PSORXED("FLD",0)) ^TMP("APSPPOS",$J,PSORXED("IRXN"))=1  ;IHS/MSC/PLS - 09/15/06
 K Y S DA=PSORXED("IRXN"),DIE="^PSRX(",FLD=0
 F  S FLD=$O(PSORXED("FLD",FLD)) Q:'FLD  D
 .I FLD=1 S APSPRCHK=1  ;IHS/MSC/PLS - 05/22/2012
 .I FLD=8 D  ;IHS/MSC/PLS - 06/21/2012
 ..Q:$$RMNRFL^APSPFUNC(PSORXED("IRXN"))
 ..S APSPRCHK=4
 .;I FLD=9,PSORXED("FLD",FLD)>$P(PSORXED("RX0"),U,9) S APSPRCHK=2  ;IHS/MSC/PLS - 10/14/2011 - Change for update to expiration date
 .I FLD=9 D  ;IHS/MSC/PLS - 05/31/2012 - APSP 1014
 ..Q:PSORXED("FLD",FLD)=$P(PSORXED("RX0"),U,9)  ;no change in value
 ..I PSORXED("FLD",FLD)>$P(PSORXED("RX0"),U,9) S APSPRCHK=2 Q
 ..I PSORXED("FLD",FLD)<$P(PSORXED("RX0"),U,9) S APSPRFLG=1 Q
 .I FLD=12!(FLD=24)!(FLD=35) D  Q
 ..I FLD=12,PSORXED("FLD",12)="@" S $P(^PSRX(DA,3),"^",7)="" Q
 ..I FLD=12,PSORXED("FLD",12)]"" S $P(^PSRX(DA,3),"^",7)=PSORXED("FLD",12) Q
 ..I FLD=24,PSORXED("FLD",24)="@" S $P(^PSRX(DA,2),"^",4)="" Q
 ..I FLD=24,PSORXED("FLD",24)]"" S $P(^PSRX(DA,2),"^",4)=PSORXED("FLD",24) Q
 ..I FLD=35,PSORXED("FLD",35)="@" S $P(^PSRX(DA,"MP"),"^")="" Q
 ..I FLD=35,PSORXED("FLD",35)]"" S $P(^PSRX(DA,"MP"),"^")=PSORXED("FLD",35) Q
 .I FLD=114 D  Q
 ..I PSORXED("FLD",114)="@" K ^PSRX(DA,"INS"),^PSRX(DA,"INS1")
 ..I PSORXED("FLD",114)'="@" D
 ...S ^PSRX(DA,"INS")=PSORXED("FLD",114)
 ...S X=PSORXED("FLD",114) D SIG^PSOHELP Q:$G(INS1)']""
 ...S PSORXED("SIG",1)=$E(INS1,2,9999999) K ^PSRX(DA,"INS1")
 ...S ^PSRX(DA,"INS1",0)="^52.0115^1^1^"_DT_"^^"
 ...S ^PSRX(DA,"INS1",1,0)=PSORXED("SIG",1)
 ..D DOLST^PSOORED3 K:PSORXED("FLD",114)="@" PSORXED("SIG") D EN^PSOFSIG(.PSORXED),UPDSIG^PSOORED3
 .; IHS/CIA/PLS - 09/07/04 - Fix for Man Expiration Date
 .I FLD=29,PSORXED("FLD",29)="" S $P(^PSRX(DA,2),"^",11)="" Q
 .S DR=FLD_"////"_PSORXED("FLD",FLD) D ^DIE
 .I FLD=4 D UDPROV^PSOOREDT Q
 .I APSPRCHK=4 D
 ..I '$P($G(PSORXED("RX2")),U,13) S APSPRCHK=0 Q  ;Original dispense not released
 ..N LASTRFL
 ..S LASTRFL=$O(^PSRX(PSORXED("IRXN"),1,$C(1)),-1)
 ..I 'LASTRFL S APSPRCHK=4_U_$P($G(PSORXED("RX2")),U,13) Q
 ..I '$P($G(^PSRX(PSORXED("IRXN"),1,LASTRFL,0)),U,18) S APSPRCHK=0 Q  ;Last refill not released
 ..S APSPRCHK=4_U_$P($G(^PSRX(PSORXED("IRXN"),1,LASTRFL,0)),U,18)
 .I FLD=9,APSPRFLG D  ;IHS/MSC/PLS - 05/31/2012
 ..Q:$$RMNRFL^APSPFUNC(PSORXED("IRXN"))  ;remaining fills no action required
 ..I '$$RMNRFL^APSPFUNC(PSORXED("IRXN")) D
 ...Q:'$P($G(PSORXED("RX2")),U,13)  ;Original dispense not released
 ...N LASTRFL
 ...S LASTRFL=$O(^PSRX(PSORXED("IRXN"),1,$C(1)),-1)
 ...I 'LASTRFL S APSPRCHK=3_U_$P($G(PSORXED("RX2")),U,13) Q
 ...Q:'$P($G(^PSRX(PSORXED("IRXN"),1,LASTRFL,0)),U,18)  ;Last refill not released
 ...S APSPRCHK=3_U_$P($G(^PSRX(PSORXED("IRXN"),1,LASTRFL,0)),U,18)
 .S APSPRFLG=0
 I $G(INSDEL) K ^PSRX(DA,"INS"),^PSRX(DA,"INS1") D DOLST^PSOORED3 K PSORXED("SIG") D EN^PSOFSIG(.PSORXED),UPDSIG^PSOORED3 G UPDX
 I $O(^TMP($J,"INS1",0)) D
 .K ^PSRX(DA,"INS"),^PSRX(DA,"INS1"),DD,PSORXED("SIG")
 .F I=0:0 S I=$O(^TMP($J,"INS1",I)) Q:'I  S (PSORXED("SIG",I),^PSRX(DA,"INS1",I,0))=^TMP($J,"INS1",I,0),DD=$G(DD)+1
 .S ^PSRX(DA,"INS1",0)=^TMP($J,"INS1",0)
 .I DD=1 S ^PSRX(DA,"INS")=^PSRX(DA,"INS1",1,0)
 .D DOLST^PSOORED3,EN^PSOFSIG(.PSORXED),UPDSIG^PSOORED3
 ;IHS/MSC/PLS - 10/14/2011 - Update expiration date when more fills are added
 I APSPRCHK D
 .N CS,DE,EXTEXP,X2,NEXPDT,DA,DIE,ISSDT,DRG
 .S CS=0
 .;IHS/MSC/PLS - 05/22/2012 - Change made to support Issue Date edit
 .;S DE=+$E(PSODRUG("DEA"),1)
 .;I DE>1,DE<6 S CS=1 S:DE=2 $P(CS,U,2)=1
 .S DRG=$P(PSORXED("RX0"),U,6)
 .S CS=$$ISSCH^APSPFNC2(DRG,"2345")
 .S $P(CS,U,2)=$$ISSCH^APSPFNC2(DRG,"2")
 .S EXTEXP=$$GET1^DIQ(50,DRG,9999999.08)
 .S ISSDT=$P(^PSRX(PSORXED("IRXN"),0),U,13)
 .S X2=$S(EXTEXP:EXTEXP,$P(CS,U,2):184,CS:184,1:366)
 .;IHS/MSC/PLS - 05/22/2012
 .;S NEXPDT=$$FMADD^XLFDT($P(PSORXED("RX0",U,13),X2)
 .S NEXPDT=$$FMADD^XLFDT(ISSDT,X2)
 .;IHS/MSC/PLS - 05/22/2012 - Quit if Issue Date edited, prescription has been released and no remaining dispenses
 .I +APSPRCHK=1,$P(PSORXED("RX2"),U,13),'$$RMNRFL^APSPFUNC(PSORXED("IRXN")) Q
 .I +APSPRCHK=3,$$EXPDT^APSPAUTO(PSORXED("IRXN"),1,+$P($P(APSPRCHK,U,2),".")) S NEXPDT=$$EXPDT^APSPAUTO(PSORXED("IRXN"),1,+$P($P(APSPRCHK,U,2),"."))
 .I +APSPRCHK=4,$$EXPDT^APSPAUTO(PSORXED("IRXN"),1,+$P($P(APSPRCHK,U,2),".")) S NEXPDT=$$EXPDT^APSPAUTO(PSORXED("IRXN"),1,+$P($P(APSPRCHK,U,2),"."))  ;IHS/MSC/PLS - 06/21/2012
 .S DA=PSORXED("IRXN"),DIE="^PSRX("
 .S DR="26///"_NEXPDT D ^DIE
 .S APSPRCHK=0
UPDX K DIE,DA,DR,FLD,X,Y,PSORXED("FLD"),DD,^TMP($J,"INS1")
KV K DIR,DIRUT,DTOUT,DUOUT
 Q
UPD ;updates dosing array
 S HENT=ENT
UPD1 ;
 I $G(PSORXED("CONJUNCTION",(HENT+1)))]"" S PSORXED("CONJUNCTION",HENT)=PSORXED("CONJUNCTION",(HENT+1)) D  G UPD
 .K PSORXED("CONJUNCTION",(HENT+1))
 .I $D(PSORXED("DOSE",(HENT+2))) D
 ..S PSORXED("DOSE",(HENT+1))=PSORXED("DOSE",(HENT+2))
 ..S PSORXED("ODOSE",(HENT+1))=$G(PSORXED("ODOSE",(HENT+2)))
 ..S PSORXED("DOSE ORDERED",(HENT+1))=$G(PSORXED("DOSE ORDERED",(HENT+2)))
 ..S PSORXED("UNITS",(HENT+1))=$G(PSORXED("UNITS",(HENT+2)))
 ..S PSORXED("NOUN",(HENT+1))=$G(PSORXED("NOUN",(HENT+2)))
 ..S PSORXED("DURATION",(HENT+1))=$G(PSORXED("DURATION",(HENT+2)))
 ..S PSORXED("CONJUNCTION",(HENT+1))=$G(PSORXED("CONJUNCTION",(HENT+2)))
 ..S PSORXED("ROUTE",(HENT+1))=$G(PSORXED("ROUTE",(HENT+2)))
 ..S PSORXED("SCHEDULE",(HENT+1))=$G(PSORXED("SCHEDULE",(HENT+2)))
 ..S PSORXED("VERB",(HENT+1))=$G(PSORXED("VERB",(HENT+2)))
 ..K PSORXED("DOSE",(HENT+2)),PSORXED("ODOSE",(HENT+2)),PSORXED("DOSE ORDERED",(HENT+2))
 ..K PSORXED("UNITS",(HENT+2)),PSORXED("NOUN",(HENT+2)),PSORXED("DURATION",(HENT+2)),PSORXED("ROUTE",(HENT+2)),PSORXED("SCHEDULE",(HENT+2)),PSORXED("VERB",(HENT+2))
 .S HENT=HENT+1
 F I=0:0 S I=$O(PSORXED("DOSE",I)) Q:'I  S SENT=$G(SENT)+1
 Q
M1 D M1^PSOOREDX
 Q
M2 D M2^PSOOREDX
 Q
