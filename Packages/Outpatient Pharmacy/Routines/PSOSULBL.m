PSOSULBL ;BHAM ISC/RTR,SAB-Print Suspended labels ;31-Aug-2009 10:35;SM
 ;;7.0;OUTPATIENT PHARMACY;**139,173,1008**;DEC 1997
 ;External reference ^PS(55 supported by DBIA 2228
 ;Modified - IHS/MSC/PLS - 02/12/09 - Calls to DQ^PSOLBL changed to DQ^APSPLBL
 ;                         08/31/09 - Added setting of PSOPULL flag so that Dispense and Fill dates are updated
 K PDUZ,REPRINT G ^PSOSULB1
BEG ;
 K PSORUNIN,PSORETRY
 S PSORUNIN="PSOSUSP"_($G(PSOSITE))
 L +@PSORUNIN:10 I '$T D
 . F PSORETRY=1:1:120 L +@PSORUNIN:60 I $T Q  ;waits a Maximum of 2 hours before continuing
 . Q
 K ^UTILITY($J,"PSOPRO") S PSOSEQ=1 F DFN=0:0 S DFN=$O(^PS(52.5,"AC",DFN)) Q:'DFN  D  D:'PSRT PID^VADPT6 D CHKDEAD D:'DEAD&($G(PSOSFLAG)) PRT
 .S PSOSFLAG=0 F ZZ=0:0 S ZZ=$O(^PS(52.5,"AC",DFN,ZZ)) Q:'ZZ!$G(PSOSFLAG)  I ZZ'>PRTDT S PSOSFLAG=1
 D PPL
 D:$D(^UTILITY($J,"PSOPRO"))&($P(PSOPAR,"^",8)) PROF
 G EXIT
PRT F SDT=0:0 S SDT=$O(^PS(52.5,"AC",DFN,SDT)) D:SDT CHK Q:'SDT
 Q
EXIT ;
 K ^TMP($J)
 I $D(PSORUNIN) L -@PSORUNIN
 D ^%ZISC
 K %,%ZIS,CNT,COM,DA,DEAD,DFN,DIRUT,DTTM,G,HDPPL,JJ,JJJ,JJJJ,PDUZ,IOP,ORD,PFIOQ,PSLION,PSRT,POP,PRF,PRTDT,PSLIO,PSNP,PSODBQ,PSOSEQ,PSOSFLAG,PSOSU,PSOTIME,PSOOUT,PSOPRFLG,PSOSEQ,PSOSUSPR,PSSPND,PST,PTL,PPLHLD,PSFNIEN,ZTSK
 K PSORUNIN,PSORETRY,PSRTONE,PSSRT,PSUSDEA,RF,RFCNT,RX,SDT,SFN,SREC,STOP,SUSPT,VADM,VAPA,X,X1,X2,XAK,XDATE,Y,Z,ZZ,WWW,PSDDDATE,SINRX,RXPR,RXPR1,GGGG,XXX,ZII,ZTDESC,ZTRTN,ZTSAVE,RRRR,RXRP,RXRP1,RXFL S:$D(ZTQUEUED) ZTREQ="@" Q
CHK I SDT'>XDATE D TMP Q
 Q
TMP F SFN=0:0 S SFN=$O(^PS(52.5,"AC",DFN,SDT,SFN)) Q:'SFN  I +$P($G(^PS(52.5,+SFN,0)),"^",6)=$G(PSOSITE),'$G(^("P")),$P(^PSRX($P(^(0),"^"),0),"^",2)=DFN,$D(^DPT(DFN,0)),$P(^PSRX($P(^PS(52.5,SFN,0),"^"),"STA"),"^")<9  D
 .I +$P($G(^PSRX($P(^PS(52.5,SFN,0),"^"),2)),"^",6)<DT,+$P($G(^("STA")),"^")<11 S RXREC=$P(^PS(52.5,SFN,0),"^") D  S DIE=52,DA=$P(^PS(52.5,SFN,0),"^"),DR="100///"_11 D ^DIE S DA=SFN,DIK="^PS(52.5," D ^DIK K DIE,DIK,DA Q
 ..D EX^PSOSUTL K RXREC Q
 .I PSRT="D" S PSSRT=$S($G(PSRTONE)="I":VA("PID"),1:$P(^DPT(DFN,0),"^")_$P(^(0),"^",9))  S PSUSDEA=$P($G(^PS(52.5,SFN,0)),"^",10) S SRT=$S(PSUSDEA["A"!(PSUSDEA["C"):"A-"_PSSRT,PSUSDEA["S":"S-"_PSSRT,1:"Z-"_PSSRT)
 .I PSRT'="D" S SRT=$S(PSRT:$P(^DPT(DFN,0),"^")_$P(^(0),"^",9),1:VA("PID"))
 .S ^TMP($J,SRT,SFN)=+^PS(52.5,SFN,0)
 Q
PPL K PPL,PPL1 S ORD="" F  S ORD=$O(^TMP($J,ORD)) Q:ORD=""  D PPL1
 Q
PPL1 S (PSOPRFLG,SUSPT)=1 S:$D(PSOPROP) PFIO=PSOPROP
 N PSOPULL  ;IHS/MSC/PLS - 08/31/09
 S:'$D(PDUZ) PDUZ=DUZ K RXPR,RXPR1,PPL F SFN=0:0 S SFN=$O(^TMP($J,ORD,SFN)) Q:'SFN  I $D(^PS(52.5,SFN,0)) S SINRX=$P($G(^PS(52.5,SFN,0)),"^") D
 .I $L($G(PPL))<240 S PPL=$P(^TMP($J,ORD,SFN),"^")_","_$G(PPL) S RXPR(SINRX)=$P(^PS(52.5,SFN,0),"^",5),RXFL(SINRX)=$P($G(^PS(52.5,SFN,0)),"^",13) S:$P(^PS(52.5,SFN,0),"^",12) RXRP(SINRX)=1
 .E  S PPL1=$P(^TMP($J,ORD,SFN),"^")_","_$G(PPL1) S RXPR1(SINRX)=$P(^PS(52.5,SFN,0),"^",5),RXFL(SINRX)=$P($G(^PS(52.5,SFN,0)),"^",13) S:$P(^PS(52.5,SFN,0),"^",12) RXRP1(SINRX)=1
 .S DFN=$P(^PS(52.5,SFN,0),"^",3) I $P(PSOPAR,"^",8),'$D(^PSRX($P(^PS(52.5,SFN,0),"^"),1)),'$G(RXPR(SINRX)),'$G(RXPR1(SINRX)) S PSOPRFLG=0
 S PSNP=$S($P(PSOPAR,"^",8):1,1:0) I $G(PPL) S PPLHLD=$G(PPL1),HDPPL=PPL K PPL1 S (PSODBQ,PSOSUSPR)=1 F GGGG=0:0 S GGGG=$O(RXPR(GGGG)) Q:'GGGG  K:'$G(RXPR(GGGG)) RXPR(GGGG)
 ;IHS/MSC/PLS - 02/12/09 - Changed to call IHS label routine
 ;I $G(PPL) D DQ^PSOLBL,SEQ D:'$G(PSOPRFLG)
 S PSOPULL=1
 I $G(PPL) D DQ^APSPLBL,SEQ D:'$G(PSOPRFLG)
 .I $G(PSOPROP)'=$G(PSLION) S ^UTILITY($J,"PSOPRO",DFN)="" Q
 .D DQ^PSOPRFSS
 I $G(PPLHLD) K RXPR S (PPL,HDPPL)=PPLHLD,(PSODBQ,PSOSUSPR)=1,PSNP=0 S:'$D(PDUZ) PDUZ=DUZ F XXX=0:0 S XXX=$O(RXPR1(XXX)) Q:'XXX  S:$G(RXPR1(XXX)) RXPR(XXX)=RXPR1(XXX)
 I $G(PPLHLD) F RRRR=0:0 S RRRR=$O(RXRP1(RRRR)) Q:'RRRR  S:$D(RXRP1(RRRR)) RXRP(RRRR)=1
 ;IHS/MSC/PLS - 02/12/09 - Changed to call IHS label routine
 ;I $G(PPLHLD) D DQ^PSOLBL,SEQ D:'$G(PSOPRFLG)
 I $G(PPLHLD) D DQ^APSPLBL,SEQ D:'$G(PSOPRFLG)
 .I $G(PSOPROP)'=$G(PSLION) S ^UTILITY($J,"PSOPRO",DFN)="" Q
 .D DQ^PSOPRFSS
 K PPL,PPL1,PPLHLD,RXPR,RXPR1,RXFL Q
SEQ ;
 S SQCOUNT=0 F JJJ=1:1:$L(HDPPL) S:$E(HDPPL,JJJ)="," SQCOUNT=SQCOUNT+1
 F JJJJ=1:1:SQCOUNT S PSFNIEN=$P(HDPPL,",",JJJJ) D:PSFNIEN
 .S PSFNIEN=$O(^PS(52.5,"B",PSFNIEN,0)) I PSFNIEN D
 ..S $P(^PS(52.5,PSFNIEN,0),"^",11)=PSOSEQ,PSOSEQ=PSOSEQ+1 S:$P(^PS(52.5,PSFNIEN,0),"^",8)&($P(^(0),"^",9))&($P(^(0),"^",6)) ^PS(52.5,"AS",$P(^PS(52.5,PSFNIEN,0),"^",8),$P(^(0),"^",9),$P(^(0),"^",6),$P(^(0),"^",11),PSFNIEN)=""
 Q
CHKDEAD D DEM^VADPT I VADM(1)="" S DEAD=0 Q
 I VADM(6)="" S DEAD=0 Q
 S PSDDDATE=$P(VADM(6),"^",2) F WWW=0:0 S WWW=$O(^PS(55,DFN,"P",WWW)) Q:'WWW  I $D(^PS(55,DFN,"P",WWW,0)),$P($G(^(0)),"^") S (DA,RXREC)=$P(^(0),"^") S SFN=$O(^PS(52.5,"B",RXREC,0)) I SFN,$D(^PS(52.5,SFN,0)) S RX=$P(^(0),"^") D DEAD
 Q
DEAD S $P(^PSRX(RX,"STA"),"^")=12,COM="Died ("_$G(PSDDDATE)_")",DA(1)=RX
 S DEAD=1 D ARECD^PSOSUTL S DIK="^PS(52.5,",DA=SFN D ^DIK K DIK
 Q
PROF ;
 S ZTRTN="PRPROF^PSOSULBL",ZTDESC="PRINT PROFILES FROM SUSPENSE",ZTDTH=$H,ZTIO=PSOPROP
 S ZTSAVE("^UTILITY($J,""PSOPRO"",")="",ZTSAVE("PSOPAR")="",ZTSAVE("PSODTCUT")="",ZTSAVE("PSOSITE")="",ZTSAVE("PSOPRPAS")="" D ^%ZTLOAD Q
PRPROF ;
 F LLL=0:0 S LLL=$O(^UTILITY($J,"PSOPRO",LLL)) Q:'LLL  I $D(^DPT(LLL,0)) S DFN=LLL D DQ^PSOPRFSS
 K PSOPAR,PSODTCUT,PSOSITE,PSOPRPAS,LLL,DFN,^UTILITY($J,"PSOPRO") D ^%ZISC S:$D(ZTQUEUED) ZTREQ="@"
 Q
