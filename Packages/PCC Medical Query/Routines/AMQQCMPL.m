AMQQCMPL ; IHS/CMI/THL - SETS SEARCH CODE ; 21 Mar 2011  11:54 AM
 ;;2.0;IHS PCC SUITE;**6**;MAY 14, 2009;Build 11
 ;-----
 K AMQQKGNO,AMQQUSQN,AMQQUQQN,AMQQSQAA,AMQQUSQL,AMQQXSQF S AMQQTOT=0
 I '$D(AMQQNOET) S X="ERR^AMQQCMPL",@^%ZOSF("TRAP")
 I $$VERSION^%ZOSV(1)["Cache" S:'$D(AMQQNOET) AMQQETRP=$ZT,X="ERR^AMQQ",@^%ZOSF("TRAP")
 I $$VERSION^%ZOSV(1)'["Cache" S:'$D(AMQQNOET) AMQQETRP=$ZT,X="ERR^AMQQ",@^%ZOSF("TRAP")
 I $D(^UTILITY("AMQQ OR",$J)) D ^AMQQCMPK
 I $D(AMQQXX) D ^AMQQCMP1 G:$D(AMQQQUIT) EXIT D ^AMQQCMP2,@AMQV("OPTION") G EXIT
RUN K AMQQRERF,AMQQQUIT
 D OUT^AMQQOPT
 I $D(AMQQQUIT) G EXIT
 I '$D(AMQQCPLF) D ^AMQQCMP1 G:$D(AMQQQUIT) EXIT D ^AMQQCMP2
DOIT ; ENTRY POINT FROM AMQQQE1
 D @AMQV("OPTION")
 I $D(AMQQCPLF)!$D(AMQQQUIT),$G(AMQV("OPTION"))'="LIST" G RUN
EXIT K Q,AMQQHOLD,AMQQLINO,AMQQFVAR,AMQQVALU,AMQQVSIT,AMQQTOT,AMQQSF,AMQQAG,AMQT,AMQP,X,X1,X2,N,G,AMQQCPLF,AMQQMULL,AMQQMUNV,AMQQMUFV,AMQQOV,AMQQXX,AMQQDIBT,AMQQSQFN,AMQQSQ1,AMQQAFNN,%,%Y,AMQQFFF,AMQQ("BP COHORT FLG")
 I $G(AMQQETRP)]"" S $ZT=AMQQETRP K AMQQETRP
 Q
 ;
LIST ; ENTRY POINT FROM AMQQCMP0
 I $D(AMQQYY(0)) X AMQV(0) Q
 I '$D(AMQQXX),$E(IOST,1,2)'="P-",AMQV("OPTION")'="COUNT" W !! D WAIT^DICD I $G(AMQQCCLS)="P" D
 .I '$G(AMQQDLIM) W !!!,"Please note:  Patients whose names are marked with an ""*"" may have aliases.",!!! H 2
 I $G(AMQQDLIM) D
 .W !!!,"Please turn on your screen capture capability",!,"to capture '^' (up hat) delimited output.",!!
 .K DIR
 .S DIR(0)="E"
 .D ^DIR
 .K DUOUR,DTOUT,DIRUT,DIR
 .;CMI/GRL  prompt for device with HOME default
 .;D PRINT^AMQQSEC E  W "  <= Not a secure device!!",*7 G DEV
 .W !!
 .S %ZIS="M"  ;force prompt for Rt margin
 .S %ZIS("B")="HOME"
 .D ^%ZIS
 .S AMQQIOP=IO
 .I POP S AMQQQUIT="" Q
 X AMQV(0)
LISTEND ; ENTRY POINT FROM AMQQQE1
 K AMQQCPLF
 I $D(AMQQQUIT) Q
 I $E(IOST,1,2)'="P-" D  Q
 .W:'$G(AMQQDLIM) !,"Total: ",+$G(AMQQTOT)
 .W:$G(AMQQDLIM) !!,"Screen capture content completed.",!,"Please turn off screen capture.",!
 .S DIR(0)="E"
 .D ^DIR
 .K DUOUT,DTOUT,DIRUT,DIR,AMQQDLIM
 W @IOF,@IOF
 D ^%ZISC
 Q
 ;
COHORT I '$D(AMQQNOET),$D(^%ZOSF("TRAP")) S X="ERRC^AMQQCMPL",@^%ZOSF("TRAP")
 I $D(AMQQEN31) D ^AMQQCMPC Q
 D ^AMQQCMP0
 Q
 ;
PRINT D ^AMQQCMPP
 K AMQQCPLF
 Q
 ;
COUNT D COUNT^AMQQCMPP
 K AMQQCPLF
 Q
 ;
SAVE D ^AMQQCMPS
 Q
 ;
OUTPUT ; ENTRY POINT FROM AMQQENQ
 D OUT^AMQQOPT I $D(AMQQQUIT) Q
 D @AMQV("OPTION")
 Q
 ;
ERRC I $D(AMQQDIBT) K ^DIBT(AMQQDIBT,1)
ERR I '$D(AMQQNOET) X "I $P($ZE,"">"")=""<INTERRUPT""!($ZE[""-CTRAP"")" I  D ^%ZISC W !!,"Session terminated...",!! H 2 S AMQQQUIT="" D EXIT S AMQQQUIT="" Q
 I '$D(AMQQNOET) X "I $P($ZE,"">"")=""<INRPT""!($ZE[""-CTRAP"")" I  D ^%ZISC W !!,"Session terminated...",!! H 2 S AMQQQUIT="" D EXIT S AMQQQUIT="" Q
 I $E(IOST,1,2)="C-" W !!,"ERROR DETECTED...SESSION ABORTED...SUSPECT MISSING DATA...NOTIFY SITE MANAGER",!!,*7 H 3 D ^%ZISC,@^%ZOSF("ERRTN")
 I $E(IOST,1,2)'="C-" D ^%ZISC
 D EXIT,EXIT^AMQQ Q
 Q
 ;
STORE D STORE^AMQQQE
 I $D(AMQQQUIT) Q
 D ^AMQQCMPS
 S AMQQCPLF=""
 K AMQV("OPTION")
 Q
 ;
MAIL D MAILX^AMQQRML
 Q
AGE D BUCKET^AMQQRMA
 Q
WORK D WORK^AMQQRMD
 Q
MONTH D MON^AMQQRMM
 Q
TIME D TIME^AMQQRMT
 Q
HSUM D HSUM^AMQQRMH
 Q
EMAN D ^AMQQEMAN
 Q
