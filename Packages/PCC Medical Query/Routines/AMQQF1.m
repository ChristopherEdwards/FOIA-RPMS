AMQQF1 ; IHS/CMI/THL - MORE ANALYTIC FUNCTIONS ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;-----
TEST N T
 S T=$T
 I '$D(AMQQNOT)=T K ^UTILITY("AMQQ",$J,AMQQAG,AMQQUATN,I)
EXIT Q
 ;
COMP S X1=Z
 S X2=$P(AMQQCOMP,";")
 D C^%DTC
 S A=X
 S X1=Z
 S X2=$P(AMQQCOMP,";",2)
 D C^%DTC
 S B=X
 Q
 ;
CDOB N X,Y,Z,%,I,A,B
 S Z=$P(^DPT(AMQP(0),0),U,3)
 I Z D COMP
 F I=0:0 S I=$O(^UTILITY("AMQQ",$J,AMQQAG,AMQQUATN,I)) Q:'I  S Y=$P(^(I),U,2) D CDOB1,TEST
 K AMQQNOT
 Q
 ;
CDOB1 I Z="" Q
 I Y<A!(Y>B)
 Q
 ;
CDOD N X,Y,Z,%,I,A,B
 S Z=""
 I $D(^DPT(AMQP(0),.35)) S Z=+^(.35) I Z D COMP
 F I=0:0 S I=$O(^UTILITY("AMQQ",$J,AMQQAG,AMQQUATN,I)) Q:'I  S Y=$P(^(I),U,2) D CDOD1,TEST
 K AMQQNOT
 Q
 ;
CDOD1 I Z="" Q
 I Y<B!(Y>A)
 Q
 ;
CAGE N X,Y,Z,%,I,A,B
 S X1=$P(^DPT(AMQP(0),0),U,3)
 I X1="" K X1 S Z="" G CAGET
 S X2=$P(AMQQCOMP,";",4)
 D C^%DTC
 S Z=X
 D COMP
CAGET F I=0:0 S I=$O(^UTILITY("AMQQ",$J,AMQQAG,AMQQUATN,I)) Q:'I  S Y=$P(^(I),U,2) D CAGE1,TEST
 K AMQQNOT
 Q
 ;
CAGE1 I Z="" Q
 I Y<A!(Y>B)
 Q
 ;
SUB N AMQQSQFS,AMQQSQFN,AMQQSQFP,I
 S AMQQSQFS=$P(AMQQCOMP,";")
 S AMQQSQFN=$P(AMQQCOMP,";",2)
 S AMQQSQFP=$P(AMQQCOMP,";",3)
 N AMQQCOMP
 F I=0:0 S I=$O(^UTILITY("AMQQ",$J,AMQQAG,AMQQUATN,I)) Q:'I  S AMQQHIT=^(I) D SUB1,TEST
 K AMQQNOT
 Q
 ;
SUB1 N I
 I AMQQSQFN N AMQP,AMQT S AMQP(AMQQSQFN)=$P(AMQQHIT,U,AMQQSQFP) G SUBX
 S AMQQDPT=AMQP(0)
 N AMQQ,AMQQGR,AMQQID,AMQQST,AMQQFIN,AMQQLAST,AMQQVAL,AMQQMLT,AMQQT,AMQQIDX,AMQQIDN,AMQQIDT,AMQQX,AMQQITR,AMQQAFNO,AMQQVDAT,AMQQVNO,AMQQLCNT,AMQQVAL1,AMQQVAL2,AMQQMULZ,AMQQLCOF
 N AMQQTAX,AMQQNNA,AMQQCPG1,AMQQVAL3,AMQQVAL4,AMQQBOOL,AMQQB,AMQQMSS,AMQQMPC,AMQQSTRT,AMQQFVAR,AMQQAG,AMQQSQVS,AMQQUATN,AMQQNVAR,AMQQT,AMQQUSQN,AMQT,AMQP,AMQQSQVN,AMQQSPEC
 S AMQQAG="SAG"
 S AMQP(0)=AMQQDPT
 S AMQQSQVS=$P(AMQQHIT,U,3)
 I AMQV("QQ",AMQQSQFS,1)[";+0;+0;" S AMQQSQVN=AMQQSQVS
SUBX K AMQQDPT,AMQQHIT
 X AMQV("QQ",AMQQSQFS,1)
 Q
 ;
BP N %,I,Z,A,B,C,D,E
 F I=0:0 S I=$O(^UTILITY("AMQQ",$J,AMQQAG,AMQQUATN,I)) Q:'I  S %=$P(^(I),U) D BP1,TEST
 Q
 ;
BP1 S A=+$P(%,"/")
 S B=+$P(%,"/",2)
 S %=AMQQCOMP
 S C=$P(%,"~")
 S D=$P(%,"~",2)
 S E=$P(%,"~",3)
 S Z="I "_A_$P(C,":")_$P(C,":",2)_E_"("_B_$P(D,":")_$P(D,":",2)_")"
 X Z
 I '$T
 Q
 ;
