AMQQGTX0(X,AMQQGTX,Y) ; IHS/CMI/THL - PROGRAMMER CALL TO CREATE TAXONOMIES AND/OR HAVE ARRAYS OF VALUES RETURNED ; [ 03/03/2009  9:28 AM ]
 ;;2.0;IHS PCC SUITE;;MAR 3, 2009
 ;-----
 ;FIRST LINE PARAMETER PASS OKAY'ED BY SAC COMMITTEE TO ALLOW OTHER PACKAGES TO CHANGE THEIR CALLS
 ;
EN ; - ENTRY POINT - Generation of taxonomy by end user
 ; X=dfn attribute in QMAN DICTIONARY OF TERMS file
 ; AMQQGTX=array for values to passed back in 
 ; Y=dfn of the Taxonomy
 I $G(Y),$G(AMQQGTX)]"" D TAX Q
 I '$D(X)!'$D(AMQQGTX) W !,"Required variables not passed" Q
 I '$P($G(^AMQQ(5,X,0)),U,14) W !,"Unacceptable QMAN term.  Taxonomy creation not allowed!" Q
 S AMQQATNM=$P(^AMQQ(5,X,0),U)
 D EN1^AMQQTX
EN1 I $D(^UTILITY("AMQQ TAX",$J)) S %=0  F  S %=$O(^UTILITY("AMQQ TAX",$J,1,%)) Q:%=""  S @(AMQQGTX_""""_%_""")")=""
 K AMQQCCLS,AMQQMULT,AMQQQUIT,AMQQCNAM,AMQQQ,AMQQURGN,DR,DIE,AMQQDF,^UTILITY("AMQQ TAX",$J),AMQQGTX,AMQQATNM
 Q
 ;
PEP(X,AMQQGTX,Y) ; - PUBLISHED ENTRY POINT - Generation of taxonomy by end user
 D EN
 Q
TAX ;EVALUATE TAXONOMY
 K ^UTILITY("AMQQ TAX",$J)
 Q:'$D(^ATXAX(+Y,0))  S %=^(0)
 S AMQQLINK=$P(%,U,12)
 S AMQQTLOK=$P(%,U,15)
 Q:'AMQQLINK!'AMQQTLOK
 N J,K,L
 S J=0
 F  S J=$O(^AMQQ(5,J)) Q:'J!(J>999)  S K=^(J,0),L=$P(K,U,14),K=$P(K,U,5) S:K=AMQQLINK&L M(K,L)=""
 S AMQQTAXT=$O(M(AMQQLINK,0))
 Q:'AMQQTAXT
 S AMQQHILO="^UTILITY(""AMQQ"",$J,""HILO"")"
 S:'$G(AMQQURGN) AMQQURGN=1
 S AMQQTLOK=$G(^DIC(+AMQQTLOK,0,"GL"))
 I AMQQTLOK["," S AMQQTGBL=$P(AMQQTLOK,",")_")"
 E  S AMQQTGBL=$P(AMQQTLOK,"(")
 Q:$G(AMQQTGBL)=""
 N AMQQXX
 S AMQQXX=""
 D REST^AMQQTX0
 D EN1
 Q
