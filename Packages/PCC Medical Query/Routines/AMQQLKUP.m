AMQQLKUP ; IHS/CMI/THL - LOOKS AT STANDARD TABLE OF CLINICAL ENTRIES FOR DFN ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
EN ; - EP - FROM AMQQMULT
 N AMQQLK,AMQQLK1,AMQQPTR,AMQQPTG,AMQQTMP,AMQQL,AMQQDFNS
 S AMQQLK=@(U_AMQQGR_"(0)")
 S AMQQLK1=+$P(AMQQLK,U,2)
 S AMQQPTR=+$P($P(^DD(AMQQLK1,.01,0),U,2),"P",2)
 I $E($P(^DIC(AMQQPTR,0,"GL"),"^",2),1,4)'="AUTT" Q
 S AMQQPTG=^DIC(AMQQPTR,0,"GL")
 ; Find DFN of AUTT entry given standard IHS code
 S AMQQDFNS=""
 F AMQQL=1:1 S AMQQTMP=$P(AMQQID,":",AMQQL) Q:AMQQTMP=""  D
 .S AMQQTMP=$O(@(AMQQPTG_"""C"","""_AMQQTMP_""","""")"))
 .S AMQQDFNS=AMQQDFNS_AMQQTMP_":"
 S AMQQLKUP(AMQQGR,AMQQID)=$E(AMQQDFNS,1,$L(AMQQDFNS)-1)
 Q
