AMQQSQ ; IHS/CMI/THL - SUBQUERY MANAGER ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;-----
 ; WHEN YOU CALL THIS ROUTINE, YOU MUST HAVE THE FOLLOWING VARIABLES:
 ;   AMQQSQAN (SQ SUBJECT)
 ;   AMQQUSQL (LEVEL)
 ;   AMQQSQAA (PARENT SQ OR AMQQUATN)
 ;   AMQQSQSN (SUBJECT NUMBER)
 ;   AMQQSQST (SUBJECT TYPE)
 ;   AMQQUSQN HOLDS THE ABSOLUTE SUBQUERY NUMBER
 ;   AMQQSQNN HOLDS THE CURRENT AMQQUSQN AT THAT LEVEL
 ;   AMQQUQQN HOLDS THE 'QQ' NUMBER
RUN D VAR
 I $P(^AMQQ(1,AMQQLINK,0),U,5)'=20 W:'$D(AMQQXX) !!,$S(AMQQCCLS="V":"Note: This visit may have ",1:"SUBQUERY: Analysis of "),"multiple ",AMQQSQSZ,"S",!
 E  D ^AMQQSQIM I $D(AMQQQUIT) G EXIT
 D GET
EXIT K %,AMQQAFN,AMQQNMAS
NOTSTD D SQKILL^AMQQKILL
 Q
 ;
VAR S (AMQQQ,AMQQMULT)=""
 I AMQQSQST'="L",AMQQSQST'="I",AMQQSQST'="G" S AMQQCOMP=";;"
 S (AMQQSQFN,AMQQSQFR)=0
 I $D(AMQQYYMI) Q
 S AMQQNAR=$S($D(AMQQONE):AMQQONE,1:"ea. patient")
 S %=AMQQSQAN
 S %=$P(%,",")
 S %=$P(%," (")
 S %=$P(%,"(")
 S AMQQSQSJ=%
 S AMQQSQSZ=$S($E(%,$L(%))="Y":($E(%,1,$L(%)-1)_"IE"),%="DIAGNOSIS":"DIAGNOSE",%="PROBLEM LIST DIAGNOSIS":"PROBLEM LIST DIAGNOSE",%="HEALTH FACTORS":"HEALTH FACTOR",$E(%,$L(%))="S":(%_"E"),1:%)
 Q
 ;
GET S AMQQSQFN=AMQQSQFN+1
 I AMQQSQFN=1 S %=+$G(^AMQQ(5,AMQQSQSN,5)) I %,$P(^(5),U,4) K AMQQSQUF D ^AMQQSQUP G:$D(AMQQSQUF) G1 D CANCEL Q
 D ^AMQQSQA
 I $D(AMQQSQNV) K AMQQSQNV S AMQQSQFN=AMQQSQFN-1 G GET
 I $D(AMQQSQQT) K AMQQSQQT D:AMQQUSQL=1 SQR Q
 I $D(AMQQQUIT),AMQQSQFN>1 K AMQQQUIT D CANCEL Q
 I $D(AMQQQUIT) Q
G1 K AMQQSQUF
 D ^AMQQSQS
 I $D(AMQQSQSQ) D RECURSE
 I $D(AMQQSQQF) D QQ
 I $D(AMQQSQNF) Q
 I $D(AMQQSQCF) S AMQQNMAS=""
 I '$D(AMQQXX),AMQQSQFN>1 W !! F %=0:0 S %=$O(^UTILITY("AMQQ",$J,"SQL",AMQQSQNN,%)) Q:'%  W ! X ^(%)
 G GET
 ;
CANCEL W !!,$S(AMQQUSQL=1:"ATTRIBUTE ",1:"SUBQUERY "),"CANCELLED!!!",!!,*7
 S AMQQSQJ1="CLEANUP^AMQQSQ"
 D TREE2^AMQQSQT
 S AMQQCOMP=""
 I AMQQUSQL=1 K ^UTILITY("AMQQ",$J,"Q",AMQQUATN) S AMQQUATN=AMQQUATN-1,AMQQXSQF=""
 Q
 ;
CLEANUP K ^UTILITY("AMQQ",$J,$S(AMQQTLVL=1:"SQXQ",1:"SQXS"),AMQQSQN1,AMQQSQN2)
 N %
 F %="SQ","SQL" K ^UTILITY("AMQQ",$J,%,AMQQSQN2)
 Q
 ;
SQR S AMQQSQFR=0
 I AMQQSQFN=1 Q
CHECK I '$G(AMQQSQNN) S %=$G(AMQQCOMP) I '+%,$P(%,";",4)="" S AMQQSQFR=1 Q
 I $D(^UTILITY("AMQQ",$J,"SQ",AMQQSQNN,"NULL")) S AMQQSQFR=6 Q
 F %=0:0 S %=$O(^UTILITY("AMQQ",$J,"SQ",AMQQSQNN,%)) Q:'%  I $P(^(%),U,6)="C" S AMQQSQFR=5 G CEXIT
 F %=0:0 S %=$O(^UTILITY("AMQQ",$J,"SQ",AMQQSQNN,%)) Q:'%  I $P(^(%),U,6,7)="O^1" S AMQQSQFR=2 G CEXIT
 F %=0:0 S %=$O(^UTILITY("AMQQ",$J,"SQ",AMQQSQNN,%)) Q:'%  S Y=$P(^(%),U,6) I "TPN"'[Y S AMQQSQFR=$S(Y="O":3,1:0)
CEXIT K AMQQNVAR,AMQQMULR
 Q
 ;
QQ S AMQQUQQN=AMQQUQQN+1
 I $G(AMQQSQQF) S ^UTILITY("AMQQ",$J,"SQXX",AMQQUQQN,AMQQSQQF)=""
 S ^UTILITY("AMQQ",$J,"SQ",AMQQSQNN,AMQQSQFN)="0^LINK^22^SUB^AMQQF1^"_$E(AMQQSQCT)_"^"_AMQQUQQN_U
 S AMQQSQQF=AMQQUQQN
 D ^AMQQATS
 K AMQQSQQF
 Q
 ;
RECURSE S AMQQUSQL=AMQQUSQL+1
 S %=$P(^AMQQ(5,AMQQSQN,0),U,5)
 S:%=9 %=AMQQSQN+($J/100000)
 S %=$P(^AMQQ(1,%,0),U,5)
 S %=$P(^AMQQ(4,%,0),U)
 S AMQQTSTG=AMQQSQNM_U_AMQQSQN_U_%_U_AMQQSQNN_U_AMQQSQSQ
 N AMQQCOMP,AMQQRECV,AMQQLINK K AMQQSQSQ
FRESH N AMQQSQAA,AMQQSQAN,AMQQSQBF,AMQQSQBS,AMQQSQCF,AMQQSQCT,AMQQSQCV,AMQQSQDF,AMQQSQDV,AMQQSQF1,AMQQSQF2,AMQQSQFL,AMQQSQFL,AMQQSQFN,AMQQSQFR,AMQQSQGF
 N AMQQSQJ1,AMQQSQJ2,AMQQSQLS,AMQQSQN,AMQQSQN1,AMQQSQN2,AMQQSQNC,AMQQSQNF,AMQQSQNM,AMQQSQNN,AMQQSQAT,AMQQSQP,AMQQSQP1,AMQQSQP2,AMQQSQPH,AMQQSQPL,AMQQSQPQ,AMQQSQPS,AMQQSQPY,AMQQSQQQ,AMQQSQQT
 N AMQQSQRC,AMQQSQRD,AMQQSQSC,AMQQSQSJ,AMQQSQSN,AMQQSQSQ,AMQQSQST,AMQQSQSZ,AMQQSQTF,AMQQSQTP,AMQQSQVV,AMQQSQZL,AMQQSQP,AMQQSQZF ; &&& AMQQSQZF ADDED
 S %=AMQQTSTG
 S AMQQSQAN=$P(%,U)
 S AMQQSQSN=$P(%,U,2)
 S AMQQSQST=$P(%,U,3)
 S AMQQRECV=$P(%,U,5,99)
 S AMQQLINK=$P(AMQQRECV,U,8)
 S AMQQSQAA=$P(%,U,4)
 S AMQQSQNN=AMQQUSQN
 S AMQQSQRC=""
 K AMQQTSTG
 I $D(AMQQXXND),$D(AMQQYYMI) D AUTO Q
 D ^AMQQSQ
R1 I $D(AMQQQUIT) Q
 D SETSUB
 S AMQQUSQL=AMQQUSQL-1
 S AMQQSQQF=AMQQSQRC
 K AMQQSQRC
 Q
 ;
SETSUB F %=1,2 S $P(AMQQCOMP,";",%)=$P(AMQQRECV,U,%)
 S $P(AMQQCOMP,";",4)=$P(AMQQRECV,U,11)
 S AMQQQ=$P(AMQQRECV,U,8,10)_"^1^^^^^"_AMQQCOMP_"^^^^^^"
 S $P(AMQQQ,U,17)=$P(AMQQRECV,U,11)
 Q
 ;
EN1 ; ENTRY POINT FROM AMQQQ2
 D VAR,GET,EXIT
 Q
 ;
AUTO N AMQQYYYY
 S AMQQYYYY=AMQQMMMM
 S %=AMQQXXND
 N AMQQXXND
 S AMQQXXND=$E(%,1,$L(%)-1)_","_AMQQYYMI_",1)"
 N AMQQMMMM,AMQQMMCC,AMQQMMVV,AMQQYYMI
 S AMQQYYMI=0
 D MULT^AMQQQ2
 D R1
 Q
 ;
