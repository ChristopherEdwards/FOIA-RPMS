AMQQSQVS ; IHS/CMI/THL - GETS VISUAL ACUITY DATA ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;-----
 N AMQQLINK,AMQQFTYP,AMQQQ,AMQQATNM,AMQQCONM,AMQQCOMP,AMQQNOCO,AMQQNOT,AMQQTMPC
 S AMQQCOMP=""
RUN D SET
 D GET
 I $D(AMQQQUIT)!($D(AMQQSQQT))!(AMQQCOMP="") G EXIT
 S AMQQNOCO=AMQQNOCO_"~"_AMQQNOCO
 S AMQQTMPC=AMQQTMPC_"~"_AMQQTMPC
 D VAND
EXIT K AMQQTMPC,X
 Q
 ;
SET S AMQQCOND=+Y
 S AMQQNOCO=$P(^AMQQ(5,+Y,0),U,8)
 S AMQQCONM=$P(Y,U,2)
 S AMQQSQCT="B"
 S AMQQSQVV=""
 S AMQQSYMB=$P(^AMQQ(5,+Y,0),U,6)
 I $D(AMQQSQNT) S AMQQSYMB="'"_AMQQSYMB K AMQQSQNT
 Q
 ;
GET D COMPV^AMQQAV2
 I $D(AMQQQUIT) Q
 I $G(AMQQCOMP)="" Q
 I AMQQNOCO=2 S AMQQTMPC="'<:"_$P(AMQQCOMP,";")_":'>:"_$P(AMQQCOMP,";",2) Q
 S AMQQTMPC=AMQQSYMB_":"_AMQQCOMP
 Q
 ;
VAND W !!!,"When I analyze the results =>",!
 W !?5,"1) Vision in ",@AMQQRV,"BOTH",@AMQQNV," eyes must meet your criteria"
 W !?5,"2) Vision in ",@AMQQRV,"AT LEAST ONE",@AMQQNV," eye must meet your criteria",!
VANDQ W !,"Your choice (1-2): 1// "
 R X:DTIME E  S X=U
 I $E(X)=U S AMQQQUIT="" Q
 I X="" S X=1
 I X?1."?" W !!,"Choose between ""and"" logic and ""or"" logic" G VANDQ
 I X=1 S AMQQSQCV=AMQQTMPC_"~&" Q
 I X=2 S AMQQSQCV=AMQQTMPC_"~!" Q
 W "  ??",*7
 G VANDQ
 ;
