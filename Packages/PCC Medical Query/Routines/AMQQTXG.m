AMQQTXG ; IHS/CMI/THL - POINTER TAXONOMY ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;-----
EN3 ; ENTRY POINT FOR POINTER TAXONOMY
 S AMQQHELP="PHELP"
 S AMQQHEL1="PHELP1"
 S AMQQLKUP="PLOOKUP"
VAR S AMQQTAXI=$P(^AMQQ(5,AMQQATN,0),U,17)
RUN D GET
 I '$D(AMQQQUIT),'$D(AMQQSCMP),AMQQTAXT'=2,'$D(AMQQXX) D LIST^AMQQTX
EXIT K X,AMQQTAXI,I,AMQQTGFG,AMQQTDIC,AMQQHELP,AMQQHEL1,AMQQLKUP,AMQQXXN,%,%Y,N,T,C
 Q
 ;
GET I '$D(AMQQXX) W !
GETR I $D(AMQQXXTT),$D(AMQQXXN) S AMQQXXN=$O(^UTILITY("AMQQ",$J,"XXTAX",AMQQXXTT,AMQQXXN)) Q:'AMQQXXN  S X=^(AMQQXXN) G GRR
 I $D(AMQQNTAX),AMQQNTAX="" Q
 I $D(AMQQNTAX) S X=AMQQNTAX,AMQQNTAX="" G GRR
 I $D(AMQQXX),$D(AMQQONE) S X="ALL" G GRR
 S %="Enter "_$S($D(^UTILITY("AMQQ TAX",$J,AMQQURGN)):"ANOTHER ",1:"")_AMQQTNAR
 K AMQQNDB
 W !,%,": "
 R X:DTIME E  S X=U
GRR I X="",'$D(^UTILITY("AMQQ TAX",$J,AMQQURGN)),'$D(AMQQSCMP) D ACA^AMQQAC Q:X=4  I X="" W ! G GETR
 I X="" Q
 I X=U S AMQQQUIT="" Q
 I X="]" S AMQQTTOT=9 K AMQQTGFG G GETR
 I X="@" S X="NULL" W " (NULL SET)"
 I X="EXIST" S X="EXISTS" W "S"
 I X="ALL",AMQQATN[681 S X="*"
 I '$D(AMQQSCMP),$D(AMQQSQNM),$D(AMQQSQSJ),AMQQSQNM'=AMQQSQSJ,AMQQSQNM'="RESULT/DIAGNOSIS" F %="ALL","ANY","EXISTS" I X=% W " ??" G GET
 I '$D(AMQQSCMP) F %="ALL","ANY","EXISTS","NULL" I X=% G GEXIT
 I X="*" D EDALL G GETR
 I X?1"?" D @(AMQQHELP_"^AMQQTXG1") G GETR
 I X?2"?" D LIST^AMQQTX G GETR
 I X?3."?" D @(AMQQHEL1_"^AMQQTXG1") G GETR
 I $E(X)="-",'$D(^UTILITY("AMQQ TAX",$J,AMQQURGN)) W "  ??",*7 G GETR
 I AMQQTAXT=2,$L(X,"-")>2 D DASH I Y W "  ??",*7 G GETR
 I $E(X)="-" S X=$E(X,2,99) S AMQQTXEX=""
 I $E(X,1,2)="[?" D WHATG^AMQQTX G GETR
 I $E(X)="[" D RESTORE^AMQQTX0 G GETR
 I $E(X)="""",$E(X,$L(X))="""" S X=$E(X,2,($L(X)-1)),AMQQNDB=""
 I X="[" W "  ??",*7 G GETR
 D @(AMQQLKUP_"^AMQQTXG1")
 I $D(AMQQQUIT) Q
 I Y'=-1,AMQQTAXT'=2 D SET^AMQQTX
 I Y=-1,$D(AMQQXX) K ^UTILITY("AMQQ TAX",$J,AMQQURGN),AMQQTAX Q
 I $G(^UTILITY("AMQQ TAX",$J,AMQQURGN))="REFUSAL" Q
 G GETR
 ;
GEXIT I X'="NULL" K ^UTILITY("AMQQ TAX",$J,AMQQURGN) S AMQQSCMP=X Q
 D:$D(AMQQCNAM) NULL^AMQQTX
 I $D(AMQQQUIT) Q
 I $D(AMQQSCMP),AMQQSCMP="NULL" Q
 I "Yy"'[$E($G(%Y)) W " ??",*7
 G GETR
 ;
EDALL S %=$P(^AMQQ(1,AMQQLINK,0),U,5)
 S %=$P(^AMQQ(4,%,0),U)
 I %="G" D EDA Q
 S X=AMQQTLOK_"""B"")"
 S %=""
 F  S %=$O(@X@(%)) Q:%=""  S Y=$O(^(%,"")) I Y'="" S ^UTILITY("AMQQ TAX",$J,AMQQURGN,Y)="" W "."
 Q
 ;
EDA N I,%
 F I=2:1 S %=$P(AMQQSSET,";",I),%=$P(%,":") Q:%=""  S ^UTILITY("AMQQ TAX",$J,AMQQURGN,%)="" W "."
 Q
 ;
DASH S Y=0
 I $L(X,"-")>3 S Y=1 Q
 I $P(X,"-")'="" S Y=1 Q
 F %=2,3 I $P(X,"-",%)="" S Y=1 Q
 Q
 ;
EN5 ; ENTRY POINT FOR HYBRID TAX
 D EN3
 Q
 ;
EN1 ; ENTRY POINT FOR FREE TEXT TAX
 S (AMQQHELP,AMQQHEL1)="FHELP"
 S AMQQLKUP="FLOOKUP"
 D VAR
 Q
 ;
EN4 ; ENTRY POINT FOR GROUP OF CODES TAXONOMY
 S AMQQHEL1="GHELP1"
 S AMQQHELP="PHELP"
 S AMQQLKUP="GLOOKUP"
 D VAR
 Q
 ;
EN2 ; ENTY POINT FOR RANGE OF CODES
 S AMQQHELP="RHELP"
 S AMQQLKUP="RLOOKUP"
 S AMQQHEL1="RHELP1"
 D VAR
 Q
 ;
