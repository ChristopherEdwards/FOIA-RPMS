APCDCHKI ; IHS/CMI/LAB - I-LINKER ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
 ;IHS/CMI - patch 4 added check for mfi created visits
 W !!,"This routine will find all In-Hospital visits that are not linked to a",!,"hospitalization and link them if possible."
 W !!,"This process could take some time so you may want to queue the report to ",!,"print after-hours.",!
 W !,"A report will be printed -- please enter the device for printing."
 W !! S %ZIS="PQ" D ^%ZIS G:POP EOJ G:$D(IO("Q")) TSKMN
INIT ;
 S:$D(ZTQUEUED) ZTREQ="@"
 K ^XTMP("ILINK",$J)
 S U="^",APCDIDFN=""
DRIVER F  S APCDIDFN=$O(^AUPNVSIT("AI",APCDIDFN)) Q:APCDIDFN'=+APCDIDFN  D PROCESS
 D ^APCDCHKP
 D EOJ
 Q
 ;
PROCESS ; Process each "In hospital" record
 ; Set variables
 I '$D(^AUPNVSIT(APCDIDFN,0)) K ^AUPNVSIT("AI",APCDIDFN) Q
 S APCDIVR=^AUPNVSIT(APCDIDFN,0)
 I $P(APCDIVR,U,11) K ^AUPNVSIT("AI",APCDIDFN) Q
 I $P(APCDIVR,U,23)=.5 K ^AUPNVSIT("AI",APCDIDFN) Q
 I $P($G(^AUPNVSIT(APCDIDFN,11)),U,13) K ^AUPNVSIT("AI",APCDIDFN) Q
 I $P(APCDIVR,U,12) K ^AUPNVSIT("AI",APCDIDFN) Q
 S APCDIDAT=+$P(APCDIVR,U),DFN=$P(APCDIVR,U,5),APCDITYP=$P(APCDIVR,U,3),APCDILOC=$P(APCDIVR,U,6),(APCDFND,APCDOLD,APCDHDFN)=0,APCDVD=$P(APCDIDAT,".") K APCDHOSP
 ; Check for hospitalization prior to (or on same day) as the "I" visit
 S APCDVDH=(9999999-APCDVD),APCDSVD=(APCDVDH-1)_".9999",APCDHDFN=""
 F  S APCDSVD=$O(^AUPNVSIT("AAH",DFN,APCDSVD)) Q:APCDSVD'=+APCDSVD!($P(APCDSVD,".")<APCDVDH)  D PROC2
 I APCDFND>1 S APCDHDFN=0 F APCDL=0:0 S APCDHDFN=$O(APCDHOSP(APCDHDFN)) Q:APCDHDFN'=+APCDHDFN  S ^XTMP("ILINK",$J,"TWOHITS",APCDIDFN,APCDHDFN)=""
 I 'APCDFND D CHKYR
 ;
 I APCDFND=1 S APCDHDFN=$O(APCDHOSP("")),DIE="^AUPNVSIT(",DA=APCDIDFN,DR=".12///`"_APCDHDFN D
 .D ^DIE K DA,DR,DIE
 .S ^XTMP("ILINK",$J,"HIT",APCDHDFN,APCDIDFN)=APCDDCD
 .D CHKPROC
 .S AUPNVSIT=APCDHDFN D MOD^AUPNVSIT
 . ; -- IHS/DAOU/EJN for HL7
 . Q:'$G(APCDIDFN)
 . I $T(A08^BTSEVENT)]"" S APCDHLER=$$A08^BTSEVENT(APCDIDFN) K APCDHLER
 . ; -- END HL7 mods
 I 'APCDFND,'APCDOLD S ^XTMP("ILINK",$J,"NOHIT",APCDIDFN)=""
 Q
 ;
PROC2 ;
 S APCDHDFN=0 F  S APCDHDFN=$O(^AUPNVSIT("AAH",DFN,APCDSVD,APCDHDFN)) Q:APCDHDFN'=+APCDHDFN  I APCDHDFN]"",$D(^AUPNVSIT(APCDHDFN,0)),'$P(^(0),U,11),$P(^(0),U,9) D CHECK
 Q
CHECK ;
 S APCDHVR=^AUPNVSIT(APCDHDFN,0)
 S APCDHDAT=+$P(APCDHVR,U),APCDHTYP=$P(APCDHVR,U,3),APCDHLOC=$P(APCDHVR,U,6)
CHKHOSP ; Check corresponding V Hospitalization for discharge date
 S APCDINPD="",APCDINPD=$S(APCDITYP="C":$O(^AUPNVCHS("AD",APCDHDFN,"")),1:$O(^AUPNVINP("AD",APCDHDFN,"")))
 Q:APCDINPD=""
 S:APCDITYP="C" APCDDCD=$P(^AUPNVCHS(APCDINPD,0),U,7)
 S:APCDITYP'="C" APCDDCD=$P(^AUPNVINP(APCDINPD,0),U)
 I APCDDCD'<APCDVD S APCDFND=APCDFND+1,APCDHOSP(APCDHDFN)=""
 Q
CHKYR ;
 S %=DT-APCDVD I %>10000 I 'APCDFND S ^XTMP("ILINK",$J,"ONEYR",APCDIDFN)="",APCDOLD=1 K ^AUPNVSIT("AI",APCDIDFN)
 Q
CHKPROC ;
 S APCDVPRC=0 F  S APCDVPRC=$O(^AUPNVPRC("AD",APCDHDFN,APCDVPRC)) Q:APCDVPRC'=+APCDVPRC  I $D(^AUPNVPRC(APCDVPRC,0)) S APCDHPRC($P(^AUPNVPRC(APCDVPRC,0),U))=APCDVPRC
 S APCDVPRC=0 F  S APCDVPRC=$O(^AUPNVPRC("AD",APCDIDFN,APCDVPRC)) Q:APCDVPRC'=+APCDVPRC  I $D(^AUPNVPRC(APCDVPRC,0)) S APCDICDP=$P(^AUPNVPRC(APCDVPRC,0),U) I $D(APCDHPRC(APCDICDP)) D DELPRC
 Q
DELPRC ;
 I $P(^AUPNVPRC(APCDHPRC(APCDICDP),0),U,4)'=$P(^AUPNVPRC(APCDVPRC,0),U,4) Q
 ;quit if provider narratives are NOT the same - per Diana 7/12/90
 S ^XTMP("ILINK",$J,"PROC ERROR",APCDVPRC)=^AUPNVPRC(APCDVPRC,0)
 S DA=APCDVPRC,DIE="^AUPNVPRC(",DR=".01///@" D ^DIE K DA,DR,DIE
 I $D(Y) K ^XTMP("ILINK",$J,"PROC ERROR",APCDVPRC) Q
 Q
EOJ ; Clean up and XIT
 D ^%ZISC
 K ^XTMP("ILINK",$J)
 K APCDIDFN,APCDFND,APCDOLD,APCDHDFN,DFN,APCDINPD,APCDVD,APCDIDAT,APCDSVD,APCDVDH,APCDDCD,APCDDT,APCDHDFN,APCDHOSP,APCDHV,APCDHDAT,APCDHLOC,APCDHTYP,APCDHVR,APCDILOC,APCDIVR,APCDTYPE,APCDITYP,IO("Q"),APCDPG,APCDT
 K APCDHPRC,APCDICDP,APCDL,APCDVPRC
 Q
TSKMN ;
 K ZTSAVE
 S ZTCPU=$G(IOCPU),ZTIO=ION,ZTRTN="INIT^APCDCHKI",ZTDTH="",ZTDESC="PCC DATA ENTRY - IN HOSP LINK" D ^%ZTLOAD D EOJ Q
