APCDEA2 ; IHS/CMI/LAB - DATA ENTRY ENTER CONT. ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;   Generate VISIT, then process MNEMONICS/TEMPLATE
 ;BJPC v1.0 patch 1
START ;
 S APCDX=""
 I $D(APCDLPAT),APCDLPAT=APCDPAT,$D(APCDLDAT),APCDLDAT=APCDDATE,$D(APCDLVST),'APCDTPLT D SAMEPAT
 S APCDLPAT=APCDPAT
 S APCDLDAT=APCDDATE
 I APCDX=1 D MNEPROC Q
 Q:APCDX=2
 K APCDLVST,APCDCLN,ZTSK
 I $D(APCDTVST) S APCDTYPE=APCDTTYP,APCDCAT=APCDTCAT,APCDLOC=APCDTLOC K APCDTVST,APCDTTYP,APCDTCAT,APCDTLOC
 K APCDALVR D ^APCDALV
 I $D(APCDAFLG)#2,APCDAFLG=2 W $C(7),!,"VISIT date not valid for current patient!",! S APCDFLG=1 Q
 Q:APCDVSIT=""
 I $D(APCDVSIT("NEW")),$P(^APCCCTRL(DUZ(2),0),U,12)]"",$P($P(^AUPNVSIT(APCDVSIT,0),U),".")'<$P(^APCCCTRL(DUZ(2),0),U,12) S DA=APCDVSIT,DIE="^AUPNVSIT(",DR="1111///R" D ^DIE K DIE,DA,DR
 ;above added for EHR and auditing of visits, d/e created
 ;visits are always set to "R"
 S APCDLVST=APCDVSIT
 S DIE="^AUPNPAT(",DR=".16///TODAY",DA=APCDPAT D ^DIE
 ;S DIE="^AUPNVSIT(",DA=APCDVSIT,DR=".13////"_DT D ^DIE K DR,DA,DIE
 S AUPNVSIT=APCDVSIT D MOD^AUPNVSIT
 I AUPNDOB]"" S X2=AUPNDOB,X1=APCDDATE D ^%DTC S AUPNDAYS=X
 I APCDTPLT S APCDMNE=APCDTPLT D ^APCDEA3,GETMNEK Q
 K DR
 I $P($G(APCDPARM),U,18)'="N" S APCDVDSP=APCDVSIT D:$D(APCDVSIT("NEW")) ^APCDVDSP D:'$D(APCDVSIT("NEW")) ^APCDEWHA K APCDVDSP
 I APCDTYPE'="C","TC"'[APCDCAT,'$D(APCDVSIT("NEW")) S X="TM" D GET1
 I $P($G(APCDPARM),U,16)="Y",$E($P(^AUTTLOC(APCDLOC,0),U,10),5,6)>49 S X="OLOC" D GET1
 I $D(APCDMINI),APCDTYPE'="C" D
 .S X=$S(APCDCAT'="H":"CL",1:"IP") D GET1
 .S APCDAMN=0 F  S APCDAMN=$O(^APCDSITE(DUZ(2),12,APCDAMN)) Q:APCDAMN'=+APCDAMN  S X=$P(^APCDTKW($P(^APCDSITE(DUZ(2),12,APCDAMN,0),U),0),U) D GET1
 .F X="PRV","PV" D GET1
 .Q
 I APCDCAT="H",APCDTYPE'="C",'$D(APCDMINI),'$D(^AUPNVINP("AD",APCDVSIT)) S X="IP" D GET1
 D MNEPROC
 Q
 ;
MNEPROC ; PROCESS MNEMONICS UNTIL DONE
 S APCDMPQ=0
 F  D GETMNE Q:APCDMPQ
 I $G(APCDVSIT) D EP^APCDKDE
 D GETMNEK
 K APCDMPQ,APCDREGU
 W !
 Q
 ;
GETMNE ; GET MNEMONIC
 W !
 K DIC,I,D,%D,X,Y,DIADD,DLAYGO
 S DIC="^APCDTKW(",DIC(0)="AEMQ",DIC("A")="MNEMONIC: ",DIC("S")="I $L($P(^APCDTKW(+Y,0),U))<5" D ^DIC K DIC("A"),DIC("S")
 I Y<0 D CHECK Q
 S APCDMNE=+Y,APCDMNE("NAME")=$P(Y,U,2)
 K APCDMOD
 D ^APCDEA3
 I $D(APCDEQX) D ^APCDEQX I $D(APCDEQX) S APCDMPQ=1 Q
 I $D(APCDMOD) W !!,"Switching to Modify Mode for ONE Mnemonic ONLY!" S APCDMODE="M",APCDVSIT=APCDLVST,APCDVLK=APCDVSIT D GETMNE K APCDVLK,APCDMOD S APCDMODE="A" W !!,"Switching back to ENTER Mode!" Q
 Q
 ;
GETMNEK ; KILL GETMNE SPECIFIC VARIABLES
 K APCDVSIT,APCDX,APCDEQX,APCDREGU
 Q
 ;
CHECK ; SEE IF PV AND PRO ENTERED CORRECTLY
 Q:$D(APCDMOD)
 S APCDMPQ=1
 K APCDNOCL D ^APCDVCHK
 Q:"EX"[$P(^AUPNVSIT(APCDVSIT,0),U,7)
 I APCDMODE'="M",'$D(^AUPNVPOV("AD",APCDVSIT)),'$D(^AUPNVPRN("AD",APCDVSIT)) W !,"PV mnemonic required!",!,APCDBEEP S:'$D(DTOUT) APCDMPQ=0 Q
 I APCDMODE'="M",'$D(^AUPNVPRV("AD",APCDVSIT)) W !,"PRV mnemonic required!",!,APCDBEEP S:'$D(DTOUT) APCDMPQ=0 Q
 I APCDMODE'="M",$D(APCDNOCL) W !,"CL mnemonic required!",!,$C(7) S:'$D(DTOUT) APCDMPQ=0 K APCDNOCL Q
 I APCDMODE'="M",$P(^AUPNVSIT(APCDVSIT,0),U,7)="H",$P(^(0),U,3)'="C",'$D(^AUPNVINP("AD",APCDVSIT)) W !,"IP Mnemonic required on Hospitalizations!",$C(7) S:'$D(DTOUT) APCDMPQ=0 Q
 I APCDMODE'="M",$P(^AUPNVSIT(APCDVSIT,0),U,3)="C",'$D(^AUPNVCHS("AD",APCDVSIT)) W !,"CHA, CHH or CHI mnemonic required with Contract Visits!",$C(7) S:'$D(DTOUT) APCDMPQ=0 Q
 Q:'APCDMPQ
 D DEDT(APCDLVST) I $P(APCDPARM,U,5)="Y",'$D(^APCDFORM("AB",APCDLVST)) S APCDFV=APCDLVST D ^APCDFORM K APCDFV
 I $P(APCDPARM,U,5)="Y",$D(^AUPNVTC("AD",APCDVSIT)) S APCDFV=APCDVSIT D ^APCDFCTC K APCDFV ;IHS/CMI/LAB - patch 2 added this line to do tc tracking
 Q
 ;
GET1 ;
 W !!
 K DIC,D,I,%D
 S DIC="^APCDTKW(",DIC(0)="EMQX" D ^DIC K DIC
 I Y<0 W !!,$C(7),$C(7),X," Mnemonic is Missing - Notify your Supervisor!" K DIC,X Q
 S APCDMNE=+Y,APCDMNE("NAME")=$P(Y,U,2)
 D ^APCDEA3
 Q
 ;
SAMEPAT ; SAME PATIENT
 K DIR,DIRUT,DIROUT
 S APCDX=+^AUPNVSIT(APCDLVST,0),APCDX=$E(APCDX,4,5)_"-"_$E(APCDX,6,7)_"-"_(1700+$E(APCDX,1,3))_$S($P(APCDX,".",2)]"":"@"_$P(APCDX,".",2),1:"")
 W !!,"You have reselected the same patient.",!
 W !,"Last VISIT is ",APCDX,!
 S DIR("A")="Choose",DIR(0)="S^1:Modify last VISIT;2:Append to last VISIT;3:Create new VISIT;4:Quit"
 D ^DIR
 S APCDX=+Y
 I $D(DIRUT) S APCDX=4
 K DIR,DIRUT,DIROUT,DUOUT,DTOUT
 D @("SAMEPAT"_APCDX)
 Q
SAMEPAT1 ;
 W "  Switching to Modify Mode."
 S APCDMODE="M",APCDVSIT=APCDLVST,APCDVLK=APCDVSIT D MNEPROC S APCDMODE="A",APCDX=2 K APCDVLK
 W !!,"Returning to Enter Mode.",!
 Q
SAMEPAT2 ;
 W "  Switching to Append Mode."
 S APCDVSIT=APCDLVST,APCDX=1,APCDAPP=1
 Q
SAMEPAT3 ;
 W "  Creating new VISIT, still in Add Mode."
 S APCDX=3,APCDADD=1
 Q
SAMEPAT4 ;
 W "  Quit",!
 S APCDX=2
 Q
 ;
DEDT(VISIT) ;EP - update 1105 of visit
 I '$G(VISIT) Q
 I '$D(^AUPNVSIT(VISIT)) Q
 Q:$P($G(^AUPNVSIT(VISIT,11)),U,5)]""
 D ^XBFMK
 S DA=VISIT,DIE="^AUPNVSIT(",DR="1105////"_DT D ^DIE
 D ^XBFMK
 Q
