APCDFOA1 ; IHS/CMI/LAB - DE FQA PROCEDURES ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
P ; Run by posting date
 S APCDVCNT=0,APCDODAT=APCDSD_".9999" F  S APCDODAT=$O(^APCDFORM("B",APCDODAT)) Q:APCDODAT=""!((APCDODAT\1)>APCDED)  S APCDDFN=$O(^APCDFORM("B",APCDODAT,"")) D V1
 Q
V1 ;
 S APCDC=0 F  S APCDC=$O(^APCDFORM(APCDDFN,11,APCDC)) Q:APCDC'=+APCDC  S APCDVDFN=$P(^APCDFORM(APCDDFN,11,APCDC,0),U) I APCDVDFN]"",$D(^AUPNVSIT(APCDVDFN,0)) D PROC
 Q
PROC ;
 I APCDPROV'=$P(^APCDFORM(APCDDFN,11,APCDC,0),U,2) Q
 S APCDVREC=^AUPNVSIT(APCDVDFN,0)
 Q:'$P(APCDVREC,U,9)
 Q:$P(APCDVREC,U,11)
 Q:'$D(^AUPNVPRC("AD",APCDVDFN))
 I APCDCLN]"",APCDCLN'=$P(APCDVREC,U,8) Q
 D CHKPOV
 Q
CHKPOV ;
 I $D(^XTMP("APCDFOA",APCDJOB,APCDBT,"DEPOV","ALL")) D CNT Q
 S (APCD1,APCD2)=0 F  S APCD1=$O(^AUPNVPRC("AD",APCDVDFN,APCD1)) Q:APCD1'=+APCD1  S APCDICDP=$P(^AUPNVPRC(APCD1,0),U) I $D(^XTMP("APCDFOA",APCDJOB,APCDBT,"DEPOV","ICDDFN",APCDICDP)) S APCD2=APCD2+1
 I APCD2>0 D CNT
 Q
CNT ;
 S APCDVCNT=APCDVCNT+1,^XTMP("APCDFOA",APCDJOB,APCDBT,"DEQAV",APCDVCNT,APCDVDFN)=""
 Q
 ;
