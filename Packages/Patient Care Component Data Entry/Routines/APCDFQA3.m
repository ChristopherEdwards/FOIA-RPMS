APCDFQA3 ; IHS/CMI/LAB - USER INTERFACE TO SELECT ICD CODES ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
BEGIN ;
 W !
 S APCD("NO DISPLAY")=0
 D ASK1
 I Y="^" S APCDSTP=1 G X
 I $D(APCDTABL) D CHECK I Y'=1 G @$S(Y=0:"BEGIN",1:"X")
X I $D(APCDTABL) D SETUTIL
 D EOJ
 Q
 ;
ASK1 ;
 S APCDA=0
 K APCD("LOW"),APCD("HI")
 S DIR("A")=$S('$D(APCDTABL):"Enter Diagnosis (or range of DX codes)",1:"Enter another Diagnosis (or range of DX codes)") D SETDIR^APCDFQA4,^DIR K DIR
 I "^"[Y G X1
 D PROCESS
 I $D(APCDTABL),'APCD("NO DISPLAY") D RANGES
 S APCD("NO DISPLAY")=0
 G ASK1
X1 Q
 ;
PROCESS ;EVALUATE USER RESPONSE
 S (APCDSUB,APCDONE)=0 ;APCDSUB=0 => NO DELETE OF CODE(S),APCDONE=0 => RANGE OF CODES ENTERED
 I $E(X,1,2)="-[" W $C(7),"  ?? Not allowed" S APCD("NO DISPLAY")=1 G X2
 I X'["-" S APCDTYP="LOW",APCDONE=1 D LOOK^APCDFQA4 G X2
 I $E(X)="-",'$D(APCDTABL) W $C(7),"  ??  No previous codes entered!" G X2
 I $L(X,"-")>3 W $C(7),"  ??"  S APCDA=1 S APCD("NO DISPLAY")=1 G X2
 I $L(X,"-")=3,$E(X,$L(X))="-" W $C(7),"  ??" S APCDA=1 S APCD("NO DISPLAY")=1 G X2
 I $L(X,"-")=3,$P(X,"-")]"" W $C(7),"  ??" S APCDA=1 S APCD("NO DISPLAY")=1 G X2
 I $E(X)="-" S APCDSUB=1 D  I 1
 . S APCDSAVE("X")=X
 . I $L(X,"-")=3 S X=$P(APCDSAVE("X"),"-",2),APCDTYP="LOW" D LOOK^APCDFQA4 I 'APCDA S X=$P(APCDSAVE("X"),"-",3),APCDTYP="HI" W ! D LOOK^APCDFQA4 Q
 . I $L(APCDSAVE("X"),"-")=2 S X=$E(X,2,99),APCDTYP="LOW",APCDONE=1 D LOOK^APCDFQA4
 E  S APCDSAVE("X")=X S APCDTYP="LOW",X=$P(APCDSAVE("X"),"-") D LOOK^APCDFQA4 I 'APCDA S APCDTYP="HI",X=$P(APCDSAVE("X"),"-",2) W ! D LOOK^APCDFQA4
X2 Q
 ;
DISPLAY ;EP - SHOW CODES IN RANGE SELECTED
 W:$D(IOF) @IOF
 W !!,"ICD codes in this range =>",!! W $P(APCD("LOW")," ") S APCDDFN=$$CODEN^ICDCODE($P(APCD("LOW")," ")) W ?9,$P($$ICDDX^ICDCODE(+APCDDFN),U,4)
 S APCD=APCD("LOW"),APCDCNT=IOSL-2 F  S APCD=$O(^ICD9("BA",APCD)) Q:APCD]APCD("HI")  S APCDDFN=$O(^(APCD,"")) W !,$P(APCD," "),?9,$P($$ICDDX^ICDCODE(APCDDFN),U,4) S APCDCNT=APCDCNT-1 I APCDCNT=0 S APCDCNT=IOSL-2 D  I 'APCDR Q
A1 .S DIR(0)="EO",DIR("A")="Enter '^' to stop display, return to continue" D ^DIR K DIR S:$D(DUOUT) DIRUT=1 S APCDR=Y
 I $S('$D(APCDR):1,APCDR'=0:1,1:0) S DIR(0)="EO",DIR("A")="Press return to continue" D ^DIR K DIR
 W !
 K APCDR Q
 ;
SETUTIL ;SET UP TMP NODES TO STORE CODES
 S APCDIRNG=0,APCD("LOW")="" F  S APCD("LOW")=$O(APCDTABL(APCD("LOW"))) Q:APCD("LOW")=""  S APCD("HI")=APCDTABL(APCD("LOW")) D
 .S APCDIRNG=APCDIRNG+1
 .S ^XTMP("APCDFQA",APCDJOB,APCDBT,APCDIRNG,"ICDB")=APCD("LOW")
 .S ^XTMP("APCDFQA",APCDJOB,APCDBT,APCDIRNG,"ICDE")=APCD("HI")
 .S APCDDFN=$O(^ICD9("BA",APCD("LOW"),"")),^XTMP("APCDFQA",APCDJOB,APCDBT,"DEPOV","ICDDFN",APCDDFN,APCDIRNG)=""
 .S APCD=APCD("LOW") F  S APCD=$O(^ICD9("BA",APCD)) Q:APCD]APCD("HI")  S APCDDFN=$O(^(APCD,"")) S ^XTMP("APCDFQA",APCDJOB,APCDBT,"DEPOV","ICDDFN",APCDDFN,APCDIRNG)=""
 .Q
 Q
RANGES ;DISPLAY TABLE OF ALL RANGES
 W:$D(IOF) @IOF
 W !!,"ICD Code Range(s) Selected So Far =>",!
 S (APCD("NUM"),APCD)=0 F  S APCD=$O(APCDTABL(APCD)) Q:APCD=""  S APCD("NUM")=APCD("NUM")+1 W !,APCD("NUM"),")  ",APCD,$S(APCD'=APCDTABL(APCD):"- "_APCDTABL(APCD),1:"")
 I '$D(APCD("BANG")) W !
 Q
 ;
SHOW ; ENTRY POINT - ALLOW USER TO SELECT FROM RANGES TO DISPLAY CODES
 D RANGES
A W !,"Enter an Item Number from the table above to display code(s): " R APCD("N"):300 W:"^"[APCD("N") ! Q:"^"[APCD("N")  I APCD("N")'?1N!(APCD("N")>APCD("NUM")) W "  ??",$C(7) G A
 F APCDI=1:1:APCD("N") S APCD=$O(APCDTABL(APCD)) I APCDI=APCD("N") S APCD("LOW")=APCD,APCD("HI")=APCDTABL(APCD) D DISPLAY Q
 S APCD("BANG")="" D RANGES K APCD("BANG")
 Q
 ;
 ;
CHECK ;ASKS USER IF SATISFIED WITH ENTERED RANGES
 W ! S DIR(0)="Y",DIR("B")="Y",DIR("A")="Are all of the ICD ranges okay" D ^DIR K DIR
 W !
 Q
 ;
EOJ ;
 I $D(APCD("NOT TAX")) K APCDSTP,APCDX
 K APCDSUB,APCDTYP,APCDDFN,DIR,APCDSAVE,APCDA,APCDCNT,APCD,APCDR,APCDI,APCDONE,APCDFLG
 Q
 ;
