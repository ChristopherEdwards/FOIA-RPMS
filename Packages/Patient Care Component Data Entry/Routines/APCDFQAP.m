APCDFQAP ; IHS/CMI/LAB - PRINT DE QA ;
 ;;2.0;IHS PCC SUITE;**11**;MAY 14, 2009;Build 58
START ;
 S APCD80D="-------------------------------------------------------------------------------"
 S Y=APCDBD D DD^%DT S APCDBDD=Y S Y=APCDED D DD^%DT S APCDEDD=Y
 I APCDMAX="" S APCDMAX=APCDVCNT
 I APCDMAX>APCDVCNT S APCDMAX=APCDVCNT
 S APCDPG=0 D HEAD
 I APCDMAX=0 S APCDPG=0 W !,"No Visits to report!",! G DONE
 S APCDGOT=APCDVCNT/APCDMAX S APCDGOT=$J(APCDGOT,$L($P(APCDGOT,".")),0)
 I '$D(^XTMP("APCDFQA",APCDJOB,APCDBT)) D HEAD W !,"No visits to report",! G DONE
 K APCDQUIT
 S APCDVDFN="" F APCDX=1:APCDGOT:APCDVCNT S APCDVDFN=$O(^XTMP("APCDFQA",APCDJOB,APCDBT,"DEQAV",APCDX,"")) Q:APCDVDFN=""!($D(APCDQUIT))  I $D(^AUPNVSIT(APCDVDFN,0)) S APCDVREC=^(0) D POV
 G:$D(APCDQUIT) DONE
 I $Y>(IOSL-11) D HEAD G:$D(APCDQUIT) DONE
DONE ;
 I '$D(APCDQUIT),$E(IOST)="C",IO=IO(0) S DIR(0)="E" D ^DIR K DIR
 K ^XTMP("APCDFQA",APCDJOB,APCDBT)
 W:$D(IOF) @IOF
 Q
POV ;
 S APCDPOVC=0,APCDPOV="" K APCDPOVA F  S APCDPOV=$O(^AUPNVPOV("AD",APCDVDFN,APCDPOV)) Q:APCDPOV=""  I $D(^AUPNVPOV(APCDPOV,0)) D POV1
 D WRT
 Q
POV1 ;
 I $D(^XTMP("APCDFQA",APCDJOB,APCDBT,"DEPOV","ALL")) S APCDPOVC=APCDPOVC+1,APCDPOVA(APCDPOVC)=APCDPOV Q
 I $D(^XTMP("APCDFQA",APCDJOB,APCDBT,"DEPOV","ICDDFN",$P(^AUPNVPOV(APCDPOV,0),U))) S APCDPOVC=APCDPOVC+1,APCDPOVA(APCDPOVC)=APCDPOV
 Q
WRT ;
 I $Y>(IOSL-(APCDPOVC+5)) D HEAD Q:$D(APCDQUIT)
 S Y=+APCDVREC D DD^%DT S APCDDATE=Y
 S APCDPAT=$P(APCDVREC,U,5) Q:APCDPAT=""
 ;S APCDHRN=$S($D(^AUPNPAT(APCDPAT,41,DUZ(2),0)):$P(^AUPNPAT(APCDPAT,41,DUZ(2),0),U,2),1:"NONE")
 S APCDHRN=$$HRN^AUPNPAT(APCDPAT,$P(APCDVREC,U,6),2)
 I APCDHRN="" S APCDHRN=$$HRN^AUPNPAT(APCDPAT,DUZ(2),2)
 I APCDHRN="",$O(^AUPNPAT(APCDPAT,41,0)) S APCDHRN=$$HRN^AUPNPAT(APCDPAT,$O(^AUPNPAT(APCDPAT,41,0)),2)
 I APCDHRN="" S APCDHRN="NONE"
 W !!,APCDHRN,?12,APCDDATE S APCDPOVD=APCDPOVA(1) W ?31,$P($$ICDDX^ICDEX(+^AUPNVPOV(APCDPOVD,0),$$VD^APCLV(APCDVDFN)),U,2),?40,$$VAL^XBDIQ1(9000010.07,APCDPOVD,.24)
 S APCDNQ=$$VAL^XBDIQ1(9000010.07,APCDPOVD,.04) W:APCDNQ]"" ?47,$E(APCDNQ,1,30),!?47,"[",$E($P($$ICDDX^ICDEX(+^AUPNVPOV(APCDPOVD,0),$$VD^APCLV(APCDVDFN)),U,4),1,30),"]"
 D POVW
 Q
POVW ;
 S APCDPOVN=1 F  S APCDPOVN=$O(APCDPOVA(APCDPOVN)) Q:APCDPOVN=""!($D(APCDQUIT))  D
 .I $Y>(IOSL-3) D HEAD Q:$D(APCDQUIT)
 .S APCDPOVD=APCDPOVA(APCDPOVN) W !?31,$P($$ICDDX^ICDEX(+^AUPNVPOV(APCDPOVD,0),$$VD^APCLV(APCDVDFN)),U,2),?40,$$VAL^XBDIQ1(9000010.07,APCDPOVD,.24) S APCDNQ=$$VAL^XBDIQ1(9000010.07,APCDPOVD,.04) W:APCDNQ]"" ?47,$E(APCDNQ,1,34)
 .W !?47,"[",$E($P($$ICDDX^ICDEX(+^AUPNVPOV(APCDPOVD,0),$$VD^APCLV(APCDVDFN)),U,4),1,30),"]"
 Q
HEAD I 'APCDPG G HEAD1
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S APCDQUIT="" Q
HEAD1 ;
 W:$D(IOF) @IOF S APCDPG=APCDPG+1
 W ?32,APCDLHDR,?74,"Page ",APCDPG,!
 W ?(80-$L($P(^DIC(4,DUZ(2),0),U))/2),$P(^DIC(4,DUZ(2),0),U),!
 W ?15,"Visit POSTING Dates:  "_APCDBDD_" and "_APCDEDD,!
 S APCDLENG=$L($P(^VA(200,APCDPROV,0),U))+19
 W ?(80-APCDLENG)/2,"Data Entry Operator:  ",$P(^VA(200,APCDPROV,0),U),!
 S APCDLENG=$S(APCDCLN]"":$L($P(^DIC(40.7,APCDCLN,0),U)),1:3)+9
 W ?(80-APCDLENG)/2,"Clinic:  ",$S(APCDCLN]"":$P(^DIC(40.7,APCDCLN,0),U),1:"ALL")
 W !,"Total Visits Found: ",APCDVCNT I $G(APCDRSM)=1 W "          Total Number of Random Visits Selected:  ",APCDMAX
 W !!?2,"HR#",?12,"Visit Date",?31,"ICD",?40,"ICD-9",?47,"Provider Narrative"
 W !,APCD80D
 Q
