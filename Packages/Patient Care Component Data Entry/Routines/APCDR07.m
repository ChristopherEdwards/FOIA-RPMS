APCDR07 ; IHS/CMI/LAB - V POV REVIEW ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
 ;BJPC v1.0 patch 1
 ;           screening off codes less than 800.
DVAR ;
 S APCDEREC=^AUPNVPOV(APCDEDFN,0)
POVCODE ; 
 S APCDICDP=$P(APCDEREC,U) I '$D(^ICD9(APCDICDP,0)) S APCDE="E004" D ERR G XIT
 S APCDICD=$P($$ICDDX^ICDCODE(APCDICDP),U,2)
 K APCDE,APCDAGEE
 D ^APCDRICD
 I $D(APCDE) D ERR G XIT
 ;I $P(APCDVREC,U,7)="H",'$D(^APCDINPT(9,11,"AC",APCDICD)),$D(APCDAGEE) S APCDE="E046" D ERR G XIT
 I $D(APCDAGEE),'$P(^AUPNVPOV(APCDEDFN,0),U,14) S APCDE="E048" D ERR G XIT
 S APCDCODE=APCDICD
POVNARR ; POV Narrative-CP 51-94- if null use 3rd piece of ICD9
 S APCDNPTR=$P(APCDEREC,U,4) I APCDNPTR="" S APCDE="E006" D ERR G XIT
 I '$D(^AUTNPOV(APCDNPTR,0)) S APCDE="E006" D ERR G XIT
 ;
FVRV ; First/Revisit-CP 95-8th piece of AUPNVPOV,0
 S APCDFVRV=$P(APCDEREC,U,8)
 S:APCDFVRV="" APCDFVRV=2
 ;
INJICDA ; Cause of Injury-CP 106-109
 S APCDICD="",APCDINJ=$P(APCDEREC,U,11),APCDICDP=$P(APCDEREC,U,9)
 ;I +(APCDCODE)<800,APCDINJ]"" S APCDE="E051" D ERR ;IHS/CMI/LAB - commented out.
 ;I +(APCDCODE)<800,APCDICDP]"" S APCDE="E061" D ERR
 ;I +(APCDCODE)>800,APCDFVRV'=1,APCDINJ]"" S APCDE="E062" D ERR
 ;I +(APCDCODE)>800,APCDFVRV'=1,APCDICDP]"" S APCDE="E063" D ERR
 G:APCDICDP="" XIT
 G:+(APCDCODE)<800 XIT
 I '$D(^ICD9(APCDICDP,0)) S APCDE="E005" D ERR G XIT
 S APCDICD=$P($$ICDDX^ICDCODE(APCDICDP),U,2)
 I $E(APCDICD)'="E" S APCDE="E005" D ERR G XIT
 K APCDE,APCDAGEE
 S APCDICD=$E(APCDICD,2,99)
 D ^APCDRICD
 I $D(APCDE) D ERR G XIT
 ;I $L(APCDICD)>5 S APCDE="E041" D ERR G XIT  ;ELIMINATE E041 PER HIMS MEETING
XIT ; Clean up and exit
 K APCDEREC,APCDICD,APCDICDP,APCDNPTR,APCDINJ,APCDE,APCDCODE,APCDFVRV,APCDAGEE
 Q
ERR ;
 S APCDE("FILE")=9000010.07,APCDE("ENTRY")=APCDEDFN
 D ERR^APCDRV
 Q
