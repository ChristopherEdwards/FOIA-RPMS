APCDSPD ; IHS/CMI/LAB - display site parameters ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
 ;
EN1 ;
 K APCDQUIT
 W:$D(IOF) @IOF
 W !!,"Display Patient Care Component Site Parameters",!
 S DIC="^APCDSITE(",DIC(0)="AEMQ" D ^DIC K DIC I Y=-1 W !!,"Goodbye" G XIT
 S APCDSITE=+Y
 S DIR(0)="SO^B:BROWSE Output on Screen;P:PRINT Output to Printer",DIR("A")="Do you want to",DIR("B")="B" K DA D ^DIR K DIR
 G:$D(DIRUT) XIT
 I Y="B" D BROWSE,XIT Q
 S XBRP="PRINT^APCDSPD",XBRC="PROC^APCDSPD",XBRX="XIT^APCDSPD",XBNS="APCD"
 D ^XBDBQUE
 D XIT
 Q
XIT ;EP
 K APCDSITE,APCDX,APCDI,APCDPG,Y
 Q
BROWSE ;
 D VIEWR^XBLM("PRINT^APCDSPD","Patient Care Component Site Parameter Display")
 Q
PROC ;
 Q
PRINT ;
 S APCDPG=0
 K APCDX,APCDI
 W:$D(IOF) @IOF W !?15,"PATIENT CARE COMPONENT DATA ENTRY SITE PARAMETERS",!
 F APCDI=.01:.01:.99 I $D(^DD(9001001.2,APCDI)) S APCDX(APCDI)=$P(^DD(9001001.2,APCDI,0),U)_U_$$VAL^XBDIQ1(9001001.2,APCDSITE,APCDI)
 S X=0 F  S X=$O(APCDX(X)) Q:X'=+X!($D(APCDQUIT))  D
 .I $Y>(IOSL-4) D FF Q:$D(APCDQUIT)
 .S L=$L($P(APCDX(X),U)) W !?(37-$S(L>37:37,1:L)),$P(APCDX(X),U),":",?40,$P(APCDX(X),U,2)
HCDB ;
 K APCDX D ENPM^XBDIQ1(9001001.21101,"APCDSITE,0",".01","APCDX(")
 S APCDI=0 F  S APCDI=$O(APCDX(APCDI)) Q:APCDI'=+APCDI  D
 .I $Y>(IOSL-4) D FF Q:$D(APCDQUIT)
 .W !?3,"Acceptable Patient CHART LOCATIONS:  ",$G(APCDX(APCDI,.01))
 .Q
MNE ;
 K APCDX D ENPM^XBDIQ1(9001001.21201,APCDSITE_",0",".01","APCDX(")
 S APCDI=0 F  S APCDI=$O(APCDX(APCDI)) Q:APCDI'=+APCDI  D
 .I $Y>(IOSL-4) D FF Q:$D(APCDQUIT)
 .W !?3,"Mnemonics automatically prompted for in MINI:  ",$G(APCDX(APCDI,.01))
 .Q
 Q
FF ;
 NEW X
 I $E(IOST)="C" S DIR(0)="E",DIR("A")="Press 'ENTER' to continue or '^' to exit" D ^DIR K DIR I $D(DIRUT) S APCDQUIT=1
 S APCDPG=APCDPG+1
 W:$D(IOF) @IOF
 W !?40,"PCC DATA ENTRY SITE PARAMETERS          Page ",APCDPG,!!
 Q
