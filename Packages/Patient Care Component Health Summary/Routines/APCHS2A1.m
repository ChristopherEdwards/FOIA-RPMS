APCHS2A1 ; IHS/CMI/LAB - PART 2A1 OF APCHS -- COMPUTATIONS FOR MEASUREMENT TRANSFORMS ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
MEASP ; ********** MEASUREMENT PANEL TRANSFORMS * 9000010.01 *******
 ;
AGEMO ; GET AGE IN MONTHS
 S APCHSPD2=-APCHSIVD\1+9999999,APCHSPD1=$P(^DPT(APCHSPAT,0),U,3)
 S APCHSPYR=$E(APCHSPD2,1,3)-$E(APCHSPD1,1,3),APCHSPMO=$E(APCHSPD2,4,5)-$E(APCHSPD1,4,5)
 I APCHSPMO<0 S APCHSPMO=APCHSPMO+12,APCHSPYR=APCHSPYR-1
 S APCHSAGE=12*APCHSPYR+APCHSPMO
 K APCHSPMO,APCHSPYR,APCHSPD1,APCHSPD2
 Q
 ;
WTPER ; GET WEIGHT PERCENTILE
 S APCHSPTY="WT" G COMMON
HTPER ; GET HEIGHT PERCENTILE
 S APCHSPTY="HT" G COMMON
COMMON ; COMMON PROCESSING FOR PERCENTILES
 D AGEMO
 S X=$$AUHTWT^APCHS2A2(APCHSPTY,$P(^DPT(APCHSPAT,0),U,2),APCHSAGE,X)
 K APCHSPTY,APCHSAGE
 Q
 ;
BMI ; GET BODY MASS INDEX
 S X=$$BMI^APCHS2A3(APCHSPAT,X,APCHSIDT) Q
RW ; GET RELATIVE WEIGHT PERCENTAGE
 S X=$$RW^APCHS2A3(APCHSPAT,X,APCHSIDT) Q
