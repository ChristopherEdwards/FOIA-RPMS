APCHTAXL ; IHS/CMI/LAB - DISPLAY IND LISTS ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;; ;
ICPTADD ;EP
 S ATXFLG=""
 I APCHFILE=81 S APCHGBL="^ICPT("
 S APCHX=APCHTAXI
 D BUILD,^APCHTAXM
 D EDIT
 K ATXFLG,APCHX,APCHL,ATX,APCHTBLE
 Q
 ;
BUILD ;BUILD APCHTBLE FOR ROUTINE ATXCODE
 S APCHCODE=0 F  S APCHCODE=$O(^ATXAX(APCHX,21,"AA",APCHCODE)) Q:APCHCODE=""  S APCHTBLE(APCHCODE)=$O(^(APCHCODE,""))
 Q
ENTER ;
 S APCH=0 F  S APCH=$O(APCHTBLE(APCH)) Q:APCH=""  S APCH("X")=$E(APCH,1,($L(APCH)-1)) D CALLDIE I $D(Y) S APCHSTP=1 Q
 W !!,"Done!"
 Q
 ;
CALLDIE ;
 S APCHHI=$E(APCHTBLE(APCH),1,($L(APCHTBLE(APCH))-1)),DIE="^ATXAX(",DR="2101///"_APCH("X")_" ",DA=APCHX,DR(2,9002226.02101)=".02////"_APCHHI_" " D ^DIE I $D(Y)
 E  I '$D(ZTQUEUED),$D(APCH("NOT TAX")) W "."
 I $D(Y),'$D(ZTQUEUED),$D(APCH("NOT TAX")) W !!,"ERROR ENCOUNTERED - REENTER RANGES",!
 K DIE,DR,DA
 Q
 ;
EDIT ;DELETE CODES IN TAXONOMY FILE, THEN ENTER NEW RANGES
 S APCH=0 F  S APCH=$O(^ATXAX(APCHX,21,APCH)) Q:APCH'=+APCH  S DA(1)=APCHX,DA=APCH,DIK="^ATXAX("_DA(1)_",21," D ^DIK K DIK,DA
 D ENTER
 Q
 ;
EOJ1 ;
 K APCHHI
 I $D(ZTQUEUED) S ZTREQ="@"
 Q
 ;
