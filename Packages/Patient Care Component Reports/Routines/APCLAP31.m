APCLAP31 ; IHS/CMI/LAB - visits by provider process ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
START ;
 S APCLBT=$H
 K ^XTMP("APCLAP3",APCLJOB,APCLBTH)
 D XTMP^APCLOSUT("APCLAP3","PCC - ALL VISITS BY PROV")
V ; Run by visit date
 S APCLODAT=APCLSD_".9999" F  S APCLODAT=$O(^AUPNVSIT("B",APCLODAT)) Q:APCLODAT=""!((APCLODAT\1)>APCLED)  D V1
 ;
END ;
 S APCLET=$H
 Q
V1 ;
 S V="" F  S V=$O(^AUPNVSIT("B",APCLODAT,V)) Q:V'=+V  I $D(^AUPNVSIT(V,0)) S APCLVREC=^(0) D
 .K APCLSKIP
 .Q:'$P(APCLVREC,U,9)
 .Q:$P(APCLVREC,U,11)
 .Q:$$DEMO^APCLUTL($P(APCLVREC,U,5),$G(APCLDEMO))
 .I $$CHKLOC^APCLOCCK(APCLLOC,$P(APCLVREC,U,6))=0 Q
 .Q:'$D(^AUPNVPOV("AD",V))
 .Q:'$D(^AUPNVPRV("AD",V))
 .S APCLVLOC=$P(APCLVREC,U,6) Q:APCLVLOC=""
 .S APCLCAT=$P(APCLVREC,U,7)
 .S DIQ(0)="E",DA=V,DIC="^AUPNVSIT(",DR=".07" D EN^DIQ1 S APCLCAT=^UTILITY("DIQ1",$J,9000010,DA,".07","E")
 .K ^UTILITY("DIQ1",$J)
 .S B=0 F  S B=$O(^AUPNVPRV("AD",V,B)) Q:B=""  D
 ..I APCLPRIM,$P(^AUPNVPRV(B,0),U,4)'="P" Q
 ..S P=$P(^AUPNVPRV(B,0),U)
 ..S F=0
 ..D @APCLPSRT
 ..Q:'F
 ..S ^(APCLCAT)=$S($D(^XTMP("APCLAP3",APCLJOB,APCLBTH,S,APCLVLOC,APCLCAT)):^(APCLCAT)+1,1:1)
 ..S ^("TOTAL")=$S($D(^XTMP("APCLAP3",APCLJOB,APCLBTH,S,"TOTAL")):^("TOTAL")+1,1:1)
 ..Q
 .Q
 Q
 ;
P ;
 I $D(APCLCDIS) S S="" D D Q:S=""
 S S=P,F=1
 Q
 ;
O ;
 Q:APCLPROV'=P
 S S=P,F=1
 Q
 ;
D ;
 I $P(^DD(9000010.06,.01,0),U,2)[6 G D6
 Q:'$D(^VA(200,P))
 S Y=$$PROVCLS^XBFUNC1(P,"I")
 Q:'Y
 Q:APCLDISC'=Y
 S S=APCLDISC
 S F=1
 Q
D6 ;
 Q:'$D(^DIC(6,P))
 S Y=$P(^DIC(6,P,0),U,4)
 Q:Y=""
 Q:APCLDISC'=Y
 S S=APCLDISC
 S F=1
 Q
 ;
A ;
 I $P(^DD(9000010.06,.01,0),U,2)[6 G A6
 Q:'$D(^VA(200,P))
 S Y=$$PROVCLS^XBFUNC1(P,"I")
 Q:'Y
 S S=Y
 S F=1
 Q
A6 ;
 Q:'$D(^DIC(6,P))
 S Y=$P(^DIC(6,P,0),U,4)
 Q:Y=""
 S S=Y
 S F=1
 Q
 ;
