APCLAUD5 ; IHS/CMI/LAB - USER INTERFACE TO SELECT ICD CODES ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
 ;cmi/anch/maw 9/10/2007 code set versioning in DISPLAY
BEGIN ;
 W !
 S APCL("NO DISPLAY")=0
 D ASK1
 I Y="^" S APCLSTP=1 G X
 I $D(APCLTABL) D CHECK I Y'=1 G @$S(Y=0:"BEGIN",1:"X")
X I $D(APCLTABL) D SETUTIL
 D EOJ
 Q
 ;
ASK1 ;
 S APCLA=0
 K APCL("LOW"),APCL("HI")
 S DIR("A")=$S('$D(APCLTABL):"Enter Diagnosis (or range of DX codes)",1:"Enter Another Diagnosis (or range of DX codes)") D SETDIR^APCLAUD6,^DIR K DIR
 I "^"[Y G X1
 D PROCESS
 I $D(APCLTABL),'APCL("NO DISPLAY") D RANGES
 S APCL("NO DISPLAY")=0
 G ASK1
X1 Q
 ;
PROCESS ;EVALUATE USER RESPONSE
 S (APCLSUB,APCLONE)=0 ;APCLSUB=0 => NO DELETE OF CODE(S),APCLONE=0 => RANGE OF CODES ENTERED
 I $E(X,1,2)="-[" W $C(7),"  ?? Not allowed" S APCL("NO DISPLAY")=1 G X2
 I X'["-" S APCLTYP="LOW",APCLONE=1 D LOOK^APCLAUD6 G X2
 I $E(X)="-",'$D(APCLTABL) W $C(7),"  ??  No previous codes entered!" G X2
 I $L(X,"-")>3 W $C(7),"  ??"  S APCLA=1 S APCL("NO DISPLAY")=1 G X2
 I $L(X,"-")=3,$E(X,$L(X))="-" W $C(7),"  ??" S APCLA=1 S APCL("NO DISPLAY")=1 G X2
 I $L(X,"-")=3,$P(X,"-")]"" W $C(7),"  ??" S APCLA=1 S APCL("NO DISPLAY")=1 G X2
 I $E(X)="-" S APCLSUB=1 D  I 1
 . S APCLSAVE("X")=X
 . I $L(X,"-")=3 S X=$P(APCLSAVE("X"),"-",2),APCLTYP="LOW" D LOOK^APCLAUD6 I 'APCLA S X=$P(APCLSAVE("X"),"-",3),APCLTYP="HI" W ! D LOOK^APCLAUD6 Q
 . I $L(APCLSAVE("X"),"-")=2 S X=$E(X,2,99),APCLTYP="LOW",APCLONE=1 D LOOK^APCLAUD6
 E  S APCLSAVE("X")=X S APCLTYP="LOW",X=$P(APCLSAVE("X"),"-") D LOOK^APCLAUD6 I 'APCLA S APCLTYP="HI",X=$P(APCLSAVE("X"),"-",2) W ! D LOOK^APCLAUD6
X2 Q
 ;
DISPLAY ;EP;SHOW CODES IN RANGE SELECTED
 W:$D(IOF) @IOF
 ;W !!,"ICD codes in this range =>",!! W $P(APCL("LOW")," ") S APCLDFN=$O(^ICD9("BA",APCL("LOW"),"")) W ?9,$P(^ICD9(APCLDFN,0),U,3)
 W !!,"ICD codes in this range =>",!! W $P(APCL("LOW")," ") S APCLDFN=$$ICDDX^ICDCODE(APCL("LOW"),U,2) W ?9,$P($$ICDDX^ICDCODE(APCLDFN),U,4)
 S APCL=APCL("LOW"),APCLCNT=IOSL-2
 F  S APCL=$O(^ICD9("BA",APCL)) Q:APCL]APCL("HI")  S APCLDFN=$O(^(APCL,"")) W !,$P(APCL," "),?9,$P($$ICDDX^ICDCODE(APCLDFN),U,4) S APCLCNT=APCLCNT-1 I APCLCNT=0 S APCLCNT=IOSL-2 D  I APCLR=U Q
A1 . R !,"<>",APCLR:DTIME W:APCLR["?" " Enter ""^"" to stop display, return to continue" G:APCLR["?" A1
 I $S('$D(APCLR):1,APCLR'=U:1,1:0) R !!,"Press return to continue",APCLR:DTIME
 W !
 K APCLR Q
 ;
SETUTIL ;SET UP UTILITY NODES TO STORE CODES
 S APCLIRNG=0,APCL("LOW")="" F  S APCL("LOW")=$O(APCLTABL(APCL("LOW"))) Q:APCL("LOW")=""  S APCL("HI")=APCLTABL(APCL("LOW")) D
 .S APCLIRNG=APCLIRNG+1
 .S ^XTMP("APCLAUD",APCLJOB,APCLBT,APCLIRNG,"ICDB")=APCL("LOW")
 .S ^XTMP("APCLAUD",APCLJOB,APCLBT,APCLIRNG,"ICDE")=APCL("HI")
 .S APCLDFN=$O(^ICD9("BA",APCL("LOW"),"")),^XTMP("APCLAUD",APCLJOB,APCLBT,"ICDDFN",APCLDFN,APCLIRNG)=""
 .S APCL=APCL("LOW") F  S APCL=$O(^ICD9("BA",APCL)) Q:APCL]APCL("HI")  S APCLDFN=$O(^(APCL,"")) S ^XTMP("APCLAUD",APCLJOB,APCLBT,"ICDDFN",APCLDFN,APCLIRNG)=""
 .Q
 Q
RANGES ;DISPLAY TABLE OF ALL RANGES
 W:$D(IOF) @IOF
 W !!,"ICD Code Range(s) Selected So Far =>",!
 S (APCL("NUM"),APCL)=0 F  S APCL=$O(APCLTABL(APCL)) Q:APCL=""  S APCL("NUM")=APCL("NUM")+1 W !,APCL("NUM"),")  ",APCL,$S(APCL'=APCLTABL(APCL):"- "_APCLTABL(APCL),1:"")
 I '$D(APCL("BANG")) W !
 Q
 ;
SHOW ; ENTRY POINT - ALLOW USER TO SELECT FROM RANGES TO DISPLAY CODES
 D RANGES
A W !,"Enter an Item Number from the table above to display code(s): " R APCL("N"):300 W:"^"[APCL("N") ! Q:"^"[APCL("N")  I APCL("N")'?1N!(APCL("N")>APCL("NUM")) W "  ??",$C(7) G A
 F APCLI=1:1:APCL("N") S APCL=$O(APCLTABL(APCL)) I APCLI=APCL("N") S APCL("LOW")=APCL,APCL("HI")=APCLTABL(APCL) D DISPLAY Q
 S APCL("BANG")="" D RANGES K APCL("BANG")
 Q
 ;
 ;
CHECK ;ASKS USER IF SATISFIED WITH ENTERED RANGES
 W ! S DIR(0)="Y",DIR("B")="Y",DIR("A")="Are all of the ICD ranges okay" D ^DIR K DIR
 W !
 Q
 ;
EOJ ;ENTRY POINT
 I $D(APCL("NOT TAX")) K APCLSTP,APCLX
 K APCLSUB,APCLTYP,APCLDFN,DIR,APCLSAVE,APCLA,APCLCNT,APCL,APCLR,APCLI,APCLONE,APCLFLG
 Q
 ;
