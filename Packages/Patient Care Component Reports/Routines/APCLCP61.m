APCLCP61 ; IHS/CMI/LAB - activity report ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 S APCLJOB=$J,APCLBT=$H
 D XTMP^APCLOSUT(APCLNSP,"PCC ACTIVITY REPORT")
V ; Run by visit date
 S APCLODAT=$O(^AUPNVSIT("B",APCLSD)) I APCLODAT="" S APCLET=$H Q
 S APCLODAT=APCLSD_".9999" F  S APCLODAT=$O(^AUPNVSIT("B",APCLODAT)) Q:APCLODAT=""!((APCLODAT\1)>APCLED)  D V1
 S APCLET=$H
 Q
V1 ;
 S APCLVDFN=0 F  S APCLVDFN=$O(^AUPNVSIT("B",APCLODAT,APCLVDFN)) Q:APCLVDFN'=+APCLVDFN  I $D(^AUPNVSIT(APCLVDFN,0)) S APCLVREC=^(0) D PROC,EOJ
 Q
PROC ;
 K APCLSKIP
 Q:$$DEMO^APCLUTL($P(APCLVREC,U,5),$G(APCLDEMO))
 Q:'$P(APCLVREC,U,9)
 Q:$P(APCLVREC,U,11)
 Q:"DXECH"[$P(APCLVREC,U,7)
 Q:"V"[$P(APCLVREC,U,3)
 I $D(APCLLOC) Q:$P(APCLVREC,U,6)=""  I '$D(APCLLOC($P(APCLVREC,U,6))) Q
 I $D(APCLCLN) Q:$P(APCLVREC,U,8)=""  I '$D(APCLCLN($P(APCLVREC,U,8))) Q
 Q:'$D(^AUPNVPOV("AD",APCLVDFN))
 Q:'$D(^AUPNVPRV("AD",APCLVDFN))
 S (APCL1,APCL2)=0 F L=0:0 S APCL2=$O(^AUPNVPRV("AD",APCLVDFN,APCL2)) Q:APCL2=""  I $P(^AUPNVPRV(APCL2,0),U,4)="P" S APCL1=APCL1+1,APCLAP=$P(^(0),U)
 I APCL1=0 Q
 I APCL1>1 Q
 S APCLVLOC=$P(APCLVREC,U,6)
 Q:APCLVLOC=""
 S APCLSU=$P(^AUTTLOC(APCLVLOC,0),U,5) I APCLSU="" S APCLSU="ZZZZ"
 D PROC2
 Q
EOJ K APCLVLOC,APCLVREC,APCLSKIP,APCL1,APCL2,APCLX,APCLY,APCLAP,APCLDISC,APCLVLOC,APCLVTM,APCLVTT
 Q
 ;
 ;
PROC2 ;
 S APCLX=0 F  S APCLX=$O(^AUPNVPRV("AD",APCLVDFN,APCLX)) Q:APCLX'=+APCLX  D
 . S APCLAP=$P(^AUPNVPRV(APCLX,0),U)
 . I $P(^DD(9000010.06,.01,0),U,2)[200 S APCLDISC=$$PROVCLSC^XBFUNC1(APCLAP) Q:'$D(^APCLACTG(APCLACTG,11,"AC",APCLDISC))  G PROC21
 . S APCLY=$P(^DIC(6,APCLAP,0),U,4)
 . I APCLY="" Q
 . I '$D(^DIC(7,APCLY,9999999)) Q
 . S APCLDISC=$P(^DIC(7,APCLY,9999999),U) I '$D(^APCLACTG(APCLACTG,11,"AC",APCLDISC)) Q
PROC21 . I '$D(^XTMP(APCLNSP,APCLJOB,APCLBT,"PATIENT",$P(APCLVREC,U,5),@APCLSORV)) D
 ..S ^("TOTAL")=$S($D(^XTMP(APCLNSP,APCLJOB,APCLBT,@APCLSORV,APCLVLOC,"TOTAL")):^("TOTAL")+1,1:1),^XTMP(APCLNSP,APCLJOB,APCLBT,"PATIENT",$P(APCLVREC,U,5),@APCLSORV)=""
 Q
