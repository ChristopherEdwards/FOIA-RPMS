APCLDIC ; IHS/CMI/LAB - FILEMAN INTERFACE UTILITY ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;;UTILITY ROUTINE USED TO MANAGE ALL INTERFACE TO FILEMAN
 ;;WITH FEW EXCEPTIONS, THIS IS THE ONLY PLACE IN ARMS FROM WHICH
 ;;FILEMAN ENTRY POINTS ARE CALLED
DIC ;EP;
 Q:$D(APCLOUT)
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 D ^DIC
 S:$D(DUOUT) APCLQUIT=""
 S:$D(DTOUT)!(X="^^") (APCLQUIT,APCLOUT)=""
 K DIC,DA,DD,DR,DINUM,D,DLAYGO
 Q
MIX ;EP;
 Q:$D(APCLOUT)
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 D MIX^DIC1
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (APCLQUIT,APCLOUT)=""
 K DIC,DA,DD,DR,DINUM,D,DLAYGO
 Q
IX ;EP;
 Q:$D(APCLOUT)
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 D IX^DIC
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (APCLQUIT,APCLOUT)=""
 K DIC,DA,DD,DR,DINUM,D,DLAYGO
 Q
DIE ;EP;
 Q:$D(APCLOUT)
 Q:'DA
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 L +@(DIE_DA_")"):4 I '$T S APCLQUIT="" Q:'$D(IOST)#2  W:$E(IOST,1,2)="C-" !!,"Entry being edited by another user.  Please try Later.",! H 3 Q
 S APCLDIDA=DA
 D ^DIE
 L -@(DIE_APCLDIDA_")"):0
 I $D(DTOUT)!$D(DUOUT) S (APCLQUIT,APCLOUT)=""
 K DIE,DA,DR,APCLDIDA
 Q
DDS ;EP;FOR SCREENMAN CALLS
 S DDSFILE=DIE
 D ^DDS
 K DDSFILE,APCLSCRN
 Q
FILE ;EP;
 K DD,DO,DTOUT,DUOUT,APCLQUIT,APCLOUT
 D FILE^DICN
 S:$D(DTOUT)!$D(DUOUT)!(X="^^") (APCLQUIT,APCLOUT)=""
 K DIC,DA,DD,DR,DINUM,DLAYGO
 Q
DIR ;EP;
 I $D(APCLOUT) K DIR S Y="" Q
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 D ^DIR
 S APCLY=Y
 S:$D(DIRUT)!$D(DIROUT)!$D(DUOUT) APCLQUIT=""
 S:X="^^"!$D(DTOUT) (APCLQUIT,APCLOUT)=""
 K DIR,DIRUT,DIROUT,DUOUT,DTOUT
 Q
DIK ;EP;
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 D ^DIK
 K DIK
 Q
IX1 ;EP;
 Q:$D(APCLOUT)
 K DTOUT,DUOUT,APCLQUIT,APCLOUT
 D IX1^DIK
 K DIK,DA
 Q
DIQ1 ;EP;
 D EN^DIQ1
 K DIC,DA,DR,DIQ
 Q
WP ;EP;TO BROWSE WORD PROCESSING FIELD
 Q:'$D(APCLILE)#2!'$D(APCLIEN)#2!'$D(APCLIELD)
 K APCLSCRN
 D WP^DDBR(APCLILE,APCLIEN,APCLIELD,"N",$G(APCLTTLE))
 Q
