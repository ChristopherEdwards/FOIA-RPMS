APCLFS ; IHS/CMI/LAB - DMS FLOW SHEET MANAGEMENT UTILITY ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;UTILITY PROGRAM TO MANAGE FLOW SHEET CREATION AND EDITING
FS ;EP;FLOW SHEET MANAGEMENT
 S IOP="HOME"
 D ^%ZIS
 D FS1 Q:$D(APCLQUIT)!$D(APCLOUT)
FSEXIT K APCLQUIT,APCLOUT,APCLJ,APCLX,APCLY,APCLSDA,APCLSNAM,APCLCINK,APCLCNK0,APCLCDA,APCLWHCH,APCLADA,APCLANAM,APCLSF,APCLCANN,APCLGO
 K ^TMP("APCLVR",$J)
 Q
FS1 D FSEXIT
 D FSDISP
 Q
FSDISP ;DISPLAY FLOW SHEET
 D VALM("APCL FLOW SHEET LIST")
 Q
FSHEAD ;PRINT HEADER FOR FLOW SHEET MANAGEMENT
 W @IOF
FSHEAD1 N X
 F X="DIABETES MANAGEMENT SYSTEM","FLOW SHEET MANAGEMENT" D
 .W !?(80-$L(X))\2,X
 Q
DXHEAD ;PRINT HEADER FOR FLOW SHEET MANAGEMENT
 W @IOF
 D FSHEAD1
 N X
 F X="Diagnosti/Medication Flow Sheets"
 W !?(80-$L(X))\2,X
 Q
FSDH ;DISPLAY HEADER FOR FLOW SHEET SYSTEM
 Q
FSADD ;EP;ENTER A NEW FLOW SHEET
 S DIR(0)="FO^3:30"
 S DIR("A")="Flow Sheet Name"
 W !
 D DIR^APCLDIC
 I Y="" S APCLQUIT="" D TABACK Q
 I X="^" S APCLQUIT="" D TABACK Q
 S (X,APCLSNAM)=Y
 S DIC="^APCHSFLC("
 S DIC(0)="L"
 D FILE^APCLDIC
 S APCLSDA=+Y
 I 'APCLSDA D TABACK Q
 D FSCEDIT
 D FSCLIST
TABACK S APCLGO="FS"
 D BACK
 Q
FSEDIT ;EP;EDIT AN EXISTING FLOW SHEET
 D SELECT
 I $D(APCLQUIT) K APCLQUIT D TEBACK Q
 D FSCLIST
TEBACK S APCLGO="FS"
 D BACK
 Q
SELECT ;SELECT AN EXISTING FLOW SHEET
 S DIR(0)="NO^1:"_APCLJ
 S DIR("A")="Which Flow Sheet"
 W !
 D DIR^APCLDIC
 I Y<1 S APCLQUIT="" Q
 Q:'$D(APCLJ(Y))
 S APCLSDA=+APCLJ(Y)
 S APCLSNAM=$P(APCLJ(Y),U,2)
 D FSCLIST
 Q
FSCEDIT ;EP;EDIT A FLOW SHEET COMPONENT
 S DA=APCLSDA
 S DIE="^APCHSFLC("
 S DR="[APCL FLOW SHEET COMPONENT]"
 D DDS^APCLDIC
 S APCLGO="FSC"
 D BACK
 Q
FSINIT ;EP;INITIALIZE ARRAY FOR FLOW SHEET DISPLAY
 K ^TMP("APCLVR",$J),APCLJ
 S VALMCNT=0
 S X="     NO. Flow sheet"
 D Z(X)
 S X="     --- ------------------------------"
 D Z(X)
 N I,J,X,Y,Z
 S I=0
 S X=""
 F  S X=$O(^APCHSFLC("B",X)) Q:X=""  D
 .S Y=0
 .F  S Y=$O(^APCHSFLC("B",X,Y)) Q:'Y  D
 ..S I=I+1
 ..S A="    "_I
 ..S:$L(A)=5 A=" "_A
 ..S A=A_"   "_X
 ..D Z(A)
 ..S APCLJ(I)=Y_U_X
 I '$D(^TMP("APCLVR",$J)) D
 .S VALMCNT=2
 .S X="NO FLOW SHEET ON FILE FOR "_APCLX
 .D Z(X)
 S APCLJ=I
 Q
VALM(APCLX) ;VALM INTERFACE
 S VALMCC=1 ;1=screen mode, 0=scrolling mode
 D TERM^VALM0
 D EN^VALM(APCLX)
 D CLEAR^VALM1
 Q
FSCLIST ;EP;TO DISPLAY ITEMS ON FLOW SHEET LIST
 D VALM("APCL FLOW SHEET COMPONENT LIST")
 Q
FSCINIT ;EP;TO LIST ITEMS ON FLOW SHEET
 N A,B,J,X,Y,Z,APCLTYPE,APCLABEL
 K ^TMP("APCLVR",$J),APCLJ,APCLCS
 S VALMCNT=0
 S X="     "_APCLSNAM
 D Z(X)
 S X="   "
 D Z(X)
 S X="     Flowsheet Components"
 D Z(X)
 S X="     NO.  ORDER  TYPE                 LABEL                 WIDTH"
 D Z(X)
 S X="     ---  -----  -------------------  --------------------  -----"
 D Z(X)
 S (J,APCLX)=0
 F  S APCLX=$O(^APCHSFLC(APCLSDA,1,"B",APCLX)) Q:'APCLX  D
 .S APCLCDA=0
 .F  S APCLCDA=$O(^APCHSFLC(APCLSDA,1,"B",APCLX,APCLCDA)) Q:'APCLCDA  D
 ..S X=$G(^APCHSFLC(APCLSDA,1,APCLCDA,0))
 ..Q:X=""
 ..S J=J+1
 ..S A="     "_J
 ..S:$L(A)=6 A=A_" "
 ..S A=A_"    "
 ..S A=A_$P(X,U)
 ..S:$L($P(X,U))=1 A=A_" "
 ..S A=A_"    "
 ..S APCLTYPE=$P($G(^APCHSFLI(+$P(X,U,2),0)),U)
 ..S APCLTYPE=APCLTYPE_$E("                    ",1,20-$L(APCLTYPE))
 ..S A=A_APCLTYPE
 ..S A=A_" "
 ..S APCLABEL=$P(X,U,3)
 ..S APCLABEL=APCLABEL_$E("                    ",1,20-$L(APCLABEL))
 ..S A=A_APCLABEL
 ..S A=A_"   "
 ..S APCLWDTH=$P(X,U,4)
 ..S A=A_APCLWDTH
 ..S X=A
 ..D Z(X)
 ..S APCLJ(APCLSDA,J)=APCLCDA_U_A
 ..D MEMLIST
 S APCLJ=J
 Q
FSCADD ;EP;TO ADD ITEM TO FLOW SHEET
 K APCL
 N X,Y
 I APCLANAM="DIAGNOSIS"!(APCLSNAM="PROBLEM LIST DIAGNOSIS") D  I 1
 .S X=0
 .F  S X=$O(^APCHSFLC(APCLSDA,1,X)) Q:'X  D
 ..S Y=$G(^APCHSFLC(APCLSDA,1,X,0))
 ..S:$P(Y,U)]"" APCL(X)=$P(Y,U)_U_$S($P(Y,U,2)]"":$P(Y,U,2),1:$P(Y,U))
 .D ^APCLFS1
 .Q:$D(APCLQUIT)
 .S X=$P(APCL("LOW"),U)
 E  D
 .D CLEAR^VALM1
 .W !?5,"Select an item to ADD to the"
 .W !!?5,APCLSNAM," Flow Sheet"
 .S DIC=APCLSF
 .S DIC(0)="AEMQZ"
 .S DIC("A")="Which "_APCLANAM_": "
 .W !
 .D DIC^APCLDIC
 .I +Y<1 S APCLQUIT="" Q
 .S APCL("LOW")=+Y
 .D X
 .S APCL("HIGH")=""
 I $D(APCLQUIT) D FSCBACK Q
 S DA(1)=APCLSDA
 S DIC="^APCHSFLC("_APCLSDA_",1,"
 S DIC(0)="L"
 S DIC("DR")=".02////"_APCL("HIGH")
 D FILE^APCLDIC:'$D(^APCHSFLC(APCLSDA,1,"B",X))
FSCBACK ;EP;S APCLGO="FSC"
 D BACK
 Q
X ;EVALUATE X FOR PROPER INTERNAL VALUE
 I APCLANAM="ADA CODE" S X=Y(0,0)
 I APCLANAM="RX" S X=+Y
 I APCLANAM="PROCEDURE (MEDICAL)" S X=Y(0,0)
 I APCLANAM="PATIENT ED TOPIC" S X=+Y
 I APCLANAM="HEALTH FACTORS" S X=+Y
 I APCLANAM="PROBLEM LIST DIAGNOSIS" S X=Y(0,0)
 Q
FSCDEL ;EP;TO DELETE ITEM FROM FLOW SHEET
 D FSCSEL
 I $D(APCLQUIT) K APCLQUIT D FSCBACK Q
 N APCLI,APCLX
 F APCLI=1:1 S APCLX=$P(APCLY,",",APCLI) Q:APCLX=""  D
 .Q:'$D(APCLJ(APCLSDA,APCLX))
 .S APCLCDA=+APCLJ(APCLSDA,APCLX)
 .S DA(1)=APCLSDA
 .S DA=APCLCDA
 .S DIK="^APCHSFLC("_DA(1)_",1,"
 .D DIK^APCLDIC
 S APCLGO="FSC"
 D BACK
 Q
FSCSEL ;EP;SELECT EXISTING ITEM FROM A FLOW SHEET
 S DIR(0)="LO^1:"_APCLJ
 S DIR("A")="Whick Flow Sheet Component(s)"
 W !
 D DIR^APCLDIC
 I Y<1 S APCLQUIT="" Q
 S APCLY=Y
 Q
BACK ;EP;SETUP FOR RETURN TO LISTMAN
 S VALMBCK="R"
 D FSINIT:APCLGO="FS"
 D FSCINIT:APCLGO="FSC"
 D MEMINIT:APCLGO="FSM"
 D TERM^VALM0
 Q
MEMBERS ;EP;TO SPECIFY THE MEMBERS FOR A FLOW SHEET COMPONENT
 D FSCSEL
 I $D(APCLQUIT) K APCLQUIT D FSCBACK Q
 F APCLI=1:1 S APCLX=$P(APCLY,",",APCLI) Q:APCLX=""!$D(APCLQUIT)  D
 .Q:'$D(APCLJ(APCLSDA,APCLX))
 .S APCLCDA=+APCLJ(APCLSDA,APCLX)
 .Q:'APCLCDA
 .S APCLTYPE=$G(^APCHSFLI(+$P($G(^APCHSFLC(+APCLSDA,1,+APCLCDA,0)),U,2),0))
 .D MEMDISP
 Q
MEMSEL ;SELECT THE MEMBER OF THE COMPONENT TO EDIT OR DELETE
 S DIR(0)="LO^1:"_APCLJ
 S DIR("A")="Whick Component Members(s)"
 W !
 D DIR^APCLDIC
 I Y<1 S APCLQUIT="" Q
 S APCLY=Y
 Q
MEMADD ;EP;TO ADD MEMBERS TO A FLOW SHEET COMPONENT
 S APCLTYPE=$G(^APCHSFLI(+$P($G(^APCHSFLC(+APCLSDA,1,+APCLCDA,0)),U,2),0))
 F  D MADD1 Q:$D(APCLQUIT)
 K APCLQUIT
 D MBACK
 Q
MADD1 W @IOF
 W !?5,"Select"
 W !?5,$P(APCLTYPE,U),?25,"to add to the"
 W !?5,$P(APCLTYPE,U),?25,"component of the "
 W !?5,APCLSNAM,?25,"Flow Sheet"
 S DIC=+$P(APCLTYPE,U,2)
 I 'DIC S APCLQUIT="" Q
 S DIC=^DIC(DIC,0,"GL")
 I DIC="" S APCLQUIT="" Q
 S APCLDIC=DIC
 S DIC(0)="AEMQZ"
 S DIC("A")="Which "_$P(APCLTYPE,U)_": "
 W !
 D DIC^APCLDIC
 I +Y<1 S APCLQUIT="" Q
 S X=+Y_";"_$E(APCLDIC,2,99)
 S $P(^APCHSFLC(APCLSDA,1,APCLCDA,2,0),U,2)="9001020.15AV"
 S DA(2)=APCLSDA
 S DA(1)=APCLCDA
 S DIC="^APCHSFLC("_APCLSDA_",1,"_APCLCDA_",2,"
 S DIC(0)="L"
 D FILE^APCLDIC
 Q
MEMDEL ;EP;TO DELETE MEMBERS FROM A FLOW SHEET COMPONENT
 N APCLY
 D MEMSEL
 I $D(APCLQUIT) K APCLQUIT D MBACK Q
 F APCLI=1:1 S APCLX=$P(APCLY,",",APCLI) Q:APCLX=""  D
 .Q:'$D(APCLJ(APCLSDA,APCLCDA,APCLX))
 .S DA=+APCLJ(APCLSDA,APCLCDA,APCLX)
 .S DA(2)=APCLSDA
 .S DA(1)=APCLCDA
 .S DIK="^APCHSFLC("_DA(2)_",1,"_DA(1)_",2,"
 .D DIK^APCLDIC
MBACK S APCLGO="FSM"
 D BACK
 Q
DELETE ;EP;TO DELETE FLOW SHEET COMPONENT
 Q
MEMDISP ;DISPLAY MEMBERS OF A COMPONENT
 D VALM("APCL FLOW SHEET MEMBERS")
 Q
MEMINIT ;EP;TO LIST ITEMS ON FLOW SHEET
 K ^TMP("APCLVR",$J),APCLJ
 S VALMCNT=0
 S X="     "_$P(APCLTYPE,U)
 D Z(X)
 S X="     ---------------------------"
 D Z(X)
 N A,B,X,Y
 S APCLMDA=0
 F  S APCLMDA=$O(^APCHSFLC(APCLSDA,1,APCLCDA,2,APCLMDA)) Q:'APCLMDA  D
 .S X=$G(^APCHSFLC(APCLSDA,1,APCLCDA,2,APCLMDA,0))
 .Q:X=""
 .S APCLGL=U_$P(X,";",2)
 .S:$E(APCLGL,$L(APCLGL))="(" APCLGL=$E(APCLGL,1,$L(APCLGL)-1)
 .S:$E(APCLGL,$L(APCLGL))="," APCLGL=$E(APCLGL,1,$L(APCLGL)-1)_")"
 .S APCLDA=+X
 .S X=$P($G(@APCLGL@(+X,0)),U,$S(APCLGL'["AUTTMSR":1,1:2))
 .S A="     "_(VALMCNT-1)
 .S:$L(A)=6 A=A_" "
 .S A=A_"    "
 .S A=A_X
 .S X=A
 .D Z(X)
 .S APCLJ(APCLSDA,APCLCDA,VALMCNT-2)=APCLMDA_U_A
 S APCLJ=VALMCNT-2
 Q
MEMLIST ;LIST MEMBERS OF EACH COMPONENT FOR DISPLAY WITH COMPONENTS
 N J,X,Y,APCLX,APCLMDA
 S (J,APCLMDA)=0
 F  S APCLMDA=$O(^APCHSFLC(APCLSDA,1,APCLCDA,2,APCLMDA)) Q:'APCLMDA  D
 .S X=$G(^APCHSFLC(APCLSDA,1,APCLCDA,2,APCLMDA,0))
 .Q:X=""
 .S J=J+1
 .S APCLGL=U_$P(X,";",2)
 .S:$E(APCLGL,$L(APCLGL))="(" APCLGL=$E(APCLGL,1,$L(APCLGL)-1)
 .S:$E(APCLGL,$L(APCLGL))="," APCLGL=$E(APCLGL,1,$L(APCLGL)-1)_")"
 .S X=$P($G(@APCLGL@(+X,0)),U,$S(APCLGL'["AUTTMSR":1,1:2))
 .S A="                 "_X
 .S X=A
 .D Z(X)
 Q
HEADER ;EP;TO SET HEADER CODE
 S VALMSG="- Prev Screen  QU Quit  ?? More Actions"
 Q
Z(X) ;SET TMP GLOBAL
 S VALMCNT=VALMCNT+1
 S ^TMP("APCLVR",$J,VALMCNT,0)=X
 Q
