APCLFS1 ; IHS/CMI/LAB - DMS FLOW SHEET MANAGEMENT UTILITY ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;UTILITY PROGRAM TO MANAGE FLOW SHEET CREATION AND EDITING
CSADD ;EP;ADD CLINIC STOP
 W @IOF
 W !?5,"Select CLINIC to add to the"
 W !?5,APCLSNAM,?25,"Flow Sheet"
 S DIC="^DIC(40.7,"
 S DIC(0)="AEMQZ"
 S DIC("A")="Which CLINIC: "
 W !
 D DIC^APCLDIC
 Q:+Y<1
 S (DINUM,X)=+Y
 S DA(1)=APCLSDA
 S DIC="^APCHSFLC("_APCLSDA_",3,"
 S DIC(0)="L"
 D FILE^APCLDIC
 D CSBACK
 Q
CSDEL ;EP;DELETE CLINIC STOP
 N APCLY
 D CSSEL
 I $D(APCLQUIT) K APCLQUIT D FSCBACK^APCLFS Q
 F APCLI=1:1 S APCLX=$P(APCLY,",",APCLI) Q:APCLX=""  D
 .Q:'$D(APCLCS(APCLSDA,APCLX))
 .S DA=$P(APCLCS(APCLSDA,APCLX),U,2)
 .S DA(1)=APCLSDA
 .S DIK="^APCHSFLC("_DA(1)_",3,"
 .D DIK^APCLDIC
CSBACK S APCLGO="FSC"
 D BACK^APCLFS
 Q
CSSEL ;SELECT CLINIC STOP
 S DIR(0)="LO^1:"_APCLCS
 S DIR("A")="Whick Clinic Stop(s)"
 W !
 D DIR^APCLDIC
 I Y<1 S APCLQUIT="" Q
 S APCLY=Y
 Q
