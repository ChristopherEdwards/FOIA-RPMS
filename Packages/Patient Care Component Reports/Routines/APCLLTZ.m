APCLLTZ ; IHS/CMI/LAB - DEVICE CALLS AND QUEUING ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;;ROUTINE USED AS CENTRAL POINT FOR ALL DEVICE HANDLING AND QUEUING
ZIS ;EP;TO CALL DEVICE
 I $G(APCLBROW) D  Q
 .S DIR(0)="SO^P:PRINT Output;B:BROWSE Output on Screen"
 .S DIR("A")="Do you want to "
 .S DIR("B")="PRINT"
 .W !
 .D DIR^APCLLTD
 .Q:$D(APCLLTQT)!$D(APCLLTOT)
 .I $E($G(X))="P" D ZIS1 Q
 .I $E($G(Y))="B" D BROWSE Q
ZIS1 ;EP;
 K DN
 S %ZIS="AEMNPQ"
 S ZIBH=$TR($H,",","")_$R(1000)
 W !
 D ^%ZIS
 I POP>0 D CLOSE Q
 S:$G(IOPAR)]"" %ZIS("IOPAR")=IOPAR
 S ZTSAVE("%ZIS*")=""
 S ZTSAVE("ZIBH")=""
 S ZTRTN="OPEN^APCLLTZ"
 I $D(IO("Q")),IO=IO(0)!$D(IO("S")) D  G ZIS
 .W *7,*7
 .W !!,"CANNOT QUEUE TO HOME OR SLAVE DEVICE."
 I '$D(IO("Q")) D  D CLOSE Q
 .I $E(IOST,1,2)="P-" D
 ..W !!,"...One moment please, while I complete your print request..."
 ..W !
 .D:$D(APCLRTN) @ZTRTN
 E  D ZTLOAD
 Q
CLOSE ;EP;TO CLOSE DEVICE
 D ^%ZISC
 K IOP,IOPAR,%ZIS,ZTSK,ZTQUEUED,ZTREQ
 Q
ZTLOAD ;EP;TO CALL %ZTLOAD
 K APCLLTDR
 S ZTIO=ION
 S ZTSAVE("ACM*")=""
 D ^%ZTLOAD
 W !!,$S($G(ZTSK)]"":"Request queued!",1:"Request cancelled.")
 D CLOSE
 H 2
 Q
OPEN ;EP;TO OPEN DEVICE AND PRINT SELECTED REPORT
 I '$D(ZTQUEUED)!(ION["HOST") S IOP=ION D ^%ZIS I POP S APCLLTQT="" Q
 U IO
 D @APCLRTN
 S:$D(ZTQUEUED) ZTREQ="@"
 D:'$D(ZTQUEUED) CLOSE
 Q
HOST ;EP;TO OPEN HOST FILE
 ;%FN - FILE NAME REQUIRED
 ;APCLLTOP - 'R' FOR READ, 'W' FOR WRITE REQUIRED, 'M' FOR READ/WRITE
 Q:'$D(%FN)!'$D(APCLLTOP)
 S POP=1
 F APCLI=51:1:54 Q:'POP  D
 .S (IOP,ION)=APCLI
 .S %ZIS("IOPAR")="("""_%FN_""":"""_APCLLTOP_""")"
 .D ^%ZIS
 I POP D  G HOST:$G(APCLLTX)<2 S APCLLTQT="" Q
 .W !!,"Waiting for HOST FILE SERVER."
 .S APCLLTX=$G(APCLLTX)+1
 K IOP,POP
 Q
BROWSE ;EP;TO BROWSE
 Q:$G(APCLRTN)=""
 S APCLFLD("BROWSE")=1
 D VIEWR^XBLM(APCLRTN)
 I $D(APCLLTQT) D  Q
 .K APCLLTQT
 .W !!,"BROWSE function temporarily unavailable."
 .D ZIS1
 D CLEAR^VALM1
 Q
