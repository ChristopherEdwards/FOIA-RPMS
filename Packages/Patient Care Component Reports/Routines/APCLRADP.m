APCLRADP ; IHS/CMI/LAB - PRINT CLINIC VISITS ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
 ;cmi/anch/maw 9/10/2007 code set versioning in PRINT1,PRINT2
 ;
INIT ;
 S APCLPG=0
 I '$D(^XTMP("APCLRAD",APCLJOB,APCLBTH)) D HEAD W !,"No visits to report."  G END
 ;
SET ;
 D HEAD
 S APCLVDFN=0
 F  S APCLVDFN=$O(^XTMP("APCLRAD",APCLJOB,APCLBTH,APCLVDFN)) Q:APCLVDFN=""!($D(APCLQUIT))  D SET2
END ;
 D DONE^APCLOSUT
 K ^XTMP("APCLRAD",APCLJOB,APCLBTH)
 Q
SET2 ;
 S APCLVREC=^AUPNVSIT(APCLVDFN,0),DFN=$P(APCLVREC,U,5)
PRINT1 ;
 I $Y>(IOSL-5) D HEAD Q:$D(APCLQUIT)
 W !,$E($P(^DPT(DFN,0),U),1,20),?22,$$HRN^AUPNPAT(DFN,$S(APCLLOC:APCLLOC,1:DUZ(2))),?29,$$VD^APCLV(APCLVDFN,"S"),?38,$P(^AUTTLOC($P(APCLVREC,U,6),0),U,7),?43,$E($$PRIMPROV^APCLV(APCLVDFN,"N"),1,10) S APCLFRST=0,APCLP=0
 F  S APCLP=$O(^AUPNVPOV("AD",APCLVDFN,APCLP)) Q:APCLP'=+APCLP  D
 .W:APCLFRST !
 .S APCLFRST=APCLFRST+1
 .;W ?54,$P(^ICD9($P(^AUPNVPOV(APCLP,0),U),0),U) W:$P(^AUPNVPOV(APCLP,0),U,4)]"" ?61,$E($P(^AUTNPOV($P(^AUPNVPOV(APCLP,0),U,4),0),U),1,19)  ;cmi/anch/maw 9/10/2007 orig line
 .W ?54,$P($$ICDDX^ICDCODE($P(^AUPNVPOV(APCLP,0),U)),U,2) W:$P(^AUPNVPOV(APCLP,0),U,4)]"" ?61,$E($P(^AUTNPOV($P(^AUPNVPOV(APCLP,0),U,4),0),U),1,19)  ;cmi/anch/maw 9/10/2007 csv
 .Q
PRINT2 ;
 S APCLV=0 F  S APCLV=$O(^XTMP("APCLRAD",APCLJOB,APCLBTH,APCLVDFN,APCLV)) Q:APCLV=""!($D(APCLQUIT))  S APCLDFN=0 D
 .S APCLFAC=$S($P(^AUPNVSIT(APCLV,0),U,6):$P(^AUTTLOC($P(^AUPNVSIT(APCLV,0),U,6),0),U,7),1:"--")
 .I $Y>(IOSL-5) D HEAD Q:$D(APCLQUIT)
 .W !,?29,$$VD^APCLV(APCLV,"S"),?38,APCLFAC,?43,$E($$PRIMPROV^APCLV(APCLV,"N"),1,10) S (APCLFRST,APCLP)=0
 .F  S APCLP=$O(^AUPNVPOV("AD",APCLV,APCLP)) Q:APCLP'=+APCLP  D
 ..W:APCLFRST !
 ..S APCLFRST=APCLFRST+1
 ..;W ?54,$P(^ICD9($P(^AUPNVPOV(APCLP,0),U),0),U) W:$P(^AUPNVPOV(APCLP,0),U,4)]"" ?61,$E($P(^AUTNPOV($P(^AUPNVPOV(APCLP,0),U,4),0),U),1,19)  ;cmi/anch/maw 9/10/2007 orig line
 ..W ?54,$P($$ICDDX^ICDCODE($P(^AUPNVPOV(APCLP,0),U)),U,2) W:$P(^AUPNVPOV(APCLP,0),U,4)]"" ?61,$E($P(^AUTNPOV($P(^AUPNVPOV(APCLP,0),U,4),0),U),1,19)  ;cmi/anch/maw 9/10/2007 csv
 ..Q
 .Q
 W !
 Q
HEAD I 'APCLPG G HEAD1
 I $E(IOST)="C",IO=IO(0) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S APCLQUIT="" Q
HEAD1 ;
 W:$D(IOF) @IOF S APCLPG=APCLPG+1
 W !
 S X=$P(^DIC(4,DUZ(2),0),U)
 W !!,$P(^VA(200,DUZ,0),"^",2),?(80-$L(X)/2),X,?70,"Page ",APCLPG
 W !?19,"READMISSIONS WITHIN 30 DAYS OF A DISCHARGE"
 W !?18,"VISITS DATES:  ",$$FMTE^XLFDT(APCLBD),"  TO  ",$$FMTE^XLFDT(APCLED)
 W !!?5,"NAME",?22,"HRCN",?29,"ADM DATE",?38,"LOC",?43,"PROV",?54,"ICD",?61,"PROVIDER NARRATIVE"
 W !,$TR($J("",80)," ","-")
 W !
 Q
 ;
