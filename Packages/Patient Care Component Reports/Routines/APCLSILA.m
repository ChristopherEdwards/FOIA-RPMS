APCLSILA ;IHS/CMI/LAB - AGGREGATE ILI REPORT; 
 ;;3.0;IHS PCC REPORTS;**24,25,26,27**;FEB 05, 1997
 ;
MEDS ;EP
 NEW C,X,Y,Z,T,L,M,N
 S T=$O(^ATXAX("B","FLU ANTIVIRAL MEDS",0))
 S C="",X=0 F  S X=$O(^AUPNVMED("AD",APCLVDFN,X)) Q:X'=+X!(C)  S Y=$P($G(^AUPNVMED(X,0)),U) D
 .Q:'Y
 .Q:'$D(^PSDRUG(Y,0))
 .S Z=0
 .S N=$P(^PSDRUG(Y,0),U)
 .I $D(^ATXAX(T,21,"B",Y)) S Z=1
 .I N["OSELTAMIVIR" S Z=1
 .I N["ZANAMIVIR" S Z=1
 .Q:'Z
 .S $P(APCLMEDS(N),U,1)=$P($G(APCLMEDS(N)),U,1)+1
 .;Q:$D(APCLMEDP(N,$P(APCLVREC,U,5)))
 .Q:$D(^XTMP("APCLSILR",APCLJ,APCLH,"APCLMEDP",$P(APCLVREC,U,5),N))
 .S $P(APCLMEDS(N),U,2)=$P($G(APCLMEDS(N)),U,2)+1
 .;S APCLMEDP(N,$P(APCLVREC,U,5))=""
 .S ^XTMP("APCLSILR",APCLJ,APCLH,"APCLMEDP",$P(APCLVREC,U,5),N)=""
 Q
VACAGE ;EP
 ;S H=$$HASVAC^APCLSILI(APCLVDFN)
 S I=$$HASIVAC^APCLSILI(APCLVDFN)
 I 'I Q
 S S2="A"
 S APCLAY=$$AGE^APCLSILU($P(APCLVREC,U,5),1,$$VD^APCLV(APCLVDFN))
 I APCLAY["<"!(APCLAY<5) D  Q
 .S APCLAY=$$AGE^APCLSILU($P(APCLVREC,U,5),2,$$VD^APCLV(APCLVDFN))
 .I APCLAY<6 Q
 .S A=$$AGEGM^APCLSILR(APCLAY)
 .I I S APCLIMMG("I",S2,A)=$G(APCLIMMG("I",S2,A))+1,APCLIMMG("I",S2,"TOTAL")=$G(APCLIMMG("I",S2,"TOTAL"))+1 D
 ..S APCLIMML(APCLLOCN,"I",S2,A)=$G(APCLIMML(APCLLOCN,"I",S2,A))+1,APCLIMML(APCLLOCN,"I",S2,"TOTAL")=$G(APCLIMML(APCLLOCN,"I",S2,"TOTAL"))+1
 ;.I H S APCLIMMG("H",S2,A)=$G(APCLIMMG("H",S2,A))+1,APCLIMMG("H",S2,"TOTAL")=$G(APCLIMMG("H",S2,"TOTAL"))+1 D
 ;..S APCLIMML(APCLLOCN,"H",S2,A)=$G(APCLIMML(APCLLOCN,"H",S2,A))+1,APCLIMML(APCLLOCN,"H",S2,"TOTAL")=$G(APCLIMML(APCLLOCN,"H",S2,"TOTAL"))+1
 S A=$$AGEG^APCLSILR(APCLAY)
 I I S APCLIMMG("I",S2,A)=$G(APCLIMMG("I",S2,A))+1,APCLIMMG("I",S2,"TOTAL")=$G(APCLIMMG("I",S2,"TOTAL"))+1 D
 .S APCLIMML(APCLLOCN,"I",S2,A)=$G(APCLIMML(APCLLOCN,"I",S2,A))+1,APCLIMML(APCLLOCN,"I",S2,"TOTAL")=$G(APCLIMML(APCLLOCN,"I",S2,"TOTAL"))+1
 ;I H S APCLIMMG("H",S2,A)=$G(APCLIMMG("H",S2,A))+1,APCLIMMG("H",S2,"TOTAL")=$G(APCLIMMG("H",S2,"TOTAL"))+1 D
 ;.S APCLIMML(APCLLOCN,"H",S2,A)=$G(APCLIMML(APCLLOCN,"H",S2,A))+1,APCLIMML(APCLLOCN,"H",S2,"TOTAL")=$G(APCLIMML(APCLLOCN,"H",S2,"TOTAL"))+1
 Q
SET7 ;
 S APCLCNT=APCLCNT+1
 S APCLADVE(APCLCNT)=$$VAL^XBDIQ1(9000010.07,X,.01)
 Q
TAB727 ;EP
 ;get adverse events on this visit
 Q:$E($$VD^APCLV(APCLVDFN),4,5)="06"
 Q:$E($$VD^APCLV(APCLVDFN),4,5)="07"
 K APCLADVE
 NEW X,P,Y,Z,APCLCLIN,T,G,APCLCNT,D,APCLCTAX
 S APCLCTAX=$O(^ATXAX("B","SURVEILLANCE ILI CLINICS",0))
 S APCLCNT=0
 I "AORSH"'[$P(^AUPNVSIT(APCLVDFN,0),U,7) Q  ;not a visit of interest
 S APCLCLIN=$$CLINIC^APCLV(APCLVDFN,"I")  ;get clinic code
 ;is there a PHN
 S X=0,P=0 F  S X=$O(^AUPNVPRV("AD",APCLVDFN,X)) Q:X'=+X!(P)  D
 .Q:'$D(^AUPNVPRV(X,0))
 .S Y=$P(^AUPNVPRV(X,0),U)
 .S Z=$$VALI^XBDIQ1(200,Y,53.5)
 .Q:'Z
 .I $P($G(^DIC(7,Z,9999999)),U,1)=13 S P=1
 I P G HASADN61
 I $P(^AUPNVSIT(APCLVDFN,0),U,7)'="H" I APCLCLIN="" Q
 I $P(^AUPNVSIT(APCLVDFN,0),U,7)'="H" I '$D(^ATXAX(APCLCTAX,21,"B",APCLCLIN)) Q  ;not in clinic taxonomy
HASADN61 ;
 S G=0,D=""
 S C=0,P1=0,P2=0
 S X=0 F  S X=$O(^AUPNVPOV("AD",APCLVDFN,X)) Q:X'=+X!(C>4)  D
 .S T=$P(^AUPNVPOV(X,0),U)
 .S I=$P(^ICD9(T,0),U)
 .I $$ICD^ATXCHK(T,$O(^ATXAX("B","APCL ILI ADV EVENTS AGGREGATE",0)),9) D SET7 Q
 .I $$ICD^ATXCHK(T,$O(^ATXAX("B","SURVEILLANCE ADV EVENT FEBRILE",0)),9) D  Q
 ..S A=$$AGE^APCLSILU(DFN,2,$$VD^APCLV(APCLVDFN))
 ..Q:A>59
 ..D SET7
 ;
 I '$D(APCLADVE) Q  ;no adverse events
 S APCLBDAT=$$FMADD^XLFDT($$VD^APCLV(APCLVDFN),-60),APCLEDAT=$$VD^APCLV(APCLVDFN)
 S APCLVACS=$$HASVAC(DFN,APCLBDAT,APCLEDAT)  ;had FLU SHOT IN 60 DAYS PRIOR TO ADVERSE EVENT?
 S APCLVACD=APCLVACS
 S APCLX="" F  S APCLX=$O(APCLADVE(APCLX)) Q:APCLX'=+APCLX  D
 .S E=$$CAT7(APCLADVE(APCLX))
 .S APCLVACT=$S(APCLVACD:1,1:0)
 .Q:$D(^XTMP("APCLSILR",APCLJ,APCLH,"TAB7PATS",DFN,E,APCLVACT))  ;already counted this patient once
 .S APCLTAB7(E,APCLVACT)=$G(APCLTAB7(E,APCLVACT))+1
 .S ^XTMP("APCLSILR",APCLJ,APCLH,"TAB7PATS",DFN,E,APCLVACT)=""
 Q
TAB7 ;EP
 ;Get date of H1N1 vaccine
 ;did they have an adverse event on this visit?
 NEW APCLADVE,APCLX,E,C
 S APCLX=0,C=0 F  S APCLX=$O(^AUPNVPOV("AD",APCLVDFN,APCLX)) Q:APCLX'=+APCLX  D
 .S I=$P($G(^AUPNVPOV(APCLX,0)),U,1)
 .I I="" Q
 .I '$$ICD^ATXCHK(I,$O(^ATXAX("B","SURVEILLANCE H1N1 ADV EV DXS",0)),9) Q
 .S I=$P($$ICDDX^ICDCODE(I),U,2)
 .S E=$$CAT(I),C=C+1
 .S APCLADVE(C)=E
 Q:'$D(APCLADVE)  ;no adverse event on this visit
 ;get beginning date to look for vaccine
 S B=$S($E(APCLED,4,5)>6:$E(APCLED,1,3),1:$E(APCLED,1.3)-1)
 S APCLBDAT=B_"0601"
 S APCLVACS=$$HASHVAC(DFN,APCLBDAT,APCLED)
 I APCLVACS="" Q  ;never had a vaccine
 S APCLVACD=$P(APCLVACS,U,1)
 ;is this visit date within 130 of vaccine
 S D=$$FMADD^XLFDT(APCLVACD,130)
 I $$VD^APCLV(APCLVDFN)>D Q  ;after 130 days from H1N1 vaccination
 S APCLVACT=$P(APCLVACS,U,2)
 S APCLVACT=$S(APCLVACT=125:"LIVE",1:"INJECTABLE")
 ;find all adverse events from date of vaccine forward 130 days
 S APCLX="" F  S APCLX=$O(APCLADVE(APCLX)) Q:APCLX'=+APCLX  D
 .S E=APCLADVE(APCLX)
 .Q:$D(^XTMP("APCLSILR",APCLJ,APCLH,"TAB7PATS",DFN,E,APCLVACT))  ;already counted this patient once
 .S APCLTAB7(E,APCLVACT)=$G(APCLTAB7(E,APCLVACT))+1
 .S ^XTMP("APCLSILR",APCLJ,APCLH,"TAB7PATS",DFN,E,APCLVACT)=""
 Q
 ;
HASHVAC(P,BD,ED) ;GET DATE OF LAST ONE does this patient have an H1N1 vaccine, if yes, return date
 NEW C,X,Y,Z,T,L,M,D
 S T=$O(^ATXAX("B","SURVEILLANCE H1N1 CVX CODES",0))
 S C="",X=0 F  S X=$O(^AUPNVIMM("AC",P,X)) Q:X'=+X  S Y=$P($G(^AUPNVIMM(X,0)),U) D
 .Q:'Y
 .Q:'$D(^AUTTIMM(Y,0))
 .S Z=$P(^AUTTIMM(Y,0),U,3)
 .Q:'$D(^ATXAX(T,21,"B",Z))
 .S D=$$VD^APCLV($P(^AUPNVIMM(X,0),U,3))
 .Q:D>ED
 .Q:D<BD
 .I D>$P(C,U,1) S C=D_U_Z
 .Q
 ;I C Q C
 S D=$$LASTCPTT^APCLAPIU(P,BD,ED,"SURVEILLANCE CPT H1N1","D")
 I D>$P(C,U,1) S C=D
 Q C
HASVAC(P,BD,ED,LIVE) ;GET DATE OF LAST ONE does this patient have an H1N1 vaccine, if yes, return date
 NEW C,X,Y,Z,T
 S LIVE=$G(LIVE)
 S T=$O(^ATXAX("B","BGP FLU IZ CVX CODES",0))
 S C=0,X=0 F  S X=$O(^AUPNVIMM("AC",P,X)) Q:X'=+X  S Y=$P($G(^AUPNVIMM(X,0)),U) D
 .Q:'Y
 .Q:'$D(^AUTTIMM(Y,0))
 .S Z=$P(^AUTTIMM(Y,0),U,3)
 .Q:'$D(^ATXAX(T,21,"B",Z))
 .S D=$$VD^APCLV($P(^AUPNVIMM(X,0),U,3))
 .Q:D>ED
 .Q:D<BD
 .I D>$P(C,U,1) S C=D_U_Z
 .Q
 S D=$$LASTCPTT^APCLAPIU(P,BD,ED,"BGP CPT FLU","D")
 I D>$P(C,U,1) S C=D
 S D=$$LASTPRCT^APCLAPIU(P,BD,ED,"BGP FLU IZ PROCEDURES","D")
 I D>$P(C,U,1) S C=D
 S D=$$LASTDXT^APCLAPIU(P,BD,ED,"BGP FLU IZ DXS","D")
 I D>$P(C,U,1) S C=D
 Q C
CAT7(E) ;
 I E="287.31" Q "ITP: 287.31"
 I E="287.4" Q "2nd TP: 287.4"
 I E="287.5" Q "TP: 287.5"
 I E="351.0" Q "Bells Palsy: 351.0"
 I E="357.0" Q "GBS: 357.0"
 I E="780.31" Q "Febrile Seizures Simple: 780.31 < 5 yrs"
 I E="780.32" Q "Febrile Seizures Complex: 780.32 < 5 yrs"
 Q "unknown"
 ;
CAT(E) ;
 I $$ICD^ATXCHK(E,$O(^ATXAX("B","SURVEILL ADV EV ANAPHYLACTIC",0)),9) Q "Anaphylactic Shock"
 I $$ICD^ATXCHK(E,$O(^ATXAX("B","SURVEILL ADV EV ENCEPHALITIS",0)),9) Q "Encephalitis/Myelitis"
 I $$ICD^ATXCHK(E,$O(^ATXAX("B","SURVEILL ADV EV SUDDEN DEATH",0)),9) Q "Sudden Death"
 I $$ICD^ATXCHK(E,$O(^ATXAX("B","SURVEILL ADV EV OPTIC NEURITIS",0)),9) Q "Optic Neuritis"
 I $$ICD^ATXCHK(E,$O(^ATXAX("B","SURVEILL ADV EV THROMBOPENIA",0)),9) Q "Thrombocytopenia"
 Q "Other"
SET8 ;
 S APCLCNT=APCLCNT+1
 S APCLADVE(APCLCNT)=$$VAL^XBDIQ1(9000010.07,X,.01)
 Q
TAB827 ;EP
 ;get adverse events on this visit
 Q:$E($$VD^APCLV(APCLVDFN),4,5)="06"
 Q:$E($$VD^APCLV(APCLVDFN),4,5)="07"
 K APCLADVE
 NEW X,P,Y,Z,APCLCLIN,T,G,APCLCNT,D,APCLCTAX
 S APCLCTAX=$O(^ATXAX("B","SURVEILLANCE ILI CLINICS",0))
 S APCLCNT=0
 I "AORSH"'[$P(^AUPNVSIT(APCLVDFN,0),U,7) Q  ;not a visit of interest
 S APCLCLIN=$$CLINIC^APCLV(APCLVDFN,"I")  ;get clinic code
 ;is there a PHN
 S X=0,P=0 F  S X=$O(^AUPNVPRV("AD",APCLVDFN,X)) Q:X'=+X!(P)  D
 .Q:'$D(^AUPNVPRV(X,0))
 .S Y=$P(^AUPNVPRV(X,0),U)
 .S Z=$$VALI^XBDIQ1(200,Y,53.5)
 .Q:'Z
 .I $P($G(^DIC(7,Z,9999999)),U,1)=13 S P=1
 I P G HASADN81
 I $P(^AUPNVSIT(APCLVDFN,0),U,7)'="H" I APCLCLIN="" Q
 I $P(^AUPNVSIT(APCLVDFN,0),U,7)'="H" I '$D(^ATXAX(APCLCTAX,21,"B",APCLCLIN)) Q  ;not in clinic taxonomy
HASADN81 ;
 S G=0,D=""
 S C=0,P1=0,P2=0
 S X=0 F  S X=$O(^AUPNVPOV("AD",APCLVDFN,X)) Q:X'=+X!(C>4)  D
 .S T=$P(^AUPNVPOV(X,0),U)
 .S I=$P(^ICD9(T,0),U)
 .I $$ICD^ATXCHK(T,$O(^ATXAX("B","SURVEILLANCE ADV EVENTS LIVE",0)),9) D  Q
 ..S A=$$AGE^APCLSILU(DFN,2,$$VD^APCLV(APCLVDFN))
 ..Q:A<24
 ..Q:A>59
 ..D SET8
 ;
 I '$D(APCLADVE) Q  ;no adverse events
 S APCLBDAT=$$FMADD^XLFDT($$VD^APCLV(APCLVDFN),-14),APCLEDAT=$$VD^APCLV(APCLVDFN)
 S APCLVACS=$$HASVAC(DFN,APCLBDAT,APCLEDAT)  ;had FLU SHOT IN 60 DAYS PRIOR TO ADVERSE EVENT?
 S APCLVACD=APCLVACS
 S APCLX="" F  S APCLX=$O(APCLADVE(APCLX)) Q:APCLX'=+APCLX  D
 .S E=$$CAT8(APCLADVE(APCLX))
 .S APCLVACT=$S($P(APCLVACS,U,2)=111:1,1:0)
 .Q:$D(^XTMP("APCLSILR",APCLJ,APCLH,"TAB8PATS",DFN,E,APCLVACT))  ;already counted this patient once
 .S APCLTAB8(E,APCLVACT)=$G(APCLTAB8(E,APCLVACT))+1
 .S ^XTMP("APCLSILR",APCLJ,APCLH,"TAB8PATS",DFN,E,APCLVACT)=""
 Q
TAB8 ;EP
 ;Get date of H1N1 vaccine
 ;did they have an adverse event on this visit?
 NEW APCLADVE,APCLX,E,C
 S APCLX=0,C=0 F  S APCLX=$O(^AUPNVPOV("AD",APCLVDFN,APCLX)) Q:APCLX'=+APCLX  D
 .S I=$P($G(^AUPNVPOV(APCLX,0)),U,1)
 .I I="" Q
 .I '$$ICD^ATXCHK(I,$O(^ATXAX("B","SURVEILL H1N1 ADV EV LV DXS",0)),9) Q
 .S I=$P($$ICDDX^ICDCODE(I),U,2)
 .S E=$$CAT8(I),C=C+1
 .S APCLADVE(C)=E
 Q:'$D(APCLADVE)  ;no adverse event on this visit
 ;get beginning date to look for vaccine
 S B=$S($E(APCLED,4,5)>6:$E(APCLED,1,3),1:$E(APCLED,1.3)-1)
 S APCLBDAT=B_"0601"
 S APCLVACS=$$HASLVAC(DFN,APCLBD,APCLED)
 I APCLVACS="" Q  ;never had a vaccine
 S APCLVACD=$P(APCLVACS,U,1)
 ;is this visit date within 40 of vaccine
 S D=$$FMADD^XLFDT(APCLVACD,40)
 I $$VD^APCLV(APCLVDFN)>D Q  ;after 40 days from H1N1 vaccination
 S APCLVACT=$P(APCLVACS,U,2)
 S APCLVACT=$S(APCLVACT=125:"LIVE",1:"INJECTABLE")
 ;find all adverse events from date of vaccine forward 130 days
 S APCLX="" F  S APCLX=$O(APCLADVE(APCLX)) Q:APCLX'=+APCLX  D
 .S E=APCLADVE(APCLX)
 .Q:$D(^XTMP("APCLSILR",APCLJ,APCLH,"TAB8PATS",DFN,E))  ;already counted this patient once
 .S APCLTAB8(E)=$G(APCLTAB8(E))+1
 .S ^XTMP("APCLSILR",APCLJ,APCLH,"TAB8PATS",DFN,E)=""
 Q
 ;
HASLVAC(P,BD,ED) ;does this patient have an H1N1 vaccine, if yes, return date
 NEW C,X,Y,Z,T,L,M,D
 S T=$O(^ATXAX("B","SURVEILL H1N1 LIVE VIRUS CVX",0))
 S C="",X=0 F  S X=$O(^AUPNVIMM("AC",P,X)) Q:X'=+X!(C]"")  S Y=$P($G(^AUPNVIMM(X,0)),U) D
 .Q:'Y
 .Q:'$D(^AUTTIMM(Y,0))
 .S Z=$P(^AUTTIMM(Y,0),U,3)
 .Q:'$D(^ATXAX(T,21,"B",Z))
 .Q:Z'=125
 .S D=$$VD^APCLV($P(^AUPNVIMM(X,0),U,3))
 .Q:D>ED
 .Q:D<BD
 .I D>$P(C,U,1) S C=D_U_Z
 .Q
 Q C
CAT8(E) ;
 I $E(E,1,3)=493 Q "Asthma"
 I E=786.07 Q "Wheezing"
 I E=488.1 Q "Influenza"
 Q "Other"
TAB9 ;EP
 ;Get date of H1N1 vaccine
 ;did they have an adverse event on this visit?
 ;COUNT VISITS PER MICHELLE THOMAS'S LAST SAMPLE
 NEW APCLADVE,APCLX,E,C
 S APCLX=0,C=0 F  S APCLX=$O(^AUPNVPOV("AD",APCLVDFN,APCLX)) Q:APCLX'=+APCLX  D
 .S I=$P($G(^AUPNVPOV(APCLX,0)),U,1)
 .I I="" Q
 .I '$$ICD^ATXCHK(I,$O(^ATXAX("B","SURVEILLANCE ADV EV NO H1N1",0)),9) Q
 .S I=$P($$ICDDX^ICDCODE(I),U,2)
 .S E=$$CAT(I),C=C+1
 .S APCLADVE(C)=E
 Q:'$D(APCLADVE)  ;no adverse event on this visit
 ;get beginning date to look for vaccine
 S B=$S($E(APCLED,4,5)>6:$E(APCLED,1,3),1:$E(APCLED,1.3)-1)
 S APCLBDAT=B_"0601"
 S APCLVACS=$$HASHVAC(DFN,APCLBD,APCLED)
 I APCLVACS]"" Q  ;had a vaccine
 ;find all adverse events from date of vaccine forward 130 days
 S APCLX="" F  S APCLX=$O(APCLADVE(APCLX)) Q:APCLX'=+APCLX  D
 .S E=APCLADVE(APCLX)
 .;Q:$D(^XTMP("APCLSILR",APCLJ,APCLH,"TAB9PATS",DFN,E))  ;already counted this patient once
 .S APCLTAB9(E)=$G(APCLTAB9(E))+1
 .;S ^XTMP("APCLSILR",APCLJ,APCLH,"TAB9PATS",DFN,E)=""
 Q
