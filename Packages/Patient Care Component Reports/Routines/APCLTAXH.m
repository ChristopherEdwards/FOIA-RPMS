APCLTAXH ; IHS/CMI/LAB - DISPLAY IND LISTS ;
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;; ;
ICD0ADD ;EP
 S ATXFLG=""
 I APCLFILE=80.1 S APCLGBL="^ICD0("
 S APCLX=APCLTAXI
 D BUILD,^APCLTAXI
 D EDIT
 K ATXFLG,APCLX,APCLL,ATX,APCLTBLE
 Q
 ;
BUILD ;BUILD APCLTBLE FOR ROUTINE ATXCODE
 S APCLCODE=0 F  S APCLCODE=$O(^ATXAX(APCLX,21,"AA",APCLCODE)) Q:APCLCODE=""  S APCLTBLE(APCLCODE)=$O(^(APCLCODE,""))
 Q
ENTER ;
 S APCL=0 F  S APCL=$O(APCLTBLE(APCL)) Q:APCL=""  S APCL("X")=$E(APCL,1,($L(APCL)-1)) D CALLDIE I $D(Y) S APCLSTP=1 Q
 W !!,"Done!"
 Q
 ;
CALLDIE ;
 S APCLHI=$E(APCLTBLE(APCL),1,($L(APCLTBLE(APCL))-1)),DIE="^ATXAX(",DR="2101///"_APCL("X")_" ",DA=APCLX,DR(2,9002226.02101)=".02////"_APCLHI_" " D ^DIE I $D(Y)
 E  I '$D(ZTQUEUED),$D(APCL("NOT TAX")) W "."
 I $D(Y),'$D(ZTQUEUED),$D(APCL("NOT TAX")) W !!,"ERROR ENCOUNTERED - REENTER RANGES",!
 K DIE,DR,DA
 Q
 ;
EDIT ;DELETE CODES IN TAXONOMY FILE, THEN ENTER NEW RANGES
 S APCL=0 F  S APCL=$O(^ATXAX(APCLX,21,APCL)) Q:APCL'=+APCL  S DA(1)=APCLX,DA=APCL,DIK="^ATXAX("_DA(1)_",21," D ^DIK K DIK,DA
 D ENTER
 Q
 ;
EOJ1 ;
 K APCLHI
 I $D(ZTQUEUED) S ZTREQ="@"
 Q
 ;
