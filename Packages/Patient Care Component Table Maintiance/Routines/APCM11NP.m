APCM11NP ; IHS/CMI/LAB - IHS gpra print 30 Jun 2010 5:58 PM ;
 ;;2.0;IHS PCC SUITE;**6**;MAY 14, 2009;Build 11
 ;
LIST ;EP
 S APCMQUIT="",APCMGPG=0
 S APCMINDB=0,APCMCOUN=0
 S APCMINDB=0 F  S APCMINDB=$O(APCMINDL(APCMINDB)) Q:APCMINDB'=+APCMINDB  D
 .S APCMORD=$P($G(^APCMMUM(APCMINDB,0)),U,4)
 .S APCMLIEN=0 F  S APCMLIEN=$O(APCMINDL(APCMINDB,APCMLIEN)) Q:APCMLIEN'=+APCMLIEN  D
 ..S APCMLORD=$S(APCMRPTT=1:$P(^APCMMUPL(APCMLIEN,0),U,5),1:$P(^APCMMUPL(APCMLIEN,0),U,6))
 ..S APCMINDL("AOI",APCMORD,APCMINDB,APCMLORD,APCMLIEN)=""
 S APCMORD=0 F  S APCMORD=$O(APCMINDL("AOI",APCMORD)) Q:APCMORD=""!(APCMQUIT)  D
 .S APCMINDB=$O(APCMINDL("AOI",APCMORD,0))
 .S APCMLORD=0 F  S APCMLORD=$O(APCMINDL("AOI",APCMORD,APCMINDB,APCMLORD)) Q:APCMLORD=""!(APCMQUIT)  D
 ..S APCMLIEN=$O(APCMINDL("AOI",APCMORD,APCMINDB,APCMLORD,0))
 ..I '$O(APCMINDL(APCMINDB,APCMLIEN,0)) D NONE Q
 ..;I '$$ANYPATS(APCMINDB,APCMINDII) D HEADER W !!,"No Patients to Report.",! Q
 ..D NPL11 ;F  S APCMINDB=$O(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMINDB)) Q:APCMINDB'=+APCMINDB!(APCMQUIT)  D NPL1
 Q
NONE ;
 I APCMPTYP="P" D HEADER
 I APCMPTYP="D" D HEADER1
 D H1
 D W^APCM11EH(" ",0,0,APCMPTYP)
 D W^APCM11EH("Total # of patients on list: 0",0,0,APCMPTYP)
 Q
NPL11 ;
 D
NEXT .;
 .;S APCMX=0 F  S APCMX=$O(^APCMMUPL(APCMLIEN,11,APCMX)) Q:APCMX'=+APCMX  D
 .;.I APCMPTYP="P",$Y>(APCMIOSL-3) D HEADER Q:APCMQUIT
 .;.D W^APCM11EH(^APCMMUPL(APCMLIEN,11,APCMX,0),0,1,APCMPTYP)
 .D:APCMPTYP="P" HEADER D H1
 .S APCMP="" F  S APCMP=$O(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP)) Q:APCMP=""  D NEXT1
 Q
NEXT1 ;
 S APCMPIEN=$S(APCMRPTT=1:$O(^VA(200,"B",APCMP,0)),1:$O(^DIC(4,"B",APCMP,0)))
 S APCMCOUN=0,APCMPCNT=0
 ;I APCMPTYP="P" D HEADER Q:APCMQUIT
 S APCMCNT=APCMINDL(APCMINDB,APCMLIEN,APCMPIEN)
 I APCMCNT<11!(APCMLIST'="R") S APCMCNT=1 G GO
 I APCMCNT<100 S APCMCNT=APCMCNT\10 G GO
 S APCMCNT=10
GO ;
 ;D W^APCM11EH($P(^APCMMUM(APCMINDB,0),U,5),0,1,APCMPTYP)
 S APCMCOM="" F  S APCMCOM=$O(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP,APCMCOM)) Q:APCMCOM=""!(APCMQUIT)  D
 .S APCMSEX="" F  S APCMSEX=$O(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP,APCMCOM,APCMSEX)) Q:APCMSEX=""!(APCMQUIT)  D
 ..S APCMAGE="" F  S APCMAGE=$O(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP,APCMCOM,APCMSEX,APCMAGE)) Q:APCMAGE=""!(APCMQUIT)  D
 ...S DFN=0 F  S DFN=$O(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP,APCMCOM,APCMSEX,APCMAGE,DFN)) Q:DFN'=+DFN!(APCMQUIT)  S APCMCOUN=APCMCOUN+1 D PRINTL
 I APCMPTYP="P",$Y>(APCMIOSL-3) D HEADER Q:APCMQUIT  D H1
 D W^APCM11EH("Total # of patients on list: "_+$G(APCMPCNT),0,2,APCMPTYP)
 D W^APCM11EH("",0,1,APCMPTYP)
 Q
 ;
PRINTL ;print one line
 Q:(APCMCOUN#APCMCNT)
 I APCMPTYP="P",$Y>(APCMIOSL-3) D HEADER Q:APCMQUIT  D
 .;S X=0 F  S X=$O(^APCMMUPL(APCMLIEN,11,X)) Q:X'=+X  W !,^APCMMUPL(APCMLIEN,11,X,0)
 .D H1
 Q:APCMQUIT
 S APCMPCNT=APCMPCNT+1
 D
 .D W^APCM11EH($E($P(^DPT(DFN,0),U),1,22),0,1,APCMPTYP)
 .D W^APCM11EH($$HRN^AUPNPAT(DFN,DUZ(2)),0,0,APCMPTYP,2,24)
 .D W^APCM11EH($E(APCMP,1,24),0,0,APCMPTYP,3,31)
 .D W^APCM11EH(APCMCOM,0,0,APCMPTYP,4,57)
 .D W^APCM11EH(APCMSEX,0,0,APCMPTYP,5,70)
 .D W^APCM11EH(APCMAGE,0,0,APCMPTYP,6,75)
 .D W^APCM11EH($P(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP,APCMCOM,APCMSEX,APCMAGE,DFN),"|||",1),0,$S(APCMPTYP="P":1,1:0),APCMPTYP,7,1)
 .D W^APCM11EH($P(^XTMP("APCM1D",APCMJ,APCMH,"LIST",APCMORD,APCMINDB,APCMLORD,APCMLIEN,APCMP,APCMCOM,APCMSEX,APCMAGE,DFN),"|||",2),0,0,APCMPTYP,8,40)
PRINTL1 .;D W^APCM11EH(" ",0,1,APCMPTYP)
 K ^TMP($J,"A")
 Q
 ;
HEADER ;EP
 G:'APCMGPG HEADER1
 K DIR I $E(IOST)="C",IO=IO(0),'$D(ZTQUEUED) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S APCMQUIT=1 Q
HEADER1 ;
 I APCMPTYP="P" W:$D(IOF) @IOF S APCMGPG=APCMGPG+1
 D W^APCM11EH($$CTR("***** CONFIDENTIAL PATIENT INFORMATION, COVERED BY THE PRIVACY ACT *****",80),0,1,APCMPTYP)
 I APCMPTYP="P" W !?3,$P(^VA(200,DUZ,0),U,2),?35,$$FMTE^XLFDT(DT),?70,"Page ",APCMGPG,!
 I APCMRPTT=1 D W^APCM11EH($$CTR("** IHS 2011 Stage 1 Meaningful Use Performance Report for EPs **",80),0,1,APCMPTYP)
 I APCMRPTT=2 D W^APCM11EH($$CTR("** IHS 2011 Stage 1 MU Performance Report for Eligible Hospitals/CAHs **",80),0,1,APCMPTYP)
 D W^APCM11EH($$CTR($P(^DIC(4,DUZ(2),0),U),80),0,1,APCMPTYP)
 S X="Reporting Period: "_$$FMTE^XLFDT(APCMBD)_" to "_$$FMTE^XLFDT(APCMED) D W^APCM11EH($$CTR(X,80),0,1,APCMPTYP)
 ;W !,$$CTR($P(^APCMMUM(APCMINDB,0),U,5))
 D W^APCM11EH($TR($J("",80)," ","-"),0,1,APCMPTYP)
 Q
H1 ;
 ;S X=$S(APCMLIST="A":"Entire Patient List",APCMLIST="R":"Random Patient List",1:"Patient List by Provider: "_APCMLPROV) D W^APCM11EH(X,0,1,APCMPTYP)
 D W^APCM11EH($P(^APCMMUM(APCMINDB,0),U,5),0,$S(APCMPTYP="D":3,1:1),APCMPTYP)
 S X=0 F  S X=$O(^APCMMUPL(APCMLIEN,11,X)) Q:X'=+X  D W^APCM11EH(^APCMMUPL(APCMLIEN,11,X,0),0,1,APCMPTYP)
 ;S N=$S(APCMCOUN<2:22,1:22) S X=0 F  S X=$O(^APCMMUM(APCMINDB,N,X)) Q:X'=+X  D W^APCM11EH(^APCMMUM(APCMINDB,N,X,0),0,1,APCMPTYP)
 D W^APCM11EH("PATIENT NAME",0,2,APCMPTYP)
 D W^APCM11EH("HRN",0,0,APCMPTYP,2,24)
 D W^APCM11EH($S(APCMRPTT=1:"EP",1:"HOSPITAL/CAH"),0,0,APCMPTYP,3,31)
 D W^APCM11EH("COMMUNITY",0,0,APCMPTYP,4,57)
 D W^APCM11EH("SEX",0,0,APCMPTYP,5,70)
 D W^APCM11EH("AGE",0,0,APCMPTYP,6,75)
 D W^APCM11EH("DENOM",0,$S(APCMPTYP="P":1,1:0),APCMPTYP,7,3)
 D W^APCM11EH("NUMERATOR",0,0,APCMPTYP,8,40)
 D W^APCM11EH($TR($J("",80)," ","-"),0,1,APCMPTYP)
 Q
CTR(X,Y) ;EP - Center X in a field Y wide.
 Q $J("",$S($D(Y):Y,1:IOM)-$L(X)\2)_X
 ;----------
USR() ;EP - Return name of current user from ^VA(200.
 Q $S($G(DUZ):$S($D(^VA(200,DUZ,0)):$P(^(0),U),1:"UNKNOWN"),1:"DUZ UNDEFINED OR 0")
 ;----------
LOC() ;EP - Return location name from file 4 based on DUZ(2).
 Q $S($G(DUZ(2)):$S($D(^DIC(4,DUZ(2),0)):$P(^(0),U),1:"UNKNOWN"),1:"DUZ(2) UNDEFINED OR 0")
