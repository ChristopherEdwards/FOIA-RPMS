BPCPC ; IHS/OIT/MJL - PATIENT CHART RELATED ROUTINES FOR GUI ;
 ;;1.5;BPC;;MAY 26, 2005
BPCCHT(RESULT,BPCIEN,BPCOPT,BPCLIM,BPCSDATE,BPCEDATE) ;EP CALL FROM REMOTE PROC: BPC CHARTDATA
 ;
 ;POSSIBLE OPTIONS (BPCOPT)
 ;1 - RETURN CHOSEN NUMBER OF VISITS (BPCLIM) WITHIN DATE RANGE
 ;2 - RETURN ALL VISITS WITHIN DATE RANGE
 ;3 - RETURN LAB VISITS ONLY WITHIN DATE RANGE
EN ;
 S U="^",XWBWRAP=1,BPCLIM=$G(BPCLIM,10),BPCSUB=$J,BPCMFLG="" K ^BGUTMP(BPCSUB),^BGURES(BPCSUB),RESULT
 S:'BPCLIM BPCLIM=10
 S RESULT="^BGURES("_BPCSUB_")"
 ;I '$D(BPCIEN) S RESULT(1)=-1,RESULT(2)="NO PATIENT IEN SENT!" Q
 I '$D(BPCIEN) S ^BGURES(BPCSUB,1)=-1,^BGURES(BPCSUB,2)="NO PATIENT IEN SENT!" Q
 S BPCOPT=$G(BPCOPT,1),BPCSDATE=$G(BPCSDATE),BPCEDATE=$G(BPCEDATE)
 S:BPCOPT=2 BPCLIM=9999999
 I BPCSDATE'="" S BPCFLG=0 D GETDATES I BPCFLG D KILL Q
 D VISIT,SETRES,KILL
 Q
GETDATES ;
 D DT^DILF("",BPCSDATE,.BPCSDAT)
 ;I BPCSDAT=-1 S RESULT(1)=-1,RESULT(2)="INVALID START DATE!",BPCFLG=1 Q
 I BPCSDAT=-1 S ^BGURES(BPCSUB,1)=-1,^BGURES(BPCSUB,2)="INVALID START DATE!",BPCFLG=1 Q
 I BPCEDATE="" S BPCEDATE="T"
 D DT^DILF("",BPCEDATE,.BPCEDAT)
 ;I BPCEDAT=-1 S RESULT(1)=-1,RESULT(2)="INVALID END DATE!",BPCFLG=1
 I BPCEDAT=-1 S ^BGURES(BPCSUB,1)=-1,^BGURES(BPCSUB,2)="INVALID END DATE!",BPCFLG=1
 Q
KILL ;
 K ^BGUTMP(BPCSUB)
 K BPCPRV,BPCCDT,BPCCLIN,BPCCN,BPCCTR,BPCDAYS,BPCDDAY,BPCDNAM,BPCDTA,BPCEDATE,BPCFATH,BPCFLAGL,BPCFLG,BPCIEN,BPCLIM,BPCLOC,BPCMED
 K BPCMOTH1,BPCMOTH2,BPCNAR,BPCOPT,BPCPDTA,BPCPOV,BPCQTY,BPCREFH,BPCREFL,BPCSC,BPCSDATE,BPCSIG,BPCSITE,BPCSUB,BPCTEST,BPCTEST1,BPCUNITS,BPCVALUE
 K BPCVDT,BPCVSIT,BPCX,BPCX1,BPCY,BPCY11,BPCY12,BPCZ,BPCCKDT,BPCSDAT,BPCEDAT,BPCDD,BPCF
 Q
VISIT ;
 S BPCX=0,BPCCN=0,BPCCTR=0 F  S BPCX=$O(^AUPNVSIT("AA",BPCIEN,BPCX)) Q:BPCX=""  S BPCVSIT="" D  Q:BPCCN=BPCLIM
 .F  S BPCVSIT=$O(^AUPNVSIT("AA",BPCIEN,BPCX,BPCVSIT)) Q:BPCVSIT=""  D VISIT1 Q:BPCCN=BPCLIM
 Q
VISIT1 ;
 S BPCMFLG=$S($D(^AUPNVMIC("AD",BPCVSIT)):"B",1:"")
 I BPCOPT=3,'$D(^AUPNVLAB("AD",BPCVSIT)) Q  ;IF OPTION=LABS ONLY
 S BPCY=^AUPNVSIT(BPCVSIT,0),BPCVDT=$P(BPCY,U,1),BPCLOC=$P(BPCY,U,6),BPCSC=$P(BPCY,U,7),BPCCLIN=$P(BPCY,U,8),BPCCKDT=$P(BPCVDT,".",1)
 I BPCSDATE'="",BPCEDATE'="",((BPCCKDT<BPCSDAT)!(BPCCKDT>BPCEDAT)) Q  ;IF NOT IN DATE RANGE
 I $L(BPCLOC) S BPCLOC=$G(^AUTTLOC(BPCLOC,0)) S:$L(BPCLOC) BPCLOC=$P(BPCLOC,U,2)
 I $L(BPCCLIN) S BPCCLIN=$G(^DIC(40.7,BPCCLIN,0)) S:$L(BPCCLIN) BPCCLIN=$P(BPCCLIN,U,1)
 S BPCX1=0 F  S BPCX1=$O(^AUPNVPRV("AD",BPCVSIT,BPCX1)) Q:BPCX1=""  D PROV
 S BPCCN=BPCCN+1,^BGUTMP(BPCSUB,BPCX,"VISIT","NONE",BPCCN)=BPCVDT_U_"VISIT"_U_BPCVSIT_U_BPCLOC_U_BPCSC_U_$G(BPCPRV)_U_BPCCLIN
 D LAB,POV,MEAS
 S BPCFLG="" S:$D(^BGUTMP(BPCSUB,BPCX,"LAB")) BPCFLG="L" S:$D(^BGUTMP(BPCSUB,BPCX,"POV")) BPCFLG=BPCFLG_"P"
 S BPCF=0
 D MED I BPCF S BPCFLG=BPCFLG_"M"
 ;S:$D(^BGUTMP(BPCSUB,BPCX,"MED")) BPCFLG=BPCFLG_"M"
 ;S $P(^BGUTMP(BPCSUB,BPCX,"VISIT","NONE"),U,6)=BPCFLG
 S BPCFLG=BPCFLG_BPCMFLG,$P(^BGUTMP(BPCSUB,BPCX,"VISIT","NONE",BPCCN),U,6)=BPCFLG
 Q
PROV ;
 Q:'$D(^AUPNVPRV(BPCX1,0))
 S BPCPDTA=^AUPNVPRV(BPCX1,0)
 Q:$P(BPCPDTA,U,4)'="P"
 S BPCPRV=$P(BPCPDTA,U,1),BPCPRV=$P($G(VA(200,BPCPRV,0)),U,1) S:'$L(BPCPRV) BPCPRV=BPCX1
 Q
LAB ;
 S BPCX1=0 F  S BPCX1=$O(^AUPNVLAB("AD",BPCVSIT,BPCX1)) Q:BPCX1=""  D LAB1
 Q
LAB1 ;
 Q:'$D(^AUPNVLAB(BPCX1,0))
 S BPCY=^AUPNVLAB(BPCX1,0),BPCY11=$G(^AUPNVLAB(BPCX1,11)),BPCY12=$G(^AUPNVLAB(BPCX1,12))
 S BPCTST=$P(BPCY,U,1),BPCTEST=$P($G(^LAB(60,BPCTST,0)),U,1) Q:'$L(BPCTEST)
 S BPCATOM=$O(^LAB(60,BPCTST,2,0))=""
 S BPCVALUE=$P(BPCY,U,4),BPCFLAGL=$P(BPCY,U,5),BPCREFL=$P(BPCY11,U,4),BPCREFH=$P(BPCY11,U,5),BPCUNITS=$P(BPCY11,U,1)
 S BPCPRNT=$P(BPCY12,U,8)
 S BPCCTR=BPCCTR+1,BPCTEST1=BPCTEST
 I $D(^BGUTMP(BPCSUB,BPCX,"LAB",BPCTEST)) S BPCTEST1=BPCTEST_BPCCTR
 S BPCSITE="" I $L($P(BPCY11,U,3)) S BPCSITE=$P(BPCY11,U,3),BPCSITE=$S(BPCSITE=72:70,BPCSITE=73:70,1:BPCSITE),BPCSITE=$P($G(^LAB(61,BPCSITE,0)),U,1)
 I BPCATOM D  Q
 .S BPCCTR(BPCTEST)=$G(BPCCTR(BPCTEST))+1
 .S BPCCDT=$P(BPCY12,U,1) S:BPCCDT="" BPCCDT=BPCVDT
 . I BPCPRNT'="",$D(^BGUTMP(BPCSUB,BPCX,"PARENT",BPCPRNT)) D  Q
 ..S BPCTEST1=^BGUTMP(BPCSUB,BPCX,"PARENT",BPCPRNT)
 ..S ^BGUTMP(BPCSUB,BPCX,"LAB",BPCTEST1,BPCTST)=BPCVDT_U_"LAB"_U_"     "_BPCTEST_U_BPCX1_U_BPCVALUE_U_BPCFLAGL_U_BPCUNITS_U_BPCREFL_U_BPCREFH_U_BPCSITE_U_BPCCDT
 ..S $P(^BGUTMP(BPCSUB,BPCX,"LAB",BPCTEST1),U,10)=BPCSITE
 .S ^BGUTMP(BPCSUB,BPCX,"LAB",BPCTEST1)=BPCVDT_U_"LAB"_U_BPCTEST_U_BPCX1_U_BPCVALUE_U_BPCFLAGL_U_BPCUNITS_U_BPCREFL_U_BPCREFH_U_BPCSITE_U_BPCCDT
 S ^BGUTMP(BPCSUB,BPCX,"LAB",BPCTEST1)=BPCVDT_U_"LAB"_U_BPCTEST_" <<PANEL>>"_U_BPCX1 ;_"^^^^^^"_BPCSITE
 S ^BGUTMP(BPCSUB,BPCX,"PARENT",BPCX1)=BPCTEST1
 Q
POV ;
 S BPCPVDTA=0
 S BPCX1=0 F  S BPCX1=$O(^AUPNVPOV("AD",BPCVSIT,BPCX1)) Q:BPCX1=""  D POV1
 S:'BPCPVDTA ^BGUTMP(BPCSUB,BPCX,"POV",1)=BPCVDT_U_"POV"_U_"No Purpose of Visit Recorded"
 K BPCPVDTA
 Q
POV1 ;
 Q:'$D(^AUPNVPOV(BPCX1,0))
 S BPCPDTA=$G(^AUPNVPOV(BPCX1,0))
 S BPCY=$P(BPCPDTA,U,1) Q:'$L(BPCY)
 S BPCPOV=$P($G(^ICD9(BPCY,0)),U,3)
 S BPCY=$P(BPCPDTA,U,4) Q:'$L(BPCY)
 S BPCNAR=$G(^AUTNPOV(BPCY,0))
 S ^BGUTMP(BPCSUB,BPCX,"POV",BPCX1)=BPCVDT_U_"POV"_U_BPCPOV_U_BPCNAR_U_BPCX1,BPCPVDTA=1
 Q
MED ;
 S BPCX1=0 F  S BPCX1=$O(^AUPNVMED("AD",BPCVSIT,BPCX1)) Q:BPCX1=""  D MED1
 Q
MED1 ;
 Q:'$D(^AUPNVMED(BPCX1,0))
 S BPCF=1
 Q
 ;FHL 12/9/97
 D:$D(^PSRX("APCC",BPCX1))  Q:BPCRXST'="0"
 .S BPCRXST="0"
 .S BPCX2=$O(^PSRX("APCC",BPCX1,""))
 .S:BPCX2'="" BPCRXD=^PSRX(BPCX2,0),BPCRXST=$P(BPCRXD,U,15)
 S BPCY=^AUPNVMED(BPCX1,0),BPCY12=$G(^AUPNVMED(BPCX1,12))
 S BPCMED=$P(BPCY,U,1),BPCMED=$P($G(^PSDRUG(BPCMED,0)),U,1) Q:'$L(BPCMED)
 S BPCDNAM=$P(BPCY,U,4),BPCSIG=$P(BPCY,U,5),BPCQTY=$P(BPCY,U,6),BPCDAYS=$P(BPCY,U,7),BPCDDAY=$P(BPCY,U,8)
 S BPCCDT=$P(BPCY12,U,1) S:BPCCDT="" BPCCDT=BPCVDT
 S ^BGUTMP(BPCSUB,BPCX,"MED",BPCMED)=BPCVDT_U_"MED"_U_BPCMED_U_BPCX1_U_BPCDNAM_U_BPCSIG_U_BPCQTY_U_BPCDAYS_U_BPCCDT
 Q
SETRES ;
 S BPCX=0,BPCCTR=2 F  S BPCX=$O(^BGUTMP(BPCSUB,BPCX)) Q:BPCX=""  D
 .S BPCY="" F  S BPCY=$O(^BGUTMP(BPCSUB,BPCX,BPCY)) Q:BPCY=""  D:BPCY'="PARENT"
 ..S BPCZ="" F  S BPCZ=$O(^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ)) Q:BPCZ=""  D
 ...S BPCDD=$G(^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ))
 ...;S:BPCY'="VISIT" ^BGURES(BPCSUB,BPCCTR)=^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ),BPCCTR=BPCCTR+1
 ...S:BPCDD'="" ^BGURES(BPCSUB,BPCCTR)=BPCDD,BPCCTR=BPCCTR+1
 ...I $O(^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ,""))'="" D  Q
 ....S BPCQ="" F  S BPCQ=$O(^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ,BPCQ)) Q:BPCQ=""  D
 .....S ^BGURES(BPCSUB,BPCCTR)=^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ,BPCQ),BPCCTR=BPCCTR+1
 ;S BPCX=0,BPCCTR=2 F  S BPCX=$O(^BGUTMP(BPCSUB,BPCX)) Q:BPCX=""  S BPCY="" F  S BPCY=$O(^BGUTMP(BPCSUB,BPCX,BPCY)) Q:BPCY=""  S BPCZ="" F  S BPCZ=$O(^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ)) Q:BPCZ=""  D SETRES1
 D TRENDS,ALERTS,REMARKS,NOK,SITEP
 ;S RESULT(1)=BPCCTR-1
 S ^BGURES(BPCSUB,1)=BPCCTR-1
 Q
SETRES1 ;
 ;S RESULT(BPCCTR)=^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ),BPCCTR=BPCCTR+1
 S ^BGURES(BPCSUB,BPCCTR)=^BGUTMP(BPCSUB,BPCX,BPCY,BPCZ),BPCCTR=BPCCTR+1
 Q
TRENDS ;
 D TRENDS^BPCLALL
 Q
ALERTS ;
 D ALERTS^BPCLALL
 Q
REMARKS ;
 D REMARKS^BPCLALL
 Q
NOK ;
 D NOK^BPCLALL1
 Q
SITEP ;
 D SITEP^BPCLALL1
 Q
MEAS ;
 D MEAS^BPCLALL1
 Q
