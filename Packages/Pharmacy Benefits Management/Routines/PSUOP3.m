PSUOP3 ;BIR/CFL,TJH,PDW-PSU PBM Outpatient Pharmacy shared variables for vers. 6 & 7 ;08/25/2003
 ;;3.0;PHARMACY BENEFITS MANAGEMENT;**14,19,20,28,30**;Oct 15, 1998
 ;
 ; Reference to file #7 supported by DBIA 2495
 ; Reference to file #50 supported by DBIA 221
 ; Reference to file #53 supported by DBIA 2511
 ; Reference to file #59 supported by DBIA 2510
 ; Reference to file #200 supported by DBIA 10060
 ; Reference to file #49  supported by DBIA 10093
 ; Reference to file #52  supported by DBIA 2512
 ;
PROVDR ;Get provider data, site number and AMIS category
 S PSUSITE=$S(PSUDIVP="":PSUSNDR,1:$$VALI^PSUTL(59,PSUDIVP,.06))
 ;
 ;Create storage global of division numbers and names for lab msgs.
 S X=PSUSITE,DIC=59,DIC(0)="XM" D ^DIC
 S X=+Y,PSUDIVNM=$$VAL^PSUTL(59,X,.01)
 S ^XTMP("PSU_"_PSUJOB,"DIV",PSUSITE)=PSUDIVNM
 ;
GETVAR ;Get shared variables
 ;Get AMIS workload category
 S PSUPST=$$VALI^PSUTL(53,PSURXP,6)
 S PSUSC=$S(PSUPST=1:"SC",PSUPST=2:"AA",PSUPST=3:"OT",PSUPST=4:"IP",1:"")
 S:$$GET1^DIQ(52,PSURXIEN,201)="YES" PSUSC="NVA"
 K PSUPROV
 D GETS^PSUTL(200,PSUPRID,"9;29;53.5;53.6","PSUPROV","I")
 I '$D(PSUPROV) D NOPROV Q
 D MOVEI^PSUTL("PSUPROV")
 S PSUPRSSN=PSUPROV(9)
 I PSUPRSSN="" S PSUPRSSN=999999999
 S ^XTMP("PSU_"_PSUJOB,"PSUPDR",PSUPRSSN,PSUPRID)=""
 S PSUDOC(9)=PSUPRSSN
 S PSUPTYP=$S(PSUPROV(53.6)=4:"F",1:"S")
 S:$$GET1^DIQ(52,PSURXIEN,201)="YES" PSUPTYP="NVA"
 S PSUPCLS="" I PSUPROV(53.5)'="" D
 .S PSUPCLS=$$VALI^PSUTL(7,PSUPROV(53.5),1)
 .I PSUPCLS="" S PSUPCLS=$$VALI^PSUTL(7,PSUPROV(53.5),.01)
 S PSUPSV=$S($L(PSUPROV(29)):$$VAL^PSUTL(49,PSUPROV(29),.01),1:"")
 S PSUPSV=$$UPPER^PSUTL(PSUPSV),PSUPSERV=""
 I $L(PSUPSV),$D(PSECT(PSUPSV)) S PSUPSERV=PSECT(PSUPSV)
 S PSUSPTY=$$GET^XUA4A72(PSUPRID,PSUFDT)
 S PSUSP1=$P(PSUSPTY,U,3),PSUSP2=$P(PSUSPTY,U,4)
 ;
 Q
 ;
NOPROV ; set up PSUPROV array when provider isn't found in ^VA(200
 F I=9,29,53.5,53.6 S PSUPROV(I)=""
 S (PSUPRSSN,PSUPTYP,PSUPCLS,PSUPSERV,PSUSP1,PSUSP2)=""
 Q
GETDRUG ;Get drug data
 K PSUDRUG
 D GETS^PSUTL(50,PSUDR,".01;2;3;14.5;20;21;22;25;31;51;52","PSUDRUG","I")
 D MOVEI^PSUTL("PSUDRUG")
 I '$D(PSUDRUG) F I=.01,2,3,14.5,20,21,22,25,31,51,52 S PSUDRUG(I)=""
 S PSUGNM=PSUDRUG(.01)
 I PSUGNM="" S PSUGNM="Unknown Generic Name"
 S PSUVANM=PSUDRUG(21)
 I PSUVANM="" S PSUVANM="Unknown VA Product Name"
 S PSUDEA=PSUDRUG(3)
 S PSUNFI=$S(PSUDRUG(51)=1:"N/F",1:"")
 S PSUDUN=PSUDRUG(14.5)
 S PSUVACLS=PSUDRUG(2)
 S PSUNDCL=PSUDRUG(22)
 S PSUNAF=$S(PSUDRUG(52):"N/F",1:"")
 S PSUNADR=PSUDRUG(20)
 ;Get the National Formulary Indicator and Restriction
 S (PSOPNFI,PSOPNFR)=""
 I $$VERSION^XPDUTL("PSN")'<4 D
 .S PSOPNFI=$$FORMI^PSNAPIS(PSUNADR,PSUNDCL)
 .S PSOPNFR=$$FORMR^PSNAPIS(PSUNADR,PSUNDCL)
GETDRUGQ Q
 ;
SETREC ;Set the record into the ^XTMP global
 S:PSUDIVP="" PSUDIVP=PSUSNDR
 S REC1="^",REC2="*",PSU2U="^"
 S REC1=REC1_$TR(PSUSITE,"^","'")_PSU2U_$TR(PSUFD,"^","'")_PSU2U
 S REC1=REC1_$TR(PSURELDT,"^","'")_PSU2U_$TR(PSURXN,"^","'")_PSU2U
 S REC1=REC1_$TR(PSUSC,"^","'")_PSU2U_PSUSSN_PSU2U_$TR(PSUVANM,"^","'")_PSU2U
 S REC1=REC1_$TR(PSUVACLS,"^","'")_PSU2U_$TR(PSUGNM,"^","'")_PSU2U
 S REC1=REC1_$TR(PSUNDC,"^","'")_PSU2U_$TR(PSUNFI,"^","'")_PSU2U
 S REC1=REC1_$TR(PSOPNFI,"^","'")_PSU2U_$TR(PSOPNFR,"^","'")_PSU2U
 S REC1=REC1_$TR(PSUDEA,"^","'")_PSU2U_$TR(PSUTYP,"^","'")_PSU2U
 S REC1=REC1_$TR(PSUCMOP,"^","'")_PSU2U_$TR(PSUMW,"^","'")_PSU2U
 S REC1=REC1_$TR(PSUPRSSN,"^","'")_PSU2U_$TR(PSUPTYP,"^","'")_PSU2U
 ;S REC1=REC1_$TR(PSUPCLS,"^","'")_PSU2U_$TR(PSUPSERV,"^","'")_PSU2U
 ;S REC2=REC2_$TR(PSUSP1,"^","'")_PSU2U_$TR(PSUSP2,"^","'")_PSU2U
 S REC2=REC2_$TR(PSUSIG,"^","'")_PSU2U_$TR(PSUWPC,"^","'")_PSU2U
 S REC2=REC2_$TR(PSUDUN,"^","'")_PSU2U_$TR(PSUDRCT,"^","'")_PSU2U
 S REC2=REC2_$TR(PSUDS,"^","'")_PSU2U_$TR(PSUQTY,"^","'")_PSU2U_PSUNAF_U
 D ICN^PSUIV1 S PSUPICN=$P($G(^XTMP("PSU_"_PSUJOB,"PSUPICN")),U,1)
 S REC2=REC2_$G(PSUPICN)_PSU2U_PSUPRID_PSU2U_$G(PSUCAN)_"^"
 S PSURCT=1+$P($G(^XTMP(PSUOPSUB,"DATA",PSUSITE,PSURXIEN,0)),U,1)
 S ^XTMP(PSUOPSUB,"DATA",PSUSITE,PSURXIEN,PSURCT,1)=REC1
 S ^XTMP(PSUOPSUB,"DATA",PSUSITE,PSURXIEN,PSURCT,2)=REC2
 S $P(^XTMP(PSUOPSUB,"DATA",PSUSITE,PSURXIEN,0),U,1)=PSURCT
 I (($E(OPVER)=6)&(PSUTYP="P"))!($E(OPVER)>6) S ^XTMP(PSUOPSUB,"RXIEN",PSURXIEN)=""
 I '$D(^XTMP("PSU_"_PSUJOB,"PSUOPFLG")) D
 .D LAB^PSULR0("OP",PSUSITE,PSURXIEN,DFN,PSUGNM,PSUVACLS)
SUMDRUG ; total drug info for summary report
 S PSUVARS="PSUTPART,PSUTFIL,PSUTRFIL,PSUTCST,PSUTQTY"
 S PSUREC=$G(^XTMP(PSUOPSUB,"DRUG",PSUSITE,PSUGNM,PSUCMOP))
 F I=1:1:5 S @$P(PSUVARS,",",I)=+$P(PSUREC,U,I)
 I PSUTYP="P" S PSUTPART=PSUTPART+1
 I PSUTYP="N" S PSUTFIL=PSUTFIL+1
 I PSUTYP="R" S PSUTRFIL=PSUTRFIL+1
 S PSUTQTY=PSUQTY+PSUTQTY
 S PSUTCST=(PSUDRCT*PSUQTY)+PSUTCST
 S REC=PSUTPART_U_PSUTFIL_U_PSUTRFIL_U_$J(PSUTCST,0,2)_U_$J(PSUTQTY,0,2)
 S $P(REC,U,6)=$S(PSUNFI="N/F":"*",1:"")
 S $P(REC,U,7)=$S(PSOPNFI="0":"#",1:"")
 S ^XTMP(PSUOPSUB,"DRUG",PSUSITE,PSUGNM,PSUCMOP)=REC
 Q
