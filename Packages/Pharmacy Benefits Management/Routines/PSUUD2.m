PSUUD2 ;BIR/TJH - PBM UNIT DOSE SUBROUTINES & FUNCTIONS ;10 JUL 1999
 ;;3.0;PHARMACY BENEFITS MANAGEMENT;**1,7,14,19**;Oct 15, 1998
 ;DBIA(s)
 ; none needed
 ;
DISAMT ; precompute dispensed amounts by drug
 N DADATE,DADRUG,DAMT,DAHOW
 K PSUDAS ; initialize Dispensed Amount Summary array
 S PSUX=0
DAL1 S PSUX=$O(^TMP($J,"PSUTA",PSUX)) G:PSUX="" DISAMTQ
 S DADATE=^TMP($J,"PSUTA",PSUX,.01,"I")
 I (DADATE<PSUSDT)!(DADATE>PSUEDT) G DAL1
 S DADRUG=$G(^TMP($J,"PSUTA",PSUX,.02,"I")) G DAL1:DADRUG=""
 S DAMT=+$G(^TMP($J,"PSUTA",PSUX,.03,"I"))
 S DAHOW=$G(^TMP($J,"PSUTA",PSUX,.05,"I"))
 S PSUDAS(DADRUG)=$G(PSUDAS(DADRUG))+$S(DAHOW=4:DAMT*-1,1:DAMT)
 G DAL1
DISAMTQ K ^TMP($J,"PSUTA") Q  ; exit point from DISAMT subroutine
 ;
SETUP ; set up some variables required later
 D SECTN^PSUTL1
 D DT^DILF("E",PSUSDT,.EXTD)
 S PSURP("START")=EXTD(0)
 D DT^DILF("E",PSUEDT,.EXTD)
 S PSURP("END")=EXTD(0)
 S X1=PSUSDT,X2=-101
 D C^%DTC K %,%H,%T
 S PSDATE=X
 S PSUEDTIM=PSUEDT+.2400
 S PSUJOB=$G(PSUJOB,$J),PSUUDSUB="PSUUD_"_PSUJOB
 K ^XTMP(PSUUDSUB)
 K PSUDTLRN
 S X1=DT,X2=3 D C^%DTC
 S ^XTMP(PSUUDSUB,0)=X_U_DT_U_"PSU PBM UNIT DOSE STATISTICAL DATA"
SETUPQ Q  ; exit from SETUP
 ;
TMPUD ; store Unit Dose data in first half of record, pieces 2-7
 S DLM="^",REC1="^"
 S REC1=REC1_$TR(PSUFACN,"^","'")_DLM_$TR(PSUDOSE(10),"^","'")_DLM_$TR(PSUDOSE(.01),"^","'")
 S REC1=REC1_DLM_PSUSSN_DLM_$TR(PSUDOSE(26),"^","'")_DLM_PSUVSSN   ;_DLM_$TR(PSUVCL,"^","'")
 ;S REC1=REC1_DLM_$TR(PSUVSV,"^","'")_DLM_$TR(PSUVS1,"^","'")_DLM_$TR(PSUVS2,"^","'")
TMPUDQ Q  ; exit from TMPUD
 ;
TMPDD ; create Dispense Drug record and store in ^XTMP
 N PSUDAMT S PSUDAMT=$G(PSUDAS(PSUDISD(.01)))
 Q:'PSUDAMT  ; per Lina B., do not store if dispensed amount=0
 S DLM="^",REC2="",PSUDTLRN(PSUFACN)=+$G(PSUDTLRN(PSUFACN))+1
 S REC2=REC1_DLM_$TR(PSUDRUG(21),"^","'")_DLM_$TR(PSUDRUG(2),"^","'")_DLM
 S REC2=REC2_$TR(PSUDRUG(.01),"^","'")_DLM_$TR(PSUDRUG(31),"^","'")_DLM
 S REC2=REC2_PSUDRUG(51)_DLM_PSUDNFI_DLM_PSUDNFR_DLM
 S REC2=REC2_$TR(PSUDISD(.02),"^","'")_DLM_$TR(PSUDRUG(14.5),"^","'")_DLM
 S REC2=REC2_$TR(PSUDRUG(16),"^","'")_DLM_PSUDAMT_DLM_PSUDRUG(52)_DLM_PSUDRUG(3)_"^"
 D ICN^PSUIV1 S PSUPICN=$P($G(^XTMP("PSU_"_PSUJOB,"PSUPICN")),U,1)
 S REC2=REC2_$G(PSUPICN)_DLM_$G(PSUDOSE(1))_DLM_PSUUDST_DLM
 S ^XTMP(PSUUDSUB,"DETAIL",PSUFACN,PSUDTLRN(PSUFACN))=REC2
 ; increase Unit Dose and Patient counts if not already counted
 I '$D(^XTMP(PSUUDSUB,"ORD",PSUFACN,PSUDOSE(.01))) D
 .S ^XTMP(PSUUDSUB,"ORD",PSUFACN,PSUDOSE(.01))=""
 .S ^XTMP(PSUUDSUB,"ORD",PSUFACN)=1+$G(^XTMP(PSUUDSUB,"ORD",PSUFACN))
 I '$D(^XTMP(PSUUDSUB,"SSN",PSUFACN,PSUSSN)) D
 .S ^XTMP(PSUUDSUB,"SSN",PSUFACN,PSUSSN)=""
 .S ^XTMP(PSUUDSUB,"SSN",PSUFACN)=1+$G(^XTMP(PSUUDSUB,"SSN",PSUFACN))
 S PSUDIV=PSUFACN D GETDIV^PSUIV3 I PSUDIVNM'="" D
 .S ^XTMP("PSU_"_PSUJOB,"PSUDIV",PSUDIVNM,PSUSSN)=""
 I PSUDIVNM="" S ^XTMP("PSU_"_PSUJOB,"PSUDIV",PSUDIV,PSUSSN)=""
 ; and store totals by drug in ^TMP("PSUUD DRUG",$J,PSUFACN
 I '$D(^XTMP(PSUUDSUB,"DRUG",PSUFACN,PSUDRUG(.01))) D
 .S ^XTMP(PSUUDSUB,"DRUG",PSUFACN,PSUDRUG(.01))=0_U_PSUDRUG(16)_U_PSUDRUG(51)_U_PSUDNFI
 S $P(^XTMP(PSUUDSUB,"DRUG",PSUFACN,PSUDRUG(.01)),U,1)=$P(^XTMP(PSUUDSUB,"DRUG",PSUFACN,PSUDRUG(.01)),U,1)+PSUDAMT
 ; and store Summary totals
 S ^XTMP(PSUUDSUB,"DIS",PSUFACN)=PSUDAMT+$G(^XTMP(PSUUDSUB,"DIS",PSUFACN))
 S ^XTMP(PSUUDSUB,"CST",PSUFACN)=(PSUDRUG(16)*PSUDAMT)+$G(^XTMP(PSUUDSUB,"CST",PSUFACN))
TMPDDQ Q  ; exit from TMPDD
