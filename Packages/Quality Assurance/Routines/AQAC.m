AQAC ;IHS/ASU/RPL ;QA/UR VARIABLES SETUP ; 11/8/89  11:24 AM
 ;;QA/UR Version 2.04;11/7/89
 ;Called from Menu Entrance of Option AQACMENU
 ;
 S Y="",Y=$O(^DIC(9.4,"C","AQAC",Y)),AQAC("VERSION")=^DIC(9.4,Y,"VERSION"),Y=$P(^DIC(9.4,Y,22,AQAC("VERSION"),0),"^",2) X ^DD("DD") S AQAC("VERDT")=Y
 D ^%AUCLS W !?22 F AQAC("I")=1:1:35 W "*"
 W !?22,"*",?56,"*",!?14,"*********      INDIAN HEALTH SERVICE      *********",!,?14,"*",?64,"*",!
 W ?14,"*  QUALITY ASSURANCE / UTILIZATION REVIEW SYSTEM",?64,"*",!,?14,"*",?64,"*",!,?14,"*********     VERSION ",AQAC("VERSION"),", ",AQAC("VERDT"),?56,"*********",!?22,"*",?56,"*",!?22 F AQAC("I")=1:1:35 W "*"
 I '$D(DUZ(2))!('$D(DUZ(0))) W !!,"YOU MUST SIGN ON PROPERLY THROUGH THE KERNEL TO USE QA/UR!" S XQUIT=1 G XQUIT
 S X=$S($D(^DIC(4,DUZ(2),0))#2:$P(^(0),"^"),1:"") W !!,?80-$L(X)\2,X
 I X="" W !!,"INVALID FACILITY; NOTIFY YOUR SITE MANAGER!" S XQUIT="" G XQUIT
 S AQACSITE=$S($D(^AUTTSITE(1,0)):$P(^(0),"^"),1:"")
 I AQACSITE="" W *7,!!,"RPMS SITE IS NOT DEFINED",!,"NOTIFY YOUR SITE MANAGER" S XQUIT=1 G XQUIT
 I DUZ(2)'=AQACSITE W *7,!!,"YOU ARE NOT SET TO THE RPMS SITE",!,"NOTIFY YOUR SITE MANAGER" S XQUIT=1 G XQUIT
 I DUZ(0)'="@" S AQACDUZ0=DUZ(0),DUZ(0)="Q"_DUZ(0)
XQUIT W ! K AQAC,AQACSITE
 Q
XIT ;Called from Menu Exit of Option AQACMENU
 I DUZ(0)'="@" S DUZ(0)=AQACDUZ0 K AQACDUZ0
 Q
