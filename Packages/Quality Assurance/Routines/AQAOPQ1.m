AQAOPQ1 ; IHS/ORDC/LJF - QIK REF-OUTCOME LEVELS ;
 ;;1.01;QAI MANAGEMENT;;OCT 05, 1995
 ;
 ;This rtn prints the quick reference for outcome levels as defined
 ;by your facility.
 ;
DEV ; >> get print device
 W !! S %ZIS="QP" D ^%ZIS G EXIT:POP
 I '$D(IO("Q")) U IO G LOOP
 K IO("Q") S ZTRTN="LOOP^AQAOPQ1",ZTDESC="QR-OUTCOME LEVELS"
 D ^%ZTLOAD D HOME^%ZIS G EXIT
 ;
 ;
 ;
LOOP ; >> loop thru qi outcome file and set array
 S AQAOX=0
 F  S AQAOX=$O(^AQAO1(3,AQAOX)) Q:AQAOX'=+AQAOX  D
 .S AQAOS=$G(^AQAO1(3,AQAOX,0)) Q:AQAOS=""
 .S X=$P(AQAOS,U)
 .S AQAOAR(1,"POTENTIAL FOR ADVERSE OUTCOME",X)=$P(AQAOS,U,2)
 .S AQAOAR(2,"     OCCURRENCE OUTCOME LEVEL",X)=$P(AQAOS,U,4)
 .S AQAOAR(3,"     ULTIMATE PATIENT OUTCOME",X)=$P(AQAOS,U,5)
 .S AQAOAR(4,"PERFORMANCE LEVEL",X)=$P(AQAOS,U,6)
 ;
INIT ; >>> initialize variables
 I $D(AQAOIOMX),IOT'="HFS" S X=AQAOIOMX X ^%ZOSF("RM")
 D INIT^AQAOUTIL
 S AQAOTY="QUICK REFERENCE - OUTCOME/PERFORMANCE LEVELS"
 D HEADING^AQAOUTIL W !,AQAOLINE,!
 ;
PRINT ;
 S AQAOX=0
 F  S AQAOX=$O(AQAOAR(AQAOX)) Q:AQAOX=""  Q:AQAOSTOP=U  D
 .S AQAOY=0 W !
 .F  S AQAOY=$O(AQAOAR(AQAOX,AQAOY)) Q:AQAOY=""  Q:AQAOSTOP=U  D
 ..S AQAOZ=""
 ..F  S AQAOZ=$O(AQAOAR(AQAOX,AQAOY,AQAOZ)) Q:AQAOZ=""  Q:AQAOSTOP=U  D
 ...I $Y>(IOSL-4) D NEWPG^AQAOUTIL Q:AQAOSTOP=U  W !,AQAOLINE,!
 ...I AQAOX=4 W !,AQAOY,?20,AQAOZ,"  =  ",AQAOAR(AQAOX,AQAOY,AQAOZ) Q
 ...W !,AQAOY,?35,AQAOZ,"   =   ",AQAOAR(AQAOX,AQAOY,AQAOZ)
 ;
 ;
EXIT ; >> eoj
 I '$D(ZTQUEUED),IOST["C-" D PRTOPT^AQAOVAR
 D ^%ZISC D KILL^AQAOUTIL
 Q
