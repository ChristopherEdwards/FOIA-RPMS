AQAQRE ;IHS/ASU/RPL 4/17/89;Staff Credentials Recredentialing Due Report ; 11/7/89  10:10 AM
 ;;STAFF CREDENTIALS Version 1.1;11/7/89
 ;
START S AQAQSITE=$P(^DIC(4,DUZ(2),0),"^",1)
ZIS W !! S %IS="Q" D ^%ZIS G:POP QUIT I '$D(IO("Q")) U IO G DQUE
QUE K IO("Q"),IO("S") I IO=IO(0) W !,"Cannot Queue to Screen or Slave Printer!",! G ZIS
 S ZTRTN="DQUE^AQAQRE",ZTDESC="RECREDENTIALING DUE STAFF CREDENTIALS REPORT" S ZTSAVE("AQAQSITE")=""
 D ^%ZTLOAD G QUIT
DQUE K ^UTILITY($J)
 S AQAQSET=^DD(9002155,1,0),AQAQSET=$P(AQAQSET,"^",3),AQAQCTR=$L(AQAQSET,":")-1
 F I=1:1:AQAQCTR S AQAQS=$P(AQAQSET,";",I) S AQAQ1=$P(AQAQS,":"),AQAQ2=$P(AQAQS,":",2) S AQAQS(AQAQ1)=AQAQ2
 S AQAQDFN=0 F I=0:0 S AQAQDFN=$O(^AQAQ(AQAQDFN)) Q:AQAQDFN'=+AQAQDFN  D C0
 G PRINT
C0 S AQAQNM=$P(^DIC(16,AQAQDFN,0),"^")
 I '$D(^AQAQ(AQAQDFN,8)) S AQAQRCDU=0 G C000
 S AQAQRCDU=$P(^AQAQ(AQAQDFN,8),"^",2) S:AQAQRCDU="" AQAQRCDU=0
C000 S AQAQSC=$P(^AQAQ(AQAQDFN,0),"^",2) S AQAQSC=$S(AQAQSC]"":AQAQS(AQAQSC),1:"<No Staff Category on File>")
C00 I '$D(^AQAQ(AQAQDFN,3)) S AQAQMLE=0 G C01
 K AQAQMLE S AQAQ3=0 F J=0:0 S AQAQ3=$O(^AQAQ(AQAQDFN,3,AQAQ3)) Q:'AQAQ3  S AQAQMLE=$P(^(AQAQ3,0),"^")
 S:'$D(AQAQMLE) AQAQMLE=0 S:AQAQMLE="" AQAQMLE=0
C01 I '$D(^AQAQ(AQAQDFN,6)) S AQAQHSV=0 G C02
 K AQAQHSV S AQAQ6=0 F J=0:0 S AQAQ6=$O(^AQAQ(AQAQDFN,6,AQAQ6)) Q:'AQAQ6  S AQAQHSV=$P(^(AQAQ6,0),"^")
 S:'$D(AQAQHSV) AQAQHSV=0 S:AQAQHSV="" AQAQHSV=0
C02 I '$D(^AQAQ(AQAQDFN,9)) S AQAQCAD=0 G C03
 K AQAQCAD S AQAQ9=0 F J=0:0 S AQAQ9=$O(^AQAQ(AQAQDFN,9,AQAQ9)) Q:'AQAQ9  S AQAQCAD=$P(^(AQAQ9,0),"^")
 S:'$D(AQAQCAD) AQAQCAD=0 S:AQAQCAD="" AQAQCAD=0
C03 I '$D(^AQAQ(AQAQDFN,4)) S AQAQCMED=0 G C04
 K AQAQCMED S AQAQ4=0 F J=0:0 S AQAQ4=$O(^AQAQ(AQAQDFN,4,AQAQ4)) Q:'AQAQ4  S AQAQCMED=$P(^(AQAQ4,0),"^")
 S:'$D(AQAQCMED) AQAQCMED=0 S:AQAQCMED="" AQAQCMED=0
C04 S ^UTILITY($J,AQAQRCDU,AQAQDFN)=AQAQNM_"^"_AQAQSC_"^"_AQAQMLE_"^"_AQAQCMED_"^"_AQAQHSV_"^"_AQAQCAD
 Q
 ;
PRINT D ^AQAQRE1
QUIT2 I $D(ZTSK) K ^%ZTSK(ZTSK)
QUIT I $D(IOST),'$D(AQAQQUIT),$D(IO),$E(IOST)="C",IO=IO(0) R X:DTIME
 W @IOF
 X ^%ZIS("C") K Y,DIC,%DT,^UTILITY($J),ZTSK,IO("Q"),IO("S"),II,I,J,K,JJ,L,M,X
 K AQAQ,AQAQQUIT,AQAQ1,AQAQ2,AQAQ3,AQAQ6,AQAQ80D,AQAQ9,AQAQCAD,AQAQCTR,AQAQDFN,AQAQDTP,AQAQHSV,AQAQNM,AQAQPG,AQAQS,AQAQSC,AQAQSET,AQAQSITE,AQAQBC,AQAQBRD,AQAQBRDD,AQAQBRDY
 K AQAQ1CAD,AQAQ4,AQAQCADY,AQAQCME,AQAQCMED,AQAQCMEO,AQAQCMEY,AQAQHSVD,AQAQHSVO,AQAQHSVY,AQAQMLE,AQAQMLED,AQAQMLEO,AQAQMLEY,AQAQRCDU,AQAQRCDY
 Q
