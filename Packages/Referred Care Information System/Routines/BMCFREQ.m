BMCFREQ ; IHS/PHXAO/TMJ - TOP FPR PROCEDURES ;    
 ;;4.0;REFERRED CARE INFO SYSTEM;;JAN 09, 2006
 ;
 W !!?20,"*****  FREQUENCY OF RCIS PROCEDURES REPORT  *****",!!
 W !,"NOTE: Report does not include Secondary referrals",!
 D EXIT
 S BMCTYPR="P"
GETDATES ;
BD ;get beginning date
 W ! S DIR(0)="D^:DT:EP",DIR("A")="Enter beginning Visit Date" D ^DIR S:$D(DUOUT) DIRUT=1 K DIR S:$D(DUOUT) DIRUT=1
 I $D(DIRUT) G EXIT
 S BMCBD=Y
ED ;get ending date
 W ! S DIR(0)="DA^"_BMCBD_":DT:EP",DIR("A")="Enter ending Visit Date:  " S Y=BMCBD D DD^%DT S Y="" D ^DIR S:$D(DUOUT) DIRUT=1 K DIR S:$D(DUOUT) DIRUT=1
 I $D(DIRUT) G BD
 S BMCED=Y
 S X1=BMCBD,X2=-1 D C^%DTC S BMCSD=X
 I $D(DIRUT) G GETDATES
 S BMCNCAN=1 D ADD^BMCRL01 I $D(BMCQUIT) D DEL^BMCRL K BMCQUIT G GETDATES
 ;
NUM S DIR(0)="NO^5:100:0",DIR("A")="How many entries do you want in the list",DIR("B")="10",DIR("?")="" D ^DIR S:$D(DUOUT) DIRUT=1 K DIR
 S BMCLNO=Y
 S Y=BMCBD D DD^%DT S BMCBDD=Y S Y=BMCED D DD^%DT S BMCEDD=Y
 S BMCTCW=0,BMCPTVS="R",BMCTYPE="D",BMCCTYP="T"
 K ^BMCRTMP(BMCRPT,11) S BMCCNTL="S" D ^BMCRL4 K BMCCNTL I $D(BMCQUIT) D DEL^BMCRL G GETDATES
ZIS ;
 K BMCANS,BMCCNT,BMCCRIT,AMQQTAX,BMCCUT,BMCDISP,BMCHIGH,BMCI,BMCNCAN,BMCSEL,BMCSKIP,BMCTEXT,BMCVAR,BMCVIEN,BMCRREC
 S XBRC="^BMCFREQ1",XBRP="^BMCFREQP",XBNS="BMC",XBRX="EXIT^BMCFREQ"
 D ^XBDBQUE
 D EXIT
 Q
EXIT ;
 K BMCBD,BMCED,BMCDOB1,BMCDOB2,BMCSEX,X,Y,Z,%,BMCFAC,BMCJOB,BMCLNO,ZTIO,ZTQUEUED,BMCCLN,BMCTYPE,BMCSC,BMCC,BMCPREC,BMCSD,BMCA,BMCC,BMCF,BMCGTOT,BMCPRC,BMCTOT,BMCD,BMCPRCN,BMCET
 K BMCQUIT,BMCAPC,BMCDATE,BMCPOV,BMCVSIT,BMCNOCK,BMCTOT,BMCPROV,BMCVTOT,BMCLINO,L,I,BMCCMA,BMCPOVN,BMCV,BMCTYPP,BMCSCP,BMCPRIM,BMCALL,BMCSEAT
 K BMCANS,AMQQTAX,BMCBDD,BMCCNT,BMCCRIT,BMCCTYP,BMCCUT,BMCDISP,BMCEDD,BMCHIGH,BMCI,BMCNCAN,BMCPTVS,BMCRPT,BMCSEL,BMCSKIP,BMCTCW,BMCTEXT,BMCVAR,BMCVIEN,BMCRREC,DFN,BMCX,BMCY,BMCTYPR
 K BMCBT
 Q
 ;
 ;
 ;
 ;
