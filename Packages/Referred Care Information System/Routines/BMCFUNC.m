BMCFUNC ; IHS/PHXAO/TMJ - FIX UNCODED DX ;
 ;;4.0;REFERRED CARE INFO SYSTEM;;JAN 09, 2006
POV ;EP
 S BMCFILE=90001.01,BMCTEMP="[BMC FUD POV]" D GETCODE G PROCESS
 ;
GETCODE ;
 I $P($G(^APCCCTRL(DUZ(2),0)),U,5)="" D SETDEF Q
 S BMC999=$P(^APCCCTRL(DUZ(2),0),U,5)
 Q
SETDEF ;SET DEFAULT OF .9999
 S BMC999=$O(^ICD9("AB",.9999,""))
 Q
XIT ;
 K BMCFILE,BMCDFN,BMCVDG,BMCVIGR,BMCCONT,BMCTEMP,BMC999,BMCG,BMCL,BMCHRN,BMCDOB,BMCVSIT,AUPNSEX,AUPNPAT,AUPNDOB,AUPNDAYS,BMCEIN,AUPNDOD,BMCCAT
 Q
PROCESS ;
 I BMC999="" W !!,"ERROR -- .9999 NOT IN ICD DIAGNOSIS FILE, NOTIFY YOUR SUPERVISOR" G XIT
 S BMCEIN="",BMCDFN="",U="^"
 I '$D(^DIC(BMCFILE)) W !!,"FILE DOES NOT EXIST -- NOTIFY YOUR SUPERVISOR" G XIT
 S BMCVDG=$P(^DIC(BMCFILE,0),U) I BMCVDG="" W !,"ERROR IN ^DIC -- NOTIFY PROGRAMMER" G XIT
 S BMCVDG=^DIC(BMCFILE,0,"GL")
 S BMCG=BMCVDG_"""B"",BMC999)"
 I '$D(@BMCG) W ?10,!!,"***There are no .9999 codes to change.***" H 3 Q
 S BMCVIGR=BMCVDG_"""B"",BMC999,BMCDFN)"
 W !!,"Searching the ",$P(^DIC(BMCFILE,0),U)," File",!
 S BMCDFN=0,BMCCONT=1 F BMCL=0:0 S BMCDFN=$O(@BMCVIGR) Q:BMCDFN'=+BMCDFN  D CONT Q:'BMCCONT  D BMCDIE
 W !!,"All done with the ",$P(^DIC(BMCFILE,0),U)," file",!
 D XIT
 Q
CONT ;
 W !!
 S DIR("A")="Continue",DIR("B")="Y",DIR(0)="Y" D ^DIR
 I $D(DIRUT) S X="N"
 S:"Nn"[X BMCCONT=""
 W !
 K DIR,DIRUT,DUOUT,DTOUT,DIROUT
 Q
BMCDIE ;
 S BMCG=BMCVDG_"BMCDFN,0)" S Y=$P(@BMCG,U,2),BMCVSIT=$P(@BMCG,U,3) I Y=""!(BMCVSIT="") W !,"ERROR IN GLOBAL -- NOTIFY PROGRAMMER - PATIENT OR VISIT DFN MISSING" Q
 D ^AUPNPAT
 I $L(BMCFILE)>7,AUPNDOB]"" S X2=AUPNDOB,X1=$P(^BMCREF(BMCVSIT,0),U)\1 D ^%DTC S AUPNDAYS=X ; re-set days of age to visit date-dob
 S Y=AUPNDOB X ^DD("DD") S BMCDOB=Y
 S BMCHRN="" I $D(^AUPNPAT(AUPNPAT,41,DUZ(2),0)) S BMCHRN=$P(^AUPNPAT(AUPNPAT,41,DUZ(2),0),U,2)
 W !,"NAME: ",$P(^DPT(AUPNPAT,0),U),"  DOB: ",BMCDOB,"  SEX: ",AUPNSEX,"  HRN: ",$S(BMCHRN]"":BMCHRN,1:"NONE")
 ;I $L(BMCFILE)>7 S BMCCAT=$P(^BMCREF(BMCVSIT,0),U,7) W !,"DATE OF VISIT: " S Y=$P(^AUPNVSIT(BMCVSIT,0),U) D DT^DIO2 S Y=""
 ;
 ;
 ;NOTE TO TONI----DECIDE WHAT REFERRAL INFOR TO DISPLAY
 ;W !!,"HELLO",!!
 ;S BMCRNUM=$$VALI^XBDIQ1(90001,BMCVDG,.02) I $D(BMCRNUM) W !,"REFERRAL NUMBER: ",BMCRNUM
 ;
 ;
 S DA=BMCDFN,DIE=BMCVDG,DR=BMCTEMP D ^DIE K DA,DIE,DR
 ;
 ;I $L(BMCFILE)>7 S AUPNVSIT=BMCVSIT D MOD^AUPNVSIT
 ;Stuff Todays's Date in field .27 Date Last Modified in BMCREF(
 ;
 S DA=BMCVSIT,DIE="^BMCREF(",DR=".27////"_DT D ^DIE K DA,DIE,DIU,DIV,DR
 ;
 Q
