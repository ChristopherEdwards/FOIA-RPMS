BMCRLU1 ; IHS/PHXAO/TMJ - GEN RETR UTILITIES ;     [ 11/28/2006  1:23 PM ]
 ;;4.0;REFERRED CARE INFO SYSTEM;**3**;JAN 09, 2006
 ;BMC 4.0*3 11/30/06 IHS/OIT/FCJ PRNT POL #
 ;
MCR ;display all current medicare data
 NEW BMCMIFN
 I '$D(^DPT(P,0)) G MCRX
 I $P(^DPT(P,0),U,19) G MCRX
 I '$D(^AUPNPAT(P,0)) G MCRX
 I '$D(^AUPNMCR(P,11)) G MCRX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCRX
 S BMCMIFN=0 F  S BMCMIFN=$O(^AUPNMCR(P,11,BMCMIFN)) Q:BMCMIFN'=+BMCMIFN  D
 .Q:$P(^AUPNMCR(P,11,BMCMIFN,0),U)>D
 .I $P(^AUPNMCR(P,11,BMCMIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 .S BMCPCNT=BMCPCNT+1,BMCPRNM(BMCPCNT)=$P(^AUPNMCR(DFN,0),U,3)_" ["_$S($P(^(0),U,4)]"":$P(^AUTTMCS($P(^(0),U,4),0),U),1:"-")_"]"
 .S BMCPCNT=BMCPCNT+1,Y=$P(^AUPNMCR(DFN,11,BMCMIFN,0),U),Z=$P(^(0),U,2),BMCPRNM(BMCPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_($E(Y,1,3)+1700)_"-" I Z]"" S BMCPRNM(BMCPCNT)=BMCPRNM(BMCPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_($E(Y,1,3)+1700)
MCRX ;
 K Y,Z
 Q
 ;
MCD ;
 NEW BMCMIFN,BMCNIFN
 I '$D(^DPT(P,0)) G MCDX
 I $P(^DPT(P,0),U,19) G MCDX
 I '$D(^AUPNPAT(P,0)) G MCDX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G MCDX
 S BMCMIFN=0 F  S BMCMIFN=$O(^AUPNMCD("B",P,BMCMIFN)) Q:BMCMIFN'=+BMCMIFN  D
 .Q:'$D(^AUPNMCD(BMCMIFN,11))
 .S BMCNIFN=0 F  S BMCNIFN=$O(^AUPNMCD(BMCMIFN,11,BMCNIFN)) Q:BMCNIFN'=+BMCNIFN  D
 ..Q:BMCNIFN>D
 ..I $P(^AUPNMCD(BMCMIFN,11,BMCNIFN,0),U,2)]"",$P(^(0),U,2)<D Q
 ..S BMCPCNT=BMCPCNT+1,BMCPRNM(BMCPCNT)=$P(^AUPNMCD(BMCMIFN,0),U,3)_"/"_$S($P(^AUPNMCD(BMCMIFN,0),U,2)]"":$P(^AUTNINS($P(^AUPNMCD(BMCMIFN,0),U,2),0),U),1:"<>")
 ..S BMCPCNT=BMCPCNT+1,Y=$P(^AUPNMCD(BMCMIFN,11,BMCNIFN,0),U),Z=$P(^(0),U,2),BMCPRNM(BMCPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_($E(Y,1,3)+1700)_"-" I Z]"" S BMCPRNM(BMCPCNT)=BMCPRNM(BMCPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_($E(Z,1,3)+1700)
 ;
MCDX ;
 Q
 ;
PI ; EP
 NEW BMCMIFN,BMCFLG
 I '$D(^DPT(P,0)) G PIX
 I $P(^DPT(P,0),U,19) G PIX
 I '$D(^AUPNPAT(P,0)) G PIX
 I '$D(^AUPNPRVT(P,11)) G PIX
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D G PIX
 S BMCMIFN=0 F  S BMCMIFN=$O(^AUPNPRVT(P,11,BMCMIFN)) Q:BMCMIFN'=+BMCMIFN  D
 .Q:$P(^AUPNPRVT(P,11,BMCMIFN,0),U)=""
 .S BMCNAME=$P(^AUPNPRVT(DFN,11,BMCMIFN,0),U) Q:BMCNAME=""
 .Q:$P(^AUTNINS(BMCNAME,0),U)["AHCCCS"
 .Q:$P(^AUPNPRVT(P,11,BMCMIFN,0),U,6)>D
 .I $P(^AUPNPRVT(P,11,BMCMIFN,0),U,7)]"",$P(^(0),U,7)<D Q
 .;BMC 4.0*3 11/30/06 IHS/OIT/FCJ PRNT POL #
 .;S BMCPCNT=BMCPCNT+1,BMCPRNM(BMCPCNT)=$P(^AUTNINS($P(^AUPNPRVT(P,11,BMCMIFN,0),U),0),U)_" - "_$P(^AUPNPRVT(P,11,BMCMIFN,0),U,2)
 .S BMCPCNT=BMCPCNT+1
 .S BMCPRNM(BMCPCNT)=$P(^AUTNINS($P(^AUPNPRVT(P,11,BMCMIFN,0),U),0),U)_" - "_$P($G(^AUPN3PPH($P(^AUPNPRVT(P,11,BMCMIFN,0),U,8),0)),U,4)
 .I '$G(BMCFLAG) S BMCPCNT=BMCPCNT+1,Y=$P(^AUPNPRVT(DFN,11,BMCMIFN,0),U,6),Z=$P(^(0),U,7),BMCPRNM(BMCPCNT)=$E(Y,4,5)_"/"_$E(Y,6,7)_"/"_($E(Y,1,3)+1700)_"-" I Z]"" S BMCPRNM(BMCPCNT)=BMCPRNM(BMCPCNT)_$E(Z,4,5)_"/"_$E(Z,6,7)_"/"_($E(Y,1,3)+1700)
 ;
PIX ;
 Q
ML ;EP - set up mailing address print array
 S BMCPCNT=0 K BMCPRNM
 F X=1:1:3 S Y=$P($G(^DPT(DFN,.11)),U,X) I Y]"" S BMCPCNT=BMCPCNT+1,BMCPRNM(BMCPCNT)=Y
 S X=$P($G(^DPT(DFN,.11)),U,4)_", "
 S Y="",Y=$P($G(^DPT(DFN,.11)),U,5) I Y S Y=$P(^DIC(5,Y,0),U)
 S X=X_$S(Y]"":Y,1:"  ")
 S X=X_" "_$P($G(^DPT(DFN,.11)),U,6)
 S BMCPCNT=BMCPCNT+1,BMCPRNM(BMCPCNT)=X
 Q
 ;
RRR(P,D) ;EP Railroad Retirement 
 ;P = PATIENT
 ;D = DATE
 ; I = IEN
 ; Y = 1:yes, 0:no
 I '$G(P) Q 0
 I '$G(D) Q 0
 NEW I,Y,J
 S Y=0,U="^"
 I '$D(^DPT(P,0)) Q Y
 I $P(^DPT(P,0),U,19) Q Y
 I '$D(^AUPNPAT(P,0)) Q Y
 I '$D(^AUPNRRE(P,11)) Q Y
 I $D(^DPT(P,.35)),$P(^(.35),U)]"",$P(^(.35),U)<D Q Y
 S I=0 F  S I=$O(^AUPNRRE(P,11,I)) Q:I'=+I  D
 . Q:$P(^AUPNRRE(P,11,I,0),U)=""
 . Q:$P(^AUPNRRE(P,11,I,0),U,1)>D
 . I $P(^AUPNRRE(P,11,I,0),U,2)]"",$P(^(0),U,2)<D Q
 . S Y=1
 Q Y
