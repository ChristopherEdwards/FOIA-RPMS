BMCRPC1 ; IHS/PHXAO/TMJ - REFERRED CARE INFO SYSTEM ;     
 ;;4.0;REFERRED CARE INFO SYSTEM;**5,6**;FEB 08, 2011
 ;IHS/ITSC/FCJ CHG TST FOR IOT FOR VIR TRM
 ; --BMC*4.0*7 2.8.2011 IHS.OIT.FCJ ADDED PATCH NUMBER PRINT IN LOGO SUB
 ; RPC code for RCIS GUI Application
 ; Routines contains code for Reading data from RCIS files
SRCHREF(RSLT,PATIENT,REFPRVDR,STRTDATE,ENDDATE,RECNMBR) ; search referral w.r.t patient, referring provider and date range
 ;; PATIENT = DFN
 ;  REFPRVDR = Provider Ien
 ; STRTDATE = start date ,  ENDDATE = end date ,  search referrels between start and end date
 ; NMBRREC   = Number of recrods to return
 ;S RSLT="Test Referaal Data" Q 
 S DI="S X=$P($G(^(0)),U,2) D LIST^DIC(8992.5,,""@;.01;.02IE;.03IE;.04;.05;.06;.09;1"",""IP"",1,X-1,X,""#"",,,""OUT1""_@X) D EN^DDIOL($G(OUT1_@X(""DILIST"",1,0)))"
 N OUT,ERR,SCR
 S SCR="I 1"
 K ^TMP($J,"PRNRCTMP")
 I +RECNMBR'>0 S RECNMBR="*"
 I ($D(PATIENT)&(PATIENT'="")) S SCR=SCR_" & (($P($G(^(0)),""^"",3))="_PATIENT_")" D EN^DDIOL($G(OUT1("DILIST",1,0)))
 I ($D(REFPRVDR)&(REFPRVDR'="")) S SCR=SCR_" & (($P($G(^(0)),""^"",6))="_REFPRVDR_")"
 I ($D(STRTDATE)&(STRTDATE'="")) D DT^DILF("TS",STRTDATE,.STRTDATE,,"") S SCR=SCR_" & (($P($G(^(0)),""^"",1))>="_STRTDATE_")"
 I ($D(ENDDATE)&(ENDDATE'="")) D DT^DILF("TS",ENDDATE,.ENDDATE,,"") S SCR=SCR_" & (($P($G(^(0)),""^"",1))<="_ENDDATE_")"
 I ($D(PATIENT)&(PATIENT'="")) D LIST^DIC(90001,"","@;.01;.02;101;.03IE;.05I;.06;.0999;1105;1112;1201;1114;1301","BQ",RECNMBR,,PATIENT,"D",SCR,"","^TMP($J,""PRNRCTMP"")") D CRTMSG(.RSLT,0) Q RSLT
 I ($D(REFPRVDR)&(REFPRVDR'="")) D LIST^DIC(90001,"","@;.01;.02;101;.03IE;.05I;.06;.0999;1105;1112;1201;1114;1301","BQ",RECNMBR,,REFPRVDR,"E",SCR,"","^TMP($J,""PRNRCTMP"")") D CRTMSG(.RSLT,0) Q RSLT
 I ($D(STRTDATE)&(STRTDATE'="")) D
 .I ($D(ENDDATE)&(ENDDATE'="")) D LIST^DIC(90001,"","@;.01;.02;101;.03IE;.05I;.06;.0999;1105;1112;1201;1114;1301","",RECNMBR,,,"B",SCR,"","^TMP($J,""PRNRCTMP"")") D CRTMSG(.RSLT,0) Q 
 Q RSLT
GTRFBYID(RSLT,REFIEN) ;; get referral
 ;; RSLT = result set returned as golbal array
 ;; REFIEN = referral ien of RCIS REFERRAL file
 N SCR,REFNUM,CMNTSX,PRIMREF
 I '$D(REFIEN) Q
 I REFIEN="" Q
 I $$GET1^DIQ(90001,REFIEN_",",.01,"")="" S RSLT="Not a valid Referral Ien" Q RSLT
 LOCK +^BMCREF(REFIEN):0.2 ;;check if record is being locked else where
 ELSE  S RSLT="The referral record cannot be opened becuase it is locked. Please try again later." Q RSLT
 S SCR="I Y="_REFIEN
 K ^TMP($J,"PRNRCTMP"),^TMP($J,"PRNRC")
 S REFNUM=$$GET1^DIQ(90001,REFIEN_",",.02,"") ;;get Ref Number... to use index in search... fast fetch
 S PRIMREF=$$GET1^DIQ(90001,REFIEN_",",102,"") ;;get primary referral  - if exsits; fetch Med Hx for primary ref too
 D LIST^DIC(90001,"","@;.01;.02;.06;.0999;101;1105;1111;1112;1201;1114;1301;1302;401;402;403;404;405;406;407;408;409;410;411;412;.03IE;.04I;.05I;.12I;.13I;.14I;.07I;.08I;.09I;.23I","Q","1",,REFNUM,"C",SCR,"","^TMP($J,""PRNRCTMP"")")
 LOCK -^BMCREF(REFIEN) ;; unlock the record
 D CRTMSG(.RSLT,1) ;;package data to be returned
 ;; fetch MED HX comments for the Referral
 S CMNTSX=$$GETMEDHX("",REFIEN)
 S ^TMP($J,"PRNRC",1)=^TMP($J,"PRNRC",1)_$E(CMNTSX,1,$L(CMNTSX)-2)
 ;; fetch MED HX comments for the Primary referral too, if this is a secondary referral
 I PRIMREF>0 S CMNTSX=$$GETMEDHX("",PRIMREF) S ^TMP($J,"PRNRC",1)=^TMP($J,"PRNRC",1)_$E(CMNTSX,1,$L(CMNTSX)-2)
 S RSLT=$NA(^TMP($J,"PRNRC"))
 Q RSLT
GETMEDHX(RSLT,REFIEN) ;; Get Medical History notes for a referral
 N CMNTS,IND,ERR,INDEX,CMDATE,RFCMTIEN,REVIEWER,OUT,SCR,CMNTSX
 S RFCMTIEN="",REVIEWER="",CMDATE="",IND="",INDEX="",CMNTS="",CMNTSX="~`",ERR=""
 S SCR="I ((($P($G(^(0)),""^"",5))=""M"") & (($P($G(^(0)),""^"",3))="_REFIEN_"))" ;;fetch only MED HX comments, for the Primary Referral
 D LIST^DIC(90001.03,"","@;.01;.04","BQ","*",,REFIEN,"AD",SCR,"","OUT")
 ;S RFCMTIEN=$$FIND1^DIC(90001.03,"","BQX",REFIEN,"AD")
 S INDEX=$O(OUT("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S RFCMTIEN=$G(OUT("DILIST",2,INDEX))
 .S CMDATE=$G(OUT("DILIST","ID",INDEX,".01"))
 .S REVIEWER=$G(OUT("DILIST","ID",INDEX,".04"))
 .K WP N WP
 .I RFCMTIEN>0 D GET1^DIQ(90001.03,RFCMTIEN_",",1,,"WP")
 .S IND=$O(WP(IND))
 .I +IND>0 F  D  Q:(+IND'>0)
 ..S CMNTS=CMNTS_WP(IND)_"~"
 ..S IND=$O(WP(IND))
 .I CMNTS'="" S CMNTSX=CMNTSX_RFCMTIEN_"^"_CMDATE_"^"_REVIEWER_"^"_$E(CMNTS,1,$L(CMNTS)-1)_"~`"
 .S INDEX=$O(OUT("DILIST","ID",INDEX)),CMNTS=""
 S RSLT=CMNTSX
 Q RSLT
CRTMSG(RSLT,DETAIL) ;; build result ;; DETAIL=0 ; search list , DETAIL=1 - get by Id
 N INDEX,REFIEN,REFNMBR,REFDATE,PATIEN,INSURNCE,PATNAME,RQSTFAC,RQSTPRV,FCLTYRFT,APPTDT,CHSSTATS,REFPRPS,PATHRN,DENLRSN,CHSELIG,VAELIG,SCHNOTES,REFTYPE,VSTTYPE,VSTNMBR,ICDCATID,CPTCATID,TYPEEXT,SUFFIX
 Q:'$D(^TMP($J,"PRNRCTMP","DILIST"))
 K ^TMP($J,"PRNRC")
 S PATHRN="",CHSELIG="",VAELIG="",INSURNCE="",PATIEN=""
 S INDEX=$O(^TMP($J,"PRNRCTMP","DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S REFIEN=$G(^TMP($J,"PRNRCTMP","DILIST",2,INDEX))
 .S REFDATE=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".01"))
 .S REFNMBR=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".02"))
 .S PATNAME=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".03","E"))
 .S RQSTFAC=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".05"))
 .S RQSTPRV=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".06"))
 .S FCLTYRFT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".0999"))
 .S APPTDT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1105"))
 .S CHSSTATS=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1112"))
 .S REFPRPS=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1201"))
 .S DENLRSN=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1114"))
 .S SCHNOTES=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1301"))
 .S SUFFIX=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"101"))
 .I PATIEN'=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".03","I")) D
 ..S PATIEN=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".03","I"))
 ..I $D(^AUPNPAT(PATIEN,41,RQSTFAC)) S PATHRN=$P($G(^AUPNPAT(PATIEN,41,RQSTFAC,0)),"^",2)
 ..S CHSELIG=$$GET1^DIQ(9000001,PATIEN_",",1112,"")
 ..S VAELIG=$$GET1^DIQ(2,PATIEN_",",1901,"")
 .I DETAIL=0 D
 ..N C32IEN,C32DATA S C32DATA="^",C32IEN=""
 ..I $O(^BMCREF(REFIEN,6,"B","")) S C32IEN=$O(^BMCREF(REFIEN,6,"B",$O(^BMCREF(REFIEN,6,"B",""),-1),""))
 ..I C32IEN>0 S C32DATA=$$GET1^DIQ(90001.6,C32IEN_","_REFIEN,.02,"")_"^"_$$GET1^DIQ(90001.6,C32IEN_","_REFIEN,.01,"")
 ..S ^TMP($J,"PRNRC",INDEX)="~`"_REFIEN_"^"_REFDATE_"^"_REFNMBR_"^"_PATIEN_"^"_PATNAME_"^"_PATHRN_"^"_RQSTPRV_"^"_FCLTYRFT_"^"_APPTDT_"^"_CHSSTATS_"^"_REFPRPS_"^"_DENLRSN_"^"_CHSELIG_"^"_VAELIG_"^"_SCHNOTES_"^"_SUFFIX_"^"_C32DATA
 .I DETAIL=1  D
 ..N SCHWIDAY,INCLCSRP,INCLFSHT,INCLHLSM,INCLHSPH,INCLEKG,INCLLBRP,INCLPCC,INCLPRNL,INCLTUBL,INCLCLNT,INCLXRYR,INCLXRYF
 ..S REFTYPE=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".04"))
 ..S VSTTYPE=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".14"))
 ..S VSTNMBR=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1111"))
 ..S ICDCATID=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".12"))
 ..S CPTCATID=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".13"))
 ..S RQSTPRV=$$GET1^DIQ(90001,REFIEN_",",.06,"I")
 ..I (REFTYPE="C") S TYPEEXT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".07")) ;; CHS - Vendor
 ..I (REFTYPE="I") S TYPEEXT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".08")) ;; IHS (ANOTHER FACILITY) - Location IEn
 ..I (REFTYPE="O") S TYPEEXT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".09")) ;; OTHER - RCIS Specific provider
 ..I (REFTYPE="N") S TYPEEXT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,".23")) ;; IN-HOUSE - Clinic Stop
 ..S SCHWIDAY=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"1302")) ;; Schedule With In days
 ..S INCLPCC=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"401")) ;; INCLUDE PCC VISIT FORM
 ..S INCLCLNT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"402")) ;; INCLUDE SPECIALTY CLINIC NOTES
 ..S INCLPRNL=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"403")) ;;INCLUDE PRENATAL RECORD(S)
 ..S INCLTUBL=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"404")) ;;INCLUDE SIGNED TUBAL CONSENT
 ..S INCLFSHT=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"405")) ;;INCLUDE FACE SHEET
 ..S INCLHLSM=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"406")) ;;INCLUDE HEALTH SUMMARY
 ..S INCLEKG=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"407")) ;; INCLUDE MOST RECENT EKG
 ..S INCLHSPH=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"408")) ;; INCLUDE HISTORY AND PHYSICAL
 ..S INCLXRYR=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"409")) ;;INCLUDE X-RAY / REPORT
 ..S INCLXRYF=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"410")) ;; INCLUDE X-RAY FILM
 ..S INCLCSRP=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"411")) ;;INCLUDE CONSULTATION REPORT
 ..S INCLLBRP=$G(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX,"412")) ;;INCLUDE MOST RECENT LAB REPORT
 ..S ^TMP($J,"PRNRC",INDEX)="~`"_REFIEN_"^"_REFDATE_"^"_REFNMBR_"^"_PATIEN_"^"_PATNAME_"^"_PATHRN_"^"_RQSTPRV_"^"_FCLTYRFT_"^"_APPTDT_"^"_CHSSTATS_"^"_REFPRPS_"^"_DENLRSN_"^"_CHSELIG_"^"_VAELIG_"^"_SCHNOTES_"^"_REFTYPE_"^"_VSTTYPE_"^"_VSTNMBR
 ..S ^TMP($J,"PRNRC",INDEX)=$G(^TMP($J,"PRNRC",INDEX))_"^"_ICDCATID_"^"_CPTCATID_"^"_TYPEEXT_"^"_SUFFIX_"^"_SCHWIDAY_"^"_INCLCSRP_"^"_INCLFSHT_"^"_INCLHLSM_"^"_INCLHSPH_"^"_INCLEKG_"^"_INCLLBRP_"^"_INCLPCC_"^"_INCLPRNL_"^"_INCLTUBL
 ..S ^TMP($J,"PRNRC",INDEX)=$G(^TMP($J,"PRNRC",INDEX))_"^"_INCLCLNT_"^"_INCLXRYR_"^"_INCLXRYF
 .S INDEX=$O(^TMP($J,"PRNRCTMP","DILIST","ID",INDEX))
 S RSLT=$NA(^TMP($J,"PRNRC"))
 K ^TMP($J,"PRNRCTMP")
 Q RSLT
GETREFDT(RSLT) ;; get Reference data for Refferal i-e ICD/CPT Categories
 K ^TMP($J)
 N OUT,OUT1,OUT2,ICDIEN,ICDCAT,CPTIEN,CPTCAT,PRPIEN,PRPTXT
 S ^TMP($J,"PRNRCRF",1)="~`" ;; RCIS ICD DIAGNOSTIC CATEGORY
 S ^TMP($J,"PRNRCRF",2)="~`" ;; RCIS CPT PROCEDURE CATEGORY
 S ^TMP($J,"PRNRCRF",3)="~`" ;; RCIS PURPOSE TEXT LIST
 D LIST^DIC(90001.51,"","@;.01","","*",,,"",,"","OUT")
 S INDEX=$O(OUT("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S ICDIEN=$G(OUT("DILIST",2,INDEX))
 .S ICDCAT=$G(OUT("DILIST","ID",INDEX,".01"))
 .S ^TMP($J,"PRNRCRF",1)=$G(^TMP($J,"PRNRCRF",1))_ICDIEN_"^"_ICDCAT_"~"
 .S INDEX=$O(OUT("DILIST","ID",INDEX))
 D LIST^DIC(90001.52,"","@;.01","","*",,,"",,"","OUT1")
 S INDEX=$O(OUT1("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S CPTIEN=$G(OUT1("DILIST",2,INDEX))
 .S CPTCAT=$G(OUT1("DILIST","ID",INDEX,".01"))
 .S ^TMP($J,"PRNRCRF",2)=$G(^TMP($J,"PRNRCRF",2))_CPTIEN_"^"_CPTCAT_"~"
 .S INDEX=$O(OUT1("DILIST","ID",INDEX))
 D LIST^DIC(90001.58,"","@;.01","","*",,,"",,"","OUT2")
 S INDEX=$O(OUT2("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S PRPIEN=$G(OUT2("DILIST",2,INDEX))
 .S PRPTXT=$G(OUT2("DILIST","ID",INDEX,".01"))
 .S ^TMP($J,"PRNRCRF",3)=$G(^TMP($J,"PRNRCRF",3))_PRPIEN_"^"_PRPTXT_"~"
 .S INDEX=$O(OUT2("DILIST","ID",INDEX))
 S RSLT=$NA(^TMP($J,"PRNRCRF"))
 Q RSLT
SRRFRDTO(RSLT,SRHSTRNG,REFTYPE) ;; Search Vendor ; Specific Provider ; Clinic Stop ; Location
 ;  search varies on Refferal type
 K ^TMP($J)
 I (('$D(REFTYPE))!('$D(SRHSTRNG))) S RSLT="Either search string or Referral Type is not present" Q RSLT
 I REFTYPE="C" D SRVNDR(.RSLT,SRHSTRNG) Q RSLT ;CHS
 I REFTYPE="I" D SRIHSFC(.RSLT,SRHSTRNG) Q RSLT ;IHS (ANOTHER FACILITY)
 I REFTYPE="O" D SROTPRV(.RSLT,SRHSTRNG) Q RSLT ;OTHER
 I REFTYPE="N" D SRCLNCST(.RSLT,SRHSTRNG) Q RSLT ;IN-HOUSE
 Q
SRVNDR(RSLT,VNRSTRNG) ;; search vendor from VENDOR file for type 'CHS' referrel
 N OUT,SCR,INDEX,VNDRIEN,VNDRNM,VNDRDUN,VNDREIN,EINSFX,MAILTO,REMITTO
 S SCR="I (($P($G(^(0)),""^"",5)="""") ! ($P($G(^(0)),""^"",5)>"_DT_"))"
 D LIST^DIC(9999999.11,"","@;.01;.07;1101;1102;1301;1302;1401;1402;1403","","*",,VNRSTRNG,"B",SCR,"","OUT")
  S INDEX=$O(OUT("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S VNDRIEN=$G(OUT("DILIST",2,INDEX))
 .S VNDRNM=$G(OUT("DILIST","ID",INDEX,".01"))
 .S VNDRDUN=$G(OUT("DILIST","ID",INDEX,".07"))
 .S VNDREIN=$G(OUT("DILIST","ID",INDEX,"1101"))
 .S EINSFX=$G(OUT("DILIST","ID",INDEX,"1102"))
 .S MAILTO=$G(OUT("DILIST","ID",INDEX,"1301"))_","_$G(OUT("DILIST","ID",INDEX,"1302"))
 .S REMITTO=$G(OUT("DILIST","ID",INDEX,"1401"))_","_$G(OUT("DILIST","ID",INDEX,"1402"))_","_$G(OUT("DILIST","ID",INDEX,"1403"))
 .S ^TMP($J,"PRNRCRVND",INDEX)="~`"_VNDRIEN_"^"_VNDRNM_"^"_VNDRDUN_"^"_VNDREIN_"^"_EINSFX_"^"_MAILTO_"^"_REMITTO
 .S INDEX=$O(OUT("DILIST","ID",INDEX))
 S RSLT=$NA(^TMP($J,"PRNRCRVND"))
 Q RSLT
SRIHSFC(RSLT,FCSTRNG) ;; search falicity from Location file for type 'IHS (ANOTHER FACILITY)' refferel
 N OUT,INDEX,FACIEN,NAME,AREA,SVCUNIT,CODE,INACTIVE,INACTDT
 D LIST^DIC(9999999.06,"","@;.01;.04;.05;.07;.27","","*",,FCSTRNG,"B",,"","OUT")
 S INDEX=$O(OUT("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S FACIEN=$G(OUT("DILIST",2,INDEX))
 .S NAME=$G(OUT("DILIST","ID",INDEX,".01"))
 .S AREA=$G(OUT("DILIST","ID",INDEX,".04"))
 .S SVCUNIT=$G(OUT("DILIST","ID",INDEX,".05"))
 .S CODE=$G(OUT("DILIST","ID",INDEX,".07"))
 .S INACTIVE="" I ($G(OUT("DILIST","ID",INDEX,".27"))'="") S INACTDT=$G(OUT("DILIST","ID",INDEX,".27")) D DT^DILF("TS",INACTDT,.INACTDT,,"") I INACTDT<=DT S INACTIVE=1
 .S ^TMP($J,"PRNRCRVND",INDEX)="~`"_FACIEN_"^"_NAME_"^"_AREA_"^"_SVCUNIT_"^"_CODE_"^"_INACTIVE_"^"_""  ;; extra empty field to keep inline with return paramaters of RPC
 .S INDEX=$O(OUT("DILIST","ID",INDEX))
 S RSLT=$NA(^TMP($J,"PRNRCRVND"))
 Q RSLT
SROTPRV(RSLT,PRVSTRNG) ;; search provider from RCIS SPECIFIC PROVIDER file for type 'OTHER' referrels
 N OUT,INDEX,PRVIEN,NAME
 D LIST^DIC(90001.53,"","@;.01","","*",,PRVSTRNG,"B",,"","OUT")
 S INDEX=$O(OUT("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S PRVIEN=$G(OUT("DILIST",2,INDEX))
 .S NAME=$G(OUT("DILIST","ID",INDEX,".01"))
 .S ^TMP($J,"PRNRCRVND",INDEX)="~`"_PRVIEN_"^"_NAME_"^"_""_"^"_""_"^"_""_"^"_""_"^"_""  ;; extra empty field to keep inline with return paramaters of RPC
 .S INDEX=$O(OUT("DILIST","ID",INDEX))
 S RSLT=$NA(^TMP($J,"PRNRCRVND"))
 Q RSLT
SRCLNCST(RSLT,CLNSTRNG) ;; search from CLINIC STOP file for IN-HOUSE referrals
 N OUT,INDEX,CLNIEN,NAME,CODE
 D LIST^DIC(40.7,"","@;.01;1","","*",,CLNSTRNG,"B",,"","OUT")
 S INDEX=$O(OUT("DILIST","ID",0))
 I +INDEX>0 F  D  Q:(+INDEX'>0)
 .S CLNIEN=$G(OUT("DILIST",2,INDEX))
 .S NAME=$G(OUT("DILIST","ID",INDEX,".01"))
 .S CODE=$G(OUT("DILIST","ID",INDEX,"1"))
 .S ^TMP($J,"PRNRCRVND",INDEX)="~`"_CLNIEN_"^"_NAME_"^"_CODE_"^"_""_"^"_""_"^"_""_"^"_""  ;; extra empty field to keep inline with return paramaters of RPC
 .S INDEX=$O(OUT("DILIST","ID",INDEX))
 S RSLT=$NA(^TMP($J,"PRNRCRVND"))
 Q RSLT
