BMCRPC2 ; IHS/PHXAO/TMJ - REFERRED CARE INFO SYSTEM ;     
 ;;4.0;REFERRED CARE INFO SYSTEM;**5,6**;FEB 08, 2011
 ;IHS/ITSC/FCJ CHG TST FOR IOT FOR VIR TRM
 ; --BMC*4.0*7 2.8.2011 IHS.OIT.FCJ ADDED PATCH NUMBER PRINT IN LOGO SUB
 ; RPC code for RCIS GUI Application
 ; Routines contains code for adding/updating data into RCIS files
SETREFRL(RSLT,REFDATE,PATIEN,TYPE,INOROUT,ICDCAT,CPTCAT,PURPOSE,PRIORITY,TYPEEXT,VISITS,NOTESCHD,HXCMNTS,RQSTPRDR,SCHWIDAY,INCLCSRP,INCLFSHT,INCLHLSM,INCLHSPH,INCLEKG,INCLLBRP,INCLPCC,INCLPRNL,INCLTUBL,INCLCLNT,INCLXRYR,INCLXRYF,TMIEN) ;;AddRef
 K ^TMP($J)
 N OUT,FDA,ERR1,X,REFNMBR,REFSTATS,RQSTFAC,PRIMPYR,CHSSTATS,CASEMGR,RS,BMCPDRG,BMCECOST,BMCIHSCT,BMCINLOS,ADTLINFO
 S ADTLINFO=0 ;;ADDITIONAL INFO flag
 ;; convert date to internal fileman format
 D DT^DILF("",REFDATE,.REFDATE) I REFDATE="-1" S RSLT="~`0^Referral Date is not in mm/dd/yyyy fromat" Q RSLT
 ;; check for duplication of record
 I PATIEN'>0 S RSLT="~`0^Patient Ien not provided" Q RSLT
 ;I $D(^BMCREF("AA",PATIEN,REFDATE)) S RSLT="~`0^A referral already exists for this patient on this date" Q RSLT
 S REFSTATS="A" ;; Setting status to ACTIVE
 S PRIMPYR="1"  ;; Setting Primary Payor to 'IHS'
 S:'RQSTPRDR RQSTPRDR=DUZ   ;; default to logged in user ien
 S RQSTFAC=DUZ(2) ;;default to facility where user is logged onto
 S CHSSTATS="P"  ;; Setting CHS APPROVAL STATUS to 'PENDING'
 S CASEMGR=$$GET1^DIQ(90001.31,RQSTFAC_",",.12,"I") ;;set default case manager if not provided
 S X=$$REFN^BMC  ;;generate Referral Number - will return 13 digit number in variable X
 Q:'X
 X $P(^DD(90001,.02,0),U,5,99) ;; Perform input transport check
 S REFNMBR=$G(X)
 I ((REFDATE="")!(PATIEN="")!(RQSTFAC="")!(INOROUT="")!(PURPOSE="")!(PRIORITY="")!(VISITS="")) S RSLT="~`0^Required field missing" Q RSLT
 S:$D(TMIEN) TMIEN=""
 S:'$D(TYPE) TYPE=""
 S:'$D(TYPEEXT) TYPEEXT=""
 S:'$D(ICDCAT) ICDCAT=""
 S:'$D(CPTCAT) CPTCAT=""
 S FDA(90001,"+1,",.01)=REFDATE
 S FDA(90001,"+1,",.02)=REFNMBR
 S FDA(90001,"+1,",.03)=PATIEN
 S FDA(90001,"+1,",.04)=TYPE
 S FDA(90001,"+1,",.05)=RQSTFAC
 S FDA(90001,"+1,",.14)=INOROUT
 S FDA(90001,"+1,",.06)=RQSTPRDR
 S FDA(90001,"+1,",.11)=PRIMPYR
 S:ICDCAT'="" FDA(90001,"+1,",.12)=ICDCAT
 S:CPTCAT'="" FDA(90001,"+1,",.13)=CPTCAT
 S FDA(90001,"+1,",1201)=PURPOSE
 S FDA(90001,"+1,",.32)=PRIORITY
 I ((TYPE="C")&(TYPEEXT'="")) S FDA(90001,"+1,",.07)=TYPEEXT ;;CHS - Vendor
 I ((TYPE="O")&(TYPEEXT'="")) S FDA(90001,"+1,",.09)=TYPEEXT ;;OTHER - RCIS Specific provider
 I ((TYPE="N")&(TYPEEXT'="")) S FDA(90001,"+1,",.23)=TYPEEXT ;;IN-HOUSE - Clinic Stop
 I ((TYPE="I")&(TYPEEXT'="")) S FDA(90001,"+1,",.08)=TYPEEXT ;;IHS (Another facility) - Location (TO IHS FACILITY)
 S:VISITS'="" FDA(90001,"+1,",1111)=VISITS
 S:CHSSTATS'="" FDA(90001,"+1,",1112)=CHSSTATS
 S:NOTESCHD'="" FDA(90001,"+1,",1301)=NOTESCHD
 S:CASEMGR'="" FDA(90001,"+1,",.19)=CASEMGR
 S:SCHWIDAY'="" FDA(90001,"+1,",1302)=SCHWIDAY ;;Schedule With In days
 S:"YN"[INCLPCC FDA(90001,"+1,",401)=INCLPCC ;;INCLUDE PCC VISIT FORM
 S:"YN"[INCLCLNT FDA(90001,"+1,",402)=INCLCLNT ;;INCLUDE SPECIALTY CLINIC NOTES
 S:"YN"[INCLPRNL FDA(90001,"+1,",403)=INCLPRNL ;;INCLUDE PRENATAL RECORD(S)
 S:"YN"[INCLTUBL FDA(90001,"+1,",404)=INCLTUBL ;;INCLUDE SIGNED TUBAL CONSENT
 S:"YN"[INCLFSHT FDA(90001,"+1,",405)=INCLFSHT ;;INCLUDE FACE SHEET
 S:"YN"[INCLHLSM FDA(90001,"+1,",406)=INCLHLSM ;;INCLUDE HEALTH SUMMARY
 S:"YN"[INCLEKG FDA(90001,"+1,",407)=INCLEKG ;;INCLUDE MOST RECENT EKG
 S:"YN"[INCLHSPH FDA(90001,"+1,",408)=INCLHSPH ;;INCLUDE HISTORY AND PHYSICAL
 S:"YN"[INCLXRYR FDA(90001,"+1,",409)=INCLXRYR ;;INCLUDE X-RAY / REPORT
 S:"YN"[INCLXRYF FDA(90001,"+1,",410)=INCLXRYF ;;INCLUDE X-RAY FILM
 S:"YN"[INCLCSRP FDA(90001,"+1,",411)=INCLCSRP ;;INCLUDE CONSULTATION REPORT
 S:"YN"[INCLLBRP FDA(90001,"+1,",412)=INCLLBRP ;;INCLUDE MOST RECENT LAB REPORT
 S:INCLPCC_INCLCLNT_INCLPRNL_INCLTUBL_INCLFSHT_INCLHLSM_INCLEKG_INCLHSPH_INCLXRYR_INCLXRYF_INCLCSRP_INCLLBRP["Y" ADTLINFO=1
 S FDA(90001,"+1,",.34)=ADTLINFO
 S FDA(90001,"+1,",.15)=REFSTATS
 S FDA(90001,"+1,",.25)=DUZ
 S FDA(90001,"+1,",.26)=DT
 S FDA(90001,"+1,",.27)=DT
 I TMIEN>0 D  ;;If Template was used for this 'Add Referral'; fetch extra info for that template
 .S BMCPDRG=$$GET1^DIQ(90001.32,TMIEN_",",.21,"I") S:BMCPDRG'="" FDA(90001,"+1,",.21)=BMCPDRG
 .S BMCECOST=$$GET1^DIQ(90001.32,TMIEN_",",1101,"") S:BMCECOST'="" FDA(90001,"+1,",1101)=BMCECOST
 .S BMCIHSCT=$$GET1^DIQ(90001.32,TMIEN_",",1103,"") S:BMCIHSCT'="" FDA(90001,"+1,",1103)=BMCIHSCT
 .S BMCINLOS=$$GET1^DIQ(90001.32,TMIEN_",",1109,"") S:BMCINLOS'="" FDA(90001,"+1,",1109)=BMCINLOS
 N FDAIEN S FDAIEN=""
 D UPDATE^DIE("","FDA","FDAIEN","ERR1")
 I $D(ERR1("DIERR")) S RSLT="~`0^Error adding referral: "_$G(ERR1("DIERR","1","TEXT",1)) Q RSLT
 D SENDXM^BMCRPC3(FDAIEN(1),"A") ;; Send mailman message
 D STATIC^BMCRPC3(PATIEN,FDAIEN(1)) ;; fill in EXP (5101 - 5114) fields in RCIS REF file...
 I $D(HXCMNTS) D
 .I HXCMNTS'="" D SETMEDHX^BMCRPC3(.RS,HXCMNTS,PATIEN,FDAIEN(1))
 I $P($G(RS),"^")="~`0" S RSLT=RS Q RSLT
 S RSLT="~`1^"_FDAIEN(1)
 Q RSLT
UPDREFRL(RSLT,REFIEN,TYPE,INOROUT,ICDCAT,CPTCAT,PURPOSE,PRIORITY,TYPEEXT,VISITS,NOTESCHD,HXCMNTS,SCHWIDAY,INCLCSRP,INCLFSHT,INCLHLSM,INCLHSPH,INCLEKG,INCLLBRP,INCLPCC,INCLPRNL,INCLTUBL,INCLCLNT,INCLXRYR,INCLXRYF) ;;update referral data
 K ^TMP($J)
 N OUT,FDA,ERR1,PATIEN,ADTLINFO
 S ADTLINFO=0 ;;ADDITIONAL INFO flag
 I '$D(REFIEN) S RSLT="~`0^Referral Ien is not provided" Q RSLT
 I REFIEN'>0 S RSLT="~`0^Referral Ien is not provided" Q RSLT
 S:'$D(TYPE) TYPE=""
 S:'$D(TYPEEXT) TYPEEXT=""
 S:'$D(ICDCAT) ICDCAT=""
 S:'$D(CPTCAT) CPTCAT=""
 S:TYPE'="" FDA(90001,REFIEN_",",.04)=TYPE
 S:INOROUT'="" FDA(90001,REFIEN_",",.14)=INOROUT
 S:ICDCAT'="" FDA(90001,REFIEN_",",.12)=ICDCAT
 S:CPTCAT'="" FDA(90001,REFIEN_",",.13)=CPTCAT
 S:PURPOSE'="" FDA(90001,REFIEN_",",1201)=PURPOSE
 S:PRIORITY'="" FDA(90001,REFIEN_",",.32)=PRIORITY
 I (TYPE="C") D  ;; CHS - Set Vendor - leave specific provider as is - delete To IHS FAC and Clinic Stop
 .S FDA(90001,REFIEN_",",.23)="@",FDA(90001,REFIEN_",",.08)="@"
 .S:(TYPEEXT'="") FDA(90001,REFIEN_",",.07)=TYPEEXT
 I (TYPE="O") D  ;; OTHER - set Specific provider - leave Vendor as is - delete To IHS FAC and Clinic Stop
 .S FDA(90001,REFIEN_",",.23)="@",FDA(90001,REFIEN_",",.08)="@"
 .S:(TYPEEXT'="") FDA(90001,REFIEN_",",.09)=TYPEEXT
 I (TYPE="N") D  ;; IN-HOUSE - set Clinic Stop - delete Specific provider - Vendor - To IHS FAC
 .S FDA(90001,REFIEN_",",.07)="@",FDA(90001,REFIEN_",",.09)="@",FDA(90001,REFIEN_",",.08)="@"
 .S:(TYPEEXT'="") FDA(90001,REFIEN_",",.23)=TYPEEXT
 I (TYPE="I") D  ;IHS(Another facility)-Set Location(FACILITY)-delete SpecificProvider-Vendor-ClinicStop
 .S FDA(90001,REFIEN_",",.07)="@",FDA(90001,REFIEN_",",.09)="@",FDA(90001,REFIEN_",",.23)="@"
 .S:(TYPEEXT'="") FDA(90001,REFIEN_",",.08)=TYPEEXT
 S:VISITS'="" FDA(90001,REFIEN_",",1111)=VISITS
 S:NOTESCHD'="" FDA(90001,REFIEN_",",1301)=NOTESCHD
 S:SCHWIDAY'="" FDA(90001,REFIEN_",",1302)=SCHWIDAY ;; Schedule With In days
 S:"YN"[INCLPCC FDA(90001,REFIEN_",",401)=INCLPCC ;; INCLUDE PCC VISIT FORM
 S:"YN"[INCLCLNT FDA(90001,REFIEN_",",402)=INCLCLNT ;; INCLUDE SPECIALTY CLINIC NOTES
 S:"YN"[INCLPRNL FDA(90001,REFIEN_",",403)=INCLPRNL ;;INCLUDE PRENATAL RECORD(S)
 S:"YN"[INCLTUBL FDA(90001,REFIEN_",",404)=INCLTUBL ;;INCLUDE SIGNED TUBAL CONSENT
 S:"YN"[INCLFSHT FDA(90001,REFIEN_",",405)=INCLFSHT ;;INCLUDE FACE SHEET
 S:"YN"[INCLHLSM FDA(90001,REFIEN_",",406)=INCLHLSM ;;INCLUDE HEALTH SUMMARY
 S:"YN"[INCLEKG FDA(90001,REFIEN_",",407)=INCLEKG ;; INCLUDE MOST RECENT EKG
 S:"YN"[INCLHSPH FDA(90001,REFIEN_",",408)=INCLHSPH ;; INCLUDE HISTORY AND PHYSICAL
 S:"YN"[INCLXRYR FDA(90001,REFIEN_",",409)=INCLXRYR ;;INCLUDE X-RAY / REPORT
 S:"YN"[INCLXRYF FDA(90001,REFIEN_",",410)=INCLXRYF ;; INCLUDE X-RAY FILM
 S:"YN"[INCLCSRP FDA(90001,REFIEN_",",411)=INCLCSRP ;;INCLUDE CONSULTATION REPORT
 S:"YN"[INCLLBRP FDA(90001,REFIEN_",",412)=INCLLBRP ;;INCLUDE MOST RECENT LAB REPORT
 S:INCLPCC_INCLCLNT_INCLPRNL_INCLTUBL_INCLFSHT_INCLHLSM_INCLEKG_INCLHSPH_INCLXRYR_INCLXRYF_INCLCSRP_INCLLBRP["Y" ADTLINFO=1
 S FDA(90001,REFIEN_",",.34)=ADTLINFO
 S FDA(90001,REFIEN_",",.27)=DT ;; Date last updated - set to current system date
 D FILE^DIE("","FDA","ERR1")
 I $D(ERR1("DIERR")) S RSLT="~`0^Error updating referral:"_$G(ERR1("DIERR","1","TEXT",1)) Q RSLT
 D SENDXM^BMCRPC3(REFIEN,"M") ;;Send mailman message
 I $D(HXCMNTS) D
 .I HXCMNTS'="" S PATIEN=$$GET1^DIQ(90001,REFIEN_",",.03,"I") D SETMEDHX^BMCRPC3(.RS,HXCMNTS,PATIEN,REFIEN)
 I $P($G(RS),"^")="~`0" S RSLT=RS Q RSLT
 S RSLT="~`1"
 Q RSLT
SETSCNRF(RSLT,PRIMREF,CALLIN,REFDATE,PATIEN,TYPE,INOROUT,ICDCAT,CPTCAT,PURPOSE,PRIORITY,TYPEEXT,HXCMNTS,SCHWIDAY,INCLCSRP,INCLFSHT,INCLHLSM,INCLHSPH,INCLEKG,INCLLBRP,INCLPCC,INCLPRNL,INCLTUBL,INCLCLNT,INCLXRYR,INCLXRYF) ;;insert sec. ref. data
 K ^TMP($J)
 N OUT,FDA,ERR1,REFNMBR,REFSTATS,RQSTFAC,RQSTPRDR,PRIMPYR,CHSSTATS,CASEMGR,SECNSFX,VISITS,ADTLINFO
 S ADTLINFO=0 ;;ADDITIONAL INFO flag
 S REFSTATS="A" ;; Setting status to ACTIVE
 S PRIMPYR="1"  ;; Setting Primary Payor to 'IHS'
 S RQSTPRDR=DUZ   ;; default to logged in user ien
 S RQSTFAC=DUZ(2) ;;default to facility where user is logged onto
 S CHSSTATS="P"  ;; Setting CHS APPROVAL STATUS to 'PENDING'
 S CASEMGR=$$GET1^DIQ(90001.31,RQSTFAC_",",.12,"I") ;;set default case manager if not provided
 S REFNMBR=$$GET1^DIQ(90001,PRIMREF_",",.02,"") ;;
 I PATIEN="" S PATIEN=$$GET1^DIQ(90001,PRIMREF_",",.03,"I")
 N Y1,Y2,Y3
 ;; Copied from BMCADDS routine - Begin  - to calculate suffix and visits
 S (Y1,Y2,Y3)=0
 I '$D(^BMCREF("S",REFNMBR)) S Y1=0
 E  S Y="" F  S Y=$O(^BMCREF("S",REFNMBR,Y)) Q:Y=""  D
 .S Y3=$E(Y,2,$L(Y)),Y2=Y2+1
 .S:Y3>Y1 Y1=Y3
 S Y1=Y1+1,Y2=Y2+1,SECNSFX="A"_Y1
 ;VISTS REMAINING
 S VISITS=($P(^BMCREF(PRIMREF,11),U,11)-Y2)
 S:VISITS<0 VISITS=0
 ;; Copied code from BMCADDS routine - End
 K Y1,Y2,Y3
 I ((REFDATE="")!(PATIEN="")!(RQSTFAC="")!(INOROUT="")!(PURPOSE="")!(PRIORITY="")!(VISITS="")) S RSLT="~`0^Required field missing" Q RSLT
 D DT^DILF("",REFDATE,.REFDATE) S:REFDATE'="-1" FDA(90001,"+1,",.01)=REFDATE
 S:'$D(TYPE) TYPE=""
 S:'$D(TYPEEXT) TYPEEXT=""
 S:'$D(ICDCAT) ICDCAT=""
 S:'$D(CPTCAT) CPTCAT=""
 S FDA(90001,"+1,",.02)=REFNMBR
 S FDA(90001,"+1,",101)=SECNSFX
 S FDA(90001,"+1,",102)=PRIMREF
 S FDA(90001,"+1,",.03)=PATIEN
 S:TYPE'="" FDA(90001,"+1,",.04)=TYPE
 S FDA(90001,"+1,",.05)=RQSTFAC
 S FDA(90001,"+1,",.14)=INOROUT
 S:CALLIN="N" FDA(90001,"+1,",.06)=RQSTPRDR
 S FDA(90001,"+1,",.11)=PRIMPYR
 S:ICDCAT'="" FDA(90001,"+1,",.12)=ICDCAT
 S:CPTCAT'="" FDA(90001,"+1,",.13)=CPTCAT
 S FDA(90001,"+1,",1201)=PURPOSE
 S FDA(90001,"+1,",.32)=PRIORITY
 I ((TYPE="C")&(TYPEEXT'="")) S FDA(90001,"+1,",.07)=TYPEEXT ;;CHS - Vendor
 I ((TYPE="O")&(TYPEEXT'="")) S FDA(90001,"+1,",.09)=TYPEEXT ;;OTHER - RCIS Specific provider
 I ((TYPE="N")&(TYPEEXT'="")) S FDA(90001,"+1,",.23)=TYPEEXT ;;IN-HOUSE - Clinic Stop
 I ((TYPE="I")&(TYPEEXT'="")) S FDA(90001,"+1,",.08)=TYPEEXT ;;IHS (Another facility) - Location (TO IHS FACILITY)
 I VISITS'="" S FDA(90001,"+1,",1111)=VISITS
 I CHSSTATS'="" S FDA(90001,"+1,",1112)=CHSSTATS
 I CASEMGR'="" S FDA(90001,"+1,",.19)=CASEMGR
 I SCHWIDAY'="" S FDA(90001,"+1,",1302)=SCHWIDAY ;;Schedule With In days
 I "YN"[INCLPCC S FDA(90001,"+1,",401)=INCLPCC ;;INCLUDE PCC VISIT FORM
 I "YN"[INCLCLNT S FDA(90001,"+1,",402)=INCLCLNT ;;INCLUDE SPECIALTY CLINIC NOTES
 I "YN"[INCLPRNL S FDA(90001,"+1,",403)=INCLPRNL ;;INCLUDE PRENATAL RECORD(S)
 I "YN"[INCLTUBL S FDA(90001,"+1,",404)=INCLTUBL ;;INCLUDE SIGNED TUBAL CONSENT
 I "YN"[INCLFSHT S FDA(90001,"+1,",405)=INCLFSHT ;;INCLUDE FACE SHEET
 I "YN"[INCLHLSM S FDA(90001,"+1,",406)=INCLHLSM ;;INCLUDE HEALTH SUMMARY
 I "YN"[INCLEKG S FDA(90001,"+1,",407)=INCLEKG ;;INCLUDE MOST RECENT EKG
 I "YN"[INCLHSPH S FDA(90001,"+1,",408)=INCLHSPH ;;INCLUDE HISTORY AND PHYSICAL
 I "YN"[INCLXRYR S FDA(90001,"+1,",409)=INCLXRYR ;;INCLUDE X-RAY / REPORT
 I "YN"[INCLXRYF S FDA(90001,"+1,",410)=INCLXRYF ;;INCLUDE X-RAY FILM
 I "YN"[INCLCSRP S FDA(90001,"+1,",411)=INCLCSRP ;;INCLUDE CONSULTATION REPORT
 I "YN"[INCLLBRP S FDA(90001,"+1,",412)=INCLLBRP ;;INCLUDE MOST RECENT LAB REPORT
 S:INCLPCC_INCLCLNT_INCLPRNL_INCLTUBL_INCLFSHT_INCLHLSM_INCLEKG_INCLHSPH_INCLXRYR_INCLXRYF_INCLCSRP_INCLLBRP["Y" ADTLINFO=1
 S FDA(90001,"+1,",.34)=ADTLINFO
 S FDA(90001,"+1,",.15)=REFSTATS
 S FDA(90001,"+1,",.25)=DUZ
 S FDA(90001,"+1,",.26)=DT
 S FDA(90001,"+1,",.27)=DT
 N FDAIEN S FDAIEN=""
 D UPDATE^DIE("","FDA","FDAIEN","ERR1")
 I $D(ERR1("DIERR")) S RSLT="~`0^Error adding Secondary Referral: "_$G(ERR1("DIERR","1","TEXT",1)) Q RSLT
 D SENDXM^BMCRPC3(FDAIEN(1),"A") ;;Send mailman message
 I $D(HXCMNTS) D
 .I HXCMNTS'="" D SETMEDHX^BMCRPC3(.RS,HXCMNTS,PATIEN,FDAIEN(1))
 I $P($G(RS),"^")="~`0" S RSLT=RS Q RSLT
 S RSLT="~`1^"_FDAIEN(1)
 Q RSLT
