BMCRR61 ; IHS/PHXAO/TMJ - PROCESS REFERRAL LIST ;   
 ;;4.0;REFERRED CARE INFO SYSTEM;;JAN 09, 2006
 ;IHS/ITSC/FCJ ADDED TEST FOR SR
 ;
 ;
 ;
START ;
 S (BMCBT,BMCBTH)=$H,BMCJOB=$J,BMCRCNT=0
 D PROCESS,END
 Q
 ;
PROCESS ;
V ; Run by visit date
 S BMCODAT=$O(^BMCREF("B",BMCSD)) I BMCODAT="" S BMCET=$H Q
 S BMCODAT=BMCSD_".9999" F  S BMCODAT=$O(^BMCREF("B",BMCODAT)) Q:BMCODAT=""!((BMCODAT\1)>BMCED)  D R1
 Q
 ;
END ;
 S BMCET=$H
 Q
R1 ;
 S BMCREF="" F  S BMCREF=$O(^BMCREF("B",BMCODAT,BMCREF)) Q:BMCREF'=+BMCREF  S BMCRREC=^BMCREF(BMCREF,0) D PROCR
 Q
PROCR ;
 Q:$P(BMCRREC,U,4)="N"
 Q:$P(BMCRREC,U,15)'="C1"  ;must be a completed-action done referral
 Q:$P($G(^BMCREF(BMCREF,1)),U)'=""  ;QUIT IF SR
 S BMCSVCD=$$AVEOS^BMCRLU(BMCREF,"I")
 Q:BMCSVCD=""  ;quit if no end date of service is available
 S BMCF=$$FACREF^BMCRLU(BMCREF)
 I '$D(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF)) D
 .F %=1:1:6 S $P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U,%)=0
 S $P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U)=$P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U)+1
 S BMCD=$P(BMCRREC,U,18)
 I BMCD="" S $P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U,2)=$P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U,2)+1 Q
 S X=$$FMDIFF^XLFDT(BMCD,BMCSVCD,1)
 S %=$S(X>181:6,X>94:5,X>32:4,1:3)
 S $P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U,%)=$P(^XTMP("BMCRR6",BMCJOB,BMCBTH,"REFERRALS",BMCF),U,%)+1
 Q
