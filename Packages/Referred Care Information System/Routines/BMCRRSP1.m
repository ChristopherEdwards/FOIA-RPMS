BMCRRSP1 ; IHS/PHXAO/TMJ - PROCESS REFERRAL LIST ;    
 ;;4.0;REFERRED CARE INFO SYSTEM;;JAN 09, 2006
 ;;IHS/ITSC/FCJ  ADDED TEST OF REFERRAL TYPES
 ;;4.0 IHS/ITSC/FCJ CHG TO PROC DATA FROM RCIS REF FILE
START ;
 S (BMCBT,BMCBTH)=$H,BMCJOB=$J,BMCRCNT=0
 D PROCESS,END
 Q
 ;
PROCESS ;
 S BMCODAT=$O(^BMCREF("B",BMCSD)) I BMCODAT="" S BMCET=$H Q
 S BMCODAT=BMCSD_".9999" F  S BMCODAT=$O(^BMCREF("B",BMCODAT)) Q:BMCODAT=""!((BMCODAT\1)>BMCED)  D R1
 Q
 ;
R1 ;
 S BMCRDT="" F  S BMCRDT=$O(^BMCREF("B",BMCODAT,BMCRDT)) Q:BMCRDT'=+BMCRDT  S BMCRREC=^BMCREF(BMCRDT,0) D PROC
 Q
END ;
 S BMCET=$H
 Q
PROC ;
 Q:'$D(^BMCREF(BMCRDT,0))
 Q:$P($G(^BMCREF(BMCRDT,1)),U)=""
 S BMCRREC=^BMCREF(BMCRDT,0),DFN=$P(BMCRREC,U,3),BMCDATE=$P(BMCRREC,U,1)
 S BMCREF=$P(^BMCREF(BMCRDT,1),U,2)
 S BMCREVP=$P(BMCRREC,U,25) ;user
 S BMCSPRV=$P(BMCRREC,U,7) ;Provider/Vendor IEN
 S:BMCSPRV'="" BMCSPRV=$P(^AUTTVNDR(BMCSPRV,0),U)
 S BMCAPPT=$P(^BMCREF(BMCRDT,11),U,5) ;Exp Appt Date
 S BMCPUR=$P($G(^BMCREF(BMCRDT,12)),U) ; Purpose
 S BMCIHSP=$P(BMCRREC,U,8) ;IHS Facility
 ;
 Q:$P(^BMCREF(BMCRDT,0),U,4)="N"
 I BMCCTYP'="A",$P(^BMCREF(BMCRDT,0),U,4)'=BMCCTYP Q
 Q:$P(^BMCREF(BMCRDT,0),U,5)'=BMCFAC
 I BMCCREV'=0,(BMCCREV'=BMCREVP) Q
 S ^XTMP("BMCRRSP",BMCJOB,BMCBTH,"DATA HITS",BMCDATE,$P(^DPT(DFN,0),U),BMCRDT)="",BMCRCNT=BMCRCNT+1
 Q
