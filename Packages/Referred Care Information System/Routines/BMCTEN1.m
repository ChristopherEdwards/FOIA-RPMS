BMCTEN1 ; IHS/PHXAO/TMJ - TOP TEN POVS ;  
 ;;4.0;REFERRED CARE INFO SYSTEM;**3**;JAN 09, 2006
 ;4.0*3 10.30.2007 IHS/OIT/FCJ ADDED CSV CHANGES
 ;
VISIT ;
 S BMCJOB=$J,BMCBT=$H
 K ^XTMP("BMCTEN",BMCJOB,BMCBT)
 D XTMP^BMCOSUT("BMCTEN","RCIS TOP TEN DX REPORT")
 S %="^XTMP(""BMCTEN"",BMCJOB,BMCBT,",BMCA=%_"""POV"",BMCPOV)",BMCB=%_"""APC"",BMCAPC)",BMCC=%_"1)",BMCE=%_"2)",BMCF=%_"3)",BMCG=%_"4)",BMCTOT=0,BMCVTOT=0,BMCLINO=0
 S BMCBD=BMCBD-.00001
 F BMCDATE=BMCBD:0 S BMCDATE=$O(^BMCREF("B",BMCDATE)) Q:'BMCDATE  Q:(BMCDATE\1)>BMCED  F BMCVIEN=0:0 S BMCVIEN=$O(^BMCREF("B",BMCDATE,BMCVIEN)) Q:'BMCVIEN  I $D(^BMCREF(BMCVIEN,0)),$D(^BMCDX("AD",BMCVIEN)) D CK
 D SET
 S BMCET=$H
 Q
 ;
CK ;
 S BMCRREC=^BMCREF(BMCVIEN,0),DFN=$P(BMCRREC,U,3)
 Q:$P($G(^BMCREF(BMCVIEN,1)),U)'=""  ;IHS/OIT/FCJ SEC REF NOT INCLUDED
 S BMCREF=BMCVIEN D SCREENS ;IHS/OIT/FCJ SCREENS USE BMCREF AS REF IEN
 Q:$D(BMCSKIP)
POV S BMCPOVN="",BMCVTOT=BMCVTOT+1,BMCCC=0
 F  S BMCPOVN=$O(^BMCDX("AD",BMCVIEN,BMCPOVN)) Q:'BMCPOVN  Q:'$D(^BMCDX(BMCPOVN,0))  S BMCPOV=+^(0),BMCCC=BMCCC+1,BMCPREC=^(0) D POVX
 Q
 ;
POVX I '$D(^ICD9($P(BMCPREC,U))) Q
 I $D(BMCPRIM),$P(BMCPREC,U,5)'="P" Q
 I $D(BMCPRIM),BMCCC>1 Q
 S BMCTOT=BMCTOT+1
 ;4.0*3 10.30.2007 IHS/OIT/FCJ ADDED CSV CHANGES NXT 2 LINES
 ;S %=$P(^ICD9(BMCPOV,0),U,5) K BMCAPC I % S BMCAPC=%
 S %=$P($$ICDDX^ICDCODE(BMCPOV,0),U,6) K BMCAPC I % S BMCAPC=%
 F X=BMCA,BMCB D UTL
 Q
 ;
SCREENS ;
 K BMCSKIP
 S BMCI=0 F  S BMCI=$O(^BMCRTMP(BMCRPT,11,BMCI)) Q:BMCI'=+BMCI!($D(BMCSKIP))  D
 .I '$P(^BMCTSORT(BMCI,0),U,8) D SINGLE Q
 .D MULT
 .Q
 Q
SINGLE ;
 K X,BMCSPEC S X="",BMCX=0
 X:$D(^BMCTSORT(BMCI,1)) ^(1)
 I X="" S BMCSKIP="" Q
 I '$D(BMCSPEC),'$D(^BMCRTMP(BMCRPT,11,BMCI,11,"B",X)) S BMCSKIP="" Q
 Q
MULT ;
 K BMCFOUN,BMCSKIP,BMCSPEC,X S BMCX=0,X=""
 X:$D(^BMCTSORT(BMCI,1)) ^(1)
 I $O(X(""))="" S BMCSKIP="" Q
 I '$D(BMCSPEC) S Y="" F  S Y=$O(X(Y)) Q:Y=""  I $D(^BMCRTMP(BMCRPT,11,BMCI,11,"B",Y)) S BMCFOUN="" Q
 I $D(BMCSPEC),$D(X) S BMCFOUN=1 Q
 S:'$D(BMCFOUN) BMCSKIP=""
 Q
UTL I X=BMCB,'$D(BMCAPC) Q
 I '$D(@X) S @X=0
 S %=@X,%=%+1,@X=%
 Q
 ;
SET F BMCPOV=0:0 S BMCPOV=$O(@BMCA) Q:'BMCPOV  S %=^(BMCPOV),@BMCC@(9999999-%,BMCPOV)=""
 F BMCAPC=0:0 S BMCAPC=$O(@BMCB) Q:'BMCAPC  S %=^(BMCAPC),@BMCE@(9999999-%,BMCAPC)=""
S1 S (X,I)=0 F  S X=$O(@BMCC@(X)) Q:'X  F Y=0:0 S Y=$O(@BMCC@(X,Y)) Q:'Y  S I=I+1,@BMCF@(I)=Y I I=BMCLNO G S2
S2 S (X,I)=0 F  S X=$O(@BMCE@(X)) Q:'X  F Y=0:0 S Y=$O(@BMCE@(X,Y)) Q:'Y  S I=I+1,@BMCG@(I)=Y I I=BMCLNO G S3
S3 Q
 ;
 ;
FF I IOST["P-" W:$D(IOF) @IOF Q
 I $E(IOST)="C",IO=IO(0),$Y>(IOSL-4) W ! S DIR(0)="EO" D ^DIR K DIR I Y=0!(Y="^")!($D(DTOUT)) S X="^"
 W:$D(IOF) @IOF
 Q
 ;
EXIT ;EP
 K A,B,C,D,E,F,G,H,I,J,K,X,Y,Z,%
 K BMCBD,BMCED,BMCDOB1,BMCDOB2,BMCSEX,A,B,C,X,Y,Z,%,BMCFAC,BMCJOB,BMCLNO,E,F,G,ZTIO,ZTQUEUED,BMCCLN,BMCTYPE,BMCSC,BMCC,BMCPREC,BMCSD,BMCET,BMCSEAT,BMCCHRT,BMCLHDR,BMCDASH,BMCA,BMCB,BMCC,BMCD,BMCE,BMCF,BMCG
 K BMCQUIT,BMCAPC,BMCDATE,BMCPOV,BMCVIEN,BMCNOCK,BMCTOT,BMCPROV,BMCVTOT,BMCLINO,L,I,BMCCMA,BMCPOVN,BMCV,BMCTYPP,BMCSCP,BMCPRIM,BMCALL
 K BMCANS,AMQQTAX,BMCBDD,BMCCNT,BMCCRIT,BMCCTYP,BMCCUT,BMCDISP,BMCEDD,BMCHIGH,BMCI,BMCNCAN,BMCPTVS,BMCRPT,BMCSEL,BMCSKIP,BMCTCW,BMCTEXT,BMCVAR,BMCVIEN,BMCRREC,DFN,BMCX,BMCY,BMCCC
 K BMCBT,BMCREF,BMCTYPR
 Q
