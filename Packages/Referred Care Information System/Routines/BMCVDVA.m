BMCVDVA ; IHS/OIT/FCJ - 2/2 EDIT VENDOR INFORMATION - CALLED FROM BMCVDV ;
 ;;4.0;REFERRED CARE INFO SYSTEM;**5,7**;JAN 09, 2006
 ;BMC*4.0*5 IHS.OIT.FCJ ORIGIAL ROUTINE FR ACHSVDVA
 ;
EDIT ;EP
 S Y=+$$DIR^XBDIR("NO^1:17","Change Which Item","","","Enter the number corresponding to the field you need to edit","",2)
 G END^BMCVDV:$D(DTOUT),A1^BMCVDV:$D(DUOUT)!(Y<1)
 S $P(^AUTTVNDR(BMCPROV,11),U,11)=DT
 D @(Y)
 G A2^BMCVDV
 ;
1 ;
 D EDITVNDR(BMCPROV,.01)
 G A1^BMCVDV
 ;
2 ;
 D EDITVNDR(BMCPROV,"1101;1102")
 D ^BMCVDVD
 Q
 ;
3 ;
 D EDITVNDR(BMCPROV,.05)
 Q
 ;
4 ;
 S BMCACO="L",P=BMCPROV,A("DISPLAY")=0
 D L^BMCVDV1
 S:'$D(DA) DA=""
 Q:'$D(DA)!(DA="")
 S DIE("NO^")="",DIE="^AUTTVNDR("_BMCPROV_",""CN"",",DA(1)=BMCPROV,DR="1;2;4;3"
 D ^DIE
 K DIE,DA
 Q
 ;
5 ;
 D EDITVNDR(BMCPROV,1701)
 Q
 ;
6 ;
 S BMCAGTP="RQ"
 D AGRDSP^BMCVDV2
 Q:'$G(DA)
 S DIE="^AUTTVNDR("_BMCPROV_",18,",DA(1)=BMCPROV,DR=".02;.04;.03;.05;.07;.06;.08;.09"
 D ^DIE
 K DIE
 Q
 ;
7 ;
 D EDITVNDR(BMCPROV,1126)
 Q
 ;
8 ;
 S BMCAGTP="PA"
 D AGRDSP^BMCVDV2
 Q:'$G(DA)
 S DIE="^AUTTVNDR("_BMCPROV_",18,",DA(1)=BMCPROV,DR=".02;.04;.03;.05;.07;.06;.08;.09"
 D ^DIE
 K DIE
 Q
 ;
9 ;
 I $P($G(^AUTTVNDR(BMCPROV,23)),U)'="" D MP^BMCVDV1,EDITVNDR(BMCPROV,"2301;2302")
 I $P($G(^AUTTVNDR(BMCPROV,23)),U)="" D EDITVNDR(BMCPROV,"2301;2302"),MP^BMCVDV1
 Q
 ;
10 ;
 ;
 S BMCAGTP="BPA"
 D AGRDSP^BMCVDV2
 Q:'$G(DA)
 S DIE="^AUTTVNDR("_BMCPROV_",18,",DA(1)=BMCPROV,DR=".02;.04;.03;.05;.07;.06;.08;.09"
 D ^DIE
 K DIE
 Q
 ;BMC*4.0*7 IHS.OIT.FCJ ADDED EMAIL, FAX AND RENUMBERED
11 ;
 D EDITVNDR(BMCPROV,"2103")
 Q
12 ;
 D EDITVNDR(BMCPROV,".07;.08")
 Q
13 ;
 D EDITVNDR(BMCPROV,"1301:1302;1303;1304;1305;1109;1114")  ;BMC*4.0*7 ADDED FIELD 1114 TO LINE
 Q
 ;
14 ;
 D EDITVNDR(BMCPROV,"1306;1307;1308;1309")
 Q
 ;
15 ;EP
 D EDITVNDR(BMCPROV,1103)
 Q
 ;
16 ;
 D EDITVNDR(BMCPROV,1110)
 Q
 ;
17 ;
 D EDITVNDR(BMCPROV,1103.01)
 Q
 ;
18 ;
 D EDITVNDR(BMCPROV,1125)
 Q
 ;
EDITVNDR(DA,DR) ;EP - Edit fields in VENDOR.
 N DIE
 S DIE="^AUTTVNDR("
 D ^DIE
 Q
 ;
