DG1010P ;ALB/JDS;REW - 1010 PRINT--INQUIRY PATIENT ; 13 SEP 84  14:47
 ;;5.3;Registration;**86,108,113,161**;Aug 13, 1993
 ;
FIND W !! S DIC="^DPT(",DIC(0)="AEQZM" D ^DIC G Q:+Y'>0 S (DA,DFN)=+Y
 S FREE=+$P(^DG(43,1,0),U,8),DGOPT=""
 I $O(^DPT(DFN,"DIS",0))'>0 W !!,"No registrations to print from.",!! G FIND
F11 R !!,"Registration date/time: ",X:DTIME G Q:'$T D UP^DGHELP G FIND:X?1"^".E!(X=""),NUM:$L(X)<7 S %DT="T" D ^%DT S DFN1=9999999-Y G W1:$D(^DPT(DFN,"DIS",DFN1)) W !!
HELP S J=0 F I=0:0 S I=$O(^DPT(DFN,"DIS",I)) Q:'I  S J=J+1,L=+(^(I,0))_"00000" W $J(J,2),".  ",$$FMTE^XLFDT($E(L,1,12),"5Z") W ?$X\40+1*40 W:$X>75 ! Q:J>20
 W !,"Enter the date and time, Entry #, or 'L' for the last registration,",!," to select the registration you wish to print a 10/10 for." G F11
NUM W:$E(X)="L" $E("LAST",$L(X)+1,99) W !! I $E(X)="L" S DFN1=$O(^DPT(DFN,"DIS",0)) G HELP:DFN1'>0,W1
 S DFN1=0 F I=1:1:+X S DFN1=$O(^DPT(DFN,"DIS",DFN1)) Q:'DFN
 G HELP:DFN1'>0 W !
W1 D NOREG^DG1010PA(DFN)
QU ;
 S DGHDFN=DFN
 I DGOPT]"" D  S DGPGM="DQ^DG1010P",DGVAR="DGOPT^PRF^FREE^DFN^DFN1^DUZ^DGPMDA^DGT^PSOINST^PSONOPG^PSOPAR^PSTYPE^GMTSTYP" D ZIS^DGUTQ G:POP EMB U IO D DQ G EMB
 .W:DGOPT'=5 !!?5,*7,"This output requires 132 column output to a PRINTER.",!?5,*7,"Output to SCREEN will be unreadable."
 G EMB
EN ;
 D:'$D(DT) DT^DICRW F I=0,.11,.121,.13,.24,.31,57,1010.15 S DGP(I)=$G(^DPT(DFN,I))
 S DGPAGE=0
 D NOW^%DTC,YX^%DTC S DGNOW=Y
 F I=0,1,2,3 S DGP("DIS",I)=$G(^DPT(DFN,"DIS",DFN1,I))
 S DGD=DGP("DIS",0),Y=+DGD X ^DD("DD") S DGAP=Y,DGCLK=$S($D(^VA(200,+$P(DGD,U,5),0)):$S($P(^(0),U,2)]"":$P(^(0),U,2)_"/"_$P(DGD,U,5),1:"unk/"_$P(DGD,U,5)),1:"unspecified"),DGNAM=$P(DGP(0),U,1)
 S DGSS=$P(DGP(0),U,9),DGSS=$E(DGSS,1,3)_"-"_$E(DGSS,4,5)_"-"_$E(DGSS,6,10)
 S DGLDOUBL="",$P(DGLDOUBL,"=",132)=""
 S (DGLDASH,DGLUND,DGL2)=""
 S $P(DGLDASH,"-",132)="",$P(DGL2,"-",131)="",$P(DGL2,"-",67)="|"
 S $P(DGLUND,"_",132)=""
 G ^DG1010S1:FREE=2
 I $$FIRST^DGUTL G Q
 W DGLDOUBL,!?35,"D E P A R T M E N T   O F   V E T E R A N S   A F F A I R S",!,DGLDOUBL,!,"FACILITY:  "
 S (DGSITE,X)=$$SITE^VASITE W $S((+X=-1):"FACILITY NOT IDENTIFIED",1:$P(X,U,2)_" ("_$P(X,U,3)_")")
 W ?96,"APPLICATION FOR MEDICAL BENEFITS",!,DGLDOUBL
 G ^DG1010P1
ADM K DGPMDA I $D(^DGPM("ATID1",DFN)) F I=0:0 S I=$O(^DGPM("ATID1",DFN,I)) Q:'I!(I>(DFN1+.9999))  S DGPMDA=$O(^(I,0))
 S DGPMDA=$S($D(DGPMDA):DGPMDA,1:0) Q
ASK W !,"PRINT ",DGX S %=1 D YN^DICN I '% W !,"ENTER 'Y'ES TO PRINT A ",DGX,".  OTHERWISE ENTER 'N'O."
 Q
DQ ;
 D NOW^%DTC,YX^%DTC S DGNOW=Y
 S X=132 X ^%ZOSF("RM") F I="DFN","DFN1","FREE","DGPMDA","DGT","PRF","GMTSTYP" S DGHOLD(I)=$S($D(@I):@I,1:"")
 I DGOPT[9&'($G(DGSTOP)) D EN^DGRPTP ; 1010T
 I DGOPT[1&'($G(DGSTOP)) S DA=DFN D EN^DG1010P ;1010
 I DGOPT[4&'($G(DGSTOP)) D EN^DG1010S1 ;SUP 1010
 I DGOPT[2&'($G(DGSTOP)) D RESTORE,^DG3PR0 ;1010I
 I DGOPT[3&'($G(DGSTOP)) D RESTORE,RET^DGBLRV ;3rd party review
 I DGOPT[6!(DGOPT[7)&'($G(DGSTOP)) S DGMTDT=$P(DGT,U,2),DGMTI=$P(DGT,U,1),DGMTYPT=$P(DGT,U,5) D RESTORE,START^DGMTP ;1010F
 I DGOPT[8&'($G(DGSTOP)) D RESTORE,ENXQ^GMTSDVR
 I DGOPT[5&'($G(DGSTOP)) S POP=0 D RESTORE,DFN^PSOSD1 K POP S X=132 X ^%ZOSF("RM") ;DRUG PROFILE NOTE: EXECUTES ^%ZIS("C")
 D ^%ZISC
 D CLOSE^DGUTQ,Q K DGHOLD,DGOPT,DGPMDA Q
Q K %,%DT,DA,DB,DFN,DFN1,DGHSFLG,DGOPT,DGL2,DGLDASH,DGLDOUBL,DGLSUP,DGLSUP1,DGLUND,DGPGM,DGPMDA,DGMTDT,DGMTI,DGMTYPT,DGNOW,DGT,DGVAR,DGX,DIC,DIRUT,DTOUT,DUOUT,FREE,I,J,L,POP,PRF,X,Y,DGSTOP
 K LMI,PSCNT,PSDIS,PSDT,PSII,PSOPRINT ;FROM DRUG PROFILE
 K GMTSTYP
 Q
RESTORE F I="DFN","DFN1","FREE","PRF","DGPMDA","DGT","GMTSTYP" S @I=DGHOLD(I)
 Q
EMB ;emboss card?
 S DFN=DGHDFN
 W !! D EMBOS^DGQEMA
 D EF
 K DGHDFN G FIND
 ;
EF ;encounter form?
 N EFX,WITHDATA,IBDFRION
 I $$PROMPRN^DG1010PA("EF") I DG1'<0 S EFX=1 D
 .S WITHDATA=1
 .D MAIN^IBDFREG(WITHDATA)
 Q
