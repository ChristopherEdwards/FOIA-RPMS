DG1010P7 ;ALB/REW - PRINT 1010 CONT'D PART VII ; 18 MAR 92
 ;;5.3;Registration;;Aug 13, 1993
 ;;1
PARTVII S DGBLANK=1
 W !!?48,"PART VII - INELIGIBLE/MISSING DATA",!,DGLUND
 K DGP,DGCHECK,DGDEPINC,DGCT,DGDEP,DGRDOB,DGDREL,DGRNAM,DGRSEX,DGRSSN,DGSP,DGSPOINC,DGTOTINC,DGTYPE,DGVETINC,DGNODE,DGREL,DGGTOT,DGINC,DGINR,DGISDT,DGPCE,DGTOT,DGDEF,F,INCTEXT
 F I="INE",.15,.3,.32,.361,.372 S DGP(I)=$G(^DPT(DFN,I))
 W !,"1.   INELIGIBLE DATE",?32,"| ","2.   TWX SOURCE",?66,"| ","3.   TWX CITY",?98,"| ","4.   TWX STATE"
 S X=$P(DGP("INE"),U,1),DGSOURCE=$S((X=""):"",(X=1):"VAMC",(X=2):"REGIONAL OFFICE",(X=3):"RPC",1:"INVALID")
 W !?5,$$DATENP^DG1010P0(DGP(.15),2,0,DGBLANK),?32,"| ",?39,$$UNK^DG1010P0(DGSOURCE,0,DGBLANK),?66,"| ",?72,$$DISP^DG1010P0(DGP("INE"),3,0,DGBLANK),?98,"| ",?105,$$POINT^DG1010P0(DGP("INE"),4,5,1,0,DGBLANK),?131,$C(13),DGLUND
 W !,"5.   REASON: ",$$DISP^DG1010P0(DGP(.3),7,0,DGBLANK),!,DGLUND
 W !,"6.   VACO DECISION: ",$$DISP^DG1010P0(DGP("INE"),6,0,DGBLANK),!,DGLUND
 S X=$P(DGP("INE"),U,7),DGSOURCE=$S((X=""):"",(X=1):"VAMC",(X=2):"REGIONAL OFFICE",(X=3):"RPC",1:"INVALID")
 W !,"7.   MISSING DATE",?32,"| ","8.   TWX SOURCE",?66,"| ","9.   TWX CITY",?98,"| ","10.  TWX STATE"
 W !?5,$$DATENP^DG1010P0(DGP(.15),3,0,DGBLANK),?32,"| ",?39,$$UNK^DG1010P0(DGSOURCE,0,DGBLANK),?66,"| ",?72,$$DISP^DG1010P0(DGP("INE"),8,0,DGBLANK),?98,"| ",?105,$$POINT^DG1010P0(DGP("INE"),9,5,1,0,DGBLANK),?131,$C(13),DGLUND
 W !,"11.  REASON: "
 F I=0:0 S I=$O(^DPT(DFN,.16,I)) W:(I'>0) ! Q:I'>0  W:(I'=1) ! W ?13,^(I,0)
 W ?131,$C(13),DGLUND
 D FOOTER^DG1010P2
 G:$G(DGSTOP) CLEANUP^DG1010P7
 D HEADER^DG1010P2
PAGE4 ;
 W !,$P(($T(REPLACE+1)),";;",2),!,DGLDOUBL
 S DGBLANK=0
 W !,"1.  ELIGIBILITY STATUS",?53,"| ","2.  STATUS DATE",?88,"| ","3.  STATUS ENTERED BY"
 S X=$P(DGP(.361),U,1),DGSOURCE=$S((X=""):"UNANSWERED",(X="P"):"PENDING VERIFICATION",(X="R"):"RE-VERIFY",(X="V"):"VERIFIED",1:"INVALID")
 W !?4,DGSOURCE,?53,"| ",?59,$$DATENP^DG1010P0(DGP(.361),2),?88,"| ",?94,$$POINT^DG1010P0(DGP(.361),6,"^VA(200,"),?131,$C(13),DGLUND
 W !,"4.  VERIFICATION METHOD",?53,"| ","5.  SERVICE VERIFICATION DATE"
 W !?4,$E($$DISP^DG1010P0(DGP(.361),5),1,48),?53,"| ",?59,$$DATENP^DG1010P0(DGP(.32),2)
 ;;?88,"| ","6.  RATED DISABILITIES"
 ;;?88,"| "
 ;;S I=0 F J=1:1 S I=$O(^DPT(DFN,.372,I)) W:(J=1)&(I'>0) ?94,"UNANSWERED" Q:I'>0  W:(I'>0) ! S X=$G(^(I,0)) W:(I'=1) !,?53,"| ",?88,"| " W ?94,$P($G(^DIC(31,+X,0)),U,1)
 W ?131,$C(13),DGLUND,!
 F I=1:1:9 W !,$P($T(WARNING+I),";;",2)
 W !,DGLUND
 W !,"SIGNATURE OF APPLICANT OR APPLICANT'S REPRESENTATIVE",?88,"| ","DATE"
 F I=1:1:4 W !,?88,"| "
 W !,$E(DGLUND,1,88),"|",$E(DGLUND,90,132)
 W !?8,"FOR VA USE ONLY",?32,"| ","VA FACILITY NUMBER",?65,"| ","ADMISSION DATE",?93,"| ","AUTHORITY FOR ADMISSION OR TREATMENT"
 F I=1:1:2 W !,?32,"| ",?65,"| ",?93,"| "
 W !,?32,"| ",?42,$P(DGSITE,U,3),?65,"| ",?93,"| "
 F I=1:1:2 W !,?32,"| ",?65,"| ",?93,"| "
 D FOOTER^DG1010P2
 G:$G(DGSTOP) CLEANUP^DG1010P7
 D:'FREE ^DG1010S1
CLEANUP ;
 K BL,DGUNK,DGP,DGD,P2,DGBLANK,DGAP,DGDMAX,DGDPC,DGI,DGL2,DGLDASH,DGLDOUBL,DGLUND,DGNA,DGNAM,DGNOTMAR,DGPAGE,DGSITE,DGSPACES,DGSS,N,NA,P,ROOT,WARNING,DGSOURCE,DGCLK
 Q
WARNING ;
 ;;*   WARNING:  If you knowingly make a false statement of any material fact in or in connection with this
 ;;    application, you are subject to prosecution in a U.S. Court.
 ;;
 ;;*   Completion of this form with signature of the veteran is certification that the veteran has received a copy
 ;;    of the Privacy Act Statement.
 ;;
 ;;*   I UNDERSTAND THE QUESTIONS, AND ALL THE ANSWERS ARE TRUE TO THE BEST OF MY KNOWLEDGE
 ;;    AND BELIEF.  I AGREE TO ACCEPT TRANSFER TO ANOTHER FACILITY IF, IN THE OPINION OF THE MEDICAL
 ;;    STAFF, SUCH TRANSFER IS DEEMED APPROPRIATE.
REPLACE ;
 ;;  THIS PAGE MAY BE REPLACED BY PAGE #4 OF VA FORM 10-10 WHICH CONTAINS PRIVACY ACT NOTICE & CONSENT TO RELEASE INFORMATION
