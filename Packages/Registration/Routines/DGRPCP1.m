DGRPCP1 ;ALB/MRL - CONSISTENCY PRINT, CONTINUED ; 14 SEP 88@2210
 ;;5.3;Registration;**108,161**;Aug 13, 1993
ST N DGSTOP,ZTSTOP
 G I:$E(DGHOW)="I",A:($E(DGHOW)="A")
 F I=DGFRD:0 S I=$O(^DPT("ADIS",I)) Q:'I!(I>DGTOD)  F DFN=0:0 S DFN=$O(^DPT("ADIS",I,DFN)) Q:('DFN)!($G(ZTSTOP))  I $D(^DGIN(38.5,DFN,0)) S DGDATA=^(0) I $D(^DPT(DFN,0)) D SET
 G PR:$D(^UTILITY($J,"DGINC")),Q
A F I=DGFRD:0 S I=$O(^DGPM("ATT1",I)) Q:'I!(I>DGTOD)  F I1=0:0 S I1=$O(^DGPM("ATT1",I,I1)) Q:('I1)!($G(ZTSTOP))  I $D(^DGPM(+I1,0)) S DFN=$P(^(0),"^",3) I $D(^DGIN(38.5,DFN,0)) S DGDATA=^(0) I $D(^DPT(DFN,0)) D SET
 G PR:$D(^UTILITY($J,"DGINC")),Q
I S DGTOD1=9999999-DGTOD,DGFRD1=9999999-DGFRD,I=DGTOD1
 F I1=0:0 S I=$O(^DGIN(38.5,"AC",I)) Q:'I!(I>DGFRD1)  F DFN=0:0 S DFN=$O(^DGIN(38.5,"AC",I,DFN)) Q:('DFN)!($G(ZTSTOP))  I $D(^DGIN(38.5,DFN,0)) S DGDATA=^(0) I $D(^DPT(DFN,0)) D SET
 G Q:'$D(^UTILITY($J,"DGINC"))
PR I $$FIRST^DGUTL G Q
 S DGPG=0,DGHDR="INCONSISTENT ELEMENTS FOR PATIENTS WITH A",Y=DGFRD X ^DD("DD") S DGFRD1="'"_Y_"'" I $P(DGTOD,".",1)'=DGFRD S Y=$P(DGTOD,".",1) X ^DD("DD") S DGFRD1=" BETWEEN "_DGFRD1_" AND '"_Y_"'"
 E  S DGFRD1=" OF "_DGFRD1
 S DGHDR=DGHDR_$S($E(DGHOW)="R":"",1:"N")_" "_$P(DGHOW,"^",2)_DGFRD1 D HDR S I=0
 F I1=0:0 S I=$O(^UTILITY($J,"DGINC",I)) Q:I=""  F I2=0:0 S I2=$O(^UTILITY($J,"DGINC",I,I2)) Q:'I2  S X=^(I2) D W
 D TRA G Q
W S DGCLK=$E($S($D(^VA(200,+$P(X,U,5),0)):$S($P(^(0),U,2)]"":$P(^(0),U,2),1:$P(X,U,5)),1:"Missing"),1,9)
 W !,$P(X,"^",1),?33,$P(X,"^",2),?56,$P(X,"^",3),?67,$TR($$FMTE^XLFDT($P(X,"^",4),"5DZ"),"/","-"),?78,DGCLK,?89,$P(X,"^",6) I $Y>40 D TRA D:$$SUBSEQ^DGUTL HDR
 Q
HDR S DGPG=DGPG+1,X=$S($D(IOM):(IOM-13),1:119) W !,DGHDR,", PAGE ",DGPG,?X,Y,!?67,"Last Day",?78,"Last",!,"Patient Name",?33,"Home Phone #",?56,"Soc Sec #",?67,"ID'ed",?78,"Edited by",?89,"Inconsistent/Missing Data Elements"
 S X1="",$P(X1,"=",131)="" W !,X1,! Q
TRA S DGCT=0,X1="",$P(X1,"*",131)="" X "F DGZ=$Y:1:$S($D(IOSL):(IOSL-25),1:41) W !" W !,X1,!,"TABLE OF INCONSISTENT/MISSING DATA ELEMENTS",$C(13),"___________________________________________",!
 F DGZ=0:0 S DGZ=$O(^DGIN(38.6,DGZ)) Q:'DGZ  I $D(^DGIN(38.6,DGZ,0)),(DGZ'=21) S DGCT=DGCT+1,DGOFF=DGCT*45-45 W ?DGOFF,DGZ,"  ",$P(^(0),"^",1) I DGCT=3 S DGCT=0 W !
 Q
SET S DGDFN=^DPT(DFN,0),DGSSN=$P(DGDFN,"^",9),DGSTORE=$S(DGHOW1="N":$S($P(DGDFN,"^",1)]"":$P(DGDFN,"^",1),1:"UNIDENTIFIED PATIENT #"_DFN),1:" "_$E(DGSSN,8,9)_$E(DGSSN,6,7)_$E(DGSSN,4,5)_$E(DGSSN,1,3)),DGINC=""
 F J=0:0 S J=$O(^DGIN(38.5,DFN,"I",J)) Q:'J  I $D(^DGIN(38.6,J)) S DGINC=DGINC_J_","
 S DGCLK1=$S($P(DGDATA,U,5):$P(DGDATA,U,5),1:$P(DGDATA,U,3))
 S DGPHONE=$P($G(^DPT(DFN,.13)),U,1)
 S DGINC=$E(DGINC,1,$L(DGINC)-1),^UTILITY($J,"DGINC",DGSTORE,DFN)=$S($P(DGDFN,"^",1)]"":$P(DGDFN,"^",1),1:"UNIDENTIFIED PATIENT #"_DFN)_"^"_DGPHONE_"^"_$P(DGDFN,"^",9)_"^"_$P(DGDATA,"^",4)_"^"_DGCLK1_U_DGINC K J,DGINC,DGSSN,DGDFN Q
Q K %,%DT,DGCLK,DGCLK1,DGFRD,DGHOW,DGHOW1,DGTOD,DGVAR,I,J,X,Y,Z,DGCT,DGCONRUN,DGDATA,DGDFN,DGEDCN,DGER,DGFRD1,DGHDR,DGINC,DGOFF,DGPG,DGPGM,DGSSN,DGSTORE,DGTOD1,DGZ,I1,I2,X1,^UTILITY($J,"DGINC"),DGZTOP,ZTSTOP,DGPHONE
 D ENDREP^DGUTL,CLOSE^DGUTQ Q
