DPTDZPO ; IHS/TUCSON/JCM - IHS PATIENT POST-MERGE ROUTINE ; [ 02/03/94  7:29 AM ]
 ;;1.0;PATIENT MERGE;;FEB 02, 1994
 ;
 ; Calls: DIC,DIE,DIK
 ;
START ;
 D INIT
 D AGPATCH
 D RESET
 D:'$D(XDRM("AUTO")) ^DPTDZPO1
END D EOJ
 Q
 ;
INIT ;
 K DPTDZPO
 S DPTDZPO("FR NAME")=$P(^DPT(XDRMRG("FR"),0),U)
 S DPTDZPO("ORIG TO NAME")=$P(^TMP("XDRMRGTO",$J,XDRMRG("TO"),0),U)
 I DPTDZPO("ORIG TO NAME")'=$P(^DPT(XDRMRG("TO"),0),U) D OTHER
 F DPTDZPO("CH")=0:0 S DPTDZPO("CH")=$O(^AUPNPAT(XDRMRG("FR"),41,DPTDZPO("CH"))) Q:'DPTDZPO("CH")  D
 . I $P(^AUPNPAT(XDRMRG("FR"),41,DPTDZPO("CH"),0),U,2)]"" S DPTDZPO("CN",DPTDZPO("CH"))=$P(^(0),U,2)
 . I '$D(^AUPNPAT(XDRMRG("TO"),41,DPTDZPO("CH"),0)) K DPTDZPO("CN",DPTDZPO("CH")) Q
 . I $D(^AUPNPAT(XDRMRG("TO"),41,DPTDZPO("CH"),0)),$P(^(0),U,2)]"",$P(^(0),U,2)=DPTDZPO("CN",DPTDZPO("CH")) K DPTDZPO("CN",DPTDZPO("CH"))
 . Q
 Q
 ;
OTHER ;
 S DIC="^DPT("_XDRMRG("TO")_",.01,",DIC(0)="FL"
 S DIC("P")=$P(^DD(2,1,0),U,2),DA(1)=XDRMRG("TO"),X=DPTDZPO("ORIG TO NAME")
 D ^DIC K DA,DIC,DIE
 Q
 ;
AGPATCH ; Sets up AGPATCH global for transmission
 I $D(^APMF(96.01,1,0)),$P(^(0),U,4) G AGPATCHX
 S DPTDZPO("TDUZ2")=$S($O(^AUPNPAT(XDRMRG("TO"),41,DUZ(2),"")):DUZ(2),1:$O(^AUPNPAT(XDRMRG("TO"),41,0))) G:'DPTDZPO("TDUZ2") AGPATCHX
 S DPTDZPO("FDUZ2")=$S($O(^AUPNPAT(XDRMRG("FR"),41,DUZ(2),"")):DUZ(2),1:$O(^AUPNPAT(XDRMRG("FR"),41,0))) G:'DPTDZPO("FDUZ2") AGPATCHT
 S DPTDZPO("P6")=DPTDZPO("TDUZ2")
 S DPTDZPO("P3")=$S(DPTDZPO("P6")]"":$P(^AUPNPAT(XDRMRG("TO"),41,DPTDZPO("P6"),0),U,2),1:"")
 S DPTDZPO("P4")=$P(^DPT(XDRMRG("TO"),0),U,1),DPTDZPO("P4")=$E(DPTDZPO("P4"),1)_$E($P(DPTDZPO("P4"),",",2),1)
 S DPTDZPO("P5")=$P(^DPT(XDRMRG("TO"),0),U,2)
 S DPTDZPO("P2")=$P(^AUPNPAT(XDRMRG("FR"),41,DPTDZPO("FDUZ2"),0),U,2)
 I DPTDZPO("TDUZ2")=DPTDZPO("FDUZ2"),DPTDZPO("P3")=DPTDZPO("P2") G AGPATCHT
 I DPTDZPO("P2")'?1.6N G AGPATCHT
 I DPTDZPO("P3")'?1.6N G AGPATCHT
 S ^AGPATCH(DT,DPTDZPO("FDUZ2"),XDRMRG("FR"))=DPTDZPO("FDUZ2")_"^"_$P(^AUPNPAT(XDRMRG("FR"),41,DPTDZPO("FDUZ2"),0),U,2)_"^"_DPTDZPO("P3")_U_DPTDZPO("P4")_U_DPTDZPO("P5")_U_DPTDZPO("P6")
AGPATCHT S ^AGPATCH(DT,DPTDZPO("TDUZ2"),XDRMRG("TO"))=""
AGPATCHX Q
 ;
RESET ;
 S DIK="^AUPNPAT(",DA=XDRMRG("FR") D ^DIK K DA,DIK
 S ^AUPNPAT(XDRMRG("FR"),0)=XDRMRG("FR")
 S ^AUPNPAT("B",XDRMRG("FR"),XDRMRG("FR"))=""
 L +^AUPNPAT(0):0 S $P(^(0),U,4)=($P(^AUPNPAT(0),U,4)+1) L -^AUPNPAT(0):0
 F DPTDZPO("CH")=0:0 S DPTDZPO("CH")=$O(DPTDZPO("CN",DPTDZPO("CH"))) Q:'DPTDZPO("CH")  D
 . S DIC="^AUPNPAT("_XDRMRG("FR")_",41,",DIC(0)="FL",DIC("P")=$P(^DD(9000001,4101,0),U,2),DIC("DR")=".02////"_DPTDZPO("CN",DPTDZPO("CH"))_";.05////M",DA(1)=XDRMRG("FR"),X="`"_DPTDZPO("CH")
 . D ^DIC K DA,DIC,DIE,DR
 . Q
 S DIK="^DPT(",DA=XDRMRG("FR") D ^DIK K DA,DIK
 S ^DPT(XDRMRG("FR"),0)="*"_$E(DPTDZPO("FR NAME"),1,28)_"*",$P(^(0),U,19)=XDRMRG("TO")
 S ^DPT("B","*"_$E(DPTDZPO("FR NAME"),1,28)_"*",XDRMRG("FR"))=""
 L +^DPT(0):0 S $P(^(0),U,4)=($P(^DPT(0),U,4)+1) L -^DPT(0):0
 S DIE="^AUPNPAT(",DA=XDRMRG("TO"),DR=".03////"_DT_";.12////"_DUZ_";.16////"_DT
 D ^DIE K DIE,DIC,DR,DA
 Q
 ;
EOJ ;
 K DPTDZPO
 Q
