VALMINIY ;ISC-SF.SEA/JLI - SEND INSTALL MESSAGE, SETUP IF ALPHA/BETA TEST SITE ;8/6/91  09:30 ;
VER ;;1;List Manager;;Aug 13, 1993
 N DIFROM
 S XQABT5=$H X ^%ZOSF("UCI") I Y'=^%ZOSF("PROD") G KILL
 S X=$T(ADRESS) Q:X=""  S XQADRS=$P(X,";",3) G:XQADRS="" KILL G:XQADRS'["@" KILL
 S X=$T(PKGNAM),(X,XQPKG)=$P(X,";",3) G:X="" KILL
 S DIC(0)="MX",DIC="^XMB(1,1,""ABPKG"",",DA(1)=1,D0=1 D ^DIC S XQDA=+Y K DIC
 S Y=$T(ABTEST) I $P(Y,";",3)'["YES" D:XQDA>0 RMVTEST G SENDMESG
 D:XQDA>0 CLROPTS I XQDA'>0 S DA(1)=1,D0=1,DIC(0)="L",DIC="^XMB(1,1,""ABPKG"",",DIC("P")=4.332 D ^DIC S XQDA=+Y
 D:XQDA>0 SETOPTS
 ;
SENDMESG ;
 S X=$T(SENMSG) I $P(X,";",3)'="YES" G KILL
 K ^UTILITY($J,"XQAB") D NOW^%DTC
 S X=$P($T(VER),";",3),^UTILITY($J,"XQAB",1)="INSTALLED V. "_X_"   "_$E(%,4,5)_"/"_$E(%,6,7)_"/"_$E(%,2,3)_"  "_$E(%_"000",9,10)_":"_$E(%_"00000",11,12)
 S (X1,X2,X3)=0 I $D(XQABT1),$D(XQABT2) S X1=$P(XQABT2,",",2)-$P(XQABT1,",",2)
 I $D(XQABT4),$D(XQABT3) S X2=$P(XQABT4,",",2)-$P(XQABT3,",",2)
 I $D(XQABT4),$D(XQABT5) S X3=$P(XQABT5,",",2)-$P(XQABT4,",",2)
 S:X1'=0 ^UTILITY($J,"XQAB",2)="  PRE-INIT TIME = "_X1_" SECONDS" S:X2'=0 ^(3)="  INIT TIME = "_X2_" SECONDS" S:X3'=0 ^(4)="  POST-INIT TIME = "_X3_" SECONDS"
 S XMY(XQADRS)="",XMY(DUZ)="",XMDUZ=.5,XMSUB=XQPKG_" "_X_" INSTALLATION",XMTEXT="^UTILITY($J,""XQAB""," D ^XMD K ^UTILITY($J,"XQAB"),XMY,XMDUZ,XMSUB,XMTEXT
 ;
KILL K %,DA,DIC,DIE,DIK,DR,X,X1,X2,X3,XQ0,XQ1,XQ2,XQ3,XQ4,XQA,XQADRS,XQB,XQBLNK,XQC,XQD1,XQDA,XQI,XQID,XQJ,XQN,XQPKG,XQZ,XQABT1,XQABT2,XQABT3,XQABT4,XQABT5
 Q
 Q
 ;
SETOPTS ;
 S $P(^XMB(1,1,"ABPKG",XQDA,0),U,2,3)=DT_U_XQADRS
 F XQI=1:1 S XQA="OPT"_XQI,XQB=$T(@XQA) Q:XQB=""  S X=$P(XQB,";",3),DIC="^XMB(1,1,""ABPKG"",XQDA,1,",DA(1)=XQDA,DA(2)=1,DIC(0)="ML",DIC("P")=4.3321 D ^DIC K DIC I Y>0 K XQID(X) D SET1
 S XQID="" F XQI=0:0 S XQID=$O(XQID(XQID)) Q:XQID=""  S DIC="^XMB(1,1,""ABPKG"",XQDA,1,",DIC(0)="M",X=XQID,DA(1)=XQDA,DA(2)=1 K XQID(XQID) D ^DIC I Y>0 S DA=+Y,DIE=DIC,DR=".01///@" D ^DIE
 Q
SET1 ;
 S XQD1=+Y F XQJ=4:1 S X=$P(XQB,";",XQJ) Q:X=""  S DIC="^XMB(1,1,""ABPKG"",XQDA,1,XQD1,1,",DIC(0)="ML",DIC("P")=4.33211,DA(3)=1,DA(2)=XQDA,DA(1)=XQD1 D ^DIC K DIC
 Q
 ;
RMVTEST D CLROPTS S DIK="^XMB(1,1,""ABPKG"",",DA(1)=1,DA=XQDA D ^DIK I $O(^XMB(1,1,"ABPKG",0))'>0 K ^XMB(1,1,"ABOPT")
 Q
 ;
CLROPTS S $P(XQBLNK," ",30)=" "
 K ^UTILITY($J,"XQAB"),^("XQOPT")
 S X1=$P(^XMB(1,1,"ABPKG",XQDA,0),U,2),X2=$P(^(0),U,3),^UTILITY($J,"XQAB",1)="    OPTION USAGE SINCE "_$E(X1,4,5)_"-"_$E(X1,6,7)_"-"_$E(X1,2,3),^(2)=" ",XQC=2 D GETDATA I $D(^UTILITY($J,"XQOPT")) D OUTPUT
 Q
GETDATA ;
 F XQ1=0:0 S XQ1=$O(^XMB(1,1,"ABPKG",XQDA,1,XQ1)) Q:XQ1'>0  S XQID=$P(^(XQ1,0),U),XQID(XQID)="" D CHECK
 Q
 ;
CHECK S XQA=$E(XQID,1,$L(XQID)-1)_$C($A($E(XQID,$L(XQID)))-1)_"z"
 F XQ2=0:0 S XQA=$O(^DIC(19,"B",XQA)) Q:XQA=""!($E(XQA,1,$L(XQID))'=XQID)  I $E(XQA,$L(XQID)+1)'="Z" D CHK2
 Q
 ;
CHK2 F XQ3=0:0 S XQ3=$O(^XMB(1,1,"ABPKG",XQDA,1,XQ1,1,XQ3)) Q:XQ3'>0  S XQ4=$P(^(XQ3,0),U) Q:$E(XQA,1,$L(XQ4))=XQ4
 I XQ3'>0 F XQ4=0:0 S XQ4=$O(^DIC(19,"B",XQA,XQ4)) Q:XQ4'>0  S XQN=$P(^DIC(19,XQ4,0),U,1,4),X=$S($D(^XMB(1,1,"ABOPT",XQ4,0)):+$P(^(0),U,2),1:0) K ^XMB(1,1,"ABOPT",XQ4,0) S ^UTILITY($J,"XQOPT",XQID,(X+1),XQA,XQ4)=XQ4_U_X_U_XQN
 Q
 ;
OUTPUT S XQID=""
 F XQ0=0:0 S XQID=$O(^UTILITY($J,"XQOPT",XQID)) Q:XQID=""  D SPACE F XQ1=0:0 S XQ1=$O(^UTILITY($J,"XQOPT",XQID,XQ1)) Q:XQ1'>0  S XQA="" F XQ2=0:0 S XQA=$O(^UTILITY($J,"XQOPT",XQID,XQ1,XQA)) Q:XQA=""  D OUTA
 S XMY(XQADRS)="",XMY(DUZ)="",XMDUZ=.5,XMSUB=XQPKG_" ALPHA/BETA TEST OPTION USAGE",XMTEXT="^UTILITY($J,""XQAB""," D ^XMD K ^UTILITY($J,"XQAB"),^("XQOPT"),XMY,XMDUZ,XMSUB,XMTEXT
 Q
 Q
 ;
SPACE S XQC=XQC+1,^UTILITY($J,"XQAB",XQC)=" "
 Q
OUTA ;
 F XQ3=0:0 S XQ3=$O(^UTILITY($J,"XQOPT",XQID,XQ1,XQA,XQ3)) Q:XQ3'>0  S X=^(XQ3),XQZ=$P(X,U,2),Y=$P(X,U,3,99) D MAIL
 Q
MAIL ;
 S XQC=XQC+1,^UTILITY($J,"XQAB",XQC)=$E($E($P(Y,U),1,24)_XQBLNK,1,26)_$P(Y,U,4)_"    "_$J(+XQZ,6)_"   "_$E($P(Y,U,2),1,38)
 Q
 ;
PKGNAM ;;LIST MANAGER;
ABTEST ;;NO;
ADRESS ;;G.MAS PROGRAMMERS@DOMAIN.NAME;
SENMSG ;;NO;
