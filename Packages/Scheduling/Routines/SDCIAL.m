SDCIAL ;ALB/TMP - INPATIENT APPOINTMENT LIST ; [ 09/13/2001  2:19 PM ]
 ;;5.3;Scheduling;**32**;Aug 13, 1993
 ;IHS/ANMC/LJF 11/30/2000 contains $N-use by IHS unknown-to be in ADT
 ;
 S DIV="",SDTT=0 D DIV^SDUTL I $T S DIC("A")="INPATIENT APPOINTMENT LIST FOR WHICH DIVISION:" D ASK^SDDIV Q:Y<0
RD R !,"FOR WARD (TYPE 'ALL' FOR ALL WARDS): ",X:DTIME Q:"^"[X  I X?.E1"?" W !,"ENTER A WARD NAME OR ALL FOR ALL WARDS"
 S X=$$UP^XLFSTR(X)
 I X="ALL" S SDW=X G RD1
 S DIC="^DIC(42,",DIC(0)="EQ"
 D ^DIC Q:X=""!(X["^")  G:Y<0 RD S SDW=+Y
RD1 D DATE^SDUTL G:POP END I BEGDATE<DT W *7,!,"Start date must be in the future" G RD1
 S ENDDATE=ENDDATE_".9999",VAR="DIV^SDW^BEGDATE^ENDDATE",VAL=DIV_"^"_SDW_"^"_BEGDATE_"^"_ENDDATE,PGM="START^SDCIAL"
 D ZIS^DGUTQ G:POP END
START K ^UTILITY($J) U IO I '$D(DT) D DT^SDUTL
 S (SDEND,SD1)=0,SDTT=$S($E(IOST,1,2)="C-"&(IOSL<66):1,1:0)
 I SDW'="ALL" S I=$P(^DIC(42,SDW,0),"^",1) D PT G WRT
 S I=0 F I1=0:0 S I=$N(^DPT("ACN",I)) Q:I<0  D PT
 G WRT
PT F I2=0:0 S I2=$N(^DPT("ACN",I,I2)) Q:I2'>0  I $D(^DPT(I2,0)) S DFN=I2 D DFN
 Q
DFN Q:'$D(^DPT(DFN,"S"))!($N(^DPT(DFN,"S",BEGDATE))'>0)!($N(^DPT(DFN,"S",BEGDATE))>ENDDATE)
 F I3=BEGDATE-.1:0 S I3=$N(^DPT(DFN,"S",I3)) Q:I3'>0!(I3>ENDDATE)  S SDSC=+^(I3,0) I $P(^(0),"^",2)'["N",$P(^(0),"^",2)'["C",$D(^SC(SDSC,0)),$S(DIV="":1,$P(^SC(SDSC,0),"^",15)=DIV:1,1:0) S ^UTILITY($J,I,I3\1,DFN,"."_$P(I3,".",2))=SDSC
 Q
WRT S I="" I $O(^UTILITY($J,I))']"" W @IOF,?29,"INPATIENT APPOINTMENT LIST",! X "F A=1:1:IOM W ""-""" W !!,"NO MATCHES FOUND!" G END
 S (SDPG,I)=0 F I1=0:0 S I=$N(^UTILITY($J,I)) Q:I<0!(SDEND)  D HD Q:SDEND  F I2=0:0 S I2=$N(^UTILITY($J,I,I2)) Q:I2'>0  D:($Y+4)>IOSL HD Q:SDEND  D APPT Q:SDEND
 G END
APPT W:SD2 !! D:($Y+6)>IOSL HD Q:SDEND  S Y=I2 D DTS^SDUTL W !,Y S SD2=1
 F I3=0:0 S I3=$N(^UTILITY($J,I,I2,I3)) Q:I3'>0!(SDEND)  D:($Y+5)>IOSL HD Q:SDEND  W !,?2,$P(^DPT(I3,0),"^",1),?34,$P(^(0),"^",9) F I4=0:0 S I4=$N(^UTILITY($J,I,I2,I3,I4)) Q:I4'>0  D WRTC Q:SDEND
 Q
WRTC Q:'$D(^DPT(I3,"S",I2_I4,0))  S SDY=^(0) I ($Y+4)>IOSL D HD Q:SDEND  W !,?2,$P(^DPT(I3,0),"^",1),?34,$P(^(0),"^",9)," (CONTINUED)"
 W !,?5,$P(^SC(+SDY,0),"^",1) S Y=I4,SD2=1 D AT^SDUTL W ?37,$J(Y,8) S SDB=50 F A=3:1:5 S Y="."_$P($P(SDY,"^",A),".",2) D AT^SDUTL W ?SDB,$J(Y,8) S SDB=SDB+10
 I $D(^SC(+SDY,"S",I2_I4)) S SD=I2_I4 F A=0:0 S A=$N(^SC(+SDY,"S",SD,1,A)) Q:A'>0  I +^(A,0)=I3 W:$P(^(0),"^",4)]"" !,?15,$P(^(0),"^",4) Q
 Q
HD I SD1,SDTT D OUT^SDUTL Q:SDEND
 S SDPG=SDPG+1,SD1=1 W !,@IOF,!,?29,"INPATIENT APPOINTMENT LIST",?69,"PAGE: ",SDPG,! S SDOS=(77-$L(I))\2 W ?SDOS,"WARD: ",I S Y=DT D DTS^SDUTL W !,?31,"DATE PRINTED: ",Y,!!
 W !!,"APPOINTMENT DATE",!,?2,"PATIENT NAME",?34,"SSN",!,?38,"APPOINT",?52,"LAB",?62,"XRAY",?72,"EKG",!,?5,"CLINIC",?38,"TIME" F A=52:10:72 W ?A,"TIME"
 W !,?15,"OTHER INFORMATION",! F A=1:1:80 W "-"
 S SD2=0 Q
END S:'$D(IOF) IOF="#" W ! W:'SDTT @IOF K ALL,DIV,POP,SDT1,%DT,A,BEGDATE,DFN,DIC,DIV,ENDDATE,I,I1,I2,I3,I4,II,SD1,SDB,SDEND,SDOS,SDPG,SDSC,SDTT,SDW,SDXX,SDY,X,Y D CLOSE^DGUTQ,SDIAL^SDKILL Q
