SDPHARM1 ;BIRMINGHAM OIFO/RON - Determine default Institution/Station no. ; 8/9/03
 ;;5.3;Scheduling;**300**;AUG 13, 1993
 ;
DEF(SDPSODFN) ;Pass in Patient
 I '$G(SDPSODFN)!('$D(^DPT(SDPSODFN,0))) Q 0
 N DA,DR,DIC,DIE,X,Y,SDPSOPRM,SDPSODFA,SDPSODF1,SDPSODF2,SDPSODF3,SDPSOPDF
 D INIT
 D PAT
 I '$D(SDPSODFA) Q 0
 S (SDPSOPRM,SDPSOPDF)=0
 S SDPSODF1="" F  S SDPSODF1=$O(SDPSODFA(SDPSODF1)) Q:SDPSODF1=""!(SDPSOPRM)  S SDPSODF2="" F  S SDPSODF2=$O(SDPSODFA(SDPSODF1,SDPSODF2)) Q:SDPSODF2=""!(SDPSOPRM)  D
 .S SDPSODF3="" F  S SDPSODF3=$O(SDPSODFA(SDPSODF1,SDPSODF2,SDPSODF3)) Q:SDPSODF3=""!(SDPSOPRM)  D
 ..I SDPSODFA(SDPSODF1,SDPSODF2,SDPSODF3) S SDPSOPDF=SDPSODF2_"^"_SDPSODF3,SDPSOPRM=1 Q
 ..S SDPSOPDF=SDPSODF2_"^"_SDPSODF3
 Q $S(SDPSOPDF:SDPSOPDF,1:0)
INIT ;Initialize variables
 ;Create primary care DSS credit pair array
 N SDPSODI,SDPSODII,SDPSODSS
 F SDPSODI=322,323,350 F SDPSODII="000",185,186,187 S SDPSODSS(SDPSODI_SDPSODII)=""
 Q
 ;
PAT ;
 N SDPSOSTA,SDPSOATZ,SDPSODIV,SDPSODCP,SDPSOCL0,SDPSOAP0,SDPSOSDT,SDPSOOUT,X,Y,X1,X2
 S SDPSOOUT=0
 I '$G(DT) S DT=$$DT^XLFDT
 S X1=DT,X2=-1 D C^%DTC S SDPSOSDT=X_".999"
 F  S SDPSOSDT=$O(^DPT(SDPSODFN,"S",SDPSOSDT)) Q:'SDPSOSDT  D
 .S SDPSOAP0=$G(^DPT(SDPSODFN,"S",SDPSOSDT,0)) Q:'+SDPSOAP0  ;Get appt 0 node
 .Q:$P(SDPSOAP0,U,2)["C"  ;Skip cancelled appointments
 .S SDPSOCL0=$G(^SC(+SDPSOAP0,0)) Q:'$L(SDPSOCL0)  ;Get clinic 0 node
 .S SDPSODCP=$$CPAIR(SDPSOCL0)  ;Get DSS credit pair
 .S SDPSODIV=$$DIV(SDPSOCL0)  ;Get clinic division
 .K SDPSOSTA I $G(SDPSODIV) K SDPSOATZ,DIC,DIQ,DD,DR S DIC=4,DR="99",DA=+SDPSODIV,DIQ(0)="I",DIQ="SDPSOATZ" D EN^DIQ1 S SDPSOSTA=$G(SDPSOATZ(4,+SDPSODIV,99,"I")) K DIC,DIQ,DR,DA,SDPSOATZ
 .I SDPSODIV>0,$G(SDPSOSTA)'="" D
 ..S SDPSODFA(SDPSOSDT,SDPSODIV,SDPSOSTA)=$S($D(SDPSODSS(SDPSODCP)):1,1:0)
 Q
 ;
CPAIR(SDPSOCL0) ;Get credit pair
 N SDPSOSDX
 S SDPSOSDX=$P($G(^DIC(40.7,+$P(SDPSOCL0,U,7),0)),U,2)
 S SDPSOSDX=SDPSOSDX_$P($G(^DIC(40.7,+$P(SDPSOCL0,U,18),0)),U,2)
 S SDPSOSDX=$E(SDPSOSDX_"000000",1,6)
 Q SDPSOSDX
 ;
DIV(SDPSOCL0) ;Get facility division name and number
 N SDPSODVX,SDPSOHLD S SDPSODVX=$P(SDPSOCL0,U,15)
 S SDPSOHLD=0
 I SDPSODVX>0 S SDPSOHLD=$P($$SITE^VASITE(,SDPSODVX),U)
 I SDPSOHLD>0 Q SDPSOHLD
 S SDPSOHLD=$P(SDPSOCL0,"^",4)
 I 'SDPSOHLD Q 0
 I SDPSOHLD,'$D(^DIC(4,SDPSOHLD,0)) S SDPSOHLD=0
 Q SDPDOHLD
