ASMTLOG1 ;(#2) PRINT CARTRIDGE MAILING LIST [ 11/22/85  11:48 AM ]
 ;IHS-OKLA CITY AREA OFFICE-LMD
START ;PRINT CARTRIDGE MAILING LIST
 S ASM(4)=0,ASM("COPIES")=2
B1 S (ASM,PG,ASM("TOT"))=0,ASM(4)=ASM(4)+1 D HEAD,HEADING
B2 S ASM=$O(^UTILITY($J,ASM)) G END:ASM="" S ASM("TOT")=ASM("TOT")+1 W ?10,$P(^AUTTLOC($P(^(ASM),"^",1),0),"^",1) F I=1:1:3 S ASM(I)=$P(^UTILITY($J,ASM),"^",I)
 S ASM("GL")=^AZOTXST(ASM(1),1,ASM(2),1,ASM(3),0),$P(^AZOTXST(ASM(1),1,ASM(2),1,ASM(3),0),"^",7)=DT,^AZOTXST("AC",DT,ASM(1),ASM(2),ASM(3))="" K ^AZOTXST("AMAIL",ASM(1),ASM(2),ASM(3))
B3 W ?42,$P($T(@$P(ASM("GL"),"^",1)),";;",2),?56,$P(ASM("GL"),"^",5),! D HEADING:$Y>50 G B2
END D TOTAL G B1:ASM(4)'=ASM("COPIES") W @IOF K ASM Q
HEADING U IO W:ASM>0 @IOF
 S PG=PG+1,LOC=^DD("SITE"),TITLE="R.P.M.S. DATA CARTRIDGES",AG("TM")=$P($H,",",2),AG("HR")=AG("TM")\3600,AG("MIN")=AG("TM")#3600\60
 S:AG("MIN")<10 AG("MIN")="0"_AG("MIN") S TME=AG("HR")_":"_AG("MIN"),USER=$P(^DIC(3,DUZ,0),"^",2)
 W !!,USER,?80-$L(LOC)\2,LOC,?72,"page ",PG,!,TME,?80-$L(TITLE)\2,TITLE S Y=DT X ^DD("DD") W ?78-$L(Y),Y,!
LABEL W !!,?15,"FACILITY",?42,"DATA",?52,"# OF RECORDS" S AG("LINE")="=" D LINE Q
HEAD U IO W @IOF,?80-$L("M E M O R A N D U M")\2,"M E M O R A N D U M",!!!,?5,"TO:",?10,"DIRECTOR, DATA PROCESSING SERVICES CENTER"
 W !!!,?3,"FROM: ",?10,"OKLAHOMA CITY AREA OFFICE",!?10,"OFFICE OF PROGRAM PLANNING AND EVALUATION",!!!,"SUBJECT:",?10,"R.P.M.S. DATA CARTRIDGES",!!!
 W "ENCLOSED ARE THE FOLLOWING DATA CARTRIDGES.....",!!! Q
LINE S:'$D(AG("PRL")) AG("OLD")="" S:AG("OLD")'=AG("LINE") AG("PRL")="",$P(AG("PRL"),AG("LINE"),79)="",AG("OLD")=AG("LINE") W !,AG("PRL"),! Q
TOTAL W !!!!,"Total cartridges sent: ",ASM("TOT"),!!!!,"Signature: _____________________________________",! Q
AG ;;REG
AAPC ;;APC
ACHS ;;CHS
