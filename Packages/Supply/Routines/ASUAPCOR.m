ASUAPCOR ;DSD/DFM - DIRECT ISSUE CORRETION;  [ 04/15/98  2:36 PM ]
 ;;3.0;SAMS;**1**;AUG 20, 1993
 F  Q:$G(ASUREPLY("CORRECT"))="Y"  Q:$D(DTOUT)!($D(DUOUT))!($D(DIROUT))  D
 .Q:$D(DTOUT)!($D(DUOUT))!($D(DIROUT))  Q:ASUREPLY("CORRECT")="Y"
 .S DIR(0)="SB^Y:YES;N:NO;3:STATION;4:PURCHASE ORDER;5:SOURCE;6:ACCOUNT;7:OBJECT;8:USER;9:SUB STATION;10:COMMON ACCOUNTING;11:SUB SUB ACTIVITY;12:NUMBER OF LINE ITEMS;13:VALUE;14:VOUCHER"
 .D CORRECT^ASUAUYRN Q:ASUREPLY("CORRECT")="Y"  D
 ..D:ASUREPLY("CORRECT")="N"
 ...S DIR(0)="NOA^3:14:0"
 ...D GETFIELD^ASUAUYRN
 ..Q:$D(DTOUT)!($D(DUOUT))!($D(DIROUT))  Q:ASUREPLY("CORRECT")="Y"
 ..I ASUREPLY("CORRECT")="3" D  Q
 ...S DIR("B")=ASUTRNS(ASUTRNS,"STATION"),ASUTRNS(ASUTRNS,"STATION")=""
 ...D STAT^ASUAUAST
 ..I ASUREPLY("CORRECT")="4" D RDPON^ASUAUPON Q
 ..I ASUREPLY("CORRECT")="5" S ASUV("ITEM #")=5 D ^ASUAUSRC Q
 ..I ASUREPLY("CORRECT")<12 D
 ...S ASUV("ITEM #")=6 D ^ASUAUACC Q:$D(DUOUT)!($D(DIROUT))!($D(DTOUT))
 ...S ASUV("ITEM #")=7 D ^ASUAUDOJ Q:$D(DUOUT)!($D(DIROUT))!($D(DTOUT))
 ...S ASUV("ITEM #")=8 D ^ASUAPSST Q:$D(DUOUT)!($D(DIROUT))!($D(DTOUT))
 ...S ASUV("ITEM #")=9 D ^ASUAUUSR Q:$D(DUOUT)!($D(DIROUT))!($D(DTOUT))
 ...S ASUV("ITEM #")=10 D ^ASUAPCAN Q:$D(DUOUT)!($D(DIROUT))!($D(DTOUT))
 ...S ASUV("ITEM #")=11 D ^ASUAUSSA
 ..I ASUREPLY("CORRECT")="12" D ^ASUAPNLI
 ..I ASUREPLY("CORRECT")="13" S ASUV("ITEM #")=12,ASUV("LOWEST")=1 D ^ASUAUVAL
 ..I ASUREPLY("CORRECT")="14" S ASUV("ITEM #")=13 D ^ASUAUVOU
 ..S ASUREPLY("CORRECT")="N" Q
 I ASUREPLY("CORRECT")="Y" D ^ASUAPUPD
 K X,Y,ASUREPLY("CORRECT")
 Q
