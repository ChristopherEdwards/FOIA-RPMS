ASURDSTD ; IHS/ITSC/LMH -DAILY STD REPORTS SERIES ;  [ 08/15/2000  10:58 AM ]
 ;;4.2T2;Supply Accounting Mgmt. System;;JUN 30, 2000
 ;This routine controls the formatting and printing of the Daily
 ;Closeout Standard reports series - Reports 1,7A,8,9,10,10A,11,13
 ;and 73. It involkes routines ^ASURD01P,^ASURD07A,^ASURD08P,^ASURD09P
 ;^ASURD10P,^ASURD10V,^ASURD11P,^ASURD13P and ^ASURD73P
 S ASUP("HLT")=$G(ASUP("HLT"))
 I '$D(IO) D HOME^%ZIS
 I '$D(DUZ(2)) W !,"Report must be run from Kernel option" Q
 I '$D(ASUL(1,"AR","AP")) D SETAREA^ASULARST
 S ASUK("PTR")="IRPT"
 ;S ZTRTN="PSER^ASURDSRT",ZTDESC="SAMS STD DAILY REPORTS"
 S ZTRTN="PSER^ASURDSTD",ZTDESC="SAMS STD DAILY REPORTS" ;IHS/ITSC/MRS 8/15/2000
 ;Added call to ASUURANG before run
 D DATE^ASUUDATE
 D ^ASUURANG
 S ASUK("PTR")="SRPT"
 D O^ASUUZIS
 I POP S IOP=$I D ^%ZIS Q
 I ASUK(ASUK("PTR"),"Q") Q
 S ASUK("PTRSEL")=1
PSER ;EP;FOR TASKMAN QUEUE OF PRINT
 D TIME^ASUUDATE
 S ASURX="W !,""S.A.M.S. Standard Reports Procedure Begun "_ASUK("DT","TIME")_"""" D ^ASUUPLOG
 D U^ASUUZIS
 D ^ASURD01P
 F ASUP("SR")=0:1 S ASUSV("TRCD")=$T(@"SEQ"+ASUP("SR")) Q:ASUSV("TRCD")["999"  D  Q:$G(ASUP("HLT"))=1
 .S ASUG("RTN")="^"_$P(ASUSV("TRCD"),";",3)
 .I ASUG("RTN")["13P",$G(ASUP("A13"))=0 Q
 .D @(ASUG("RTN"))
 .Q:$G(ASUP("HLT"))=1
 D TIME^ASUUDATE
 U IO(0)
 D TIME^ASUUDATE
 S ASURX="W !,""S.A.M.S. Standard Reports Procedure Begun "_ASUK("DT","TIME")_"""" D ^ASUUPLOG
 K ASUP("SR")
 I ASUK(ASUK("PTR"),"Q")'=1 D C^ASUUZIS,^ASUCOKIL K ASUSV
 Q
SEQ ;;ASURD07A;REPORT 7A ;ASUP("CKS")=6
 ;;ASURD08P;REPORT 8  ;ASUP("CKS")=7
 ;;ASURD09P;REPORT 9  ;ASUP("CKS")=8
 ;;ASURD10P;REPORT 10A;ASUP("CKS")=9
 ;;ASURD10V;REPORT 10 ;ASUP("CKS")=10
 ;;ASURD11P;REPORT 11 ;ASUP("CKS")=11
 ;;ASURD13P;REPORT 13 ;ASUP("CKS")=12
 ;;ASURD73P;REPORT 73 ;ASUP("CKS")=13
 ;;999;END OF SEQ    ;
