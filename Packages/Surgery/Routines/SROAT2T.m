SROAT2T ;B'HAM ISC/MAM - ATTENDING SURGEON CUMULATIVE ; [ 07/27/98   2:33 PM ]
 ;;3.0; Surgery ;**50**;24 Jun 93
 S SRPAGE=1
 D HDR W !!,?8,"ATTENDING CODE",?50,"TOTAL CASES",!,?8,"--------------",?50,"-----------"
 S PRINT=1,(HDR,CODE)="" F I=0:0 S CODE=$O(^TMP("SRTC",$J,CODE)) Q:CODE=""  D PRINT
 W !!,?8,"TOTAL CASES FROM "_$E(SRSD,4,5)_"/"_$E(SRSD,6,7)_"/"_$E(SRSD,2,3)_" TO "_$E(SRED,4,5)_"/"_$E(SRED,6,7)_"/"_$E(SRED,2,3),?50,$J(^TMP("SRTOT",$J),6)
 I $E(IOST)'="P" D BREAK I SRSOUT Q
 S PRINT=2,SRSS=0 F I=0:0 S SRSS=$O(^TMP("SRTOT",$J,SRSS)) Q:SRSS=""!(SRSOUT)  D SPEC
 Q
PRINT S SRCODE=$S(CODE=0:"0. STAFF",CODE=1:"1. ATTENDING IN O.R.",CODE=2:"2. ATTENDING IN O.R. SUITE",CODE=3:"3. ATTENDING NOT PRESENT, BUT AVAILABLE",1:"*  ATTENDING CODE NOT ENTERED")
 W !,?8,SRCODE,?50,$J($S(PRINT=1:^TMP("SRTC",$J,CODE),1:^TMP("SRTOT",$J,SRSS,CODE)),6)
 Q
HDR ; print heading
 I +$Y W @IOF
 W !,?(80-$L(SRINST)\2),SRINST,?76,"PAGE",!,?32,"SURGICAL SERVICE",?78,SRPAGE,!,?22,"ATTENDING SURGEON CUMULATIVE REPORT"
 W !,?(80-$L(SRFRTO)\2),SRFRTO
 I $E(IOST)="P" W !,?30,"DATE PRINTED: ",SRPRINT,!!,?8,"REVIEWED BY: ",?53,"DATE REVIEWED: "
 S SRPAGE=SRPAGE+1 W ! F LINE=1:1:80 W "="
 Q
BREAK W !!!,"Press RETURN to continue, or '^' to quit:  " R X:DTIME I '$T!(X["^") S SRSOUT=1 Q
 I X["?" W !!,"Enter RETURN if you want to display the totals for each specialty, or '^' to",!,"exit from this report." G BREAK
 Q
SPEC ; SPECIALTIES
 S TOTAL=^TMP("SRTOT",$J,SRSS) I 'TOTAL Q
 S SRSSN=$S($E(SRSS,1,2)="ZZ":"SPECIALTY NOT ENTERED",1:SRSS)
 D HDR W !!,?(80-$L(SRSSN)\2),SRSSN,!!,?8,"ATTENDING CODE",?50,"TOTAL CASES",!,?8,"--------------",?50,"-----------"
 S CODE="" F I=0:0 S CODE=$O(^TMP("SRTOT",$J,SRSS,CODE)) Q:CODE=""  D PRINT
 W !!,?8,"TOTAL CASES FOR THIS SPECIALTY",?50,$J(^TMP("SRTOT",$J,SRSS),6)
 I $E(IOST)'="P" D BREAK I SRSOUT Q
 Q
