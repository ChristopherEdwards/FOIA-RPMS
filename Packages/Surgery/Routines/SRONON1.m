SRONON1 ;B'HAM ISC/ADM - NON-O.R. PROC. RPT. (CONT) ; [ 02/26/97  10:25 AM ]
 ;;3.0; Surgery ;**38,48,63**;24 Jun 93
 I $Y>(IOSL-10) D FOOT^SRONON G:SRSOUT END D HDR^SRONON G:SRSOUT END
 D UL G:SRSOUT END K ^UTILITY($J,"W") S SRL=0
IND I $O(^SRF(SRTN,40,0)) S SRL=1 D:$Y>(IOSL-10) UL W !,"Indications for Procedure:" S SRIND=0 F  S SRIND=$O(^SRF(SRTN,40,SRIND)) Q:'SRIND!SRSOUT  S X=^SRF(SRTN,40,SRIND,0),DIWL=2,DIWR=76,DIWF="N" D ^DIWP
 D OUT G:SRSOUT END I SRL D UL S SRL=0
HIS K ^UTILITY($J,"W") I $O(^SRF(SRTN,39,0)) S SRL=1 D:$Y>(IOSL-10) UL W !,"Brief Clinical History:" S SRP=0 F  S SRP=$O(^SRF(SRTN,39,SRP)) Q:'SRP!SRSOUT  S X=^SRF(SRTN,39,SRP,0),DIWL=2,DIWR=76,DIWF="N" D ^DIWP
 D OUT G:SRSOUT END I SRL D UL S SRL=0
FIND K ^UTILITY($J,"W") I $O(^SRF(SRTN,38,0)) S SRL=1 D:$Y>(IOSL-10) UL W !,"Operative Findings:" S SRP=0 F  S SRP=$O(^SRF(SRTN,38,SRP)) Q:'SRP!SRSOUT  S X=^SRF(SRTN,38,SRP,0),DIWL=2,DIWR=76,DIWF="N" D ^DIWP
 D OUT G:SRSOUT END I SRL D UL S SRL=0
SPEC K ^UTILITY($J,"W") I $O(^SRF(SRTN,9,0)) S SRL=1 D:$Y>(IOSL-10) UL W !,"Specimens:" S SRP=0 F  S SRP=$O(^SRF(SRTN,9,SRP)) Q:'SRP!SRSOUT  S X=^SRF(SRTN,9,SRP,0),DIWL=2,DIWR=76,DIWF="N" D ^DIWP
 D OUT G:SRSOUT END I SRL D UL S SRL=0
 K ^UTILITY($J,"W") I $O(^SRF(SRTN,12,0)) S SRL=1 D:$Y>(IOSL-10) UL W !,"Provider's Dictation:" S SRP=0 F  S SRP=$O(^SRF(SRTN,12,SRP)) Q:'SRP!SRSOUT  S X=^SRF(SRTN,12,SRP,0),DIWL=1,DIWR=76,DIWF="N" D ^DIWP
 D OUT G:SRSOUT END I SRL D UL S SRL=0
 K ^UTILITY($J,"W") I $O(^SRF(SRTN,43,0)) S SRL=1 D:$Y>(IOSL-10) UL W !,"Occurrences:" S SRC=0 F  S SRC=$O(^SRF(SRTN,43,SRC)) Q:'SRC!SRSOUT  D COMP
 G:SRSOUT END I SRL D UL S SRL=0
 K ^UTILITY($J,"W") I $O(^SRF(SRTN,5,0)) D:$Y>(IOSL-10) UL W !,"General Comments:" S SRCOM=0 F  S SRCOM=$O(^SRF(SRTN,5,SRCOM)) Q:'SRCOM!SRSOUT  S X=^SRF(SRTN,5,SRCOM,0),DIWL=2,DIWR=76,DIWF="N" D ^DIWP
 D OUT G:SRSOUT END
P K ^UTILITY($J,"W") I SRT="UL",$Y<(IOSL-10) W ! G P
 D FOOT^SRONON
END S X=SRTN K SRT,SRTN,^UTILITY($J,"W") S SRTN=X Q:$D(SRTDFN)
 W:$E(IOST)="P" @IOF I $D(ZTQUEUED) Q:$G(ZTSTOP)  S ZTREQ="@" Q
 I $D(SRSITE("KILL")) K SRSITE
 D ^%ZISC,^SRSKILL W @IOF Q
 Q
OUT Q:SRSOUT  I $D(^UTILITY($J,"W")) F V=1:1:^UTILITY($J,"W",DIWL) D:$Y>(IOSL-10) UL Q:SRSOUT  W !,?DIWL,^UTILITY($J,"W",DIWL,V,0)
 Q
UL I $Y>(IOSL-10) D FOOT^SRONON Q:SRSOUT  D HDR^SRONON Q:SRSOUT  I 1
 E  I SRT="UL" D UL1
Q Q:SRT="UL"  I $Y>(IOSL-9) D FOOT^SRONON Q:SRSOUT  D HDR^SRONON Q:SRSOUT  I 1
 Q
UL1 I '$D(SRNIGHT),IO(0)=IO,'$D(ZTQUEUED) W !
 W $C(13) F X=1:1:79 W "_"
 Q
COMP Q:SRSOUT  D:$Y>(IOSL-10) UL W !,?3,$P(^SRF(SRTN,43,SRC,0),"^"),?50,"Date:" S Y=$P(^(0),"^",3) I Y X ^DD("DD") W ?57,Y
 D:$Y>(IOSL-10) UL W !,?6,"Treatment: ",$P(^SRF(SRTN,43,SRC,0),"^",4) S Y=$P(^(0),"^",2),C=$P(^DD(130.0126,1,0),"^",2) D:Y'="" Y^DIQ W ?58,"Outcome: ",Y
 K ^UTILITY($J,"W") I $O(^SRF(SRTN,43,SRC,1,0)) D:$Y>(IOSL-10) UL W !,?6,"Comments:" S SRCOM=0 F  S SRCOM=$O(^SRF(SRTN,43,SRC,1,SRCOM)) Q:'SRCOM!SRSOUT  S X=^SRF(SRTN,43,SRC,1,SRCOM,0),DIWL=6,DIWR=79,DIWF="N" D ^DIWP
 D OUT
 Q
N S Z=$S(Z="":Z,$D(^VA(200,Z,0)):$P(^(0),U,1),1:Z)
 Q
