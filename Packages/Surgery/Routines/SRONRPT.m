SRONRPT ;TAMPA/CFB - NURSES REPORT ; 27 Jan 1989  8:28 AM
 ;;3.0; Surgery ;;24 Jun 93
 I '$D(SRSITE) D ^SROVAR S SRSITE("KILL")=1
 S SRSOUT=0 I '$D(SRTN) K SRNEWOP D ^SROPS G:'$D(SRTN) END
 Q:'$D(SRTN)
 Q:SRTN<1  K %ZIS,IO("Q") S %ZIS="Q" D ^%ZIS G:POP END I $D(IO("Q")) K IO("Q") S ZTRTN="RPT^SRONRPT",ZTDESC="NURSE INTRAOPERATIVE REPORT",ZTSAVE("SRSITE(")="",ZTSAVE("SRTN")=SRTN,ZTSAVE("SRT")="UL" D ^%ZTLOAD G END
 G RPT
EN2 S DIC=130,DIC(0)="QAEM" D ^DIC S SRTN=+Y Q:+Y<1  G SRONRPT
RPT ; entry when queued
 S SRSOUT=0 S:$E(IOST)="P" SRT="UL" U IO S VV=0 G ^SRONRPT0
HDR I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 S:'($D(SRT)#2) SRT="Q" S VV=VV+1 W:$Y @IOF W !!!! D UL W !,?5,"MEDICAL RECORD            |  NURSE INTRAOPERATIVE REPORT   PAGE ",VV D UL W !
 Q
FOOT Q:SRSOUT  W ! D UL W !,"Nurse's Signature: ",?50 D NOW^%DTC S Y=% D D^DIQ W Y
 D UL S DFN=$P(^SRF(SRTN,0),U,1) Q:DFN=""  D DEM^VADPT W !,$E(VADM(1),1,30),?50,VA("PID"),!,"Ward: ",$S($D(^DPT(DFN,.1)):^(.1),1:""),?50,"Room-Bed: ",$S($D(^DPT(DFN,.101)):^(.101),1:"")
 D UL W !,"VAMC: "_SRSITE("SITE"),?50,"SF 509  Progress Notes" I SRT="Q" R !,"Press RETURN to continue  ",X:DTIME S:X="^" SRSOUT=1 Q:SRSOUT
 Q
UL Q:SRT'="UL"!(SRSOUT)  I IO(0)=IO,'$D(ZTQUEUED) W !
 W $C(13) F X=1:1:79 W "_"
 Q
END W ! D ^SRSKILL D ^%ZISC W @IOF I $D(SRSITE("KILL")) K SRSITE
 Q
