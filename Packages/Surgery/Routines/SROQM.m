SROQM ;B'HAM ISC/ADM - QUARTERLY REPORT (CONTINUED) ; [ 07/31/00  9:59 AM ]
 ;;3.0; Surgery ;**62,82,95**; Jun 93
 ;
 ;** NOTICE: This routine is part of an implementation of a nationally
 ;**         controlled procedure.  Local modifications to this routine
 ;**         are prohibited.
 ;
 S (SRCNT,SRPAGE)=1 K ^TMP("SRMSG",$J) D HDR Q:SRSOUT  D ^SROQM0,HDR Q:SRSOUT  D ^SROQM1
 S XMSUB="QUARTERLY REPORT - SURGICAL SERVICE",XMDUZ="SURGERY DATA MANAGER"
 S XMY("G.SR-QUARTERLY@"_SRD)=""
 S XMTEXT="^TMP(""SRMSG"",$J," N I D ^XMD
 K XMDUZ,^TMP("SRMSG",$J)
 Q
HDR ; write page header
 I $D(ZTQUEUED) D ^SROSTOP I SRHALT S SRSOUT=1 Q
 S SRBLANK="" F I=1:1:22 S SRBLANK=SRBLANK_" "
 S SRLINE=SRBLANK_"QUARTERLY REPORT - SURGICAL SERVICE",SRBLANK="" F I=1:1:(75-$L(SRLINE)) S SRBLANK=SRBLANK_" "
 S SRLINE=SRLINE_SRBLANK_"PAGE" D LINE
 S SRBLANK="" F I=1:1:34 S SRBLANK=SRBLANK_" "
 S SRLINE=SRBLANK_"VERSION 3.0",SRBLANK="" F I=1:1:(77-$L(SRLINE)) S SRBLANK=SRBLANK_" "
 S SRLINE=SRLINE_SRBLANK_SRPAGE D LINE
 D BLANK S SRLINE=" Hospital: "_$E(SRINST,1,44),SRBLANK="" F I=1:1:(57-$L(SRLINE)) S SRBLANK=SRBLANK_" "
 S SRLINE=SRLINE_SRBLANK_"Station Number: "_SRSTATN D LINE
 S SRLINE=" For Dates: "_SRSD_"     to: "_SRED_"            Fiscal Year: "_SRYR D LINE
 S SRLINE="" F I=1:1:79 S SRLINE=SRLINE_"="
 D LINE S SRPAGE=SRPAGE+1
 Q
BLANK ; blank line
 S ^TMP("SRMSG",$J,SRCNT)="",SRCNT=SRCNT+1
 Q
LINE ; store line in ^TMP
 S ^TMP("SRMSG",$J,SRCNT)=SRLINE,SRCNT=SRCNT+1
 Q
