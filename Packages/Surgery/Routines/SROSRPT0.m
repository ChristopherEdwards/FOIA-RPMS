SROSRPT0 ;TAMPA/CFB - SURGEON'S REPORT ; 30 Jan 1989  10:44 AM
 ;;3.0; Surgery ;;24 Jun 93
 U IO F X=0:.1:1.1 S SRTN(X)=$S($D(^SRF(SRTN,X))#2:^(X),1:"")
 D HDR^SROSRPT W !,"PREOPERATIVE DIAGNOSIS:" I $D(^SRF(SRTN,33)) K X S X=$P(^(33),"^") W !,"   PRIMARY: "_X S SRPOST=1
 I $D(^SRF(SRTN,14)) W !,"   OTHER: " S DIAG=0 F I=0:0 S DIAG=$O(^SRF(SRTN,14,DIAG)) Q:'DIAG  W $P(^SRF(SRTN,14,DIAG,0),"^")
 D UL G:SRSOUT END^SROSRPT1 W !,"SURGEON: " S SRTN(.1)=$S($D(^SRF(SRTN,.1)):^(.1),1:"") S Z=$P(SRTN(.1),"^",4) D N W $E(Z,1,30),?40,"1ST ASST: " S Z=$P(SRTN(.1),"^",5) D N W $E(Z,1,30)
 D UL G:SRSOUT END^SROSRPT1 W !,"2ND ASST: " S Z=$P(SRTN(.1),"^",6) D N W $E(Z,1,30),?40,"CASE TYPE: " S Q(3)=^DD(130,.035,0),Q(7)=$P(SRTN(0),"^",10) D S W Q(7)
 D UL G:SRSOUT END^SROSRPT1 I $D(^SRF(SRTN,28)) W !,"OTHER ASSISTANTS: " S SROTHER=0 F I=0:0 S SROTHER=$O(^SRF(SRTN,28,SROTHER)) Q:'SROTHER  S Z=$P(^SRF(SRTN,28,SROTHER,0),"^") D N W ?20,$E(Z,1,30),!
 D UL G:SRSOUT END^SROSRPT1 W !,"ATTEND SURG: " S Z=$P(SRTN(.1),"^",13) D N W $E(Z,1,30)
 S X=$P(SRTN(.1),"^",16),SRATT=$S(X=0:"0. STAFF",X=1:"1. ATTENDING IN O.R.",X=2:"2. ATTENDING IN O.R. SUITE",X=3:"3. ATTENDING NOT PRESENT,",1:"") W ?40,"ATT CODE: "_SRATT W:X=3 !,?50,"BUT AVAILABLE"
 D UL G:SRSOUT END^SROSRPT1 S SRTN(.3)=$S('$D(^SRF(SRTN,.3)):"",1:^SRF(SRTN,.3)) W !,"ATTEND ANESTH: " S Z=$P(SRTN(.3),U,4) D N W $E(Z,1,30)
 W !,"ANESTHESIA SUPERVISOR CODE: ",$S($P(SRTN(.3),"^",6)="":"",1:$P(^SRO(132.95,$P(SRTN(.3),"^",6),0),"^",1))
 D UL G:SRSOUT END^SROSRPT1 W !,"ANESTHETIST: " S Z=$P(SRTN(.3),U,1) D N W $E(Z,1,30),?40,"ASST ANES: " S Z=$P(SRTN(.3),U,3) D N W $E(Z,1,30)
 D UL G:SRSOUT END^SROSRPT1 W !,"PERFUSIONIST: " S Z=$P(SRTN(.1),U,19) D N W $E(Z,1,30),?40,"ASST PRFST: " S Z=$P(SRTN(.1),U,20) D N W $E(Z,1,30)
 D UL G:SRSOUT END^SROSRPT1 W !,"OR SUPPORT PERSONNEL: ",!,?10,"CIRCULATING",?50,"SCRUBBED" K V
 S Q(3)=^DD(130.28,3,0) F V2=19,23 S V1=0 F V=0:0 S V=$O(^SRF(SRTN,V2,V)) Q:'V  I $D(^(V,0)) S V1=V1+1,V(V2,V)=^(0)
 F V=1:1 Q:('$D(V(19,V)))&('$D(V(23,V)))  S V1=5 W ! F V2=19,23 S:'$D(V(V2,V)) V(V2,V)="" W ?V1,$S($P(V(V2,V),U,1)="":"",$D(^VA(200,$P(V(V2,V),U,1),0))#2:$P(^(0),U,1),1:"") D NURSE
 K V,V1,V2 D UL G:SRSOUT END^SROSRPT1 W !,"ANESTHESIA TECHNIQUE:" F V=0:0 S V=$O(^SRF(SRTN,6,V)) Q:'V  S Q(7)=$P(^(V,0),U),Q(3)=^DD(130.06,.01,0) D S W !,?5,Q(7) W:$P(^SRF(SRTN,6,V,0),U,3)="Y" "  (PRINCIPAL)" D DRUG
 D UL G:SRSOUT END^SROSRPT1 S SRTN(.2)=$S($D(^SRF(SRTN,.2)):^(.2),1:"") W !,"ANES BEGINS: " S X=$P(SRTN(.2),U,1),%DT="ET" D:X>0 ^%DT W ?40,"ANES ENDS: " S X=$P(SRTN(.2),U,4),%DT="ET" D:X>0 ^%DT
 D UL G:SRSOUT END^SROSRPT1 W !,"OP BEGINS: " S X=$P(SRTN(.2),U,2),%DT="ET" D:X>0 ^%DT W ?40,"OP ENDS: " S X=$P(SRTN(.2),U,3),%DT="ET" D:X>0 ^%DT
 I $D(^SRF(SRTN,3)) D UL G:SRSOUT END^SROSRPT1 W !,"TUBES AND DRAINS:  "_$P(^SRF(SRTN,3),"^")
 G ^SROSRPT1
UL Q:SRSOUT  I $Y>(IOSL-10) D FOOT^SROSRPT Q:SRSOUT  D HDR^SROSRPT I 1
 E  I SRT="UL" D UL1
Q Q:SRT="UL"  I $Y>(IOSL-10) D FOOT^SROSRPT Q:SRSOUT  D HDR^SROSRPT I 1
 Q
UL1 I '$D(SRNIGHT),IO(0)=IO,'$D(ZTQUEUED) W !
 W $C(13) F X=1:1:79 W "_"
 Q
DRUG W !,?8,"AGENTS:" F V1=0:0 S V1=$O(^SRF(SRTN,6,V,V1)) Q:'V1  F V2=0:0 S V2=$O(^SRF(SRTN,6,V,V1,V2)) Q:'V2  I $D(^(V2,0)) S T=^(0) W ?16,$S(+T=0:"",$D(^PSDRUG(+T,0)):$P(^(0),U),1:"UNKNOWN"),!
 Q
NURSE ; more nurse info
 S V1=V1+25,Q(7)=$P(V(V2,V),U,3) D S W ?V1,$E(Q(7),1,7) S V1=V1+12
 Q
N S Z=$S(Z="":Z,$D(^VA(200,Z,0)):$P(^(0),U,1),1:Z) Q
S Q:Q(7)=""  S Z1=$P(Q(3),U,3) F X1=1:1 Q:Q(7)=$P($P(Z1,";",X1),":",1)  Q:X1=50
 Q:X1=50  S Q(7)=$P($P(Z1,";",X1),":",2) Q
 Q:X1=50  S Q(7)=$P($P(Z1,";",X1),":",2) Q
