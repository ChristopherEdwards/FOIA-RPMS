SROSRPT1 ;TAMPA/CFB - SURGEON'S REPORT ; 30 Jan 1989  11:24 AM;
 ;;3.0; Surgery ;;24 Jun 93
 S X(.5)=$S($D(^SRF(SRTN,.5)):^(.5),1:"") I $P(X(.5),"^")'="" D UL G:SRSOUT END W !,"SPONGE AND NEEDLE COUNT VERIFIED AS " S Q(7)=$P(X(.5),U,1),Q(3)=^DD(130,.52,0) D S W Q(7)," BY: " S Z=$P(X(.5),U,9) D N W $E(Z,1,30)
 D UL G:SRSOUT END W !,"MATERIAL SENT TO LAB FOR ANALYSIS: "
 I $O(^SRF(SRTN,9,0)) W !,"SPECIMENS: " S V=0 F I=0:0 S V=$O(^SRF(SRTN,9,V)) Q:'V  W !,?4,^SRF(SRTN,9,V,0)
 I $O(^SRF(SRTN,41,0)) W !,"CULTURES: " S V=0 F I=0:0 S V=$O(^SRF(SRTN,41,V)) Q:'V  W !,?4,^SRF(SRTN,41,V,0)
 I $Y>(IOSL-10) D FOOT^SROSRPT G:SRSOUT END D HDR^SROSRPT G:SRSOUT END
 S SRPOST=0 I $D(^SRF(SRTN,34)) D UL W !,"POST OPERATIVE DIAGNOSIS: ",!,"   PRIMARY: "_$P(^SRF(SRTN,34),"^") S SRPOST=1
 I $O(^SRF(SRTN,15,0)) W !,"   OTHER: " S DIAG=0 F I=0:0 S DIAG=$O(^SRF(SRTN,15,DIAG)) Q:'DIAG  W $P(^SRF(SRTN,15,DIAG,0),"^") S Q(7)=$P(^(0),"^",2)
 I $Y>(IOSL-10) D FOOT^SROSRPT G:SRSOUT END D HDR^SROSRPT G:SRSOUT END
 D UL G:SRSOUT END S X=$P(^SRF(SRTN,0),"^",3) W !,$S(X="J":"MAJOR ",X="N":"MINOR ",1:"") W "OPERATION(S) PERFORMED: "
 S SRP=$P(^SRF(SRTN,"OP"),"^") S:$P(^("OP"),"^",2) SRP=SRP_" ("_$P(^ICPT($P(^SRF(SRTN,"OP"),"^",2),0),"^")_")"
 I $P($G(^SRF(SRTN,30)),"^")&$P($G(^SRF(SRTN,.2)),"^",10) S SRP="** ABORTED ** "_SRP
 K SROPS,MM,MMM S:$L(SRP)<65 SROPS(1)=SRP I $L(SRP)>64 S SRP=SRP_"  " F M=1:1 D LOOP Q:MMM=""
 W !,"   PRIMARY: "_SROPS(1) I $D(SROPS(2)) W !,?12,SROPS(2) I $D(SROPS(3)) W !,?12,SROPS(3)
 S Q(7)=$P(^SRF(SRTN,"OP"),"^",3)
 S SROP="" I $D(^SRF(SRTN,13)) W !,"   OTHER: " S CNT=0 F I=0:0 S CNT=$O(^SRF(SRTN,13,CNT)) Q:CNT=""  S SRP=$P(^SRF(SRTN,13,CNT,0),"^") D CPT W ?10,SRP,!
 D UL G:SRSOUT END S S(31)=$S($D(^SRF(SRTN,31)):^(31),1:""),Y=$P(S(31),"^",6) D:Y D^DIQ W !,"DATE/TIME OF DICTATION:  "_Y
 S Y=$P(S(31),"^",7) D:Y D^DIQ W !,"DATE/TIME TRANSCRIBED: "_Y
OPNOTE G ^SROSRPT2
END Q:$D(SRNIGHT)
 S X=SRTN K SRT,SRTN S SRTN=X I $D(ZTQUEUED) Q:$G(ZTSTOP)  S ZTREQ="@" Q
 D ^SRSKILL,^%ZISC W @IOF I $D(SRSITE("KILL")) K SRSITE
 Q
LOOP ; break procedure if greater than 65 characters
 S SROPS(M)="" F LOOP=1:1 S MM=$P(SRP," "),MMM=$P(SRP," ",2,200) Q:MMM=""  Q:$L(SROPS(M))+$L(MM)'<65  S SROPS(M)=SROPS(M)_MM_" ",SRP=MMM
 Q
UL Q:SRSOUT  I $Y>(IOSL-10) D FOOT^SROSRPT Q:SRSOUT  D HDR^SROSRPT I 1
 E  I SRT="UL" D UL1
Q Q:SRT="UL"  I $Y>(IOSL-9) D FOOT^SROSRPT Q:SRSOUT  D HDR^SROSRPT I 1
 Q
UL1 I '$D(SRNIGHT),IO(0)=IO,'$D(ZTQUEUED) W !
 W $C(13) F X=1:1:79 W "_"
 Q
CPT I $D(^SRF(SRTN,13,CNT,2)),$P(^(2),"^") S SRP=SRP_" ("_$P(^(2),"^")_")"
 Q
N S Z=$S(Z="":Z,$D(^VA(200,Z,0)):$P(^(0),U,1),1:Z) Q
S Q:Q(7)=""  S Z1=$P(Q(3),U,3) F X1=1:1 Q:Q(7)=$P($P(Z1,";",X1),":",1)  Q:X1=50
 Q:X1=50  S Q(7)=$P($P(Z1,";",X1),":",2) Q
