SROSRPT2 ;B'HAM ISC/MAM - OPERATION REPORT (CONT) ;  4 DEC 1989  7:00 AM
 ;;3.0; Surgery ;;24 Jun 93
 D UL G:SRSOUT END K ^UTILITY($J,"W") W !,"INDICATIONS FOR OPERATION:",! S SRIND=0 F I=0:0 S SRIND=$O(^SRF(SRTN,40,SRIND)) Q:'SRIND  S X=^SRF(SRTN,40,SRIND,0),DIWL=3,DIWR=76,DIWF="N" D ^DIWP
 I $D(^UTILITY($J,"W")) F V=1:1:^UTILITY($J,"W",3) D:$Y>(IOSL-10) UL G:SRSOUT END W !,?3,^UTILITY($J,"W",3,V,0)
 K ^UTILITY($J,"W") I $D(^SRF(SRTN,12)) W !,"SURGEON'S DICTATION:",! F V=1:1:$P(^SRF(SRTN,12,0),"^",4) S X=^SRF(SRTN,12,V,0),DIWL=3,DIWR=76,DIWF="N" D ^DIWP
 I $D(^UTILITY($J,"W")) F V=1:1:^UTILITY($J,"W",3) D:$Y>(IOSL-10) UL G:SRSOUT END W !,?3,^UTILITY($J,"W",3,V,0)
P K ^UTILITY($J,"W") I SRT="UL",$Y<(IOSL-10) W ! G P
 D FOOT^SROSRPT
END Q:$D(SRTDFN)!($D(SRNIGHT))  S X=SRTN K SRT,SRTN S SRTN=X
 W:$E(IOST)="P" @IOF I $D(ZTQUEUED) Q:$G(ZTSTOP)  S ZTREQ="@" Q
 I $D(SRSITE("KILL")) K SRSITE
 D ^%ZISC,^SRSKILL W @IOF Q
UL Q:SRSOUT  I $Y>(IOSL-10) D FOOT^SROSRPT Q:SRSOUT  D HDR^SROSRPT I 1
 E  I SRT="UL" D UL1
Q Q:SRT="UL"  I $Y>(IOSL-9) D FOOT^SROSRPT Q:SRSOUT  D HDR^SROSRPT I 1
 Q
UL1 I '$D(SRNIGHT),IO(0)=IO,'$D(ZTQUEUED) W !
 W $C(13) F X=1:1:79 W "_"
 Q
CPT I $D(^SRF(SRTN,13,CNT,2)),$P(^(2),"^") S SRP=SRP_" ("_$P(^(2),"^")_")"
 Q
