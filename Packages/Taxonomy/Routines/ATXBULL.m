ATXBULL ; IHS/OHPRD/TMJ - THE ROUTINE TO TRIGGER PT TAXONOMY BULLETIN ;  
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
EN ; ENTRY POINT
 ;THIS ROUTINE IS SENT THE V POV-DFN (ATXPOVDA) AND THE TAXONOMY DFN
 ;(ATXDT) BY THE TAXONOMY SYSTEM ROUTINE ^ATXPOV.  ATXBULL SETS
 ;NECESSARY VARIABLES THEN SENDS A BULLETIN TO REGISTERED RECIPIENTS TO
 ;NOTIFY THEM OF A PCC VISIT BASED ON THE TAXONOMY'S RELATED ICD'S
 D ^XBKVAR
 D INIT
 G EXIT
 Q
 ;
INIT D VARS
 Q
 ;
EXIT K ATXDOLH,ATXPOVDA,ATXDT,ATXPDFN,ATXDUZ,ATXI,ATXJ,ATXVST
 K %X,%Y,X1,X2
 I $D(ZTQUEUED) S ZTREQ="@"
 Q
 ;
VARS ;
 S X=0
CHK ;
 I '$D(^AUPNVPOV(ATXPOVDA)) G X1
 G:X>100 X1
 I '$P(^AUPNVPOV(ATXPOVDA,0),U,3) H 5 S X=X+1 G CHK
 I +^AUPNVPOV(ATXPOVDA,0)'=ATXICD G X1
 ;check to see if bulletin should be sent at all
 K ATXSTOP
 I $G(^ATXAX(ATXDT,31))]"" X ^ATXAX(ATXDT,31) I $D(ATXSTOP) K ATXSTOP G X1
 I $P(^ATXAX(ATXDT,0),U,17)="N" D CHECK I '$D(ATXPOVDA) G X1 ;Exit if same POV in 30 days
 ;
 I $P(^ATXAX(ATXDT,0),U,19)="1" D CHECK2 I '$D(ATXPOVDA) G X1 ;Exit if REVISIT
 ;
 ;
 I $P(^ATXAX(ATXDT,0),U,21)'="" D CHECK3 I '$D(ATXPOVDA) G X1 ;Exit if the Bulletin Visit Type is not contained in Visit Type
 ;
 D DIQ1
X1 K ATXPOVDA,ATXDT
 Q
 ;
CHECK ; If pt had same pov within last 30 days don't send another bulletin
 S ATXVDATE=$P($P(^AUPNVSIT($P(^AUPNVPOV(ATXPOVDA,0),U,3),0),U),".")
 S X1=ATXVDATE,X2=-30 D C^%DTC S ATXID=9999999-X
 S ATXPTDA=$P(^AUPNVPOV(ATXPOVDA,0),U,2)
 S ATXVDATE=9999999-ATXVDATE F  S ATXVDATE=$O(^AUPNVPOV("AA",ATXPTDA,ATXVDATE)) Q:ATXVDATE>ATXID!('$D(ATXPOVDA))!('ATXVDATE)  D
 . S ATXGETDA=0 F  S ATXGETDA=$O(^AUPNVPOV("AA",ATXPTDA,ATXVDATE,ATXGETDA)) Q:'ATXGETDA  I ATXICD=+^AUPNVPOV(ATXGETDA,0) K ATXPOVDA Q
 K ATXPTDA,ATXGETDA,ATXID,ATXVDATE
 Q
 ;
 ;
CHECK2 ;Don't send bulletin if NOT First Visit
 ;
 I $P(^AUPNVPOV(ATXPOVDA,0),U,8)'=1 K ATXPOVDA Q  ;Kill ATXPOVDA if a REVISIT
 Q
 ;
CHECK3 ;Don't send bulletin if Bulletin Visit Type is Not contained in Visit Type
 S ATXTYPE=$P(^ATXAX(ATXDT,0),U,21) ;Visit Type for which to send bulletins
 S ATXVST=$P($G(^AUPNVPOV(ATXPOVDA,0)),U,3) ;Get Visit DFN
 I $P($G(^AUPNVSIT(ATXVST,0)),U,3)'[ATXTYPE K ATXPOVDA Q  ;Kill ATXPOVDA if NOT Right Visit Type
 Q
 ;
DIQ1 ;
 S DIC="9000010.07",DR=".01:.04;.07;.08;.09;.11;.13",(DA,D0)=ATXPOVDA
 D EN^DIQ1
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.01)) XMB(1)=^(.01) ;POV
 ;S:$D(ATXICD) XMB(8)=$P(^ICD9(ATXICD,0),U,3)
 S:ATXICD XMB(8)=$P($$ICDDX^ICDCODE(ATXICD,$P($P(^AUPNVSIT($P(^AUPNVPOV(DA,0),U,3),0),U),".")),U,4)
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.02)) XMB(2)=^(.02) ;PT NAME
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.03)) XMB(3)=^(.03) ;VISIT DATE
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.04)) XMB(4)=^(.04) ;NARRATIVE
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.07)) XMB(7)=^(.07) ;NARRATIVE
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.09)) XMB(9)=^(.09) ;INJURY CAUS
 I $D(XMB(9)),XMB(9)]"" S XMB(9.1)=$P($$ICDDX^ICDCODE(XMB(9),$P($P(^AUPNVSIT($P(^AUPNVPOV(DA,0),U,3),0),U),".")),U,4)
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.11)) XMB(11)=^(.11) ;ACC PLACE
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.13)) XMB(13)=^(.13) ;INJURY DATE
 S:$D(^UTILITY("DIQ1",$J,9000010.07,DA,.08)) XMB(14)=^(.08) ;1ST/REVSIT
 ;ABOVE LINE ADDED FOR INJURY,USED 14, FLD 8 USED BY ICD DIQ1+4
 S XMB(20)=$P(^ATXAX(ATXDT,0),U)
 S ATXPDFN=$P(^AUPNVPOV(DA,0),U,2)
 S ATXVST=$S($P(^ATXAX(ATXDT,0),U,3):$P(^(0),U,3),1:$P(^AUPNVSIT($P(^AUPNVPOV(DA,0),U,3),0),U,6))
 I $D(^AUPNPAT(ATXPDFN,41,ATXVST,0)) S XMB(99)=$S($P(^AUTTLOC(ATXVST,0),U,7)]"":$P(^(0),U,7),1:"  ")_" "_$P(^AUPNPAT(ATXPDFN,41,ATXVST,0),U,2)
 E  I $D(DUZ(2))#2,DUZ(2) S:$D(^AUPNPAT(ATXPDFN,41,DUZ(2),0)) XMB(99)=$S($P(^AUTTLOC(DUZ(2),0),U,7)]"":$P(^(0),U,7),1:"  ")_" "_$P(^AUPNPAT(ATXPDFN,41,DUZ(2),0),U,2)
 S XMB(15)=$P(^DIC(4,$P(^AUPNVSIT($P(^AUPNVPOV(DA,0),U,3),0),U,6),0),U)
 ;IHS/CMI/GRL  multiple additions
 S ATXCOM=$P($G(^AUPNPAT(ATXPDFN,11)),U,17) I $G(ATXCOM)]"" S XMB(16)=$P(^AUTTCOM(ATXCOM,0),U) ;current residence 
 I $G(ATXCOM)]"" S ATXSU=$P($G(^AUTTCOM(ATXCOM,0)),U,5) I ATXSU]"" S XMB(17)=$P($G(^AUTTSU(ATXSU,0)),U)  ;service unit  ;IHS/CMI/GRL **5**
 S X=$P(^DPT(ATXPDFN,0),U,3),XMB(18)=$E(X,4,5)_"/"_$E(X,6,7)_"/"_(1700+($E(X,1,3)))  ;dob
 S X=$P($G(^AUPNPAT(ATXPDFN,11)),U,8),XMB(19)=$S(X:$P(^AUTTTRI(X,0),U),1:"")  ;tribe
 S XMB(21)=$P($G(^AUPNPAT(ATXPDFN,11)),U,9)  ;tribal quantum
 S X=$P($G(^AUPNPAT(ATXPDFN,11)),U,27) I X]"" S XMB(22)="Pre-bic Tribe: "_$P(^AUTTTRI(X,0),U) ;pre-bic tribe
 S X=$P($G(^DPT(ATXPDFN,0)),U,9) S XMB(23)=$S(X:$E(X,1,3)_"-"_$E(X,4,5)_"-"_$E(X,6,9),1:"Not on file")   ;IHS/CMI/GRL **4** SSN 
 ;
GETHRNS ;Get all HRN's for this patient 
 I $D(^AUPNPAT(ATXPDFN,41)) D
 .Q:$P(^AUPNPAT(ATXPDFN,41,0),U,4)=1
 .S ATXFAC=0,CTR=30
 .F  S ATXFAC=$O(^AUPNPAT(ATXPDFN,41,ATXFAC)) Q:'ATXFAC  D
 ..S ATXHRN=$P(^AUPNPAT(ATXPDFN,41,ATXFAC,0),U,2),XMB(CTR)=$P(^DIC(4,ATXFAC,0),U)_" ("_ATXHRN_")" S CTR=CTR+1
 ;
 I ATXDT,$D(^ATXAX(ATXDT,0)),$P(^ATXAX(ATXDT,0),U,7)]"" S XMB=$P(^XMB(3.6,$P(^ATXAX(ATXDT,0),U,7),0),U)
 S ATXDUZ=DUZ,DUZ=.5
 D ^XMB S DUZ=ATXDUZ
 K XMB,^UTILITY("DIQ1",$J,9000010.07,DA)
 K ATXCOM,ATXSU,ATXFAC,ATXHRN
 Q
