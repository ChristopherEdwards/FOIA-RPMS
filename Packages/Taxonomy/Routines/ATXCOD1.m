ATXCOD1 ; IHS/CMI/LAB - CODE RANGES IN TABLE ;
 ;;5.1;TAXONOMY;**11,15**;FEB 04, 1997;Build 20
 ;
 I 'ATXSUB D ADD I 1
 E  D DEL
 D EOJ
 Q
 ;
ADD ; SECTION FOR ADDING CODES TO TABLE
 I '$D(ATXTBLE) S ATXTBLE(ATX("LOW"))=ATX("HI")_U_ATXSYS G X1
 S ATXDONE=0
 S ATXVAL="" F  S ATXVAL=$O(ATXTBLE(ATXVAL)) Q:ATXVAL=""   I $P(ATXTBLE(ATXVAL),U,2)=ATXSYS D GETCASE,ADDPROC Q:ATXDONE
 D:'ATXDONE SETRANGE
 ;D SETRANGE
 K ATXDONE,ATXVAL,ATXCASE
X1 Q
 ;
ADDPROC ; CASE SPECIFIC ADD PROCESSING
 G @("ADD"_ATXCASE)
ADD2 ;;
ADD3 ;;
ADD4 ;;
 S ATXTBLE(ATX("LOW"))=$P(ATXTBLE(ATXVAL),U,1)_U_ATXSYS
 K ATXTBLE(ATXVAL)
ADD6 ;;
ADD7 ;;
ADD9 ;;
ADD10 ;;
 S ATXDONE=1
 Q
ADD11 ;;
ADD12 ;;
 S ATX("LOW")=ATXVAL
ADD5 ;;
ADD8 ;;
 K ATXTBLE(ATXVAL)
 Q
ADD1 ;;
 I $O(^ICD9("BA",ATX("HI")))=ATXVAL,$E(ATXVAL)=$E(ATX("HI"))!(ATXVAL&ATX("HI")) S ATXTBLE(ATX("LOW"))=ATXTBLE(ATXVAL) K ATXTBLE(ATXVAL) S ATXDONE=1
 Q
ADD13 ;;
 I $O(^ICD9("BA",$P(ATXTBLE(ATXVAL),U,1)))=ATX("LOW"),$E(ATXVAL)=$E(ATX("LOW"))!(ATXVAL&ATX("LOW")) S ATX("LOW")=ATXVAL K ATXTBLE(ATXVAL)
 ; continue
 Q
 ;
SETRANGE ;
 ;;
SET1 ;;
SET5 ;;
SET8 ;;
SET11 ;;
SET12 ;;
SET13 ;;
 S ATXTBLE(ATX("LOW"))=ATX("HI")_U_ATXSYS
 Q
 ;
DEL ; SECTION FOR DELETING CODES FROM TABLE
 I '$D(ATXTBLE) G X2
 S ATXDONE=0
 S ATXVAL="" F  S ATXVAL=$O(ATXTBLE(ATXVAL)) Q:ATXVAL=""  I $P(ATXTBLE(ATXVAL),U,2)=ATXSYS D GETCASE,DELPROC Q:ATXDONE
X2 Q
 ;
DELPROC ;CASE SPECIFIC DEL PROCESSING
 G @("DEL"_ATXCASE)
DEL2 ;;
 I ATX("HI")=$P(ATXTBLE(ATXVAL),U,1) K ATXTBLE(ATXVAL)
 E  S ATXTBLE($O(^ICD9("BA",ATX("HI"))))=ATXTBLE(ATXVAL) K ATXTBLE(ATXVAL)
 S ATXDONE=1
 Q
 ;
DEL3 ;;
DEL6 ;;
 S ATXTBLE($O(^ICD9("BA",ATX("HI"))))=ATXTBLE(ATXVAL)_U_ATXSYS K ATXTBLE(ATXVAL)
 S ATXDONE=1
 Q
 ;
DEL4 ;;
DEL5 ;;
DEL7 ;;
DEL8 ;;
 K ATXTBLE(ATXVAL)
 I ATXCASE'=8,ATXCASE'=5 S ATXDONE=1
 Q
 ;
DEL10 ;;
DEL11 ;;
DEL12 ;;
 D @$S($E(ATXTBLE(ATXVAL))?1N:"NUMBER",1:"LETTER")
 S ATXTBLE(ATXVAL)=ATX("NEWHI")_U_ATXSYS
 I ATXCASE=10 S ATXDONE=1
 Q
 ;
DEL9 ;;
 D @$S($E(ATXTBLE(ATXVAL))?1N:"NUMBER",1:"LETTER")
 S ATXTBLE($O(^ICD9("BA",ATX("HI"))))=ATXTBLE(ATXVAL)_U_ATXSYS
 S ATXTBLE(ATXVAL)=ATX("NEWHI")_U_ATXSYS
 S ATXDONE=1
 Q
 ;
DEL1 ;;
DEL13 ;;
 Q
 ;
GETCASE ; SUBROUTINE TO DETERMINE ATXCASE # FROM INPUT CODE RANGE
 S ATX("TLOW")=ATXVAL,ATX("THI")=$P(ATXTBLE(ATXVAL),U,1)  ;_U_ATXSYS
 D CASEA:ATX("TLOW")]ATX("LOW"),CASEB:ATX("LOW")=ATX("TLOW"),CASEC:ATX("LOW")]ATX("TLOW")
 K ATX("TLOW"),ATX("THI")
 Q
 ;
CASEA ;
 I ATX("HI")]ATX("TLOW") S ATXCASE=$S(ATX("THI")]ATX("HI"):3,ATX("HI")=ATX("THI"):4,1:5)
 E  S ATXCASE=$S(ATX("TLOW")]ATX("HI"):1,1:2)
 Q
 ;
CASEB ;
 S ATXCASE=$S(ATX("THI")]ATX("HI"):6,ATX("HI")=ATX("THI"):7,1:8)
 Q
 ;
CASEC ;
 I ATX("THI")]ATX("LOW") S ATXCASE=$S(ATX("THI")]ATX("HI"):9,ATX("HI")=ATX("THI"):10,1:11)
 E  S ATXCASE=$S(ATX("LOW")=ATX("THI"):12,1:13)
 Q
 ;
NUMBER ;
 S ATX("CODE")=ATX("LOW")-5 F  Q:ATX("LOW")]$O(^ICD9("BA",ATX("CODE")_" "))  S ATX("CODE")=ATX("CODE")-5
 S ATX("CODE")=$O(^ICD9("BA",ATX("CODE")_" ")) F  S ATX("NEWHI")=ATX("CODE"),ATX("CODE")=$O(^ICD9("BA",ATX("CODE"))) Q:ATX("CODE")=ATX("LOW")
 Q
 ;
LETTER ;
 S ATX("LET")=$E(ATX("LOW"))_" " F  S ATX("NEWHI")=$S($L(ATX("LET"))>2:ATX("LET"),1:ATX("LOW")),ATX("LET")=$O(^ICD9("BA",ATX("LET"))) Q:ATX("LET")=ATX("LOW")
 Q
EOJ ;
 K ATXCASE,ATXDONE,ATXNEXT,ATXONE,ATXVAL
 Q
 ;
