ATXPAT ; IHS/OHPRD/TMJ -  ENTER/EDIT PAT TAX FILE ; 
 ;;2.0;IHS PCC SUITE;;MAY 14, 2009
 ;
 Q:$D(ATXQT)
 ;
START ;
 D INIT
 I '$D(^ATXAX(ATXDT,0))!('$D(^ATXPAT(ATXDT,0))#2) D EOJ Q
 S ATXPV=0
 F ATXL=0:0 S ATXPV=$O(^AUPNVPOV("B",ATXDI,ATXPV)) Q:ATXPV'=+ATXPV  S ATXPD=$P(^AUPNVPOV(ATXPV,0),U,2),ATXVIS=$P(^(0),U,3),ATXVIS=$P(^AUPNVSIT(ATXVIS,0),U) D @$S($D(ATXAD):"DIEADD",1:"DIEDEL")
 D EOJ
 Q
 ;
INIT ;
 S ATXDI=DA(1) ;DFN OF THE ICD CODE
 S ATXDT=DA ;DFN OF THE TAXONOMY
 Q
 ;
DIEADD ; ENTRY POINT - ADD PT TO PT TAXONOMY FILE FOR THIS TAXONOMY
 Q:'$D(^ATXPAT(ATXDT,0))#2
 I $P(^ATXAX(ATXDT,0),U,6)="" Q
 I ATXVIS<$P(^ATXAX(ATXDT,0),U,6) Q
 I '$P(^ATXAX(ATXDT,0),U,18)
 E  S ATXLV=$P(^AUPNVSIT(ATXVISDA,0),U,6) I ATXLV'=$P(^ATXAX(ATXDT,0),U,18) Q
 I $D(^ATXPAT(ATXDT,11,ATXPD)) S $P(^(0),U,2)=$P(^(ATXPD,0),U,2)+1 Q
 S ^ATXPAT(ATXDT,11,ATXPD,0)=ATXPD_"^1"
 S $P(^ATXPAT(ATXDT,11,0),U,3)=ATXPD,$P(^(0),U,4)=$P(^(0),U,4)+1
 NEW (DT,DTIME,DUZ,IO,IOF,IOM,IOS,IOSL,IOXY,U,XQDIC,XQPSM,XQY,IOST,XQYO,ZTQUEUED,ATXPD,ATXDT) D
 . S DA=ATXPD,DA(1)=ATXDT,DIK="^ATXPAT(ATXDT,11," D IX1^DIK K DIK,DA
 Q
 ;
DIEDEL ; ENTRY POINT - SUBTRACT ONE FROM VISIT COUNTER; IF 0, DELETE PT
 Q:'$D(^ATXPAT(ATXDT,0))#2
 Q:'$D(^ATXPAT(ATXDT,11,ATXPD,0))
 I $P(^ATXAX(ATXDT,0),U,6)="" Q
 I ATXVIS<$P(^ATXAX(ATXDT,0),U,6) Q
 S $P(^(0),U,2)=$P(^ATXPAT(ATXDT,11,ATXPD,0),U,2)-1
 I $P(^ATXPAT(ATXDT,11,ATXPD,0),U,2) Q
 NEW (DT,DTIME,DUZ,IO,IOF,IOM,IOS,IOSL,IOXY,U,XQDIC,XQPSM,XQY,IOST,XQYO,ZTQUEUED,ATXPD,ATXDT) D
 . S DA=ATXPD,DA(1)=ATXDT,DIK="^ATXPAT(ATXDT,11," D ^DIK K DIK,DA
 Q
 ;
EOJ ;
 K ATXPV,ATXDI,ATXDT,ATXAD,ATXH,ATXY,ATXPD,ATXLV
 Q
 ;
