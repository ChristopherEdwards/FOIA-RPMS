ATXTAXF ; IHS/CMI/LAB - DISPLAY IND LISTS ;
 ;;5.1;TAXONOMY;**11**;FEB 04, 1997;Build 48
 ;; ;
ICD9ADD ;EP
 S ATXFLG=""
 I ATXFILE=80 S ATXGBL="^ICD9("
 I ATXFILE=80.1 S ATXGBL="^ICD0("
 S ATXX=ATXTAXI
 D BUILD,^ATXCODE
 D EDIT
 K ATXFLG,ATXX,ATXL,ATX,ATXTBLE,ATXSYS
 Q
 ;
BUILD ;BUILD ATXTBLE FOR ROUTINE ATXCODE
 NEW X,Y,A,B
 S X="" F  S X=$O(^ATXAX(ATXX,21,"B",X)) Q:X=""  D
 .S Y=0 F  S Y=$O(^ATXAX(ATXX,21,"B",X,Y)) Q:Y=""  D
 ..S A=$P(^ATXAX(ATXX,21,Y,0),U,1),B=$P(^ATXAX(ATXX,21,Y,0),U,2),C=$P(^ATXAX(ATXX,21,Y,0),U,3)
 ..S ATXTBLE(A)=B_U_C
 Q
ENTER ;
 S ATX=0 F  S ATX=$O(ATXTBLE(ATX)) Q:ATX=""  S ATX("X")=$$STRIP^XLFSTR(ATX," ") D CALLDIE I $D(Y) S ATXSTP=1 Q
 I $P(^ATXAX(ATXX,0),U,8),$P(^(0),U,15)=80 D DFNS^ATXAX  ;IF CREATED BY TAXONOMY SYSTEM - EDIT 41 MULTIPLE IN FILE 80
 W !!,"Done!"
 Q
 ;
CALLDIE ;
 S ATXHI=$$STRIP^XLFSTR($P(ATXTBLE(ATX),U,1)," ")
 S ATXSYS=$P(ATXTBLE(ATX),U,2)
 S DIE="^ATXAX(",DR="2101///"_ATX("X")_" ",DA=ATXX,DR(2,9002226.02101)=".02////"_ATXHI_" "_";.03////"_ATXSYS D ^DIE I $D(Y)
 E  I '$D(ZTQUEUED),$D(ATX("NOT TAX")) W "."
 I $D(Y),'$D(ZTQUEUED),$D(ATX("NOT TAX")) W !!,"ERROR ENCOUNTERED - REENTER RANGES",!
 K DIE,DR,DA
 Q
 ;
EDIT ;DELETE CODES IN TAXONOMY FILE, THEN ENTER NEW RANGES
 ;IF CREATED BY TAXONOMY SYSTEM, DELETE ALL FROM ICD9 41 MULTIPLE
 W !,"hold on..."
 I $P(^ATXAX(ATXX,0),U,8),$P(^(0),U,15)=80 D DFNS^ATXDEL  ;delete out of icd9 41 multiple
 S ATX=0 F  S ATX=$O(^ATXAX(ATXX,21,ATX)) Q:ATX'=+ATX  S DA(1)=ATXX,DA=ATX,DIK="^ATXAX("_DA(1)_",21," D ^DIK K DIK,DA
 D ENTER
 Q
 ;
EOJ1 ;
 K ATXHI
 I $D(ZTQUEUED) S ZTREQ="@"
 Q
 ;
