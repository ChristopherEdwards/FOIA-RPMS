ATXTAXL ; IHS/CMI/LAB - DISPLAY IND LISTS ;
 ;;5.1;TAXONOMY;**11**;FEB 04, 1997;Build 48
 ;; ;
ICPTADD ;EP
 S ATXFLG=""
 I ATXFILE=81 S ATXGBL="^ICPT("
 S ATXX=ATXTAXI
 D BUILD,^ATXTAXM
 D EDIT
 K ATXFLG,ATXX,ATXL,ATX,ATXTBLE
 Q
 ;
BUILD ;BUILD ATXTBLE FOR ROUTINE ATXCODE
 S ATXCODE=0 F  S ATXCODE=$O(^ATXAX(ATXX,21,"AA",ATXCODE)) Q:ATXCODE=""  S ATXTBLE(ATXCODE)=$O(^(ATXCODE,""))
 Q
ENTER ;
 S ATX=0 F  S ATX=$O(ATXTBLE(ATX)) Q:ATX=""  S ATX("X")=$E(ATX,1,($L(ATX)-1)) D CALLDIE I $D(Y) S ATXSTP=1 Q
 W !!,"Done!"
 Q
 ;
CALLDIE ;
 S ATXHI=$E(ATXTBLE(ATX),1,($L(ATXTBLE(ATX))-1)),DIE="^ATXAX(",DR="2101///"_ATX("X")_" ",DA=ATXX,DR(2,9002226.02101)=".02////"_ATXHI_" " D ^DIE I $D(Y)
 E  I '$D(ZTQUEUED),$D(ATX("NOT TAX")) W "."
 I $D(Y),'$D(ZTQUEUED),$D(ATX("NOT TAX")) W !!,"ERROR ENCOUNTERED - REENTER RANGES",!
 K DIE,DR,DA
 Q
 ;
EDIT ;DELETE CODES IN TAXONOMY FILE, THEN ENTER NEW RANGES
 S ATX=0 F  S ATX=$O(^ATXAX(ATXX,21,ATX)) Q:ATX'=+ATX  S DA(1)=ATXX,DA=ATX,DIK="^ATXAX("_DA(1)_",21," D ^DIK K DIK,DA
 D ENTER
 Q
 ;
EOJ1 ;
 K ATXHI
 I $D(ZTQUEUED) S ZTREQ="@"
 Q
 ;
