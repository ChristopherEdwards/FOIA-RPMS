ATXTV1 ; IHS/CMI/LAB - DISPLAY IND LISTS ;
 ;;5.1;TAXONOMY;**11**;FEB 4, 1997;Build 48
 ;; ;
EP(ATXTAXI,ATXTYPE) ;EP - CALLED FROM OPTION
 NEW ATXRPTTT,ATXRPTT1,ATXRPTT2
 D EN
 Q
EOJ ;EP
 D EN^XBVK("ATX")
 Q
 ;; ;
EN ;EP -- main entry point for 
 D TERM^VALM0
 D EN^VALM("ATX TAXONOMY VIEW ONE")
 D CLEAR^VALM1
 D FULL^VALM1
 W:$D(IOF) @IOF
 D EOJ
 Q
 ;
HDR ; -- header code
 S VALMHDR(1)="Display of the "_$$NAME(ATXTAXI,ATXTYPE)_" taxonomy"
 S VALMHDR(2)="* View Taxonomies"
 Q
 ;
NAME(I,T) ;
 I T="L" Q $P(^ATXLAB(I,0),U)
 I T'="L" Q $P(^ATXAX(I,0),U)
 Q ""
INIT ; -- init variables and list array
 ;
 S VALMSG="Select the Appropriate Action   Q to Quit"
 I ATXTYPE="L" S ATXFILE=60
 I ATXTYPE'="L" S ATXFILE=$P(^ATXAX(ATXTAXI,0),U,15)
 I ATXTYPE="L" D LAB Q
 I $P(^ATXAX(ATXTAXI,0),U,13) D CANDISP Q
 K ATXITEM S ATXHIGH="",C=0
 S ATXX=0 F  S ATXX=$O(^ATXAX(ATXTAXI,21,"B",ATXX)) Q:ATXX=""  D
 .S ATXY=$O(^ATXAX(ATXTAXI,21,"B",ATXX,ATXY)) Q:ATXY'=+ATXY  D
 ..S C=C+1
 ..S ATXITMI=$P(^ATXAX(ATXTAXI,21,ATXY,0),U)
 ..I ATXFILE=9999999.05 S ATXITEM(C,0)=C_")  "_ATXITMI I 1
 ..E  S ATXITEM(C,0)=C_")  "_$$VAL^XBDIQ1($P(^ATXAX(ATXTAXI,0),U,15),ATXITMI,.01)
 ..S ATXITEM("IDX",C,C)=ATXITMI
 .Q
 S (VALMCNT,ATXHIGH)=C
 Q
CANDISP ;
 K ATXITEM S ATXHIGH="",C=0
 S ATXX=0 F  S ATXX=$O(^ATXAX(ATXTAXI,21,"B",ATXX)) Q:ATXX=""  D
 .S ATXY=0 F  S ATXY=$O(^ATXAX(ATXTAXI,21,"B",ATXX,ATXY)) Q:ATXY=""  D
 ..S C=C+1
 ..S ATXITEM(C,0)=C_")  "_$P(^ATXAX(ATXTAXI,21,ATXY,0),U)_" through  "_$P(^ATXAX(ATXTAXI,21,ATXY,0),U,2) I $P(^ATXAX(ATXTAXI,21,ATXY,0),U,3) S $E(ATXITEM(C,0),40)=$P(^ICDS($P(^ATXAX(ATXTAXI,21,ATXY,0),U,3),0),U,1)
 ..S ATXITEM("IDX",C,C)=ATXX
 .Q
 S (VALMCNT,ATXHIGH)=C
 Q
LAB ;
 K ATXITEM S ATXHIGH="",C=0
 S ATXX=0 F  S ATXX=$O(^ATXLAB(ATXTAXI,21,ATXX)) Q:ATXX'=+ATXX  D
 .S C=C+1
 .S ATXITMI=$P(^ATXLAB(ATXTAXI,21,ATXX,0),U)
 .S ATXITEM(C,0)=C_")  "_$P($G(^LAB(60,ATXITMI,0)),U)
 .S ATXITEM("IDX",C,C)=ATXITMI
 .Q
 S (VALMCNT,ATXHIGH)=C
 Q
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 Q
 ;
EXPND ; -- expand code
 Q
 ;
BACK ;go back to listman
 D TERM^VALM0
 S VALMBCK="R"
 D INIT
 D HDR
 K DIR
 K X,Y,Z,I
 Q
