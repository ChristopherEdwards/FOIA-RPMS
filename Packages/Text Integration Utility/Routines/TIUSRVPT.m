TIUSRVPT ; SLC/JER - Set Methods for Documents ;31-OCT-2001 16:44:33
 ;;1.0;TEXT INTEGRATION UTILITIES;**122**;Jun 20, 1997
SETTEXT(TIUY,TIUDA,TIUX,SUPPRESS) ; Save Text - use Buffered I/O
 N PAGES,PAGE S TIUY=0,SUPPRESS=$G(SUPPRESS,0)
 I '$D(^TIU(8925,TIUDA)) S TIUY="0^0^0^Attempt to file data in a Nonexistent Entry." Q
 S PAGE=$P($G(TIUX("HDR")),U),PAGES=$P($G(TIUX("HDR")),U,2)
 I $S('PAGE:1,'PAGES:1,1:0) S TIUY="0^0^0^Invalid Message" Q
 I PAGE=1 D MERGTEMP^TIUEDI1(TIUDA) K ^TIU(8925,+TIUDA,"TEMP"),^("TEXT")
 M ^TIU(8925,+TIUDA,"TEMP")=TIUX("TEXT")
 ;if done, commit changes
 I 'SUPPRESS,(PAGE=PAGES),$D(^TIU(8925,TIUDA,"TEMP")) D
 . N TIUC,TIUI,TIU S (TIUC,TIUI)=0
 . F  S TIUI=$O(^TIU(8925,TIUDA,"TEMP",TIUI)) Q:+TIUI'>0  D
 . . S TIUC=TIUC+1
 . I TIUC>0 S ^TIU(8925,TIUDA,"TEMP",0)="^^"_TIUC_U_TIUC_U_DT_"^^"
 . D GETTIU^TIULD(.TIU,TIUDA)
 . K ^TIU(8925,TIUDA,"TEXT")
 . D MERGTEXT^TIUEDI1(+TIUDA,.TIU)
 . K ^TIU(8925,TIUDA,"TEMP")
 ; Acknowledge success / ask for next page
 S TIUY=TIUDA_U_PAGE_U_PAGES
 Q
ADMNCLOS(TIUY,TIUDA,MODE,PERSON)       ; Post Administrative Closure Information
 N TIUX,TIUI,TIUCLBY,TIUCLTTL
 I '$D(^TIU(8925,TIUDA)) S TIUY="0^Attempt to file data in a Nonexistent Entry." Q
 S MODE=$G(MODE,"S")
 S PERSON=$G(PERSON,DUZ)
 S TIUCLBY=$$SIGNAME^TIULS(PERSON)
 S TIUCLTTL=$$SIGTITL^TIULS(PERSON)
 S TIUX(.05)=7
 S TIUX(1606)=$G(DT)
 S TIUX(1607)=TIUCLBY
 S TIUX(1608)=TIUCLTTL
 S TIUX(1613)=MODE
 D FILE^TIUSRVP(.TIUY,TIUDA,.TIUX)
 S TIUI=$P($G(^TIU(8925,TIUDA,"TEXT",0)),U,3)+1
 ;If scanned document set document body to informational text
 I MODE="S" D
 . S ^TIU(8925,+TIUDA,"TEXT",TIUI,0)=" ",TIUI=TIUI+1
 . S ^TIU(8925,+TIUDA,"TEXT",TIUI,0)="                           *** SCANNED DOCUMENT ***",TIUI=TIUI+1
 . S ^TIU(8925,+TIUDA,"TEXT",TIUI,0)="                            SIGNATURE NOT REQUIRED",TIUI=TIUI+1
 . S ^TIU(8925,+TIUDA,"TEXT",TIUI,0)=" ",TIUI=TIUI+1
 S ^TIU(8925,TIUDA,"TEXT",TIUI,0)=" ",TIUI=TIUI+1
 S ^TIU(8925,TIUDA,"TEXT",TIUI,0)="Administrative Closure: "_$$DATE^TIULS(DT,"MM/DD/CCYY"),TIUI=TIUI+1
 S ^TIU(8925,TIUDA,"TEXT",TIUI,0)="                    by: "_TIUCLBY,TIUI=TIUI+1
 S ^TIU(8925,TIUDA,"TEXT",TIUI,0)="                        "_TIUCLTTL
 S ^TIU(8925,+TIUDA,"TEXT",0)="^^"_TIUI_U_TIUI_U_DT_"^^"
 D ALERTDEL^TIUALRT(TIUDA)
 Q
