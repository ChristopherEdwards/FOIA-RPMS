ABMDBRUN ; IHS/ASDST/DMJ - Reprint Unpaid Bills ;
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;
 K ABM,ABMY S ABMP("CNT")=0
SEL W ! K DIC S DIC(0)="QEAM",DIC="^ABMDEXP(",DIC("A")="Select FORM to Re-Print: ",DIC("S")="I '$P($G(^(1)),U,3)"
 D ^DIC G XIT:$D(DUOUT)!$D(DTOUT)!(X=""),SEL:Y<1
 S ABMP("FORM")=Y
 S ABMP("TYP")=0,ABM("NODX")="",ABM("OVER-DUE")=""
 D ^ABMDRSEL G XIT:$D(DTOUT)!$D(DUOUT)
 ;
 S ABMP("X")=$S($G(ABMY("DT"))="V":"AD",$G(ABMY("DT"))="A":"AP",$G(ABMY("DT"))="X":"AX",$G(ABMY("DT"))="P":"AE",$D(ABMY("INS")):"AJ",$D(ABMY("PAT")):"D",1:1)
 I ABMP("X") S ABM=0 F  S ABM=$O(^ABMDBILL(DUZ(2),ABM)) G XIT:'ABM D DATA
 G OTH:$G(ABMY("DT"))="" S ABMP("DT")=ABMY("DT",1)-1
 I "APV"[$G(ABMY("DT")) S ABMP("DT")=ABMY("DT",1)-1 F  S ABMP("DT")=$O(^ABMDBILL(DUZ(2),ABMP("X"),ABMP("DT"))) G XIT:'+ABMP("DT")!(ABMP("DT")>ABMY("DT",2)) D
 .S ABM="" F  S ABM=$O(^ABMDBILL(DUZ(2),ABMP("X"),ABMP("DT"),ABM)) Q:'ABM  D DATA
 I $G(ABMY("DT"))="X" S ABMP("DT")=ABMY("DT",1)-1 F  S ABMP("DT")=$O(^ABMDTXST(DUZ(2),"B",ABMP("DT"))) G XIT:'ABMP("DT")!(ABMP("DT")>ABMY("DT",2)) D
 .S ABMP("DTD")=0 F  S ABMP("DTD")=$O(^ABMDTXST(DUZ(2),"B",ABMP("DT"),ABMP("DTD"))) Q:'ABMP("DTD")  D
 ..I $P(^ABMDTXST(DUZ(2),ABMP("DTD"),0),U,2)'=+ABMP("FORM") Q
 ..S ABM=0 F  S ABM=$O(^ABMDBILL(DUZ(2),"AX",ABMP("DTD"),ABM)) Q:'ABM  D DATA
OTH S ABM="",ABMP("RI")=$S(ABMP("X")="AJ":ABMY("INS"),1:ABMY("PAT")) F  S ABM=$O(^ABMDBILL(DUZ(2),ABMP("X"),ABMP("RI"),ABM)) Q:'ABM  D DATA
 G XIT
 ;
DATA ;CHECK FOR HIT
 Q:"BT"'[$P(^ABMDBILL(DUZ(2),ABM,0),"^",4)
 S ABMP("HIT")=0 D ^ABMDRCHK Q:'ABMP("HIT")
 S ABMP("CNT")=ABMP("CNT")+1
 S:'$D(ABMY("FORM")) ABMY("FORM")=ABMP("FORM")
 S ABMY(ABM)=""
 Q
 ;
XIT K ABM
 Q
