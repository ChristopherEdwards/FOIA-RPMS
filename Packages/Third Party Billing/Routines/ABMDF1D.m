ABMDF1D ; IHS/ASDST/DMJ - Set UB82 Print Array - Part 4 ;
 ;;2.6;IHS 3P BILLING SYSTEM;**6**;NOV 12, 2009
 ;Original;TMD;
 ;
 S ABMP("GL")="^ABMDBILL(DUZ(2),"_ABMP("BDFN")_","
 D ^ABMDESM1
 I $D(ABMP("FLAT")) S ABMF(16)="NON-CVD"
 S ABMS="" F I=17:1:39 S ABMS=$O(ABMS(ABMS)) Q:'ABMS  D
 .S ABMF(I)=$S($P($G(ABMP("FLAT")),U,6)]"":$P(ABMP("FLAT"),U,6),1:$P($G(^AUTTREVN(ABMS,0)),U,2))
 .S $P(ABMF(I),U,2)=$S($D(ABMS("CPT")):$P(ABMS("CPT",1),U,2),$P($G(ABMP("FLAT")),U,7)]"":$P(ABMP("FLAT"),U,7),$P(ABMS(ABMS),U,3)]"":$J($P(ABMS(ABMS),U,3),7,2),1:"")
 .I $P(ABMS(ABMS),U,3)=""&($P(ABMS(ABMS),U,4)]"") S $P(ABMF(I),U,2)=$P(ABMS(ABMS),U,4)
 .S $P(ABMF(I),U,3)=ABMS
 .S $P(ABMF(I),U,4)=$P(ABMS(ABMS),U,2)
 .S $P(ABMF(I),U,5)=$P(ABMS(ABMS),U)
 .S $P(ABMF(I),U,6)=$P(ABMS(ABMS),U,5)
 I '+ABMS,$D(ABMS("CPT")) S ABMS=1 F I=I:1:39 S ABMS=$O(ABMS("CPT",ABMS)) Q:ABMS=""  D
 .S ABMF(I)=$P(ABMS("CPT",ABMS),U)_U_$P(ABMS("CPT",ABMS),U,2)_U_$P(ABMS("CPT",ABMS),U,3)_U_$P(ABMS("CPT",ABMS),U,4)_U_$P(ABMS("CPT",ABMS),U,5)
 ;
 I I=39,+ABMS K ABMF("C") F I=39:1 S ABMS=$O(ABMS(ABMS)) Q:'ABMS  D
 .S ABMR(I)=$P($G(^AUTTREVN(ABMS,0)),U,2)
 .I $P(ABMS(ABMS),U,3)]"" S $P(ABMR(I),U,2)=$J($P(ABMS(ABMS),U,3),7,2)
 .I $P(ABMS(ABMS),U,3)=""&($P(ABMS(ABMS),U,4)]"") S $P(ABMR(I),U,2)=$P(ABMS(ABMS),U,4)
 .S $P(ABMR(I),U,3)=ABMS
 .S $P(ABMR(I),U,4)=$P(ABMS(ABMS),U,2)
 .S $P(ABMR(I),U,5)=$P(ABMS(ABMS),U)
 .S $P(ABMR(I),U,6)=$P(ABMS(ABMS),U,5)
 .Q
 I I=39 S $P(ABMF(I),U,1)="     TOTAL CHARGES"_U_U_"001"_U_U_ABMS("TOT")
 I I<39 S $P(ABMF(I+1),U,1)="     TOTAL CHARGES"_U_U_"001"_U_U_ABMS("TOT")
 E  S ABMR("TOT")="     TOTAL CHARGES"_U_U_"001"_U_U_ABMS("TOT")
 ;
 S ABM("P8")=ABMS("TOT")-ABMP("PAID")-ABMP("TW")
 I $D(ABMP("FLAT")) S ABM("P8")=ABM("P8")-$P(ABMS($O(ABMS(0))),U,5)
 I ABM("P8")<0 S ABM("P8")=0
 ;I ABMP("PAYOR")=44 S $P(ABMF(44),U)=ABM("P8")+$P(ABMF(44),U,2),$P(ABMF(44),U,3)=ABM("P8") S:$P(ABMF(44),U,2)="" $P(ABMF(44),U,2)=0  ;IHS/SD/AML NOHEAT 2/15/2011
 ;E  S $P(ABMF(ABMP("PAYOR")),U,6)=ABM("P8")+$P(ABMF(ABMP("PAYOR")),U,7),$P(ABMF(ABMP("PAYOR")),U,8)=ABM("P8") S:$P(ABMF(ABMP("PAYOR")),U,2)="" $P(ABMF(ABMP("PAYOR")),U,2)=0  ;IHS/SD/AML NOHEAT 2/15/2011
 S $P(ABMY("TOT"),U,2)=$P(ABMY("TOT"),U,2)+ABM("P8")
 I '$D(ABMY("TINS",$P(^ABMDBILL(DUZ(2),ABMP("BDFN"),0),U,8))) S ABMY("TINS",$P(^(0),U,8))="",$P(ABMY("TOT"),U,3)=$P(ABMY("TOT"),U,3)+1
 K ABMS
 ;
 G ^ABMDF1E
