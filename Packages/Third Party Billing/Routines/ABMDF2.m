ABMDF2 ; IHS/ASDST/DMJ - Set HCFA-1500 Print Array ; 
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;Original;TMD;
 ;
 K ABMP S U="^",ABMP("XMIT")=0,ABMP("EXP")=2,ABMY("TOT")="0^0^0"
BDFN S ABMY("N")=0 F  S ABMY("N")=$O(ABMY(ABMY("N"))) Q:'ABMY("N")  D
 .S ABMP("BDFN")="" F  S ABMP("BDFN")=$O(ABMY(ABMY("N"),ABMP("BDFN"))) Q:'ABMP("BDFN")  D
 ..Q:'$D(^ABMDBILL(DUZ(2),ABMP("BDFN"),0))
 ..D ENT
 ..I +$O(ABMR("")) S ABMR("MORE")=""
 ..D ^ABMDF2X
 ..I +$O(ABMR("")) D HCFA
XMIT ..I ABMP("XMIT")=0 S ABM("XM")="" F  S ABM("XM")=$O(^ABMDTXST(DUZ(2),"B",DT,ABM("XM"))) Q:'ABM("XM")  D  Q:ABMP("XMIT")
 ...Q:'$D(^ABMDTXST(DUZ(2),ABM("XM"),0))  Q:$P(^(0),U,2)'=ABMP("EXP")
 ...I $D(ABMY("TYP")),$P(^ABMDTXST(DUZ(2),ABM("XM"),0),U,3)=ABMY("TYP") S ABMP("XMIT")=ABM("XM")
 ...I $D(ABMY("INS")),$P(^ABMDTXST(DUZ(2),ABM("XM"),0),U,4)=ABMY("INS") S ABMP("XMIT")=ABM("XM")
 ..I '+ABMP("XMIT") S DIC="^ABMDTXST(DUZ(2),",DIC(0)="L",X=DT,DIC("DR")=".02////2;.07////1;.08////1;"_$S($D(ABMY("TYP")):".03////"_$P(ABMY("TYP"),U),$D(ABMY("INS")):".04////"_ABMY("INS"),1:".03////A")_";.05////"_DUZ
 ..I  K DD,DO D FILE^DICN S ABMP("XMIT")=+Y
 ..S DIE="^ABMDBILL(DUZ(2),",DA=ABMP("BDFN"),DR=".04////B;.16////A;.17////"_ABMP("XMIT") D ^ABMDDIE Q:$D(ABM("DIE-FAIL"))
 ..K ^ABMDBILL(DUZ(2),"AS",+^ABMDBILL(DUZ(2),ABMP("BDFN"),0),"A",ABMP("BDFN")),^ABMDBILL(DUZ(2),"AC","A",ABMP("BDFN"))
 ..S ABM=ABMP("BDFN"),ABM("L")=ABMP("XMIT") K ABMP S ABMP("XMIT")=ABM("L"),ABMP("BDFN")=ABM
 G XIT
 ;
HCFA ;EP for printing HCFA 1500's
 S ABMR="" F ABMR("I")=37:2:47 S ABMR=$O(ABMR(ABMR)) Q:'ABMR  D
 .S ABMF(ABMR("I"))=$P(ABMR(ABMR),U,2)_U_$S(ABMP("VTYP")=111!($G(ABMP("BTYP"))=111):1,1:3)_U_$P(ABMR(ABMR),U,4)_U_U_$P(ABMR(ABMR),U,5)_U_$P(ABMR(ABMR),U)_U_$P(ABMR(ABMR),U,6)_U_$P(ABMR(ABMR),U,7)
 .S ABMF(ABMR("I")+1)=""
 .I $P(ABMR(ABMR),U,2)'=$P(ABMR(ABMR),U,3) S ABMF(ABMR("I")+1)=$P(ABMR(ABMR),U,3)
 .I $L($P(ABMR(ABMR),U,8))>19 S ABMU("LNG")=19,ABMU("TXT")=$P(ABMR(ABMR),U,8),ABMU=2 D LNG^ABMDWRAP S $P(ABMF(ABMR("I")),U,4)=ABMU(1),$P(ABMF(ABMR("I")+1),U,4)=ABMU(2) K ABMU I 1
 .E  S $P(ABMF(ABMR("I")),U,4)=$P(ABMR(ABMR),U,8)
 .K ABMR(ABMR)
 .Q
 I ABMR("I")=47,+$O(ABMR(ABMR)) D ^ABMDF2X G HCFA
 S ABMF(50)=$S($D(ABMF(50)):ABMF(50),1:"")_ABMR("TOT")
 S $P(ABMF(50),U,3)=$S($P(ABMF(50),U,3)>999:$FN($P(ABMF(50),U,3),",",0),1:$FN($P(ABMF(50),U,3),",",2))
 I ABMR("I")'=47 F ABMR("I")=ABMR("I"):1:48 K ABMF(ABMR("I"))
 K ABMR("MORE")
 D ^ABMDF2X
 Q
 ;
ENT ;EP for setting up export array
 D ^ABMDF2A
 G ^ABMDF2B
 ;
XIT K ABM,ABMV,ABMF
 Q
