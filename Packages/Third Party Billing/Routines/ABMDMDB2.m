ABMDMDB2 ;IHS/ASDST/DMJ - MEDICARE B CLAIM SPLIT   
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;
 ; IHS/SD/SDR - v2.5 p9 - IM16055
 ;   Fixed global reference
 ;
START ;set start
 S $P(^ABMDEXP(20,0),"^",8)=$P($G(^ABMDEXP(20,0)),"^",8)_",25,26"
 S ^ABMQUES(25,0)="DATE LAST SEEN^W25^ABMDE30^25^ABMDE3C"
 S ^ABMQUES(26,0)="SUPERVISING PROV UPIN^W26^ABMDE30^26^ABMDE3C"
 S ^ABMQUES("B","DATE LAST SEEN",25)=""
 S ^ABMQUES("B","SUPERVISING PROV UPIN",26)=""
 Q:$G(^ABMCNVRT("MDB2","START"))
 S ^ABMCNVRT("MDB2","START")=$H
ALL ;all sites
 S ABMDUZ2=DUZ(2)
 S DUZ(2)=0
 F  S DUZ(2)=$O(^ABMDCLM(DUZ(2))) Q:'DUZ(2)  Q:DUZ(2)'=+DUZ(2)  D
 .D ONE
 S DUZ(2)=ABMDUZ2
 K ABMDUZ2
 S ^ABMCNVRT("MDB2","STOP")=$H
 Q
ONE ;one site
 Q:$P($G(^ABMDPARM(DUZ(2),1,5)),U)
 W !!,"Site= ",$P($G(^AUTTLOC(DUZ(2),0)),"^",2)
 S ABMDT=3010700
 F  S ABMDT=$O(^ABMDCLM(DUZ(2),"AD",ABMDT)) Q:'ABMDT  D
 .S ABMCLM=0
 .F  S ABMCLM=$O(^ABMDCLM(DUZ(2),"AD",ABMDT,ABMCLM)) Q:'ABMCLM  D
 ..D CLAIM
 Q
CLAIM ;one claim
 S ABMVTYP=+$P(^ABMDCLM(DUZ(2),ABMCLM,0),"^",7)
 S ABMCLIN=+$P(^ABMDCLM(DUZ(2),ABMCLM,0),"^",6)
 Q:ABMVTYP=999
 Q:ABMVTYP=997
 Q:ABMCLIN=39
 S I=0,ABMCR=0
 F  S I=$O(^ABMDCLM(DUZ(2),ABMCLM,13,I)) Q:'I  D
 .S ABMINS=+$P(^ABMDCLM(DUZ(2),ABMCLM,13,I,0),U)
 .Q:$P($G(^AUTNINS(ABMINS,2)),U)'="R"
 .S ABMCR=ABMINS
 Q:'ABMCR
 S ABMCOV=0
 S I=0
 F  S I=$O(^AUTTPIC("C",ABMCR,I)) Q:'I  D
 .Q:$P(^AUTTPIC(I,0),U)'["B"
 .S ABMCOV=I
 Q:$$UPRV^ABMDUTL(ABMCLM,ABMCOV)
 Q:$D(^AUTNINS(ABMCR,17,"B",ABMCLIN))
 Q:$D(^AUTTPIC(ABMCOV,11,ABMCLIN,0))
 Q:$P($G(^ABMNINS(DUZ(2),ABMCR,1,ABMVTYP,0)),"^",7)="N"
 S ABMPAT=$P(^ABMDCLM(DUZ(2),ABMCLM,0),U)
 S ABMVDT=$P(^ABMDCLM(DUZ(2),ABMCLM,0),"^",2)
 S I=0,ABMDB=0
 F  S I=$O(^ABMDCLM(DUZ(2),"B",ABMPAT,I)) Q:'I  D
 .Q:$P(^ABMDCLM(DUZ(2),I,0),"^",2)'=ABMVDT
 .Q:$P(^ABMDCLM(DUZ(2),I,0),"^",7)'=999
 .S ABMDB=1
 Q:ABMDB
 D MAIN^ABMDSPLB(ABMCLM)
 W "."
 Q
