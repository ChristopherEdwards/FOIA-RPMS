ABMDRPOL ; IHS/ASDST/DMJ - REPORT OF PI POLICY HOLDERS ; 
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;Original;TMD;
 ;
 W ! K DIR S DIR(0)="Y",DIR("A")="Limit Report to a Specfic INSURER",DIR("B")="Y" D ^DIR K DIR G XIT:$D(DTOUT)!$D(DUOUT) I Y'=1 S ABM(1)="A",ABM(2)="ZZ" G ZIS
 ;
SEL K DIC W ! S DIC="^AUTNINS(",DIC(0)="QEAM" D ^DIC K DIC
 G XIT:X=""!$D(DTOUT)!$D(DUOUT),SEL:+Y<1
 S (ABM(1),ABM(2))=$P(^AUTNINS(+Y,0),U)
 ;
ZIS W !!!,"NOTE: This Report Requires 132 Character Width."
 W ! S %ZIS="NQ",%ZIS("B")="" D ^%ZIS G:'$D(IO)!$G(POP) XIT
 S ABM("ION")=ION G:$D(IO("Q")) QUE
 ;
PRQUE ;ENTER FROM TASK MANABMER
 S L=0,DIC="^AUPNPRVT(",FLDS="[ABMD TM PI ELIGIBLE]",BY="[ABMD TM PI ELIGIBLE]",FR=ABM(1)_",?,?",TO=ABM(2)_",?,?"
 S IOP=ABM("ION")_";"_IOST_";132"_";"_IOSL,PG=1
 D EN1^DIP
XIT D ^%ZISC
 D:$D(ZTQUEUED) KILL^%ZTLOAD
 K ABM,TO,BY,FROM,ZTSK,POP,FLDS,DIC,PG,IOP,%ZIS,DIR
 Q
QUE S ZTRTN="PRQUE^ABMDRPOL",ZTDESC="REPORT OF PI POLICY HOLDERS"
 D QUE^ABMDRUTL
 G XIT
