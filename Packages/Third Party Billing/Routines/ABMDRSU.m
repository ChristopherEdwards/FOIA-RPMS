ABMDRSU ; IHS/ASDST/DMJ - Display Summarized Claim ;
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;Original;TMD;
 ;
SEL K ABM,ABMP
 S ABM("STA")="F",ABM("STA","NM")="Flagged as Billable"
 S ABM("NODX")="" D ^ABMDRSEL I $D(DTOUT)!$D(DUOUT)!$D(DIROUT) K ABM,DTOUT,DUOUT,DIROUT Q
 S ABMP("HEAD2")="SUMMARIZED LISTING of CLAIMS "_ABM("STA","NM"),ABM("HD",0)="" D HD^ABMDRHD S ABMP("HEAD3")=ABM("HD",0)
 S ABMP("OPT")=ABM("STA"),ABMP("PG")=0
 I ABMP("OPT")'="",'$D(^ABMDCLM(DUZ(2),"AS",ABMP("OPT"))) D  G DONE
 .W !,"No Claims to report",!
 .S DIR(0)="E" D ^DIR K DIR
 S ABMQ("RP")="ENT^ABMDRSU",ABMQ("NS")="ABM",ABMQ("RX")="POUT^ABMDRUTL"
 D ^ABMDRDBQ
 Q
 ;
ENT ;TaskMan Entry Point
 S ABMP("CDFN")="" K ABMP("QUIT")
 I ABMP("OPT")="" F ABMP("OPT")="E","F","R","U","C" D L2 Q
L2 ;LOOP THROUGH 'AS' X-REF      
 F  S ABMP("CDFN")=$O(^ABMDCLM(DUZ(2),"AS",ABMP("OPT"),ABMP("CDFN"))) Q:ABMP("CDFN")=""!($D(ABMP("QUIT")))  D
 .Q:'$D(^ABMDCLM(DUZ(2),ABMP("CDFN"),0))  S ABM("V")=$P(^(0),U,7),ABM("L")=$P(^(0),U,3),ABM("I")=$P(^(0),U,8),ABM("P")=$P(^(0),U),ABM("D")=$P(^(0),U,2)
 .Q:ABM("L")=""!(ABM("I")="")!(ABM("P")="")!(ABM("D")="")!(ABM("V")="")
 .Q:'$D(^AUTNINS(ABM("I"),0))
 .I $D(ABMY("PAT")),ABMY("PAT")'=ABM("P") Q
 .I $D(ABMY("LOC")),ABMY("LOC")'=ABM("L") Q
 .I $D(ABMY("INS")),ABMY("INS")'=ABM("I") Q
 .I $D(ABMY("TYP")) Q:ABMY("TYP")'[$P($G(^AUTNINS(ABM("I"),2)),U)
 .I $D(ABMY("DT")),ABM("D")<ABMY("DT",1)!(ABM("D")>ABMY("DT",2)) Q
 .D ^ABMDRSU1
DONE Q
 ;
SELT ;EP
SEL2 S ABMP("PG")=0,ABMP("SEL")=0
 S ABMP("HEAD2")="SUMMARIZED CLAIM DISPLAY"
 K %P,DIR W !!
LOOP S DIR("A")="Select CLAIM",DIR(0)="PO^9002274.3:QZEM" D ^DIR K DIR
 I $D(DUOUT)!($D(DIROUT)) K ABMP Q
 I X="" S ABMQ("RP")="ENT2^ABMDRSU",ABMQ("RX")="POUT^ABMDRUTL",ABMQ("NS")="ABMP" D ^ABMDRDBQ Q
 I +Y<1 G LOOP
 S ABMP("SEL")=ABMP("SEL")+1,ABMP("SEL",ABMP("SEL"))=+Y
 G LOOP
 ;
ENT2 ;EP FOR LOOPING THROUGH ABMP ARRAY
 S ABMP("SEL")=0 F  S ABMP("SEL")=$O(ABMP("SEL",ABMP("SEL"))) Q:ABMP("SEL")=""  S ABMP("CDFN")=ABMP("SEL",ABMP("SEL")) D ^ABMDRSU1 Q:$D(ABMP("QUIT"))
 Q
