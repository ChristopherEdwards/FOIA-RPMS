ABMDTDER ; IHS/ASDST/DMJ - PRINT REPORT OF 3P ERROR CODES ; 
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;
START ;START HERE
 W !!! S %ZIS="NQ" D ^%ZIS Q:POP
 I IO'=IO(0) D QUE,HOME^%ZIS Q
 I $D(IO("S")) S IOP=ION D ^%ZIS
LOOP ;LOOP HERE
 S ABM("PAGE")=0,$P(ABM("EQ"),"=",80)="" D HDR
 S DA=0 F  S DA=$O(^ABMDERR(DA)) Q:'DA!($G(ABMQUIT))  D
 .S ABM("E#")=$E("00",1,3-$L(DA))_DA
 .W !,ABM("E#")
 .W ?6,$S($P($G(^ABMDERR(DA,31,DUZ(2),0)),"^",3)="E":"ERROR",$P($G(^(0)),"^",3)="W":"WARNING",1:"NONE")
 .W ?16,$P(^ABMDERR(DA,0),U)
 .I $Y+6>IOSL D
 ..I $E(IOST)="C"  W ! S DIR(0)="E" D ^DIR K DIR I 'Y S ABMQUIT=1
 ..Q:$G(ABMQUIT)
 ..D HDR
 I $D(IO("S")) D ^%ZISC
 W $$EN^ABMVDF("IOF")
 K ABM,ABMQUIT Q
QUE ;QUE HERE
 S ZTRTN="LOOP^ABMDTDER",ZTDESC="3P ERROR CODE LIST"
 K ZTSK D ^%ZTLOAD W:$D(ZTSK) !,"Task # ",ZTSK," queued.",!
 Q
HDR ;REPORT HEADER
 S ABM("PAGE")=ABM("PAGE")+1
 W $$EN^ABMVDF("IOF"),!,?5,"ERROR CODE LIST: ",$P(^AUTTLOC(DUZ(2),0),"^",2),?70,"Page: ",ABM("PAGE")
 W !!,"E#    STATUS    NARRATIVE"
 W !,ABM("EQ"),!
 Q
