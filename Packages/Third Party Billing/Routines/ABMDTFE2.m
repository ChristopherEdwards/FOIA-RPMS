ABMDTFE2 ; IHS/ASDST/DMJ - FEE SCHEDULE for Ambulatory Surg ;
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;
 ; IHS/SD/SDR - v2.6 CSV
 ;
 S U="^"
 W !
FEE K DIC S DIC="^ABMDFEE(",DIC(0)="QEAML",DIC("A")="Select FEE SCHEDULE: " S:$P($G(^ABMDPARM(DUZ(2),1,0)),U,9)]"" DIC("B")=$P(^(0),U,9) D ^DIC
 G XIT:$D(DUOUT)!$D(DTOUT)
 I +Y<1 G FEE
 S ABM("FEE")=+Y
SEL W !!,"----- FEE SCHEDULE CATEGORIES -----",!
 K DIR S DIR(0)="SO^1:MED/SURG FEES;2:PROF COMPONENT FEES;3:RADIOLOGY FEES;4:LABORATORY FEES;5:ANESTHESIA FEES;6:DENTAL FEES;7:ROOM & BOARD;8:HCPCS FEES",DIR("A")="Select Desired CATEGORY" D ^DIR
 G XIT:$D(DIROUT)!$D(DIRUT)
 S ABM=+Y
 ;
 S ABM("SUB")=$S(ABM=1:11,ABM=2:19,ABM=3:15,ABM=4:17,ABM=5:23,ABM=6:21,ABM=7:31,ABM=8:13)
 K DIC S DA(1)=ABM("FEE"),(DIC,DIE)="^ABMDFEE("_DA(1)_","_ABM("SUB")_","
 S ABM("DICS")=9002274.01_ABM("SUB") X:$D(^DD(ABM("DICS"),.01,12.1)) ^DD(ABM("DICS"),.01,12.1)
 I ABM=7 S DIC("W")="W "" - "",$P(^AUTTREVN(Y,0),U,2),?65,$J($FN($P(^ABMDFEE(DA(1),31,Y,0),U,2),"","",2),9)"
 I ABM=6 S DIC("W")="W "" - "",$P(^AUTTADA(Y,0),U,2),?65,$J($FN($P(^ABMDFEE(DA(1),21,Y,0),U,2),"","",2),9)"
 I "123458"[ABM S DIC("W")="W "" - "",$P($$CPT^ABMCVAPI(Y,""),U,3),?65,$J($FN($P(^ABMDFEE(DA(1),ABM(""SUB""),Y,0),U,2),"","",2),9)"  ;CSV-c
 S DR=".02"_$S("134"[ABM:";11",1:"")
 ;
EDIT W !! S DIC(0)="QLEAM" D ^DIC K DIC
 G SEL:X=""!$D(DUOUT)!$D(DTOUT)
 I +Y<1 G EDIT
 S DA=+Y
 K DIR S DIR(0)="NO^1:8:1",DIR("A")="Enter GROUP NUMBER" D ^DIR K DIR G SEL:$D(DIRUT) S DR=".02////"_$S(Y=1:260,Y=2:348,Y=3:400,Y=4:492,Y=5:561,Y=6:730,Y=7:779,1:844)
 D ^ABMDDIE
 G EDIT
 ;
XIT K ABM,DIR,DIC,DIE
 Q
