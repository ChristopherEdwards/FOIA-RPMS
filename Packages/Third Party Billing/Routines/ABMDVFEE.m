ABMDVFEE ; IHS/SD/SDR - VIEW CPT FEES ;
 ;;2.6;IHS Third Party Billing System;**9**;NOV 12, 2009
 ;
 S U="^" W !
FEE K DIC
 S DIC="^ABMDFEE(",DIC(0)="QEAML"
 S DIC("A")="Select FEE SCHEDULE: "
 S:$P($G(^ABMDPARM(DUZ(2),1,0)),U,9)]"" DIC("B")=$P(^(0),U,9)
 S DIC("S")="I DUZ(2)=$P($G(^ABMDFEE(X,0)),""^"",4)"
 S DIC("DR")=".02;.04////"_DUZ(2)  ;abm*2.6*1 NO HEAT
 D ^DIC
 G XIT:$D(DUOUT)!$D(DTOUT)
 I +Y<1 G FEE
 S ABM("FEE")=+Y
SEL W !!,"----- FEE SCHEDULE CATEGORIES -----",!
 S DIR(0)="SO^1:MEDICAL FEES;2:SURGICAL FEES;3:RADIOLOGY FEES;4:LABORATORY FEES;5:ANESTHESIA FEES;6:DENTAL FEES;7:REVENUE CODE;8:HCPCS FEES;9:DRUG FEES;10:CHARGE MASTER"
 S DIR("A")="Select Desired CATEGORY"
 D ^DIR K DIR
 G XIT:$D(DIROUT)!$D(DIRUT)
 S ABM=+Y
 ;
 S ABM("SUB")=$S(ABM=1:19,ABM=2:11,ABM=3:15,ABM=4:17,ABM=5:23,ABM=6:21,ABM=7:31,ABM=8:13,ABM=9:25,ABM=10:32)
EDIT K DIC
 S DA(1)=ABM("FEE")
 S DIC="^ABMDFEE("_DA(1)_","_ABM("SUB")_","
 S:'$D(@(DIC_"0)")) @(DIC_"0)")="^9002274.01"_ABM("SUB")_"P"
 S ABM("DICS")=9002274.01_ABM("SUB") X:$D(^DD(ABM("DICS"),.01,12.1)) ^DD(ABM("DICS"),.01,12.1)
 I ABM=7 S DIC("W")="W "" - "",$P($G(^AUTTREVN(Y,0)),U,2),?65,$J($FN($P($$ONE^ABMFEAPI(DA(1),31,Y,DT),U),"","",2),9)"
 I ABM=6 S DIC("W")="W "" - "",$P($G(^AUTTADA(Y,0)),U,2),?65,$J($FN($P($$ONE^ABMFEAPI(DA(1),21,Y,DT),U),"","",2),9)"
 I "123458"[ABM S DIC("W")="W "" - "",$P($$CPT^ABMCVAPI(Y,DT),U,3),?65,$J($FN($P($$ONE^ABMFEAPI(DA(1),ABM(""SUB""),Y,DT),U),"","",2),9)"  ;CSV-c
 ;
 W !!
 S DIC(0)="QEAM"
 D ^DIC K DIC
 G SEL:X=""!$D(DUOUT)!$D(DTOUT)
 I +Y<1 G EDIT
EFFDT ;
 S ABMCODE=+Y
 W !!,"Eff. Date",?16,"Global",?25,"Technical",?35,"Professional",?48,"Updated By",?69,"Updated on"
 S ABMEFDT=0
 F  S ABMEFDT=$O(^ABMDFEE(ABM("FEE"),ABM("SUB"),ABMCODE,1,ABMEFDT)) Q:'ABMEFDT  D
 .S ABMREC=$G(^ABMDFEE(ABM("FEE"),ABM("SUB"),ABMCODE,1,ABMEFDT,0))
 .W !,$$SDT^ABMDUTL($P(ABMREC,U))
 .W ?12,$J($FN($P(ABMREC,U,2),",",2),10),?24,$J($FN($P(ABMREC,U,3),",",2),10),?37,$J($FN($P(ABMREC,U,4),",",2),10)
 .W ?48,$E($$GET1^DIQ(200,$P(ABMREC,U,6),".01","E"),1,19),?69,$$SDT^ABMDUTL($P(ABMREC,U,5))
 ;
 G EDIT
 ;
XIT K ABM,DIR,DIC,DIE
 Q
