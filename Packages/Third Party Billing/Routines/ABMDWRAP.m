ABMDWRAP ; IHS/ASDST/DMJ - UTILITY FOR WRAP-AROUND PRINTING ;
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;
 ; IHS/SD/SDR - v2.5 p8
 ;    Made PRTTXT an EP
 ;
PRTTXT ; EP - UTIL FOR WRAP-AROUND
 S ABMU("TAB")=$S($D(ABMU("TAB")):ABMU("TAB"),1:0),ABMU("LNG")=ABMU("RM")-ABMU("LM")
 I $D(ABMU("2TXT")) S ABMU("2TAB")=$S($D(ABMU("2TAB")):ABMU("2TAB"),1:0),ABMU("2LNG")=ABMU("2RM")-ABMU("2LM")
 F ABMU("Q")=1:1 Q:(ABMU("TXT")=""!("    "[ABMU("TXT")))&'$D(ABMU("2TXT"))  D PRTTXT2
 ;
XIT K ABMU
 Q
 ;
PRTTXT2 K ABMU("FLG") I $L(ABMU("TXT"))<ABMU("LNG") S ABMU("F")=ABMU("TXT"),ABMU("TXT")="" G PRTTXT3
 S ABMU("FLG")="" F ABMU("C")=ABMU("LNG"):-1:1 S ABMU("L")=$E(ABMU("TXT"),ABMU("C")) Q:ABMU("L")=" "!(ABMU("L")="-")!(ABMU("L")="\")!(ABMU("L")=",")!(ABMU("L")="/")
 S ABMU("F")=$E(ABMU("TXT"),1,ABMU("C")-1),ABMU("TXT")=$E(ABMU("TXT"),ABMU("C")+1,255)
 K:"    "[ABMU("TXT") ABMU("FLG")
 ;
PRTTXT3 I $D(ABMU("2TXT")) D 2
 W ?ABMU("LM"),ABMU("F") I $D(ABMU("2TXT")) W ?ABMU("2LM"),ABMU("2F")
 I ABMU("Q")=1 F ABMU("I")=1:1 Q:'$D(ABMU(ABMU("I")))  W @$P(ABMU(ABMU("I")),U),$P(ABMU(ABMU("I")),U,2)
 W:$D(ABMU("FLG")) ! S ABMU("LM")=ABMU("LM")-ABMU("TAB"),ABMU("LNG")=ABMU("LNG")+ABMU("TAB"),ABMU("TAB")=0
 I $D(ABMU("2TXT")) S ABMU("2LM")=ABMU("2LM")-ABMU("2TAB"),ABMU("2LNG")=ABMU("2LNG")+ABMU("2TAB"),ABMU("2TAB")=0
 Q
 ;
2 I ABMU("2TXT")=""!("    "[ABMU("2TXT")) K ABMU("2TXT") Q
 I $L(ABMU("2TXT"))<ABMU("2LNG") S ABMU("2F")=ABMU("2TXT"),ABMU("2TXT")="" Q
 S ABMU("FLG")="" F ABMU("2C")=ABMU("2LNG"):-1:1 S ABMU("2L")=$E(ABMU("2TXT"),ABMU("2C")) Q:ABMU("2L")=" "!(ABMU("2L")="-")
 S ABMU("2F")=$E(ABMU("2TXT"),1,ABMU("2C")-1),ABMU("2TXT")=$E(ABMU("2TXT"),ABMU("2C")+1,255)
 Q
LNG ;EP FOR WRAP-AROUND FOR FIXED LENGTH
 S ABMU("TAB")=$S($D(ABMU("TAB")):ABMU("TAB"),1:0)
 F ABMU("Q")=1:1:(ABMU+1) Q:(ABMU("TXT")=""!("    "[ABMU("TXT")))  D LNG2
 Q
 ;
LNG2 K ABMU("FLG") I $L(ABMU("TXT"))<ABMU("LNG")!(ABMU("Q")=ABMU) S ABMU("F")=ABMU("TXT"),ABMU("TXT")="" G LNG3
 S ABMU("FLG")="" F ABMU("C")=ABMU("LNG")+1:-1:1 S ABMU("L")=$E(ABMU("TXT"),ABMU("C")) Q:ABMU("L")=" "!(ABMU("L")="-")!(ABMU("L")="\")!(ABMU("L")=",")!(ABMU("L")="/")
 S ABMU("F")=$E(ABMU("TXT"),1,ABMU("C")-1)
 I ABMU("F")]"" S ABMU("TXT")=$E(ABMU("TXT"),ABMU("C")+1,255)
 E  S ABMU("F")=$E(ABMU("TXT"),1,ABMU("LNG")),ABMU("TXT")=$S($P(ABMU("TXT")," ",2,99)]"":$P(ABMU("TXT")," ",2,99),1:$E(ABMU("TXT"),ABMU("LNG")+1,256))
 K:"    "[ABMU("TXT") ABMU("FLG")
 ;
LNG3 S ABMU(ABMU("Q"))=ABMU("F")
 S ABMU("LNG")=ABMU("LNG")+ABMU("TAB"),ABMU("TAB")=0
 Q
