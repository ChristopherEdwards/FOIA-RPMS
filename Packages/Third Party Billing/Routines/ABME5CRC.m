ABME5CRC ; IHS/ASDST/DMJ - 837 CRC Segment 
 ;;2.6;IHS 3P BILLING SYSTEM;**6,8**;NOV 12, 2009
 ;Transaction Set Header
 ;  
EP(ABMTYP) ;EP - START HERE
 ;ABMTYP = type of CRC to create
 K ABMREC("CRC"),ABMR("CRC")
 S ABME("RTYPE")="CRC"
 D LOOP
 K ABME,ABM
 Q
LOOP ;LOOP HERE
 F I=10:10:80 D
 .D @I
 .I $D(^ABMEXLM("AA",+$G(ABMP("INS")),+$G(ABMP("EXP")),ABME("RTYPE"),I)) D @(^(I))
 .I $G(ABMREC("CRC"))'="" S ABMREC("CRC")=ABMREC("CRC")_"*"
 .S ABMREC("CRC")=$G(ABMREC("CRC"))_ABMR("CRC",I)
 Q
10 ;segment
 S ABMR("CRC",10)="CRC"
 Q
20 ;CRC01 - Code Category
 I ABMTYP="AMB" S ABMR("CRC",20)="07"
 I ABMTYP="VIS" S ABMR("CRC",20)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8)),U,21)
 I $G(ABMSPIEN)'="" S ABMR("CRC",20)="ZZ"
 Q
30 ;CRC02 - Yes/No Condition or Response Code
 S ABMR("CRC",30)=""
 I ABMTYP="AMB" S ABMR("CRC",30)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),12)),U,15)
 I ABMTYP="VIS" S ABMR("CRC",30)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8)),U,22)
 I +$G(ABMSPIEN)'=0 S ABMR("CRC",30)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,0)),U,2)
 Q
40 ;CRC03 - Condition Indicator
 S ABMR("CRC",40)=""
 I ABMTYP="AMB",(ABMR("CRC",30)="Y") D
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,0))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",40)=$P($G(^ABMCNDIN($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN,0)),0)),U)
 I ABMTYP="VIS" D
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,0))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",40)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN,0)),U)
 I +$G(ABMSPIEN)'=0 D
 .I ABMR("CRC",30)="N" S ABMR("CRC",40)="NU" Q
 .S ABMRIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,1,0))
 .Q:+ABMRIEN=0
 .S ABMR("CRC",40)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,1,ABMRIEN,0)),U)
 Q
50 ;CRC04 - Condition Indicator
 S ABMR("CRC",50)=""
 I ABMTYP="AMB",(ABMR("CRC",30)="Y") D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",50)=$P($G(^ABMCNDIN($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN,0)),0)),U)
 I ABMTYP="VIS" D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",50)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN,0)),U)
 I +$G(ABMSPIEN)'=0 D
 .Q:+$G(ABMRIEN)=0
 .S ABMRIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,1,ABMRIEN))
 .Q:+ABMRIEN=0
 .S ABMR("CRC",50)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,1,ABMRIEN,0)),U)
 Q
60 ;CRC05 - Condition Indicator
 S ABMR("CRC",60)=""
 I ABMTYP="AMB",(ABMR("CRC",30)="Y") D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",60)=$P($G(^ABMCNDIN($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN,0)),0)),U)
 I ABMTYP="VIS" D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",60)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN,0)),U)
 I +$G(ABMSPIEN)'=0 D
 .Q:+$G(ABMRIEN)=0
 .S ABMRIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,1,ABMRIEN))
 .Q:+ABMRIEN=0
 .S ABMR("CRC",60)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,1,ABMRIEN,0)),U)
 Q
70 ;CRC06 - Condition Indicator
 S ABMR("CRC",70)=""
 I ABMTYP="AMB",(ABMR("CRC",30)="Y") D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",70)=$P($G(^ABMCNDIN($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN,0)),0)),U)
 I ABMTYP="VIS" D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",70)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN,0)),U)
 Q
80 ;CRC07 - Condition Indicator
 S ABMR("CRC",80)=""
 I ABMTYP="AMB",(ABMR("CRC",30)="Y") D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",80)=$P($G(^ABMCNDIN($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),14,ABMCIEN,0)),0)),U)
 I ABMTYP="VIS" D
 .Q:+ABMCIEN=0
 .S ABMCIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN))
 .Q:+ABMCIEN=0
 .S ABMR("CRC",80)=$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),8.5,ABMCIEN,0)),U)
 Q
