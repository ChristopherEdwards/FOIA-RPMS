ABME5L5 ; IHS/ASDST/DMJ - Header 
 ;;2.6;IHS Third Party Billing;**6,8**;NOV 12, 2009
 ;Header Segments
 ;
START ;START HERE
 S ABMLOOP=2300
 D ^ABME5CLM
 D WR^ABMUTL8("CLM")
 I $P(ABMB6,U,4)'="",($E(ABMP("BTYP"),1,2)=11) D
 .D EP^ABME5DTP("096","TM",$P(ABMB6,U,4))
 .D WR^ABMUTL8("DTP")
 D EP^ABME5DTP(434,"RD8",$P(ABMB7,U),$P(ABMB7,U,2))
 D WR^ABMUTL8("DTP")
 I $P(ABMB6,U),($E(ABMP("BTYP"),1,2)=11) D
 .D EP^ABME5DTP(435,"DT",$TR($P(ABMB6,U,1,2),U,"."))
 .D WR^ABMUTL8("DTP")
 I $P($G(^ABMNINS(ABMP("LDFN"),ABMP("INS"),1,ABMP("VTYP"),1)),U,11)'="" D
 .D ^ABME5CN1
 .D WR^ABMUTL8("CN1")
 S ABMB5TR=$P(ABMB5,U,1,3)
 S ABMB5TR=$TR(ABMB5TR,U)
 I +ABMB5TR D
 .D ^ABME5CL1
 .D WR^ABMUTL8("CL1")
 I $D(^ABMDBILL(DUZ(2),ABMP("BDFN"),71)) D
 .K ABM71CNT
 .S ABM71IEN=0
 .F  S ABM71IEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),71,ABM71IEN)) Q:+ABM71IEN=0  D  Q:$G(ABM71CNT)=10
 ..D ^ABME5PWK,WR^ABMUTL8("PWK")
 ..S ABM71CNT=+$G(ABM71CNT)+1
 I $P(ABMB9,U,9) D
 .D EP^ABME5AMT("F5")
 .D WR^ABMUTL8("AMT")
 I $P(ABMB4,U,9)'="" D
 .D EP^ABME5REF("F8")
 .D WR^ABMUTL8("REF")
 I $P(ABMB5,U,8)'="" D
 .D EP^ABME5REF("G4")
 .D WR^ABMUTL8("REF")
 I $P(ABMB5,U,11)'="" D
 .D EP^ABME5REF("9F")
 .D WR^ABMUTL8("REF")
 I $P(ABMB5,U,12)'="" D
 .D EP^ABME5REF("G1")
 .D WR^ABMUTL8("REF")
 ;auto accident state
 I $P(ABMB8,U,16)'="" D
 .D EP^ABME5REF("LU")
 .D WR^ABMUTL8("REF")
 D EP^ABME5REF("EA")
 D WR^ABMUTL8("REF")
 I $P($G(^ABMDPARM(ABMP("LDFN"),1,2)),U,13)="Y"&(($E(ABMP("BTYP"),1,2)=11)!($E(ABMP("BTYP"),1,2)="12")) D
 .D EP^ABME5K3
 .D WR^ABMUTL8("K3")
 I $D(^ABMDBILL(DUZ(2),ABMP("BDFN"),61)) D
 .D EP^ABME5NTE("ADD")
 .Q:$TR($G(ABMR("NTE",30))," ")=""
 .D WR^ABMUTL8("NTE")
 ;EPSDT Referral
 I $D(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,0)) D
 .S ABMSPIEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,0))
 .Q:+ABMSPIEN=0
 .Q:$P($G(^ABMDBILL(DUZ(2),ABMP("BDFN"),59,ABMSPIEN,0)),U,2)=""
 .D EP^ABME5CRC("EPSDT")
 .D WR^ABMUTL8("CRC")
 Q
