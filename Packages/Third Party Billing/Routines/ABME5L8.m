ABME5L8 ; IHS/ASDST/DMJ - Header 
 ;;2.6;IHS 3P BILLING SYSTEM;**6,8**;NOV 12, 2009
 ;Header Segments
START ;START HERE
 D PAYED^ABMUTLP
 N ABMI
 S ABMI=0
 F  S ABMI=$O(ABMP("INS",ABMI)) Q:'ABMI  D
 .S ABMLINE=ABMP("INS",ABMI)
 .I $P(ABMLINE,U)=ABMP("INS"),$P(ABMLINE,"^",3)="I" Q
 .; Loop 2320 - Other Subscriber Information
 .S ABMLOOP=2320
 .;abm*2.6*8 start new code
 .I $G(ABMSBR(ABMI)) D
 ..S ABMSFILE=$P(ABMSBR(ABMI),"-",1)
 ..S ABMSIEN=$P(ABMSBR(ABMI),"-",2)
 .;abm*2.6*8 end new code
 .D EP^ABME5SBR(ABMI)
 .D WR^ABMUTL8("SBR")
 .F ABML="OA","PR" D
 ..Q:'$D(ABMP(+ABMLINE,ABML))  ;quit if no data for insurer in ABMP adj array
 ..D EP^ABME5CAS
 ..D WR^ABMUTL8("CAS")
 .I $G(ABMP("PAYED",+ABMLINE)) D
 ..D EP^ABME5AMT("D")
 ..D WR^ABMUTL8("AMT")
 .D ^ABME5OI
 .D WR^ABMUTL8("OI")
 .; Loop 2330A - Other Subscriber Name
 .S ABMLOOP="2330A"
 .D EP^ABME5NM1("IL")
 .D WR^ABMUTL8("NM1")
 .D EP^ABME5N3(ABMSFILE,ABMSIEN)
 .D WR^ABMUTL8("N3")
 .D EP^ABME5N4(ABMSFILE,ABMSIEN)
 .D WR^ABMUTL8("N4")
 .; Loop 2330B - Other Payer Name
 .S ABMLOOP="2330B"
 .D EP^ABME5NM1("PR",+ABMLINE)
 .D WR^ABMUTL8("NM1")
 .I $G(ABMP("PAYED",+ABMLINE)) D
 ..S ABMPDT=$G(ABMP("PDT",+ABMLINE))
 ..D EP^ABME5DTP(573,"D8",ABMPDT)
 ..D WR^ABMUTL8("DTP")
 ..K ABMPDT
 .I $G(ABMP("PNUM",ABMI))'="" D
 ..;D EP^ABME5NM2("QC",ABMI)
 ..;D WR^ABMUTL8("NM1")
 .;D OTHR
 Q
OTHR ;other payer info
 ; Loops 2330C - 2330I 0 Other Payer Info
 N J
 F J=1:1:4 D
 .S ABMPTYP=$E("AOTR",J)
 .S ABMPROV=$O(ABMP("PRV",ABMPTYP,0))
 .I ABMPROV D
 ..S ABMPNBR=$$NPI^ABMEEPRV(ABMPROV,ABMP("LDFN"),+ABMLINE)
 ..Q:ABMPNBR=""
 ..D EP^ABME5NM2($P("71^72^ZZ^DN","^",J))
 ..D WR^ABMUTL8("NM1")
 ..S ABMITYP=$P(ABMLINE,"^",2)
 ..S ABMITYP=$S(ABMITYP="R":"1C",ABMITYP="D":"1D",1:"G2")
 ..I ABMITYP="G2",$$BCBS1^ABMERUTL(+ABMLINE) S ABMITYP="1A"
 ..D EP^ABME5RF2(ABMITYP)
 ..D WR^ABMUTL8("REF")
 Q
