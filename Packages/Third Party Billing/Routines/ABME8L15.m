ABME8L15 ; IHS/ASDST/DMJ - Header 
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;Header Segments
 ;
 ; IHS/SD/SDR - v2.5 p10 - IM20395
 ;  Split out lines bundled by rev code
 ;
 ; IHS/SD/SDR - v2.5 p11 - NPI
 ;
 ; IHS/SD/SDR - v2.5 p12 - IM25247
 ;   Add missing REG segment for TIN if NPI ONLY
 ;
 ; IHS/SD/SDR - v2.5 p13 - IM25888
 ;   Correction for TOO segment
 ;
EP ;START HERE
 S ABMLXCNT=0
 K ABM
 D ^ABMEHGRV
 S ABMI=0
 F  S ABMI=$O(ABMRV(ABMI)) Q:'+ABMI  D
 .S ABMJ=-1
 .F  S ABMJ=$O(ABMRV(ABMI,ABMJ)) Q:'+ABMJ  D
 ..S ABMK=0
 ..F  S ABMK=$O(ABMRV(ABMI,ABMJ,ABMK))  Q:'+ABMK  D
 ...D LOOP
 K ABMI,ABMJ,ABMK
 Q
 ;
LOOP ;
 Q:$P(ABMRV(ABMI,ABMJ,ABMK),U,2)=""
 S ABMLXCNT=ABMLXCNT+1
 D EP^ABME8LX
 D WR^ABMUTL8("LX")
 D EP^ABME8SV3
 D WR^ABMUTL8("SV3")
 I $P(ABMRV(ABMI,ABMJ,ABMK),U,23)'=""!($P(ABMRV(ABMI,ABMJ,ABMK),U,24)'="") D
 .D ^ABME8TOO
 .D WR^ABMUTL8("TOO")
 I $P(ABMRV(ABMI,ABMJ,ABMK),U,10)'=$P(ABMB7,U) D
 .Q:'$P(ABMRV(ABMI,ABMJ,ABMK),U,10)
 .D EP^ABME8DTP(472,"D8",$P(ABMRV(ABMI,ABMJ,ABMK),U,10))
 .D WR^ABMUTL8("DTP")
 ;
 ; Loop 2420A - Rendering Physician
 I $P($G(ABMRV(ABMI,ABMJ,ABMK)),U,13) D
 .S ABM("PRV")=$P(ABMRV(ABMI,ABMJ,ABMK),U,13)
 .Q:ABM("PRV")=$O(ABMP("PRV","D",0))
 .D EP^ABME8NM1(82,ABM("PRV"))
 .D WR^ABMUTL8("NM1")
 .D EP^ABME8PRV("PE",ABM("PRV"))
 .D WR^ABMUTL8("PRV")
 .I ABMNPIU="N" D
 ..Q:((ABMRCID="99999")!(ABMRCID="AHCCCS866004791"))  ;AZ Medicaid
 ..D EP^ABME8REF("EI",9999999.06,DUZ(2))
 ..D WR^ABMUTL8("REF")
 .I ABMNPIU'="N" D
 ..D EP^ABME8REF(ABMP("RTYPE"),200,ABM("PRV"))
 ..D WR^ABMUTL8("REF")
 Q
