ABME8L5 ; IHS/ASDST/DMJ - Header 
 ;;2.6;IHS Third Party Billing;**1**;NOV 12, 2009
 ;Header Segments
 ;
 ; IHS/SD/SDR - v2.5 p9 - IM18032
 ;    Put Medicaid Resubmission Number as REF*F8
 ;
 ; IHS/SD/SDR - v2.5 p13 - POA changes
 ;   Added call to new routine ABME8K3
 ; IHS/SD/SDR - abm*2.6*1 - HEAT6439 - Added code for PWK claim attachments
 ;
START ;START HERE
 D ^ABME8CLM
 D WR^ABMUTL8("CLM")
 I $P(ABMB6,U,4)'="" D
 .D EP^ABME8DTP("096","TM",$P(ABMB6,U,4))
 .D WR^ABMUTL8("DTP")
 D EP^ABME8DTP(434,"RD8",$P(ABMB7,U),$P(ABMB7,U,2))
 D WR^ABMUTL8("DTP")
 I $P(ABMB6,U) D
 .D EP^ABME8DTP(435,"DT",$TR($P(ABMB6,U,1,2),U,"."))
 .D WR^ABMUTL8("DTP")
 I $P($G(^ABMNINS(ABMP("LDFN"),ABMP("INS"),1,ABMP("VTYP"),1)),U,11)'="" D
 .D ^ABME8CN1
 .D WR^ABMUTL8("CN1")
 S ABMB5TR=$P(ABMB5,U,1,3)
 S ABMB5TR=$TR(ABMB5TR,U)
 I +ABMB5TR D
 .D ^ABME8CL1
 .D WR^ABMUTL8("CL1")
 ;start new code abm*2.6*1 HEAT6439
 I $D(^ABMDBILL(DUZ(2),ABMP("BDFN"),71)) D
 .K ABM71CNT
 .S ABM71IEN=0
 .F  S ABM71IEN=$O(^ABMDBILL(DUZ(2),ABMP("BDFN"),71,ABM71IEN)) Q:+ABM71IEN=0  D  Q:$G(ABM71CNT)=10
 ..D ^ABME8PWK,WR^ABMUTL8("PWK")
 ..S ABM71CNT=+$G(ABM71CNT)+1
 ;end new code HEAT6439
 D EP^ABME8AMT("C5")
 D WR^ABMUTL8("AMT")
 I $P(ABMB9,U,9) D
 .D EP^ABME8AMT("F5")
 .D WR^ABMUTL8("AMT")
 I $P(ABMB4,U,9)'="" D
 .D EP^ABME8REF("F8")
 .D WR^ABMUTL8("REF")
 I $P(ABMB5,U,8)'="" D
 .D EP^ABME8REF("G4")
 .D WR^ABMUTL8("REF")
 I $P(ABMB5,U,11)'="" D
 .D EP^ABME8REF("9F")
 .D WR^ABMUTL8("REF")
 I $P(ABMB5,U,12)'="" D
 .D EP^ABME8REF("G1")
 .D WR^ABMUTL8("REF")
 D EP^ABME8REF("EA")
 D WR^ABMUTL8("REF")
 I $P($G(^ABMDPARM(ABMP("LDFN"),1,2)),U,13)="Y"&(($E(ABMP("BTYP"),1,2)=11)!($E(ABMP("BTYP"),1,2)="12")) D
 .D EP^ABME8K3
 .D WR^ABMUTL8("K3")
 I $D(^ABMDBILL(DUZ(2),ABMP("BDFN"),61)) D
 .D EP^ABME8NTE("ADD")
 .Q:$TR($G(ABMR("NTE",30))," ")=""
 .D WR^ABMUTL8("NTE")
 Q
