ABME8L8 ; IHS/ASDST/DMJ - Header 
 ;;2.6;IHS 3P BILLING SYSTEM;**3**;NOV 12, 2009
 ;Header Segments
 ; IHS/SD/SDR - abm*2.6*3 - HEAT7574 - tribal self-insured
 ;
START ;START HERE
 D PAYED^ABMUTLP
 N ABMI
 S ABMI=0
 F  S ABMI=$O(ABMP("INS",ABMI)) Q:'ABMI  D
 .S ABMLINE=ABMP("INS",ABMI)
 .I $P(ABMLINE,U)=ABMP("INS"),$P(ABMLINE,"^",3)="I" Q
 .D EP^ABME8SBR(ABMI)
 .D WR^ABMUTL8("SBR")
 .F ABML="OA","PR" D
 ..Q:'$D(ABMP(+ABMLINE,ABML))  ;quit if no data for insurer in ABMP adj array
 ..D EP^ABME8CAS
 ..D WR^ABMUTL8("CAS")
 .;I $G(ABMP("PAYED",+ABMLINE)) D  ;abm*2.6*3 HEAT7574
 .I ($G(ABMP("PAYED",+ABMLINE))!($P($G(^ABMNINS(ABMP("LDFN"),+ABMLINE,0)),U,11)="Y")) D  ;abm*2.6*3 HEAT7574
 ..D EP^ABME8AMT("C4")
 ..D WR^ABMUTL8("AMT")
 .;start new code abm*2.6*3 HEAT7574
 .I ($P($G(^ABMNINS(ABMP("LDFN"),+ABMLINE,0)),U,11)="Y") D
 ..S ABMB6AMT=0
 ..D EP^ABME8AMT("B6")
 ..D WR^ABMUTL8("AMT")
 .;end new code HEAT7574
 .I $G(ABMSBR(ABMI)) D
 ..S ABMSFILE=$P(ABMSBR(ABMI),"-",1)
 ..S ABMSIEN=$P(ABMSBR(ABMI),"-",2)
 ..D EP^ABME8DMG(ABMSFILE,ABMSIEN)
 ..D WR^ABMUTL8("DMG")
 .D ^ABME8OI
 .D WR^ABMUTL8("OI")
 .D EP^ABME8NM1("IL")
 .D WR^ABMUTL8("NM1")
 .D EP^ABME8N3(ABMSFILE,ABMSIEN)
 .D WR^ABMUTL8("N3")
 .D EP^ABME8N4(ABMSFILE,ABMSIEN)
 .D WR^ABMUTL8("N4")
 .D EP^ABME8NM1("PR",+ABMLINE)
 .D WR^ABMUTL8("NM1")
 .;I $G(ABMP("PAYED",+ABMLINE)) D  ;abm*2.6*3 HEAT7574
 .I $G(ABMP("PAYED",+ABMLINE))'="" D  ;abm*2.6*3 HEAT7574
 ..;S ABMPDT=$G(ABMP("PDT",+ABMLINE))  ;abm*2.6*3 HEAT7574
 ..S ABMPDT=$S($P($G(ABMP("PAYED",+ABMLINE)),U,2)'="":$P(ABMP("PAYED",+ABMLINE),U,2),$G(ABMP("PDT",+ABMLINE))'="":ABMP("PDT",+ABMLINE),1:"")  ;abm*2.6*3 HEAT7574
 ..D EP^ABME8DTP(573,"D8",ABMPDT)
 ..D WR^ABMUTL8("DTP")
 ..K ABMPDT
 .I $G(ABMP("PNUM",ABMI))'="" D
 ..D EP^ABME8NM2("QC",ABMI)
 ..D WR^ABMUTL8("NM1")
 .D OTHR
 Q
OTHR ;other payer info
 N J
 F J=1:1:4 D
 .S ABMPTYP=$E("AOTR",J)
 .S ABMPROV=$O(ABMP("PRV",ABMPTYP,0))
 .I ABMPROV D
 ..S ABMPNBR=$$NPI^ABMEEPRV(ABMPROV,ABMP("LDFN"),+ABMLINE)
 ..Q:ABMPNBR=""
 ..D EP^ABME8NM2($P("71^72^73^DN","^",J))
 ..D WR^ABMUTL8("NM1")
 ..S ABMITYP=$P(ABMLINE,"^",2)
 ..S ABMITYP=$S(ABMITYP="R":"1C",ABMITYP="D":"1D",1:"G2")
 ..I ABMITYP="G2",$$BCBS1^ABMERUTL(+ABMLINE) S ABMITYP="1A"
 ..D EP^ABME8RF2(ABMITYP)
 ..D WR^ABMUTL8("REF")
 Q
