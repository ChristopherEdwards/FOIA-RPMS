ABMEH95 ; IHS/ASDST/DMJ - HCFA-1500 EMC RECORD YA0 (Batch Trailer) ;     
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;Original;DMJ;
 ;
 ; IHS/ASDS/DMJ - 03/01/01 - V2.4 P5 - NOIS HQW-0301-100010
 ;     Modified to accommodate new Envoy electronic format
 ;
START ;START HERE
 K ABMREC(95)
 S ABME("RTYPE")=95
 D SET^ABMERUTL,LOOP
 F I="LTOT","RTOT","DTOT","CTOT" S ABMRT(99,I)=+$G(ABMRT(99,I))+$G(ABMRT(95,I))
 K ABM,ABME,ABMRT(95)
 Q
LOOP ;LOOP HERE
 F I=10:10:130 D
 .D @I
 .I $D(^ABMEXLM("AA",+$G(ABMP("INS")),+$G(ABMP("EXP")),95,I)) D @(^(I))
 .I '$G(ABMP("NOFMT")) S ABMREC(95)=$G(ABMREC(95))_ABMR(95,I)
 Q
10 ;1-3 Record type
 S ABMR(95,10)="YA0"
 Q
20 ;4-18 EMC Provider ID
 S ABMR(95,20)=$G(ABMP("EMCPRID"))
 S ABMR(95,20)=$$FMT^ABMERUTL(ABMR(95,20),15)
 Q
30 ;19-31 Type of Batch
 S ABMR(95,30)=$G(ABMP("TOB"))
 S ABMR(95,30)=$$FMT^ABMERUTL(ABMR(95,30),3)
 Q
40 ;22-25 Batch #
 S ABMR(95,40)=$G(ABMEF("BATCH#"))
 S ABMR(95,40)=$$FMT^ABMERUTL(ABMR(95,40),"4NR")
 Q
50 ;26-31 Batch ID
 S ABMR(95,50)=""
 S ABMR(95,50)=$$FMT^ABMERUTL(ABMR(95,50),6)
 Q
60 ;32-40 Provider Tax ID
 S ABMR(95,60)=$G(ABMRT(95,60))
 S ABMR(95,60)=$$FMT^ABMERUTL(ABMR(95,60),9)
 Q
70 ;41-46 Reserved
 S ABMR(95,70)=""
 S ABMR(95,70)=$$FMT^ABMERUTL(ABMR(95,70),6)
 Q
80 ;47-53 Batch Service Line Count
 S ABMR(95,80)=$$FMT^ABMERUTL(+$G(ABMRT(95,"LTOT")),"7NR")
 Q
90 ;54-60 Batch Record Count
 S ABMR(95,90)=$$FMT^ABMERUTL(+$G(ABMRT(95,"RTOT")),"7NR")
 Q
100 ;61-67 Batch Claim Count
 S ABMR(95,100)=$$FMT^ABMERUTL(+$G(ABMRT(95,"CTOT")),"7NR")
 Q
110 ;68-76 Batch Total Charges
 S ABMR(95,110)=$$FMT^ABMERUTL(+$G(ABMRT(95,"DTOT")),"9NRJ2")
 Q
120 ;77-197 Filler (National Use)
 S ABMR(95,120)=""
 S ABMR(95,120)=$$FMT^ABMERUTL(ABMR(95,120),121)
 Q
130 ;198-320 Filler (Local Use)
 S ABMR(95,130)=""
 S ABMR(95,130)=$$FMT^ABMERUTL(ABMR(95,130),123)
 Q
