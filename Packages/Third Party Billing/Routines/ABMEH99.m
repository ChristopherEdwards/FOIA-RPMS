ABMEH99 ; IHS/ASDST/DMJ - HCFA-1500 EMC RECORD ZA0 (File Transfer) ;   
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;Original;DMJ;
 ;
 ; ABM*2.4*9 IHS/FCS/DRS 09/21/01 ; Part 14b 09/17/01 IHS/FCS/DRS - Payer ID for Envoy
 ;
START ;START HERE
 K ABMREC(99)
 S ABME("RTYPE")=99
 D SET^ABMERUTL,LOOP
 K ABM,ABME,ABMRT
 Q
LOOP ;LOOP HERE
 F I=10:10:110 D
 .D @I
 .I $D(^ABMEXLM("AA",+$G(ABMP("INS")),+$G(ABMP("EXP")),99,I)) D @(^(I))
 .I '$G(ABMP("NOFMT")) S ABMREC(99)=$G(ABMREC(99))_ABMR(99,I)
 Q
10 ;1-3 Record type
 S ABMR(99,10)="ZA0"
 Q
20 ;4-19 Sub ID
 S ABMR(99,20)=ABMP("SUBID")
 S ABMR(99,20)=$$FMT^ABMERUTL($G(ABMR(99,20)),16)
 Q
30 ;20-28 Reserved
 S ABMR(99,30)=""
 S ABMR(99,30)=$$FMT^ABMERUTL(ABMR(99,30),9)
 Q
40 ;29-44 Receiver ID
 S ABMR(99,40)=""
 I $$ENVOY^ABMEF19 D
 .S ABMR(99,40)=$$ENVY^ABMERUTL(ABMP("INS"),ABMP("VTYP"))
 I ABMR(99,40)="" S ABMR(99,40)=$$RCID^ABMERUTL(ABMP("INS"))
 S ABMR(99,40)=$$FMT^ABMERUTL(ABMR(99,40),16)
 Q
50 ;45-51 File Service Line Count
 S ABMR(99,50)=$$FMT^ABMERUTL(+$G(ABMRT(99,"LTOT")),"7NR")
 Q
60 ;52-58 File Record Count
 S ABMR(99,60)=$$FMT^ABMERUTL(+$G(ABMRT(99,"RTOT")),"7NR")
 Q
70 ;59-65 File Claim Count
 S ABMR(99,70)=$$FMT^ABMERUTL(+$G(ABMRT(99,"CTOT")),"7NR")
 Q
80 ;66-69 Batch Count
 S ABMR(99,80)=ABMEF("BATCH#")
 S ABMR(99,80)=$$FMT^ABMERUTL(ABMR(99,80),"4NR")
 Q
90 ;70-80 Total Charges for the File
 S ABMR(99,90)=$$FMT^ABMERUTL(+$G(ABMRT(99,"DTOT")),"11NRJ2")
 Q
100 ;81-200 Filler (National Use)
 S ABMR(99,100)=""
 S ABMR(99,100)=$$FMT^ABMERUTL(ABMR(99,100),120)
 Q
110 ;201-320 Filler (Local Use)
 S ABMR(99,110)=""
 S ABMR(99,110)=$$FMT^ABMERUTL(ABMR(99,110),120)
 Q
