ABMF400 ; IHS/SD/SDR - 3P BILLING - Find Medicare 400/900 references ;  
 ;;2.6;IHS 3P BILLING SYSTEM;;NOV 12, 2009
 ;
EN ;EP
 W !!,"Now going through 3P Exp Local Mods file looking for Medicare references..."
 K ABMLST,ABMFOUND
 S ABMINS=0
 F  S ABMINS=$O(^ABMEXLM("AA",ABMINS)) Q:+ABMINS=0  D
 .S ABMEXP=0
 .F  S ABMEXP=$O(^ABMEXLM("AA",ABMINS,ABMEXP)) Q:+ABMEXP=0  D
 ..S ABMRTYP=""
 ..F  S ABMRTYP=$O(^ABMEXLM("AA",ABMINS,ABMEXP,ABMRTYP)) Q:ABMRTYP=""  D
 ...S ABMI=0
 ...F  S ABMI=$O(^ABMEXLM("AA",ABMINS,ABMEXP,ABMRTYP,ABMI)) Q:+ABMI=0  D
 ....S ABMLST($P($G(^ABMEXLM("AA",ABMINS,ABMEXP,ABMRTYP,ABMI)),U,2))=""
 ;
 S ABMRTN=""
 F  S ABMRTN=$O(ABMLST(ABMRTN)) Q:ABMRTN=""  D
 .S ABMLCNT=0
 .F  S ABMLCNT=$O(^ROUTINE(ABMRTN,0,ABMLCNT)) Q:+ABMLCNT=0  D
 ..I $G(^ROUTINE(ABMRTN,0,ABMLCNT))["400" S ABMFOUND(400,ABMRTN,ABMLCNT)=$G(^ROUTINE(ABMRTN,0,ABMLCNT))
 ..I $G(^ROUTINE(ABMRTN,0,ABMLCNT))["900" S ABMFOUND(900,ABMRTN,ABMLCNT)=$G(^ROUTINE(ABMRTN,0,ABMLCNT))
 ;
 I '$D(ABMFOUND) W !!,"NO Medicare entries found."
 ;
 I $D(ABMFOUND) D
 .W !!,"***WARNING***  DO SCREEN PRINT OR REFERENCE INSTALL FILE"
 .W !!,"Entries were found that contain possible Medicare entries that should be"
 .W !,"reviewed."
 .W !,"NUMBER",?9,"ROUTINE",?20,"LINE",?25,"CODE"
 .S ABMI=0
 .F  S ABMI=$O(ABMFOUND(ABMI)) Q:+ABMI=0  D
 ..S ABMRTN=""
 ..F  S ABMRTN=$O(ABMFOUND(ABMI,ABMRTN)) Q:ABMRTN=""  D
 ...S ABMLCNT=0
 ...F  S ABMLCNT=$O(ABMFOUND(ABMI,ABMRTN,ABMLCNT)) Q:+ABMLCNT=0  D
 ....W !,ABMI,?9,ABMRTN,?20,ABMLCNT,?25,$G(ABMFOUND(ABMI,ABMRTN,ABMLCNT))
 Q
