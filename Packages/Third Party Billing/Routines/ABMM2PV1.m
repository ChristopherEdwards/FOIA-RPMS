ABMM2PV1 ;IHS/SD/SDR - MU Patient Volume EP Report ;
 ;;2.6;IHS 3P BILLING SYSTEM;**11**;NOV 12, 2009;Build 133
 ;
COMPUTE ;EP - gather data report
 ;specified 90-day
 I ABMY("90")="B" D  Q
 .S X1=ABMY("SDT")
 .S X2=89
 .D C^%DTC
 .S (ABMY("EDT"),ABMP("EDT"))=X
 .D VISITS
 .D BILLS
 .D ENROLL
 .D CALC^ABMM2PV2
 .D PRINT^ABMM2PV3
 ;
 I ABMY("90")="E" D  Q
 .S X1=ABMY("SDT")
 .S X2=89
 .D C^%DTC
 .S (ABMY("EDT"),ABMP("EDT"))=X
 .D VISITS
 .D BILLS
 .D ENROLL
 .D CALC^ABMM2PV2
 .D PRINT^ABMM2PV3
 ;
 ;User specified
 I ABMY("90")="C" D VISITS,BILLS,ENROLL,CALC^ABMM2PV2,PRINT^ABMM2PV3 Q
 ;
 ;automated
 S (ABMY("SDT"),ABMP("SDT"))=(ABMY("QYR")-1700)_"0101"
 S ABMP("EDT")=(ABMY("QYR")-1700)_"1231"
 I +$G(ABMY("ADT"))'=0 D
 .S ABMP("EDT")=ABMY("ADT")
 .S X1=ABMY("ADT")
 .S X2=-365
 .D C^%DTC
 .S (ABMY("SDT"),ABMP("SDT"))=X
 D VISITS
 D BILLS
 D ENROLL
 D CALC^ABMM2PV2
 ;
 I ABMY("RTYP")="SEL",'$D(ABMPRVDR) Q
 I ABMY("RTYP")="GRP",(+$G(^XTMP("ABM-PVP2",$J,"PRV TOP"))>29.99)
 D PRINT^ABMM2PV3
 K ABMY("EDT")
 Q
 ;
VISITS ;
 S ABMSDT=ABMP("SDT")
 S ABMEDT=ABMP("EDT")+.999999
 S ABMINS=0,ABMOINS="NO BILL"
 S ABMITYP="X"
 F  S ABMSDT=$O(^AUPNVSIT("B",ABMSDT)) Q:'ABMSDT!(ABMSDT>ABMEDT)  D
 .S ABMVDFN=0
 .F  S ABMVDFN=$O(^AUPNVSIT("B",ABMSDT,ABMVDFN)) Q:'ABMVDFN  D
 ..S ABMSCAT=$$GET1^DIQ(9000010,ABMVDFN,.07,"I")  ;service cat
 ..S ABMCLNC=$$GET1^DIQ(9000010,ABMVDFN,.08,"I")  ;clinic
 ..S ABMP("VDT")=$P($$GET1^DIQ(9000010,ABMVDFN,.01,"I"),".")  ;visit date
 ..Q:"^39^D1^D2^76^63^51^52^72^22^42^54^57^66^71^77^B5^C6^"[("^"_$$GET1^DIQ(40.7,$$GET1^DIQ(9000010,ABMVDFN,.08,"I"),1,"E")_"^")&(ABMY("RTYP")="SEL")  ;exclude clinics
 ..S ABMPT=$$GET1^DIQ(9000010,ABMVDFN,.05,"I")  ;patient
 ..;if SEL report type, service cat MUST be S,O,R, or (A w/clinic'=30)
 ..I "^H^I^C^T^N^E^D^X^R^"[("^"_ABMSCAT_"^") Q
 ..I (ABMSCAT="A")&(ABMCLNC=30) Q
 ..S ABMVLOC=$$GET1^DIQ(9000010,ABMVDFN,.06,"I")
 ..Q:ABMVLOC=""
 ..I ($$GET1^DIQ(9000010,ABMVDFN,.12)'="")&(ABMSDT>$$GET1^DIQ(9000010,ABMVDFN,.12,"I")&($$GET1^DIQ(9000010,ABMVDFN,1111,"I")'="R")) Q
 ..Q:'$D(ABMF(ABMVLOC))  ;not a selected location
 ..I ABMY("RTYP")="GRP" D GRPVST Q
 ..S ABMPIEN=0
 ..K ABMPRVC
 ..F  S ABMPIEN=$O(^AUPNVPRV("AD",ABMVDFN,ABMPIEN)) Q:'ABMPIEN  D
 ...S ABMPRV=$$GET1^DIQ(9000010.06,ABMPIEN,.01,"I")
 ...Q:'$D(ABMPRVDR(ABMPRV))
 ...;skip provider if on visit more than once
 ...Q:$D(ABMPRVC(ABMPRV))
 ...S ABMPRVC(ABMPRV)=1
 ...D CALCDTS
 ...S ABMDTFLG=0
 ...S ABMP("BDT")=ABMP("BSDT")
 ...F  D  Q:ABMDTFLG=1
 ....I ABMP("VDT")<ABMP("BSDT") Q  ;visit is before 90-day window
 ....S ^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"),ABMPRV)=+$G(^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"),ABMPRV))+1
 ....S ^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"),ABMPRV,ABMVLOC)=+$G(^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"),ABMPRV,ABMVLOC))+1
 ....S ^XTMP("ABM-PVP2",$J,"PT VSTS",ABMPT,ABMSDT,ABMVDFN)=""  ;list of visits by patient,DOS
 ....S ^XTMP("ABM-PVP2",$J,"VISITS",ABMVDFN)=""  ;list of visits to check for pymt
 ....S ^XTMP("ABM-PVP2",$J,"VISIT CNT",ABMP("BDT"))=+$G(^XTMP("ABM-PVP2",$J,"VISIT CNT",ABMP("BDT")))+1  ;count of visits
 ....S ^XTMP("ABM-PVP2",$J,"ALL VISITS",ABMP("BDT"),ABMVDFN)=""  ;list of all visits looked at
 ....I (^XTMP("ABM-PVP2",$J,"VISIT CNT",ABMP("BDT"))#1000&(IOST["C")) W "." U 0 W "."
 ....K ABMITYP
 ....D PTDATA
 ....S X1=ABMP("BDT")
 ....S X2=1
 ....D C^%DTC
 ....I X>ABMP("BEDT") S ABMDTFLG=1 Q
 ....S ABMP("BDT")=X
 ;
 Q
GRPVST ;EP
 S ABMPIEN=0
 K ABMPRVC
 F  S ABMPIEN=$O(^AUPNVPRV("AD",ABMVDFN,ABMPIEN)) Q:'ABMPIEN  D
 .S ABMPRV=$$GET1^DIQ(9000010.06,ABMPIEN,.01,"I")
 .;skip provider if on visit more than once
 .Q:$D(ABMPRVC(ABMPRV))
 .S ABMPRVC(ABMPRV)=1
 .S ABMPRVCL=+$$GET1^DIQ(200,ABMPRV,53.5,"I")
 .I ABMPRVCL=0 S ABMPRV("O",ABMPRV)=""
 .I ABMPRVCL'=0 D
 ..I '$D(^ABMMUPRM(1,2,"B",ABMPRVCL)) S ABMPRV("O",ABMPRV)=""
 ..I $$GET1^DIQ(7,ABMPRVCL,9999999.01,"E")=11 D
 ...I '$D(^ABMMUPRM(1,1,"B",ABMVLOC)) S ABMPRV("O",ABMPRV)=""
 ...I $D(^ABMMUPRM(1,1,"B",ABMVLOC)) D
 ....S ABMVIEN=$O(^ABMMUPRM(1,1,"B",ABMVLOC,0))
 ....I $P($G(^ABMMUPRM(1,1,ABMVIEN,0)),U,2)=1 S ABMPRV("E",ABMPRV)=""
 ....I $P($G(^ABMMUPRM(1,1,ABMVIEN,0)),U,2)'=1 S ABMPRV("O",ABMPRV)=""
 ..I $$GET1^DIQ(7,ABMPRVCL,9999999.01,"E")'=11,$D(^ABMMUPRM(1,2,"B",ABMPRVCL)) S ABMPRV("E",ABMPRV)=""
 D CALCDTS
 S ABMDTFLG=0
 S ABMP("BDT")=ABMP("BSDT")
 F  D  Q:ABMDTFLG=1
 .S ^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"))=+$G(^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT")))+1
 .S ^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"),ABMVLOC)=+$G(^XTMP("ABM-PVP2",$J,"PRV-DENOM",ABMP("BDT"),ABMVLOC))+1
 .S ^XTMP("ABM-PVP2",$J,"PT VSTS",ABMPT,ABMSDT,ABMVDFN)=""  ;list of visits by patient,DOS
 .S ^XTMP("ABM-PVP2",$J,"VISITS",ABMVDFN)=""  ;list of visits to check for pymt
 .S ^XTMP("ABM-PVP2",$J,"VISIT CNT",ABMP("BDT"))=+$G(^XTMP("ABM-PVP2",$J,"VISIT CNT",ABMP("BDT")))+1  ;count of visits
 .I (^XTMP("ABM-PVP2",$J,"VISIT CNT",ABMP("BDT"))#1000&(IOST["C")) W "." U 0 W "."
 .D GPTDATA
 .;
 .S X1=ABMP("BDT")
 .S X2=1
 .D C^%DTC
 .I X>ABMP("VDT") S ABMDTFLG=1 Q
 .S ABMP("BDT")=X
 .K ABMITYP
 Q
CALCDTS ;EP
 ;Calculate forward and back 89 days for visit
 S X1=$P(ABMSDT,".")
 S X2=$P(ABMY("SDT"),".")
 D ^%DTC
 S X1=$P(ABMSDT,".")
 S X2=$S(X<89:-X,1:-89)
 D C^%DTC
 S ABMP("BSDT")=X
 ;
 S ABMP("BEDT")=ABMP("VDT")
 Q
BILLS ;EP
 S ABMCNT=0
 S ABMDUZ2=0
 S ABMFOUND=0
 F  S ABMDUZ2=$O(^ABMDBILL(ABMDUZ2)) Q:'ABMDUZ2  D
 .Q:'$D(^ABMDBILL(ABMDUZ2,0))
 .S ABMVDFN=0
 .F  S ABMVDFN=$O(^XTMP("ABM-PVP2",$J,"VISITS",ABMVDFN)) Q:'ABMVDFN  D
 ..S ABMBILLF=0
 ..Q:($G(^XTMP("ABM-PVP2",$J,"VISITS",ABMVDFN))=1)  ;already counted this visit on report
 ..Q:'$D(^ABMDBILL(ABMDUZ2,"AV",ABMVDFN))  ;visit not under this DUZ(2)
 ..K ABMBILLN,ABMSAV
 ..S ABMP("BDFN")=0
 ..F  S ABMP("BDFN")=$O(^ABMDBILL(ABMDUZ2,"AV",ABMVDFN,ABMP("BDFN"))) Q:'ABMP("BDFN")  D  Q:ABMBILLF
 ...S (ABMBILLN,ABMSAV)=$P($G(^ABMDBILL(ABMDUZ2,ABMP("BDFN"),0)),U)
 ...I $P($G(^ABMDBILL(ABMDUZ2,ABMP("BDFN"),0)),U,4)="X" Q
 ...S ABMSDT=$P($$GET1^DIQ(9000010,ABMVDFN,".01","I"),".")
 ...S ABMVLOC=$P($G(^ABMDBILL(ABMDUZ2,ABMP("BDFN"),0)),U,3)
 ...S ABMP("VDT")=$P($G(^ABMDBILL(ABMDUZ2,ABMP("BDFN"),7)),U)
 ...S ABMINS=$P($G(^ABMDBILL(ABMDUZ2,ABMP("BDFN"),0)),U,8)
 ...S ABMPT=$P($G(^ABMDBILL(ABMDUZ2,ABMP("BDFN"),0)),U,5)
 ...K ABMDX
 ...D PRIMPOV^ABMM2PV7  ;get primary POV for bill
 ...D ARBILLS
 ..I +$G(ABMFOUND)=1 D OTHERVST  ;check for other visits on DOS to mark as paid
 ..;
 ..;now look thru bills found and remove zero pays when a payment was found - GROUP PROVIDERS
 ..I ABMY("RTYP")="GRP" D  Q
 ...S ABMP("BDT")=0
 ...F  S ABMP("BDT")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"))) Q:'ABMP("BDT")  D
 ....S ABMGRP=""
 ....F  S ABMGRP=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMGRP)) Q:ABMGRP=""  D
 .....S ABMVDFN=0
 .....F  S ABMVDFN=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMGRP,ABMVDFN)) Q:'ABMVDFN  D
 ......S ABMP("BDFN")=0
 ......F  S ABMP("BDFN")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMGRP,ABMVDFN,ABMP("BDFN"))) Q:'ABMP("BDFN")  D
 .......I $D(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMGRP,ABMVDFN,ABMP("BDFN"))) D
 ........K ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMGRP,ABMVDFN,ABMP("BDFN"))
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMGRP))+1
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMVLOC,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMVLOC,ABMGRP))+1
 ...K ^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS")
 ...;
 ...S ABMP("BDT")=0
 ...F  S ABMP("BDT")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"))) Q:'ABMP("BDT")  D
 ....S ABMGRP=""
 ....F  S ABMGRP=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMGRP)) Q:ABMGRP=""  D
 .....S ABMVDFN=0
 .....F  S ABMVDFN=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMGRP,ABMVDFN)) Q:'ABMVDFN  D
 ......S ABMP("BDFN")=0
 ......F  S ABMP("BDFN")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMGRP,ABMVDFN,ABMP("BDFN"))) Q:'ABMP("BDFN")  D
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMVLOC,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMVLOC,ABMGRP))+1
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMGRP))+1
 ...K ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS")
 ..;
 ..;now look thru bills found and remove zero pays when a payment was found - INDIVIDUAL PROVIDERS
 ..S ABMP("BDT")=0
 ..F  S ABMP("BDT")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"))) Q:'ABMP("BDT")  D
 ...S ABMPRV=0
 ...F  S ABMPRV=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMPRV)) Q:'ABMPRV  D
 ....S ABMGRP=""
 ....F  S ABMGRP=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMPRV,ABMGRP)) Q:ABMGRP=""  D
 .....S ABMVDFN=0
 .....F  S ABMVDFN=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN)) Q:'ABMVDFN  D
 ......S ABMP("BDFN")=0
 ......F  S ABMP("BDFN")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN,ABMP("BDFN"))) Q:'ABMP("BDFN")  D
 .......I $D(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN,ABMP("BDFN"))) D
 ........K ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN,ABMP("BDFN"))
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMPRV,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMPRV,ABMGRP))+1
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMPRV,ABMVLOC,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM PD",ABMP("BDT"),ABMPRV,ABMVLOC,ABMGRP))+1
 ..K ^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS")
 ..;
 ..S ABMP("BDT")=0
 ..F  S ABMP("BDT")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"))) Q:'ABMP("BDT")  D
 ...S ABMPRV=0
 ...F  S ABMPRV=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMPRV)) Q:'ABMPRV  D
 ....S ABMGRP=""
 ....F  S ABMGRP=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMPRV,ABMGRP)) Q:ABMGRP=""  D
 .....S ABMVDFN=0
 .....F  S ABMVDFN=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN)) Q:'ABMVDFN  D
 ......S ABMP("BDFN")=0
 ......F  S ABMP("BDFN")=$O(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN,ABMP("BDFN"))) Q:'ABMP("BDFN")  D
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMPRV,ABMVLOC,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMPRV,ABMVLOC,ABMGRP))+1
 .......S ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMPRV,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD",ABMP("BDT"),ABMPRV,ABMGRP))+1
 ..K ^XTMP("ABM-PVP2",$J,"PRV-NUM ZEROPD BILLS")
 Q
ARBILLS ;
 S ABMBILLN=+ABMBILLN_" "
 S ABMSAV=+ABMSAV
 F  S ABMBILLN=$O(^BARBL(ABMPAR,"B",ABMBILLN)) Q:$G(ABMBILLN)=""!(ABMBILLN'[ABMSAV)  D  Q:ABMBILLF
 .S ABMARIEN=0
 .S ABMHOLD=DUZ(2)
 .S DUZ(2)=ABMPAR
 .F  S ABMARIEN=$O(^BARBL(DUZ(2),"B",ABMBILLN,ABMARIEN)) Q:'ABMARIEN  D   Q:ABMBILLF
 ..S ABMCBAMT=$$GET1^DIQ(90050.01,ABMARIEN_",",15,"I")  ;Current Bill Amount
 ..;I ABMCBAMT'=0 Q  ;only if current bill amount is zero
 ..S ABMARACT=$$GET1^DIQ(90050.01,ABMARIEN_",",3,"I")  ;A/R BILL, A/R ACCOUNT
 ..S D0=ABMARACT
 ..S ABMITYP=$$VALI^BARVPM(8)   ;GET 'VIP INSURER TYPE' CODE
 ..S ABMGRP=$S(ABMITYP="D":"MCD",ABMITYP="K":"CHIP",(ABMITYP="P"&($D(ABMI("INS",ABMINS)))):"K",1:"OTHR")
 ..S ABMABILN=$P($G(^BARBL(DUZ(2),ABMARIEN,0)),U)
 ..S ^XTMP("ABM-PVP2",$J,"VISITS",ABMVDFN)=1
 ..;
 ..D CALCDTS
 ..S ABMDTFLG=0
 ..S ABMP("BDT")=ABMP("BSDT")
 ..F  D  Q:ABMDTFLG=1
 ...I (ABMCNT#1000&(IOST["C")) W "."
 ...S ABMCNT=+$G(ABMCNT)+1
 ...;I ABMY("RTYP")="SEL" D PTDATA
 ...I ABMY("RTYP")="SEL" D
 ....S ABMPIEN=0
 ....K ABMPRVC
 ....F  S ABMPIEN=$O(^AUPNVPRV("AD",ABMVDFN,ABMPIEN)) Q:'ABMPIEN  D
 .....S ABMPRV=$$GET1^DIQ(9000010.06,ABMPIEN,.01,"I")
 .....Q:'$D(ABMPRVDR(ABMPRV))
 .....;skip provider if on visit more than once
 .....Q:$D(ABMPRVC(ABMPRV))
 .....S ABMPRVC(ABMPRV)=1
 .....D CALCDTS
 .....S ABMDTFLG=0
 .....S ABMP("BDT")=ABMP("BSDT")
 .....F  D  Q:ABMDTFLG=1
 ......I ABMP("VDT")<ABMP("BSDT") Q  ;visit is before 90-day window
 ......K ABMITYP
 ......D PTDATA
 ......S X1=ABMP("BDT")
 ......S X2=1
 ......D C^%DTC
 ......I X>ABMP("BEDT") S ABMDTFLG=1 Q
 ......S ABMP("BDT")=X
 ...I ABMY("RTYP")="GRP" D GPTDATA
 ...S X1=ABMP("BDT")
 ...S X2=1
 ...D C^%DTC
 ...I X>ABMP("BEDT") S ABMDTFLG=1 Q
 ...S ABMP("BDT")=X
 ..;
 ..D TRANS
 .S DUZ(2)=ABMHOLD
 Q
TRANS ;
 S ABMTRIEN=0
 F  S ABMTRIEN=$O(^BARTR(DUZ(2),"AC",ABMARIEN,ABMTRIEN)) Q:'ABMTRIEN  D  Q:ABMBILLF
 .S ABMTRTYP=$P($G(^BARTR(DUZ(2),ABMTRIEN,1)),U)
 .S ABMADJT=$P($G(^BARTR(DUZ(2),ABMTRIEN,1)),U,3)
 .I ABMTRTYP=49 Q  ;skip BILL NEW transactions
 .I (ABMTRTYP'=40)&("^113^114^121^132^137^138^139^"'[("^"_ABMADJT_"^")) D ZEROPD Q  ;payment or payment credit
 .I $P($G(^BARTR(DUZ(2),ABMTRIEN,0)),U,7)=1 Q  ;message trans
 .S ABMTRAMT=$$GET1^DIQ(90050.03,ABMTRIEN,3.5)  ;debit-credit field
 .I ABMTRAMT<(.01) Q  ;don't count 0 pymts or reversals
 .I ABMY("RTYP")="GRP" D GRPBILL Q
 .S ABMPIEN=0
 .K ABMPRVC
 .F  S ABMPIEN=$O(^AUPNVPRV("AD",ABMVDFN,ABMPIEN)) Q:'ABMPIEN  D  Q:ABMBILLF
 ..S ABMPRV=$$GET1^DIQ(9000010.06,ABMPIEN,".01","I")
 ..Q:'$D(ABMPRVDR(ABMPRV))
 ..;skip provider if on visit more than once
 ..Q:$D(ABMPRVC(ABMPRV))
 ..S ABMPRVC(ABMPRV)=1
 ..D CALCDTS
 ..S ABMDTFLG=0
 ..S ABMP("BDT")=ABMP("BSDT")
 ..F  D  Q:ABMDTFLG=1
 ...S ^XTMP("ABM-PVP2",$J,"PRV-NUM PD BILLS",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN,ABMP("BDFN"))=""
 ...S ^XTMP("ABM-PVP2",$J,"PRV-NUM PD DET",ABMP("BDT"),ABMPRV,ABMGRP,ABMVDFN,ABMP("BDFN"))=ABMTRAMT
 ...S ABMBILLF=1
 ...S ^XTMP("ABM-PVP2",$J,"PRV ENC CNT",ABMP("BDT"),ABMPRV,ABMVLOC,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV ENC CNT",ABMP("BDT"),ABMPRV,ABMVLOC,ABMGRP))+1
 ...S ^XTMP("ABM-PVP2",$J,"PRV ENC CNT",ABMP("BDT"),ABMPRV,ABMGRP)=+$G(^XTMP("ABM-PVP2",$J,"PRV ENC CNT",ABMP("BDT"),ABMPRV,ABMGRP))+1
 ...S ^XTMP("ABM-PVP2",$J,"PRV-VST",ABMP("BDT"),ABMVDFN,ABMPRV)=""
 ...S ^XTMP("ABM-PVP2",$J,"VISITS",ABMVDFN)=1
 ...I (ABMCNT#1000&(IOST["C")) W "."
 ...S ABMCNT=+$G(ABMCNT)+1
 ...D PTDATA
 ...S X1=ABMP("BDT")
 ...S X2=1
 ...D C^%DTC
 ...I X>ABMP("BEDT") S ABMDTFLG=1 Q
 ...S ABMP("BDT")=X
 Q
ZEROPD ;EP
 D ZEROPD^ABMM2PV7
 Q
GRPBILL ;
 D GRPBILL^ABMM2PV7
 Q
GRPOTHVS ;
 D GRPOTHVS^ABMM2PV7
 Q
OTHERVST ;EP
 D OTHERVST^ABMM2PV7
 Q
GPTDATA ;EP
 D GPTDATA^ABMM2PV7
 Q
PTDATA ;EP
 D PTDATA^ABMM2PV7
 Q
ENROLL ;EP
 D ENROLL^ABMM2PV7
 Q
