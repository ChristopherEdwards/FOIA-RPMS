ABMPT268 ; IHS/SD/SDR - 3P BILLING 2.6 Patch 8 POST INIT ;  
 ;;2.6;IHS Third Party Billing;**8**;NOV 12, 2009
 ;
 Q
POST ;
 D EXP31
 D EXP33
 D ERRCODES
 D PRVCLASS
 D CHRGSUMM
 D MUCONV
 Q
EXP31 ;
 K DIC,DR,DINUM,DLAYGO,DIE
 S DIC="^ABMDEXP("
 S DIC(0)="LM"
 S DLAYGO=9002274
 S X="837I (UB) 5010",DINUM=31
 K DD,DO
 D ^DIC
 Q:Y<0
 S DA=+Y
 S DIE="^ABMDEXP("
 S DR=".04////ABMEF31;.07///837 5010 INSTITUTIONAL;.08///1,2,3,4,5,6,8,14,19,21,22,23,24,28,31;.11////ABMDES1;.15///E"
 D ^DIE
EXP33 ;
 K DIC,DR,DINUM,DLAYGO,DIE
 S DIC="^ABMDEXP("
 S DIC(0)="LM"
 S DLAYGO=9002274
 S X="837D (ADA) 5010",DINUM=33
 K DD,DO
 D ^DIC
 Q:Y<0
 S DA=+Y
 S DIE="^ABMDEXP("
 S DR=".04////ABMEF33;.07///837 5010 DENTAL;.08///1,2,3,4B,5,14,16,17,18,19,28,34,35;.11////ABMDES3;.15///E"
 D ^DIE
 Q
ERRCODES ;
 ;240 - POA VALUE '1' NOT APPLICABLE FOR THIS EXPORT MODE
 K DIC,X
 S DIC="^ABMDERR("
 S DIC(0)="LM"
 S DINUM=240
 S X="POA VALUE '1' NOT APPLICABLE FOR THIS EXPORT MODE"
 S DIC("DR")=".02///Select different POA Value"
 S DIC("DR")=DIC("DR")_";.03///E"
 K DD,DO
 D FILE^DICN
 D SITE(240)
 Q
SITE(ABMX) ;Add SITE multiple
 S DUZHOLD=DUZ(2)
 S DUZ(2)=0
 F  S DUZ(2)=$O(^ABMDCLM(DUZ(2))) Q:'+DUZ(2)  D
 .S DIC(0)="LX"
 .S DA(1)=ABMX
 .S DIC="^ABMDERR("_DA(1)_",31,"
 .S DIC("P")=$P(^DD(9002274.04,31,0),U,2)
 .S DINUM=DUZ(2)
 .S X=$P($G(^DIC(4,DUZ(2),0)),U)
 .S DIC("DR")=".03////E"
 .D ^DIC
 .K DA,DIC,DINUM
 S DUZ(2)=DUZHOLD
 K DUZHOLD,DLAYGO,ABMX
 Q
 ;
PRVCLASS ;
 F ABMI=1:1 S ABMLN=$P($T(PCLASS+ABMI),";;",2) Q:ABMLN="END"  D
 .S DA(1)=1
 .S DIC="^ABMMUPRM("_DA(1)_",2,"
 .S DIC(0)="QLM"
 .S X=$P(ABMLN,U)
 .D ^DIC
 Q
PCLASS ;
 ;;00^PHYSICIAN
 ;;11^PHYSICIAN ASSISTANT
 ;;16^PEDIATRIC NURSE PRACT.
 ;;17^NURSE MIDWIFE
 ;;18^PHYSICIAN (CONTRACT)
 ;;21^NURSE PRACTITIONER
 ;;41^OB/GYN (CONTRACT)
 ;;44^PHYSICIAN (TRIBAL)
 ;;45^OSTEOPATH
 ;;49^CONTRACT PSYCHIATRIST
 ;;52^DENTIST
 ;;64^NEPHROLOGIST
 ;;68^EMERGENCY ROOM PHYSICIAN
 ;;70^CARDIOLOGIST
 ;;71^INTERNAL MEDICINE
 ;;72^OB/GYN
 ;;73^ORTHOPEDIST
 ;;74^OTOLARYNGOLOGIST
 ;;75^PEDIATRICIAN
 ;;76^RADIOLOGIST
 ;;77^SURGEON
 ;;78^UROLOGIST
 ;;79^OPHTHALMOLOGIST
 ;;80^FAMILY PRACTICE
 ;;81^PSYCHIATRIST
 ;;82^ANESTHESIOLOGIST
 ;;83^PATHOLOGIST
 ;;85^NEUROLOGIST
 ;;86^DERMATOLOGIST
 ;;A1^SPORTS MEDICINE PHYSICIAN
 ;;A9^HEPATOLOGIST
 ;;B1^GASTROENTEROLOGIST
 ;;B2^ENDOCRINOLOGIST
 ;;B3^RHEUMATOLOGIST
 ;;B4^ONCOLOGIST-HEMATOLOGIST
 ;;B5^PULMONOLOGIST
 ;;B6^NEUROSURGEON
 ;;END
CHRGSUMM ;
 S DIE="^ABMDEXP("
 S DR=".11////ABMDES4"
 F DA=23,33 D
 .D ^DIE
 Q
MUCONV ;
 S ABMIEN=0
 F  S ABMIEN=$O(^ABMMUPRM(1,1,ABMIEN)) Q:'ABMIEN  D
 .S ABMLOC=+$P($G(^ABMMUPRM(1,1,ABMIEN,0)),U)
 .Q:'$D(^ABMMUPRM(1,1,ABMLOC))
 .S $P(^ABMMUPRM(1,1,ABMIEN,0),U,2)=$P($G(^ABMMUPRM(1,1,ABMLOC,0)),U,2)
 .S DA(1)=1
 .S DA=ABMLOC
 .S DIK="^ABMMUPRM("_DA(1)_",1,"
 .D ^DIK
 Q
