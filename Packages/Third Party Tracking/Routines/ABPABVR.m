ABPABVR ;PAYMENT BATCH VERIFICATION REPORT [ 05/24/91  1:10 PM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
A0 D END,^%AUCLS W !!
 W "Print PAYMENT BATCH VERIFICATION REPORT.....",!!!
FRDT K DIC S DIC("A")="Select PAYMENT BATCH BEGINNING DATE: "
 S DIC="^ABPAPBAT(",DIC(0)="AEQZ" D ^DIC
 I +Y<0 G END
 S FR=+$E(+Y,4,5)_"/"_+$E(+Y,6,7)_"/"_+$E(+Y,2,3)
TODT K DIC S DIC("A")="Select PAYMENT BATCH ENDING DATE: "
 S DIC="^ABPAPBAT(",DIC(0)="AEQZ" W !! D ^DIC
 I +Y<0 G FRDT
 L ^ABPAPBAT(+Y,0):3 I '$T D  G A0
 .W *7,!!?15,"<<< BATCH FILE IN USE - CANNOT ACCESS >>>"
 S TO=+$E(+Y,4,5)_"/"_+$E(+Y,6,7)_"/"_+$E(+Y,2,3)
SORT W !!,"THIS REPORT CAN BE SORTED BY",!!?5,"1 - PAYMENT BATCH DATE"
 W !?5,"2 - FACILITY NAME",!!,"Select your SORT PREFERENCE// "
 R ABPAX:DTIME I '$T G A0
 I ABPAX["^" G A0
 I (+ABPAX<1)!(+ABPAX>2) W *7,"  ??"
 W $S(+ABPAX=1:"  PAYMENT BATCH DATE",1:"  FACILITY NAME")
 S BY=$S(+ABPAX=1:"[ABPABVR1]",1:"[ABPABVR2]"),L=0,DIC="^ABPVAO("
 S DHD="AO PRIVATE INSURANCE - BATCH VERIFICATION REPORT - "
 S DHD=$S(BY="[ABPABVR1]":DHD_"DATE ORDER",1:DHD_"FACILITY ORDER")
 F ABPAI=$L(DHD):1:79 S DHD=DHD_" "
 S DHD=DHD_"For the period:  "_FR_" through "_TO
 I BY="[ABPABVR2]" D
 .S FR="A,"_FR_",STANDARD"
 .S TO="ZZZZZ,"_TO_",STANDARD"
 S %ZIS("A")="Select DEVICE or [Q]ueue: "
DIP W ! D EN1^DIP D PAUSE^ABPAMAIN
END K DIC,X,Y,FR,TO,L,BY,DHD,%ZIS("A"),ABPAX,ABPAI Q
