ABPACLG2 ;CHECK LOG UTILITY FUNCTIONS - PART 2; [ 06/27/91  7:38 AM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
 Q  ;;NOT AN ENTRY POINT
 ;---------------------------------------------------------------------
START ;PROCEDURE TO SELECT FUNCTION TO PERFORM
 S DX=0,DY=12 X XY D EOP^ABPAMAIN W !,"Check Log FUNCTIONS"
 K DIR,ABPA("FUNC")
 S DIR(0)="SO^1:EDIT CHECK NUMBER;2:RETURN CHECK;3:TRANSFER CHECK"
 S DIR(0)=DIR(0)_";4:FLAG AS IMPROPERLY LOGGED"
 S DIR("A")="FUNCTION" D ^DIR K DIR
 I +Y'>0!(+Y'<5) D  G GETCHK^ABPACLG1
 .D CLEAR^ABPACLG1 K ABPACHK D HEAD^ABPACLG1
 S ABPA("FUNC")=$S(Y=1:"E",Y=2:"R",Y=3:"T",Y=4:"I"),ABPA("FD")=Y(0)
 I ABPA("FUNC")="E" D  G GETCHK^ABPACLG1
 .;------------------------------------------------------------------
 .;PROCEDURE TO EDIT THE CHECK NUMBER
 .D HEAD^ABPACLG1 S DX=0,DY=12 X XY D EOP^ABPAMAIN W !,ABPA("FD")
 .K DIC,DIE,DIR,DA,DR
 .S DA(2)=ABPADFN(1),DA(1)=ABPADFN(2),DA=ABPADFN(3)
 .S DIE="^ABPACHKS("_DA(2)_",""I"","_DA(1)_",""C"","
 .S DR=".01" W ! D ^DIE S ABPACHK("NNUM")=X
 .S DR="4///"_DUZ_";5///NOW" D ^DIE
 .I ABPACHK("NNUM")'=ABPACHK("NUM") D
 ..S ^TMP("ABPACLG1","E",ABPACHK("NUM"),ABPACHK("AMT"),INSPTR,ABPACHK("NNUM"),DT)=ABPACHK("XMIT")
 .D CLEAR^ABPACLG1 K ABPACHK D HEAD^ABPACLG1
 .;------------------------------------------------------------------
 I "IR"[ABPA("FUNC") K QFLG D  I $D(QFLG)=1 K QFLG G GETCHK^ABPACLG1
 .I ABPACHK("AMT")'=ABPACHK("RAMT") D
 ..W *7,!?5,"<<< INVALID FUNCTION - CHECK PARTIALLY PROCESSED >>>"
 ..H 2 D CLEAR^ABPACLG1 K ABPACHK D HEAD^ABPACLG1 S QFLG=""
 G:ABPA("FUNC")="I" START^ABPACLG7 G START^ABPACLG3
