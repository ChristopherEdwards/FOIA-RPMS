ABPACLG7 ;CHECK LOG MODIFICATIONS REPORT; [ 07/01/91  4:31 PM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
 Q  ;;NOT AN ENTRY POINT
START ;ENTRY POINT
 ;---------------------------------------------------------------------
IMPROPER ;PROCEDURE TO PROCESS IMPORPERLY LOGGED CHECKS
 S DIR(0)="Y",DIR("A")="FLAG CHECK NUMBER  >>>  "_ABPACHK("NUM")
 S DIR("A")=DIR("A")_"  <<<  AS IMPROPERLY LOGGED.  ARE YOU SURE"
 D HEAD^ABPACLG1 S DX=0,DY=12
 X XY D EOP^ABPAMAIN W *7 D ^DIR K DIR I 'Y D  G GETCHK^ABPACLG1
 .D CLEAR^ABPACLG1 K ABPACHK D HEAD^ABPACLG1
 W ! D WAIT^DICD K DIC,DIE,DR,DA
 S DA(2)=ABPADFN(1),DA(1)=ABPADFN(2),DA=ABPADFN(3)
 S DIE="^ABPACHKS("_DA(2)_",""I"","_DA(1)_",""C"","
 S DR="4///"_DUZ_";5///NOW;6///"_ABPA("FUNC")_";8///0"
 D ^DIE K ^ABPACHKS("RB",ABPADFN(1),ABPADFN(2),ABPADFN(3))
 I $D(^ABPACHKS(DA(2),"I",DA(1),"C",DA,"SP",0))'=1 D
 .S ^ABPACHKS(DA(2),"I",DA(1),"C",DA,"SP",0)="^9002270.31113DA^^0"
 S %DT="T",X="NOW" D ^%DT K DIC,DIE,DR
 S DIC="^ABPACHKS("_DA(2)_",""I"","_DA(1)_",""C"","_DA_",""SP"","
 K DA S DA(3)=ABPADFN(1),DA(2)=ABPADFN(2),DA(1)=ABPADFN(3)
 S DIC(0)="LZ",X=Y D ^DIC S ABPADFN(4)=+Y K DIC,DIE,DA,DR
 S DA(3)=ABPADFN(1),DA(2)=ABPADFN(2),DA(1)=ABPADFN(3),DA=ABPADFN(4)
 S DIE="^ABPACHKS("_DA(3)_",""I"","_DA(2)_",""C"","_DA(1)_",""SP"","
 S DR="1///"_ABPA("FUNC")_";2///"_DUZ_";3///"_ABPACHK("AMT")_";4"
 W ! D ^DIE I ABPADFN(1)=1 D
 .S ABPA("$P")=+$P(^ABPAPBAT($P(ABPACHK("XMIT"),"."),0),"^",12)
 .S ABPA("$P")=ABPA("$P")+ABPACHK("AMT")
 .S $P(^ABPAPBAT($P(ABPACHK("XMIT"),"."),0),"^",12)=ABPA("$P")
 S ^TMP("ABPACLG1","I",ABPACHK("NUM"),ABPACHK("AMT"),INSPTR,ACTPTR,DT)=ABPACHK("XMIT")
 D CLEAR^ABPACLG1 K ABPACHK D HEAD^ABPACLG1
 G GETCHK^ABPACLG1
