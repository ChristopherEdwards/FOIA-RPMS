ABPAEEC0 ;ENTER/EDIT PVT INS CLAIMS; [ 07/09/91  11:19 AM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
 Q  ;;NOT AN ENTRY POINT
WRITE F ABPAJ=3:1 Q:$P(ABPATEXT,";",ABPAJ)=""  D
 .S @("P"_ABPAJ)=$P(ABPATEXT,";",ABPAJ)
 W ! S DX=P5,DY=P6 S:IOST["QUME" DY=DY+1 X XY W P3,$J(P4,22)
 I $D(P7)=1 W " " W:$D(@P7)=1 @P7
 Q
 ;
REVON S DX=0,DY=19 W ! X XY D EOP^ABPAMAIN W:IOST["QUME" !
 W:$D(ABPARON) @(ABPARON)
 Q
 ;
START D XIT S $P(ABPAL,"-",79)="",$P(ABPALL,"=",79)="" D SCREEN
 D PAT I $D(ABPATDFN)'=1 D XIT Q
 D DOS G:$D(ABPADOS)'=1 START G:$D(ABPAAMT)=1 EDIT^ABPAEEC1
 D THEREST
CONT G EDIT^ABPAEEC1
 ;
SCREEN S ABPA("HD",1)=ABPATLE
 S ABPA("HD",2)="ENTER/EDIT CLAIM DATA" D ^ABPAHD
 F ABPAI=1:1 S ABPATEXT=$T(PROMPT+ABPAI) Q:ABPATEXT=""  D WRITE
 W !,ABPALL
 Q
 ;
PAT D REVON W "Enter the NAME of the patient (format = LNAME,FNAME MI.)"
 W:$D(ABPARON)=1 @(ABPAROFF)
 S ABPAPTN="" D ^ABPAPATL
 Q:$D(ABPATDFN)'=1  I +ABPATDFN'>0 D SCREEN G PAT
 S ABPAPNAM=ABPAPAT,ABPAHRN=$P(^ABPVAO(ABPATDFN,0),"^",3)
 S ABPAFAC=$P(^DIC(4,$P(^ABPVAO(ABPATDFN,0),"^",2),0),"^")
 L ^ABPVAO(ABPATDFN):3 I '$T D  H 3 G PAT
 .W *7,!!?5,"<<< PATIENT RECORD UNAVAILABLE AT THIS TIME -- "
 .W "TRY AGAIN LATER >>>"
 D:$Y>3 SCREEN F ABPAI=1:1:3 S ABPATEXT=$T(PROMPT+ABPAI) D WRITE
 Q
 ;
DOS K DIC,DIE,DA,DR,X,Y
 S DA(1)=+ABPATDFN,DIC="^ABPVAO("_DA(1)_",1,",DIC(0)="AELQZ"
 I $D(^ABPVAO(DA(1),1,0))'=1 D
 .S ^ABPVAO(DA(1),1,0)="^9002270.21ID^^0"
 S DIC("A")="DATE: " D REVON W "Enter the DATE OF SERVICE being billed"
 W:$D(ABPARON)=1 @(ABPAROFF) D ^DIC Q:+Y<1  S ABPACDFN=+Y,ABPADOS=Y(0,0)
 I +$P(Y,"^",3)<1 D  Q
 .S ABPATYPE=$P(Y(0),"^",4) S:ABPATYPE="O" ABPATYPE="OUTPATIENT"
 .S:ABPATYPE="I" ABPATYPE="INPATIENT (HOSPITAL ONLY)"
 .S:ABPATYPE="P" ABPATYPE="INPATIENT (PHYSICIAN ONLY)"
 .S:ABPATYPE="D" ABPATYPE="DENTAL" S ABPADV=$P(Y(0),"^",5)
 .S ABPAINS=$P(Y(0),"^",6),ABPAINS=$P(^AUTNINS(ABPAINS,0),"^")
 .S ABPAAMT="$"_$J($P(Y(0),"^",7),8,2),ABPAPHNM=$P(Y(0),"^",8)
 .S ABPAPNUM=$P(Y(0),"^",9) D:$Y>3 SCREEN
 .F ABPAI=4:1:8 S ABPATEXT=$T(PROMPT+ABPAI) D WRITE
 D:$Y>3 SCREEN S ABPATEXT=$T(PROMPT+4) D WRITE
 Q
 ;
THEREST F ABPAK=5:1:9 K DIC,DIE,DA,DR,X,Y D
 .S LBL="FLD"_+ABPAK_"^ABPAEEC1" D @LBL
 K DIC,DIE,DA,DR,X,Y
 S DA(1)=+ABPATDFN,DA=+ABPACDFN,DIE="^ABPVAO("_DA(1)_",1,"
 F ABPACN=DT_1:1 Q:$D(^ABPVAO("CN",ABPACN))=0
 S DR=".02////"_ABPACN_";.18///OPEN" D ^DIE
 Q
 ;
XIT L
 K ABPA,ABPAI,ABPAJ,DIC,X,Y,ABPATDFN,ABPAPNAM,ABPAHRN,ABPAL,ABPALL
 K ABPAFAC,ABPATYPE,ABPAINS,ABPADV,ABPAAMT,ABPADOS,ABPAK,ABPACN,DTOUT
 K DFOUT,DUOUT,DQOUT,DLOUT,ABPAPHNM,ABPAPNUM
 Q
 ;
PROMPT ;;FLD #;TITLE;X-POSITION;Y-POSITION;VARIABLE NAME
 ;;(1)   ;Patient Name:;7;8;ABPAPNAM
 ;;(2)   ;Facility:;7;9;ABPAFAC
 ;;(3)   ;Health Record Number:;7;10;ABPAHRN
 ;;(4)   ;Date of Service:;7;11;ABPADOS
 ;;(5)   ;Visit Type:;7;12;ABPATYPE
 ;;(6)   ;Days or Visits:;7;13;ABPADV
 ;;(7)   ;Insurance Company:;7;14;ABPAINS
 ;;   (a);Policy Holder Name:;7;15;ABPAPHNM
 ;;   (b);Policy Number:;7;16;ABPAPNUM
 ;;(8)   ;Claim Amount:;7;17;ABPAAMT
