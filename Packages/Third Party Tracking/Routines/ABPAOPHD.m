ABPAOPHD ;POSTING LOG REPORT HEADER; [ 03/10/91  4:31 PM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
START S X=0,SITENAME=$P(^DIC(4,DUZ(2),0),"^",1)
NXTX S X=$O(ABPA("HD",X)) G:+X=0 MOVE
 S AU("MAX")=X G NXTX
MOVE F I=AU("MAX"):-1:1 S ABPA("HD",I+2)=ABPA("HD",I)
WRITE S ABPA("HD",1)=SITENAME,$P(AU("LINE"),"-",$L(SITENAME)+1)=""
 S ABPA("HD",2)=AU("LINE"),AU("MAX")=AU("MAX")+2
 S XX="",$P(XX,"*",80)=""
 D NOW^%DTC S Y=% X ^DD("DD") S RUNTM=$P(Y,"@",2)
 S RUNDT=+%I(1)_"/"_+%I(2)_"/"_+$E(+%I(3),2,3)_" AT "_RUNTM
 S ABPAPG=ABPAPG+1 S PG="PAGE: "_ABPAPG
 D ^%AUCLS
 W "RUN DATE: ",RUNDT,?(79-($L(PG))),PG,!,XX
 F I=1:1:AU("MAX") D
 .W !,"*",?40-(($L(ABPA("HD",I))/2)),ABPA("HD",I),?78,"*"
 W !,XX S X="FROM: "_FC W !!?(40-($L(X)/2)),X
 W !!,"THE FOLLOWING ENTRIES HAVE BEEN ADDED TO YOUR DATABASE:"
 K X,Y,I,AU("MAX"),SITENAME,XX,AU("LINE"),RUNDT,RUNTM,PG
 F I=3:1 Q:$D(ABPA("HD",I))=0  D  K ABPA("HD",I)
 .S ABPA("HD",(I-2))=ABPA("HD",I)
QUIT Q
