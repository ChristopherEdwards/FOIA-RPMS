ABPAOPT ;CHECK/SET SYSTEM PARAMETERS; [ 07/10/91  11:43 AM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
CHECK ;ENTRY POINT TO VERIFY SYSTEM PARAMTER SET-UP
 K ABPAOPT I $D(^ABPAFAC(DUZ(2),0))'=1 G NOTSET
 F I=2:1:11 S ABPAOPT(I-1)=$P(^ABPAFAC(DUZ(2),0),"^",I)
 I $D(ABPAOPT)'=10 G NOTSET
 I $D(^ABPACHKS(1,0))'=1 G NOTSET
 K QFLG F I=1:1:10 D  I $D(QFLG)=1 K QFLG G NOTSET
 .I $D(ABPAOPT(I))'=1 S QFLG="" Q
 .I ABPAOPT(I)="" S QFLG="" Q
 G SETDESC
NOTSET U IO(0) W *7,!!?5,"SYSTEM PARAMETERS NOT SET"
 U IO(0) W "...PLEASE CONTACT YOUR SYSTEM MANAGER!!!" H 3
 K ABPAOPT U IO Q
 ;
SET ;ENTRY POINT CALLED BY THE 'ABPASETOPTS' OPTION - ESTABLISHES ALL
 ;NECESSARY SYSTEM PARAMETERS
 S ABPAHD1="Set-up SYSTEM OPTIONS" D HEADER^ABPAMAIN
 K DIC,DIE,DA,DR,X,Y S DIC="^ABPAFAC(",DIC(0)="L"
 S X=$P(^DIC(4,DUZ(2),0),"^",1) D ^DIC I +Y<1 D  Q
 .W *7,!!?5,"*** ACCESS NOT ALLOWED - PLEASE CONTACT YOUR SYSTEM"
 .W " MANAGER! ***" H 4
 S DIE=DIC,DA=+Y,DIE("NO^")=""
 W !! S DR=".05;1;2///Y;3;4;4.5;5;6///Y;7///Y;8///Y;9" D ^DIE
 K DIE,DA,DR S DA=$O(^DIC(19,"B","ABPA/MONTH/UTILRPT/AUTO",""))
 K ABPAIO,%IS,IOP I $D(^DIC(19,DA,200))=1 S %ZIS("B")=$P(^(200),"^",2)
 S %IS="N",%ZIS("A")="PRINTER TO USE FOR OVERNIGHT REPORTS: "
 F I=0:0 D  Q:$D(ABPAIO)=1
 .K ABPAIO D ^%ZIS I POP!($E(IOST,1)'="P") D  Q
 ..S %ZIS("A")="PRINTER TO USE FOR OVERNIGHT REPORTS: "
 ..W *7,"   MUST SELECT A 'PRINTER' DEVICE"
 .S ABPAIO=+IO
 I +DA>0 D
 .S DIE="^DIC(19,",DR="200///T@6P;201///"_ABPAIO_";202///1D" D ^DIE
 K DIE,DA,DR S DA=$O(^DIC(19,"B","ABPA/FYTD/UTILRPT/AUTO",""))
 I +DA>0 D
 .S DIE="^DIC(19,",DR="200///T@7P;201///"_ABPAIO_";202///1D" D ^DIE
 K DIE,DA,DR S DA=$O(^DIC(19,"B","ABPA/MONTH/DISTRPT/AUTO",""))
 I +DA>0 D
 .S DIE="^DIC(19,",DR="200///T@9P;201///"_ABPAIO_";202///1D" D ^DIE
 K DIE,DA,DR S DA=$O(^DIC(19,"B","ABPA/FYTD/DISTRPT/AUTO",""))
 I +DA>0 D
 .S DIE="^DIC(19,",DR="200///T@10P;201///"_ABPAIO_";202///1D" D ^DIE
 K DIE,DA,DR S DA=$O(^DIC(19,"B","ABPA/UNPROCESSED/CHECKS/AUTO",""))
 I +DA>0 D
 .S DIE="^DIC(19,",DR="200///T+1@7A;201///"_ABPAIO_";202///1D" D ^DIE
 I $D(^ABPACHKS(1,0))'=1 D
 .S ^ABPACHKS(1,0)=DUZ(2),^ABPACHKS("B",DUZ(2),1)=""
 .S ^ABPACHKS(1,"I",0)="^9002270.31^^0"
 .S ^ABPACHKS(0)="AO PVT-INS CHECK LOG^9002270.03P^1^1"
 I +$P(^ABPAFAC(DUZ(2),0),"^",12)>0 D
 .I $D(^ABPACHKS("B",+$P(^ABPAFAC(DUZ(2),0),"^",12)))=0 D
 ..S ^ABPACHKS(2,0)=$P(^ABPAFAC(DUZ(2),0),"^",12)
 ..S ^ABPACHKS("B",$P(^ABPAFAC(DUZ(2),0),"^",12),2)=""
 ..S ^ABPACHKS(2,"I",0)="^9002270.31^^0"
 ..S ^ABPACHKS(0)="AO PVT-INS CHECK LOG^9002270.03P^1^2"
 K ABPAMESS S ABPAMESS="This concludes the SYSTEM SET-UP function..."
 S ABPAMESS(2)="... Press any key to continue ... " D PAUSE^ABPAMAIN
 S IOP=$I D ^%ZIS K IOP G CHECK
SETDESC F I=1:1 S X=$T(DESC+I) Q:X=""  D
 .S ABPAOPT(I,"DESC")=$P(X,";",4)
 Q
 ;
DESC ;;PARAMTER #;DESCRIPTION
 ;;1;Ask for FIELD RECEIPT NUMBER
 ;;2;Ask for CHECK NUMBER
 ;;3;MANAGING OFFICE FOR 3P
 ;;4;3P RESOURCES SPECIALIST NAME
 ;;5;TOP MARGIN FOR TRANSMITTAL
 ;;6;MANUAL ENTRIES ON TRANSMITTAL
 ;;7;ALLOW MULTIPLE OPEN BATCHES
 ;;8;ALLOW MULTIPLE USER BATCHES
 ;;9;AGE CLAIMS BY
 ;;10;3P RESOURCES SPECIALIST TITLE
 ;;11;2nd ACCOUNTING POINT
