ABPAPD3 ;AO PVT-INS PAYMENT ENTRY CONTINUED; [ 06/27/91  5:36 PM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
 W !,ABPAX
 W !,"Updating the bill records for: ",$P(^ABPVAO(ABPATDFN,0),"^")
 K DIC,DIE,DA,DR,ABPA("QF") S ABPADOS=ABPAFRDT-1
 F ABPA("I")=0:0 D  Q:$D(ABPA("QF",1))=1
 .S ABPADOS=$O(^ABPVAO("PC",ABPATDFN,ABPADOS))
 .I +ABPADOS=0!(ABPADOS>ABPATODT) S ABPA("QF",1)="" Q
 .K ABPA("QF",2) S ABPACDFN=0 F ABPA("II")=0:0 D  Q:$D(ABPA("QF",2))=1
 ..S ABPACDFN=$O(^ABPVAO("PC",ABPATDFN,ABPADOS,ABPACDFN))
 ..I +ABPACDFN=0 S ABPA("QF",2)="" Q
 ..Q:$D(^ABPVAO(ABPATDFN,1,ABPACDFN,0))=0
 ..S ABPAVTYP=$P(^ABPVAO(ABPATDFN,1,ABPACDFN,0),"^",17)
 ..Q:$D(ABPACSCR(+ABPACDFN))=1
 ..K DIE,DR,DA S DIE="^ABPVAO("_ABPATDFN_",1,"
 ..S DR=".18///"_"PA",DA(1)=ABPATDFN,DA=+ABPACDFN D ^DIE
 ..I $D(^ABPVAO(DA(1),"P",ABPADDFN,"D",0))=0 D
 ...S ^ABPVAO(DA(1),"P",ABPADDFN,"D",0)="^9002270.222DA^^0"
 ..K DIC,DIE,DR,DA S DA(1)=ABPADDFN,DA(2)=ABPATDFN
 ..S DIC="^ABPVAO("_DA(2)_",""P"","_DA(1)_",""D"",",DIC(0)="LZ"
 ..S X=$E(ABPADOS,4,7)_$E(ABPADOS,2,3) D ^DIC S DA=+Y
 ..I +$P(Y,"^",3)<1 D
 ...S X=""""_$E(ABPADOS,4,7)_$E(ABPADOS,2,3)_""""
 ...D ^DIC S DA=+Y
 ..S DIE=DIC,DR="1///"_ABPACDFN D ^DIE W "." Q
DISP K DIC,DIE,DA,DR,ABPA("QF"),ABPADOS,ABPA("I"),ABPA("II"),ABPACDFN
 K ABPAVTYP,ABPA("QF"),ABPAPTR,ABPADATA,ABPATYPE
 S U="^",DC=1,D0=ABPATDFN K DXS W @IOF,! D ^ABPAPDB K DXS,D0 W !
 S ABPA("I")="Current Payment Transactions" W ?(40-($L(ABPA("I"))/2))
 W ABPA("I"),!!,"TRN  DATE REC'D  PAID AMOUNT  CODE   CLAIM #    "
 W "CHECK NUMBER   DATE POSTED",!,"---  ----------  -----------  "
 W "----  --------  ---------------  -----------"
 G ^ABPAPD5
