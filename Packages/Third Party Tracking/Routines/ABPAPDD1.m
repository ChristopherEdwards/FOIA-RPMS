ABPAPDD1 ;PVT-INS PYMT DELETE CONTINUED;[ 06/25/91  4:00 PM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
BEG D XIT S U="^"
 S ABPA("HD",1)=ABPATLE,ABPA("HD",2)="DELETE existing payments"
 D ^ABPAHD W !,"*"
 S X="CURRENT BATCH FACILITY: "_$P(^DIC(4,ABPASITE,0),"^",1)
 W ?(40-($L(X)/2)),X,?78,"*",! F X=1:1:79 W "*"
 S X="BATCH DATE = "_ABPABDT W !!?(40-($L(X)/2)),X
PAT W !! D ^ABPAPATL
 I $D(ABPATDFN)'=1 K ABPA("HD") D  G FAC^ABPAPDD0
 .S ABPA("HD",1)=ABPATLE,ABPA("HD",2)="DELETE existing payments"
 .D ^ABPAHD S X="BATCH DATE = "_ABPABDT W !!?(40-($L(X)/2)),X
 G:+ABPATDFN<1 BEG
 L ^ABPVAO(ABPATDFN):3 I '$T D  G PAT
 .W *7,!!?5,"<<< PATIENT RECORD UNAVAILABLE AT THIS TIME -- "
 .W "TRY AGAIN LATER"
LOOK I $D(^ABPVAO("BD",ABPABDFN,ABPATDFN))'=10 D  G PAT
 .W *7,!!?10,"<<< NO PAYMENT RECORD (FOR THIS BATCH DATE) FOUND >>>"
 .L ^ABPVAO(ABPATDFN)
 D ^ABPAPDEM
 I $D(ABPADDFN)=0 G BEG
 I +ABPADDFN<1 D  G PAT
 .W *7,!!?10,"<<< GLOBAL ERROR DETECTED - PLEASE CONTACT YOUR SYSTEM"
 .L ^ABPVAO(ABPATDFN)
 .W " MANAGER"
 S $P(ABPAX,"=",80)="",$P(ABPAXX,"-",80)=""
CONT G ^ABPAPDD2
XIT K ABPARECV,ABPAPD,ABPAENT,ABPADDFN,ABPATDFN,ABPADT,ABPADTD,ABPAPAT,D
 K ABPA,ABPAL,DIC,C,ABPADT,ABPAQKS,ABPAQK,ABPAHRN,ABPARECV,DA,J,K,Z,XQH
 K ABPAC,ABPAI,ABPAXX,ABPAINS,DIE,DR,%DT,ABPAFRDT,ABPATODT,ABPAX Q
