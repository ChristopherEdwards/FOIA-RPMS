ABPAPDV1 ;PVT-INS PYMT DISPLAY CONTINUED [ 06/26/91  11:02 AM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
BEG D XIT S U="^"
 S ABPA("HD",1)=ABPATLE,ABPA("HD",2)="DISPLAY existing payments"
 D ^ABPAHD W !,"*"
 S X="CURRENT BATCH FACILITY: "_$P(^DIC(4,ABPASITE,0),"^",1)
 W ?(40-($L(X)/2)),X,?78,"*",! F X=1:1:79 W "*"
 S X="BATCH DATE = "_ABPABDT W !!?(40-($L(X)/2)),X
PAT W !! D ^ABPAPATL
 I $D(ABPATDFN)'=1 D  G FAC^ABPAPDV0
 .K ABPA("HD") S ABPA("HD",1)=ABPATLE
 .S ABPA("HD",2)="DISPLAY existing payments"
 .D ^ABPAHD S X="BATCH DATE = "_ABPABDT W !!?(40-($L(X)/2)),X
 G:+ABPATDFN<1 BEG
LOOK I $D(^ABPVAO("BD",ABPABDFN,ABPATDFN))'=10 D  G PAT
 .W *7,!!?10,"<<< NO PAYMENT RECORD (FOR THIS BATCH DATE) FOUND >>>"
 D ^ABPAPDEM
 I $D(ABPADDFN)=0 G BEG
 I +ABPADDFN<1 D  G PAT
 .W *7,!!?10,"<<< GLOBAL ERROR DETECTED - PLEASE CONTACT YOUR SYSTEM"
 .W " MANAGER"
 S $P(ABPAX,"=",80)="",$P(ABPAXX,"-",80)="" G ^ABPAPDV2
XIT K ABPARECV,ABPAPD,ABPAENT,ABPADDFN,ABPATDFN,ABPADT,ABPADTD,ABPAPAT,D
 K ABPA,ABPAL,DIC,C,ABPADT,ABPAQKS,ABPAQK,ABPAHRN,ABPARECV,DA,J,K,Z,XQH
 K ABPAC,ABPAI,ABPAXX,ABPAINS,DIE,DR,%DT,ABPAFRDT,ABPATODT,ABPAX Q
