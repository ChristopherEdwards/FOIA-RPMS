ABPAVAR ;PVT INS VARIABLE INITIALIZER; [ 08/07/91  11:24 AM ]
 ;;1.4;AO PVT-INS TRACKING;*0*;IHS-OKC/KJR;JULY 25, 1991
 D CRT G MISC
CRT ;ENTRY POINT - SET CRT PARAMETERS
 D DT^DICRW S U="^",ABPAHOME="",ABPARON="",ABPAROFF="",ABPAEOL=""
 S ABPAEOP="",ABPABON="",ABPABOFF=""
 S IOP=$I D ^%ZIS K IOP
 D CURRENT^%ZIS
 I $D(IOST(0))=1&($D(^%ZIS(2,IOST(0),5))=1) D
 .S ABPAHOME=$P(^%ZIS(2,IOST(0),5),"^",3)_",#"
 .S ABPARON=$P(^%ZIS(2,IOST(0),5),"^",4)
 .S ABPAROFF=$P(^%ZIS(2,IOST(0),5),"^",5)
 .S ABPAEOL=$P(^%ZIS(2,IOST(0),5),"^",6)
 .S ABPAEOP=$P(^%ZIS(2,IOST(0),5),"^",7) I ABPAEOL["[K" D
 ..S ABPAEOP="F ABPAI=1:1:IOSL-(DY+1) W *27,""[K"",! "
 ..S ABPAEOP=ABPAEOP_"I ABPAI=(IOSL-(DY+1)) X XY"
 .S ABPABON=$P(^%ZIS(2,IOST(0),5),"^",8)
 .S ABPABOFF=$P(^%ZIS(2,IOST(0),5),"^",9)
 Q
MISC ;
 S:$D(DUZ(2))'=1 DUZ(2)=$P(^AUTTSITE(1,0),"^")
TITLE ;
 S ABPATLE="AO 3P BILL TRACKING SYSTEM "
 S ABPAVER=$O(^DIC(9.4,"C","ABPA",""))
 I ABPAVER]"",$D(^DIC(9.4,ABPAVER,"VERSION")) D
 .S ABPAVER="V."_^DIC(9.4,ABPAVER,"VERSION")
 .S ABPATLE=ABPATLE_ABPAVER Q
KERNEL ;
 S ABPAKVER=$O(^DIC(9.4,"B","KERNEL",""))
 I ABPAKVER]"" I $D(^DIC(9.4,ABPAKVER,"VERSION"))=1 D
 .S ABPAKVER=^("VERSION") Q
USER ;
 S ABPAUSER="??" I $D(^DIC(3,DUZ,.1))=1 S ABPAUSER=$P(^(.1),"^",4)
 S ABPADEV=$I
OPT ;
 D ^ABPAOPT
 Q
