ABPVEEC0 ;ENTER/EDIT PVT INS CLAIMS; [ 06/06/91  8:33 AM ]
 ;;2.0;FACILITY PVT-INS TRACKING;*0*;IHS-OKC/KJR;AUGUST 7, 1991
 Q  ;;NOT AN ENTRY POINT
WRITE F ABPVJ=3:1 Q:$P(ABPVTEXT,";",ABPVJ)=""  D
 .S @("P"_ABPVJ)=$P(ABPVTEXT,";",ABPVJ)
 W ! S DX=P5,DY=P6 S:IOST["QUME" DY=DY+1 X XY W P3,$J(P4,22)
 I $D(P7)=1 W " " W:$D(@P7)=1 @P7
 Q
 ;
REVON S DX=0,DY=18 W ! X XY D EOP^ABPVZMM W:IOST["QUME" !
 W:$D(ABPVRON) @(ABPVRON)
 Q
 ;
INIT D XIT
 S $P(ABPVL,"-",79)="",$P(ABPVLL,"=",79)=""
 D SCREEN
 Q
 ;
SCREEN S ABPV("HD",1)=ABPVTLE,ABPV("HD",2)="Enter/Edit Claim"
 I $D(ABPVCDFN)=1 D
 .S ABPV("HD",2)=ABPV("HD",2)_" #"_$P(^ABPVFAC(ABPVCDFN,0),"^")
 D ^ABPVHD
 F ABPVI=1:1 S ABPVTEXT=$T(PROMPT+ABPVI) Q:ABPVTEXT=""  D WRITE
 W !,ABPVLL,!
 Q
 ;
PAT D REVON W "Enter the NAME of the patient (format = LNAME,FNAME MI.)"
 W:$D(ABPVRON)=1 @(ABPVROFF)
 K DFN,DIC("S"),DIC("B")
 S DIC="^AUPNPAT(",DIC(0)="AEQZ" D ^DIC I +Y<1 Q
 S Y(0,0)=^DPT(+Y,0),ABPVPDFN=+Y,ABPVPNAM=$P(Y(0,0),"^")
 S ABPVPSSN=$P(Y(0,0),"^",9) K Y
 S ABPVHRN="" I $D(^AUPNPAT(ABPVPDFN,41,DUZ(2),0))=1 D
 .S ABPVHRN=$P(^AUPNPAT(ABPVPDFN,41,DUZ(2),0),"^",2)
 S ABPVFAC=$P(^DIC(4,DUZ(2),0),"^") I $Y>23 D SCREEN Q
 F ABPVI=1:1:3 S ABPVTEXT=$T(PROMPT+ABPVI) D WRITE
 Q
 ;
THEREST F ABPVK=5,7,8 D
 .K DIC,DIE,DA,DR,X,Y
 .S LBL="FLD"_+ABPVK_"^ABPVEEC1" D @LBL
 Q
 ;
XIT L
 K ABPV,ABPVI,ABPVJ,DIC,X,Y,ABPVPDFN,ABPVPNAM,ABPVHRN,ABPVL,ABPVLL
 K ABPVFAC,ABPVTYPE,ABPVINS,ABPVDV,ABPVAMT,ABPVDOS,ABPVK,ABPVCN,DTOUT
 K DFOUT,DUOUT,DQOUT,DLOUT,P1,P2,P3,P4,P5,P6,P7,ABPVDATE,ABPVMESS,DIC
 K DIE,DA,DR,DATA,DIR,YY,J,ABPVCDFN,ABPVTEXT,CNT,CT,II,LBL,ABPVTXFG
 K ABPVTXDT,ABPVCNT,ABPVPHNM,ABPVPNUM,ABPVPSSN
 Q
 ;
MAIN D INIT,PAT I $D(ABPVPDFN)'=1 D XIT Q
 D DOS^ABPVEEC3 G:$D(ABPVDOS)'=1 MAIN G:$D(ABPVAMT)=1 EDIT^ABPVEEC1
 D THEREST S ABPVTXFG="N",ABPVTXDT=""
 G EDIT^ABPVEEC1
 ;
PROMPT ;;FLD #;TITLE;X-POSITION;Y-POSITION;VARIABLE NAME
 ;;(1)   ;Patient Name:;7;7;ABPVPNAM
 ;;(2)   ;Facility:;7;8;ABPVFAC
 ;;(3)   ;Health Record Number:;7;9;ABPVHRN
 ;;(4)   ;Date of Service:;7;10;ABPVDOS
 ;;(5)   ;Visit Type:;7;11;ABPVTYPE
 ;;(6)   ;Days or Visits:;7;12;ABPVDV
 ;;(7)   ;Insurance Company:;7;13;ABPVINS
 ;;   (a);Policy Holder Name:;7;14;ABPVPHNM
 ;;   (b);Policy Number:;7;15;ABPVPNUM
 ;;(8)   ;Claim Amount:;7;16;ABPVAMT
