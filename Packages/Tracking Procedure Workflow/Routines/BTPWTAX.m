BTPWTAX ;VNGT/HS/ALA-CMET Taxonomy List ; 05 Feb 2009  11:33 AM
 ;;1.0;CARE MANAGEMENT EVENT TRACKING;;Feb 07, 2011
 ;
 ;
LST(DATA,PROC) ; EP -- BQI GET CMET TAXONOMY LIST
 ; Input
 ;  PROC - Retrieves taxonomies for a particular CMET procedure.  
 ;        If left blank, it retrieves all taxonomies for CMET
 ;
 NEW UID,II,TIEN,TTXT,TAXV,X
 S UID=$S($G(ZTSK):"Z"_ZTSK,1:$J)
 S DATA=$NA(^TMP("BTPWTAX",UID))
 K @DATA
 ;
 S PROC=$G(PROC,"")
 S II=0
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BTPWTAX D UNWIND^%ZTER" ; SAC 2006 2.2.3.3.2
 ;
 S @DATA@(II)="T00030TAXONOMY_NAME^T00015TAXONOMY_IEN^T00020TAX_CATEGORY^T00003TAX_SITE_DEFINED^T00030TAX_ID^T00003TAX_ITEMS^T00030REGISTER^T00003USER_EDITABLE"_$C(30)
 ;
 I PROC'="" D PRCS
 ;
 I PROC="" D CMET
 ;
DONE ;
 S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
CMET ; EP
 S PROC=0
 F  S PROC=$O(^BTPW(90621,PROC)) Q:'PROC  D PRCS
 Q
 ;
ERR ;
 D ^%ZTER
 NEW Y,ERRDTM
 S Y=$$NOW^XLFDT() X ^DD("DD") S ERRDTM=Y
 S BMXSEC="Recording that an error occurred at "_ERRDTM
 I $D(II),$D(DATA) S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
PRCS ;
 NEW TIEN,TDATA,TY,ID,IID,USER,CCAT
 S TIEN=0
 F  S TIEN=$O(^BTPW(90621,PROC,1,TIEN)) Q:'TIEN  D
 . NEW DA,IENS
 . S DA(1)=PROC,DA=TIEN,IENS=$$IENS^DILF(.DA)
 . S TDATA=^BTPW(90621,PROC,1,TIEN,0)
 . S ID="",TY=$P(TDATA,U,3),CAT="",USER="",IID=""
 . I TY'="" S ID=$$GET1^DIQ(90621.1,TY,.06,"E"),CAT=$$GET1^DIQ(90621.1,TY,.07,"E"),IID=$$GET1^DIQ(90621.1,TY,.06,"I")
 . S II=II+1
 . S USER=$S(IID="CM":"YES",1:"NO")
 . S CCAT=$$CAT^BTPWPDSP(PROC),CCAT=$$LOWER^VALM1(CCAT)
 . S @DATA@(II)=$P(TDATA,U,1)_U_$P(TDATA,U,2)_U_CAT
 . S @DATA@(II)=@DATA@(II)_U_$S($$GET1^DIQ(90621.01,IENS,.04,"I")=1:"YES",1:"NO")_U_ID_U_$S('$$ENTRS^BQITAXX($P(TDATA,U,2)):"NO",1:"YES")_U_CCAT_U_USER_$C(30)
 Q
