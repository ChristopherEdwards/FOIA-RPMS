BPMXPRB ;IHS/PHXAO/AEF - REPOINT & RESEQUENCE PROBLEM FILE
 ;;1.0;IHS PATIENT MERGE;;MAR 01, 2010
 ;IHS/OIT/LJF 10/26/2006 routine originated from Phoenix Area Office
 ;                       changed namespace from BZXM to BPM
 ;;
DESC ;----- ROUTINE DESCRIPTION
 ;;
 ;;BPMXPRB:
 ;;THIS ROUTINE MERGES THE ENTRIES IN THE PROBLEM FILE #9000011.
 ;;SINCE BOTH FROM AND TO PATIENTS MAY HAVE PROBLEM #1, THEY NEED TO
 ;;BE RESEQUENCED IN ADDITION TO HAVING THE PATIENT POINTERS UPDATED.
 ;;
 ;;THIS ROUTINE IS CALLED BY THE SPECIAL MERGE ROUTINE DRIVER - ^BPMXDRV
 ;;
 ;;THE IHS PATIENT MERGE SOFTWARE ENTERS AT EN LINE LABEL.  IT IS EXPECTED
 ;;THAT THE FOLLOWING GLOBAL WOULD HAVE BEEN SET UP BY THE PATIENT MERGE
 ;;SOFTWARE:
 ;;  ^TMP("XDRFROM",$J,FROMIEN,TOIEN,FROMIEN_GLOBROOT,TOIEN_GLOBROOT)=FILE
 ;;EXAMPLE:
 ;;  ^TMP("XDRFROM",2804,6364,1991,"6364;DPT(","1991;DPT(")=2
 ;;WHERE =2 IS THE PARENT FILE (VA PATIENT FILE)
 ;;
 ;;$$END
 ;
 N I,X F I=1:1 S X=$P($T(DESC+I),";;",2) Q:X["$$END"  D EN^DDIOL(X)
 Q
EN(BPMRY) ;EP
 ;----- MAIN ENTRY POINT FROM DUPLICATE PATIENT MERGE SOFTWARE
 ;
 ;      BPMRY  =  TEMP GLOBAL SET UP BY THE PATIENT MERGE SOFTWARE,
 ;                 I.E., "^TMP(""XDRVROM"",$J)"
 ;
 N BPMFR,BPMTO
 ;
 S BPMFR=$O(@BPMRY@(0))
 Q:'BPMFR
 S BPMTO=$O(@BPMRY@(BPMFR,0))
 Q:'BPMTO
 ;
 D PROC(BPMFR,BPMTO)
 Q
PROC(BPMFR,BPMTO) ;
 ;----- PROCESS DATA
 ;
 N BPM
 ;
 D BLD(BPMTO,.BPM)
 ;
 D LOOP(BPMFR,BPMTO,.BPM)
 Q
LOOP(BPMFR,BPMTO,BPM) ;
 ;----- LOOP THROUGH AC XREF TO FIND ENTRIES TO MERGE
 ;
 N BPMD0
 ;
 S BPMD0=0
 F  S BPMD0=$O(^AUPNPROB("AC",BPMFR,BPMD0)) Q:'BPMD0  D
 . D ONE(BPMFR,BPMTO,BPMD0,.BPM)
 ;
 Q
ONE(BPMFR,BPMTO,BPMD0,BPM) ;
 ;----- PROCESS ONE ENTRY
 ;
 N BPMNUM,DA,DIE,DITC,DR,X,Y
 ;
 S BPMNUM=$$NUM(BPMTO,BPMD0,.BPM)
 ;
 S DITC=1
 S DIE="^AUPNPROB("
 S DA=BPMD0
 S DR=".02////"_BPMTO
 S DR=DR_";.07////"_BPMNUM
 D ^DIE
 ;
 S BPM(BPMTO,BPMNUM)=BPMD0
 ;
 Q
BLD(BPMTO,BPM) ;
 ;----- BUILD ARRAY OF PROBLEMS FOR "TO" ENTRY
 ;
 N N,X,Y
 ;
 S X=0
 F  S X=$O(^AUPNPROB("AC",BPMTO,X)) Q:'X  D
 . S Y=$G(^AUPNPROB(X,0))
 . S N=$P(Y,U,7)
 . I N S BPM(BPMTO,N)=X
 Q
NUM(BPMTO,BPMD0,BPM) ;
 ;----- RETURNS PROBLEM NUMBER
 ;
 N Y
 S Y=$P($G(^AUPNPROB(BPMD0,0)),U,7) S:Y="" Y=1
 I $D(BPM(BPMTO,Y)) D
 . F Y=1:1 Q:'$D(BPM(BPMTO,Y))
 Q Y
