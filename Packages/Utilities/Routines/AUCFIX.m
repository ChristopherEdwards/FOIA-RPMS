%AUCFIX ; COUNT ENTIRES IN FILEMAN FILES AND FIX [ 02/16/88  8:00 AM ]
 ;
 W !,"This program counts primary entries for FileMan files and fixes 0th node.",!
 S U="^"
 D ^%AUDSET
 Q:'$D(^UTILITY("AUDSET",$J))
 S AUCFIXFL="" F AUCFIXL=0:0 S AUCFIXFL=$O(^UTILITY("AUDSET",$J,AUCFIXFL)) Q:AUCFIXFL'=+AUCFIXFL  D AUCFIXFL
 D EOJ
 Q
 ;
AUCFIXFL ;
 W !,AUCFIXFL
 I AUCFIXFL=3.081 W "  skipping" Q
 I '$D(^DIC(AUCFIXFL,0,"GL")) W !!,"No data global specified in ^DIC!" Q
 S AUCFIXGB=^DIC(AUCFIXFL,0,"GL")
 I '$D(@($S($E(AUCFIXGB,$L(AUCFIXGB))="(":$E(AUCFIXGB,1,$L(AUCFIXGB)-1),1:$E(AUCFIXGB,1,$L(AUCFIXGB)-1)_")"))) W !!,"Bad global!" Q
 S AUCFIXGB=AUCFIXGB_"AUCFIXNX)"
 S (AUCFIXHI,AUCFIXNX,AUCFIXC)=0
 F AUCFIXL=0:0 S AUCFIXNX=$O(@(AUCFIXGB)) Q:AUCFIXNX'=+AUCFIXNX  S AUCFIXHI=AUCFIXNX,AUCFIXC=AUCFIXC+1 W:'(AUCFIXC#50) "."
 W !,"    Count=",AUCFIXC,?22,"High DFN=",AUCFIXHI
 S AUCFIXNX="",AUCFIXX=$O(@(AUCFIXGB)),AUCFIXX=^(0),AUCFIXY=$P(AUCFIXX,U,4),AUCFIXX=$P(AUCFIXX,U,3)
 I AUCFIXC=AUCFIXY,AUCFIXHI=AUCFIXX W ?50,"[correct]" Q
 W ?50,"[incorrect -- fixed]" S $P(^(0),U,3)=AUCFIXHI,$P(^(0),U,4)=AUCFIXC
 Q
 ;
EOJ ;
 K AUCFIXHI,AUCFIXX,AUCFIXY,AUCFIXC,DIC,DIC(0),AUCFIXFL,AUCFIXGB,AUCFIXL,AUCFIXNX
 Q
