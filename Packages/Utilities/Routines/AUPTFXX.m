%AUPTFXX ;BRJ/IHS OHPD-TUCSON EXCHANGE OLD ENTRY NUMBER FOR NEW [ 03/20/87  9:22 AM ]
 ;CALLED FROM ^%AUPTFX
 W !,*7,"%AUPTFXX cannot be run stand-alone.  Bye Bye!"
 Q
SWAP ;POSITION DATA GLOBAL AND SWAP POINTER VALUES 
EN ;ENTRY FROM ^AUPTFX
 N AUPTPGM S AUPTPGM="<"_$T(+0)_">"
 W !!,*7,AUPTPGM,?11,"Beginning to exchange pointer values for:"
 S AUPTPO=""
 F L=0:0 S AUPTPO=$O(AUPTDAL(AUPTPO)) Q:AUPTPO=""  W !,?11,"File/Subfile // ",$P(AUPTDAL(AUPTPO),U,3),"   ",$P(AUPTDAL(AUPTPO),U,4) W:AUPTPO=(-1) !,?18,"Field // ",$P(AUPTDAL(-1),U,5),"    ",$P(AUPTDAL(-1),U,6),!,?18,"Piece // ",AUPTP
 W:'AUPTDSPY !
 F AUPTL=1:1:AUPTDACT X AUPTO(AUPTL,0)
 S AUPTI=1 F L=0:0 X AUPTO(AUPTI,1) Q:'+AUPTI  I +DA I $D(@AUPTPGL) D CKSWAP I AUPTSWAP D BLDGNDE,PUT^AUPTFXD:AUPTDIN,SWAPX:'AUPTDIN
 Q
CKSWAP ;DETERMINE IF SWAP AND SAVE OLD/NEW VALUES
 S AUPTSWAP=0,AUPTOLDX=$P(@AUPTPGL,U,AUPTP)
 I AUPTOLDX'="" I $D(@AUPTUFLE) S AUPTNEWX=@AUPTUFLE,AUPTSWAP=1
 Q
BLDGNDE ;BUILD GLOBAL NODE OF FILE ENTRY
 S AUPTI1="",AUPTGNDE=AUPTORGG
 F L=0:0 S AUPTI1=$O(AUPTDAL(AUPTI1)) Q:AUPTI1=""  S AUPTGNDE=AUPTGNDE_@($P(AUPTDAL(AUPTI1),U,1))_","_$P($P(AUPTDAL(AUPTI1),";",1),U,2)_$S(AUPTI1'=(-1):",",1:")")
 Q
SWAPX ;EXCHANGE DATA VALUES - (DINUM=X) VALUES DONE IN ^AUPTFXDI
 I 'AUPTDSPY W "-"
 E  W !!,AUPTPGM,?11,"<OLD> ",AUPTGNDE,"=",@AUPTGNDE
 S $P(@AUPTGNDE,U,AUPTP)=AUPTNEWX
 I 'AUPTDSPY W "X"
 E  W !,?11,"<NEW> ",AUPTGNDE,"=",@AUPTGNDE
 D RESETXR:$D(^DD(AUPTPSFL,AUPTPSFD,1,0))
 Q
RESETXR ;RESET ANY TRIGGER,X-REF,ETC
 W:AUPTDSPY !,?11,"Executing cross references, triggers etc.",!,?11
 S AUPTXI1=0,AUPTEC=5
 F L=0:0 S AUPTXI1=$O(^DD(AUPTPSFL,AUPTPSFD,1,AUPTXI1)) Q:AUPTXI1=""  D ERR^AUPTFXT:'($D(^(AUPTXI1,1))&$D(^(2))) Q:AUPTSKIP  S X=AUPTOLDX X:'AUPTDIN ^(2) W "|" S X=AUPTNEWX X ^DD(AUPTPSFL,AUPTPSFD,1,AUPTXI1,1) W "|"
 S AUPTSKIP=0
 Q
