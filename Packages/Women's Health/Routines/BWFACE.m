BWFACE ;IHS/ANMC/MWR - PRINT A FACE SHEET (FROM REG.);15-Feb-2003 21:51;PLS
 ;;2.0;WOMEN'S HEALTH;**8**;MAY 16, 1996
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  DISPLAY/PRINT A FACE SHEET, FROM IHS REGISTRATION.
 ;
 D SETVARS^BWUTL5 S BWPOP1=0
 F  D RUN Q:BWPOP1
 W @IOF
 D EXIT
 Q
 ;
RUN ;EP
 D TITLE^BWUTL5("DISPLAY/PRINT A PATIENT FACE SHEET")
 D PATIENT I Y<0 S BWPOP1=1 Q
 ;
RUN1 ;EP
 ;---> PATIENT ALREADY SELECTED.
 D DEVICE Q:BWPOP
 D FACE
 Q
 ;
EXIT ;EP
 D KILLALL^BWUTL8
 Q
 ;
 ;
PATIENT ;EP
 ;---> SELECT PATIENT (RETURN BWDFN).
 W !!,"   Select the patient whose Face Sheet you wish to display."
 D PATLKUP^BWUTL8(.Y)
 Q:Y<0  S BWDFN=+Y
 Q
 ;
 ;
DEVICE ;EP
 ;---> GET DEVICE AND POSSIBLY QUEUE TO TASKMAN.
 S ZTRTN="DEQUEUE^BWFACE",ZTSAVE("BWDFN")=""
 D ZIS^BWUTL2(.BWPOP,1)
 Q
 ;
 ;
FACE ;EP
 ;---> DISPLAY CODE FOR FACE SHEET.
 U IO
 S BWCRT=$S($E(IOST)="C":1,1:0)
 I BWCRT N IOF S IOF="!"
 D
 .I '$D(BWDFN) W !?5,"A Patient has not been selected.",! Q
 .S DFN=BWDFN
 .;
 .;---> IF THIS IS NOT IHS, DO NOT CALL IHS REGISTRATION FACE SHEET.
 .;---> VA/STATE SITES MAY SUBSTITUTE AN ALTERNATE CALL BELOW.
 .I $$AGENCY^BWUTL5(DUZ(2))'="i" D  Q
 ..;---> VA/STATE SUBSTITUTE ALTERNATE PATIENT INQUIRY CODE HERE:
 ..;D ^VA_ROUTINE SUCH AS EN^DGRPD, AND QUIT.
 ..;EXAMPLE: D EN^DGRPD Q
 ..W !!?8,"* Because this is not an Indian Health Service site,"
 ..W !?10,"the program cannot display an IHS Face Sheet."
 ..W !?10,"Contact your technical support people to modify"
 ..W !?10,"line FACE+13 of the routine BWFACE."
 .;
 .S DFN=BWDFN D START^AGFACE
 I BWCRT D DIRZ^BWUTL3 W @IOF
 D ^%ZISC
 Q
 ;
FOLLUP(BWDFN) ;EP
 ;---> CALLED BY PROCEDURE FOLLOW-UP MENU (BWPROC1).
 N (BWDFN,DT,DTIME,DUZ,IO,U) D SETVARS^BWUTL5
 D RUN1
 Q
 ;
DEQUEUE ;EP
 ;---> EP FOR TASKMAN QUEUE OF PRINTOUT.
 D SETVARS^BWUTL5,FACE^BWFACE,EXIT
 Q
