BWLAB ;IHS/ANMC/MWR - ADD/EDIT PROCEDURE BY LAB STAFF;15-Feb-2003 21:55;PLS
 ;;2.0;WOMEN'S HEALTH;**8**;MAY 16, 1996
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  CALLED BY OPTION: "BW LAB ADD A NEW PROCEDURE" TO ACCESSION
 ;;  PROCEDURES.
 ;
 D SETVARS^BWUTL5
 F  D  Q:BWPOP
 .D TITLE^BWUTL5("LAB: ENTER NEW PROCEDURES")
 .D NEW
 D EXIT
 Q
 ;
NEW ;EP
 ;---> SELECT PATIENT.
 S BWPOP=0 N BWDFN,DIR,DR
 D PATLKUP^BWUTL8(.Y,"ADD","NOCDC")
 I Y<0 S BWPOP=1 Q
 S BWDFN=+Y
 ;---> SELECT PROCEDURE.
 D NEW1^BWPROC I BWPOP S BWPOP=0 Q
 I '$G(DA) D  Q
 .W !?5,"* FAILURE TO ADD NEW PROCEDURE.  "
 .W "PLEASE CONTACT YOUR SITE MANAGER" D DIRZ^BWUTL3
 D EDIT2(DA)
 Q
 ;
EDIT ;EP
 ;---> CALLED BY OPTION: "BW LAB EDIT ACCESSION".
 ;---> EDIT JUST THE ACCESSION FIELDS OF AN EXISTING PROCEDURE.
 D SETVARS^BWUTL5
 D TITLE^BWUTL5("EDIT AN ACCESSIONED PROCEDURE")
 D LKUPPCD^BWPROC(.Y)
 Q:Y<0!($D(DIROUT))
 ;---> DA=IEN OF PROCEDURE IN PROCEDURE FILE 9002086.1.
 S DA=+Y
 D EDIT2(DA)
 D EXIT
 Q
 ;
 ;
EDIT2(DA) ;EP
 ;---> REQUIRED VARIABLES: DA=IEN IN ^BWPCD(.
 Q:'$G(DA)
 S (AUPNPAT,BWDFN)=$P(^BWPCD(DA,0),U,2)
 D DDS^BWFMAN(9002086.1,"[BW PROC-FORM-LAB]",DA,"C",.BWCHG,.BWPOP)
 Q
 ;
EXIT ;EP
 D KILLALL^BWUTL8
 Q
