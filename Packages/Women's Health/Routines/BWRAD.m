BWRAD ;IHS/ANMC/MWR - UTIL: MOSTLY PATIENT DATA;15-Feb-2003 22:09;PLS
 ;;2.0;WOMEN'S HEALTH;**8**;MAY 16, 1996
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  EDIT BW MAMMOGRAM DIAG CODE TRANSLATIONS FILE.
 ;;  PRINT BW MAMMOGRAM DIAG CODE TRANSLATIONS FILE.
 ;
 ;
EDIT ;---> MATCH BW RESULTS/DIAGNOSES WITH RADIOLOGY DIAGNOSTIC CODES.
 ;---> CALLED BY OPTION "BW EDIT DIAG TRANSLATION".
 D SETVARS^BWUTL5
 F  D  Q:$G(Y)<0
 .D TITLE^BWUTL5("EDIT BW DIAGNOSTIC CODE TRANSLATION FILE")
 .D DIC^BWFMAN(9002086.32,"QEMAL",.Y,"   Select RESULT/DIAGNOSIS: ")
 .Q:Y<0
 .D DIE^BWFMAN(9002086.32,".01;.02",+Y,.BWPOP)
 D EXIT
 Q
 ;
SCREEN ;
 ;---> SCREEN CALLED BY FILEMAN ^DD.
 ;---> SCREEN ONLY FOR RESULTS/DIAGNOSES THAT APPLY TO MAMMOGRAMS.
 ;---> HARDCODED FOR MAM IENS=25, 26 OR 28.  CALLED BY ^DIC.
 N I
 F I=3:1:19 I $P(^(0),U,I)=25!($P(^(0),U,I)=26)!($P(^(0),U,I)=28) Q
 Q
 ;
 ;
PRINT ;EP
 ;---> CALLED BY OPTION "BW PRINT DIAG TRANSLATION".
 S DIC="^BWRADX("
 S FLDS="[BW PRINT DIAG CODE TRANSLATION]"
 S BY=.01,FR="AAAA",TO="ZZZZ"
 D EN1^DIP
 D EXIT
 Q
 ;
EXIT ;EP
 D KILLALL^BWUTL8
 Q
