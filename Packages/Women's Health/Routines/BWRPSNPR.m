BWRPSNPR ;IHS/ANMC/MWR - REPORT: RETRIEVE A SNAPSHOT;15-Feb-2003 22:11;PLS
 ;;2.0;WOMEN'S HEALTH;**8**;MAY 16, 1996
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  CALLED BY OPTION: "BW PRINT/RETRIEVE SNAPSHOT" TO DISPLAY A
 ;;  PREVIOUS SNAPSHOT.
 ;
 D SETVARS^BWUTL5 S BWPOP=0,BWFAC=DUZ(2)
 N A,B,C,D,E,F,G,H,J,K,L,M,N,P,Q,R,S,X,Y
 D TITLE^BWUTL5("RETRIEVE A PROGRAM SNAPSHOT")
 D RETRIEVE G:BWPOP EXIT
 D DEVICE   G:BWPOP EXIT
 D LOADVARS
 D ^BWRPSNP1
 ;
EXIT ;EP
 D KILLALL^BWUTL8
 Q
 ;
 ;
RETRIEVE ;EP
 ;---> RETRIEVE A SNAPSHOT
 N DIC,Y
 W !!?3,"Select the Snapshot you wish to display or print."
 D DIC^BWFMAN(9002086.71,"QEMA",.Y,"   Select Snapshot: ")
 I Y<0 S BWPOP=1 Q
 S BWDT=+Y
 Q
 ;
DEVICE ;EP
 ;---> GET DEVICE AND POSSIBLY QUEUE TO TASKMAN.
 S ZTRTN="DEQUEUE^BWRPSNPR"
 F BWSV="DT" D
 .I $D(@("BW"_BWSV)) S ZTSAVE("BW"_BWSV)=""
 D ZIS^BWUTL2(.BWPOP,1)
 Q
 ;
DEQUEUE ;EP
 ;---> QUEUED REPORT
 N A,B,C,D,E,F,G,H,J,K,L,M,N,P,Q,R,S,X,Y
 D SETVARS^BWUTL5,LOADVARS,^BWRPSNP1,EXIT
 Q
 ;
LOADVARS ;EP
 S Y=^BWSNAP(BWDT,0)
 S BWFAC=$P(Y,U,2),A=$P(Y,U,3),B=$P(Y,U,4),C=$P(Y,U,5),D=$P(Y,U,6)
 S E=$P(Y,U,7),F=$P(Y,U,8),G=$P(Y,U,9),H=$P(Y,U,10),S=$P(Y,U,11)
 S J=$P(Y,U,12),K=$P(Y,U,13),L=$P(Y,U,14),P=$P(Y,U,15),Q=$P(Y,U,16)
 S R=$P(Y,U,17)
 Q
