BWUPDATE ;IHS/ANMC/MWR - POST-INIT ROUTINE ;15-Feb-2003 22:11;PLS
 ;;2.0;WOMEN'S HEALTH;**8**;MAY 16, 1996
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  CALLED BY BWPOST TO UPDATE TABLE FILES.
 ;
 ;
 D SETVARS^BWUTL5
 ;
 ;---> ADD "Chart Review" to BW BREAST TX NEED File #9002086.51.
 D DIC^BWFMAN(9002086.51,"ML",.Y,"","","","Chart Review")
 ;---> RE-INDEX.
 ;
 ;---> ADD "Letter Canceled" to BW NOTIF OUTCOME File #9002086.405.
 D DIC^BWFMAN(9002086.405,"ML",.Y,"","","","Letter Canceled")
 D:$P(Y,U,3) DIE^BWFMAN(9002086.405,".02////1",+Y)
 ;---> RE-INDEX.
 ;
 ;---> ADD "Patient Deceased" to BW NOTIF OUTCOME File #9002086.405.
 ;---> MUST HAVE IEN OF 99 (THEREFORE HARD SET).
 S ^BWNOTO(99,0)="Patient Deceased^1"
 S ^BWNOTO("B","Patient Deceased",99)=""
 S ^BWNOTO("U","PATIENT DECEASED",99)=""
 ;
 ;---> ADD "SCREENING MAMMOGRAM" to RADIOLOGY PROCEDURES File #71
 ;---> FOR .01 VALUE PASSED TO V RADIOLOGY FILE BY WOMEN'S HEALTH.
 D DIC^BWFMAN(71,"ML",.Y,"","","","SCREENING MAMMOGRAM")
 D:Y>0
 .N BWY S BWY=+Y
 .D:$P(Y,U,3) DIE^BWFMAN(71,"6///DETAILED;7///NO;9///76092;11///NO",+Y)
 .;---> IF POINTER TO RADIOLOGY PROCEDURES FILE #71 IS NULL FOR
 .;---> SCREENING MAMMOGRAM IN BW PROCEDURE FILE, UPDATE IT.
 .D:'$P(^BWPN(28,0),U,17) DIE^BWFMAN(9002086.2,".17////"_BWY,28)
 ;
 ;---> KILL OF ERRONEOUS XREF GLOBAL (DUE TO TYPO IN VERSION 1).
 K ^BWDAG("P")
 Q
