BWUTL8 ;IHS/ANMC/MWR - UTIL: PATLKUP, SELECT, KILLALL;15-Feb-2003 22:15;PLS
 ;;2.0;WOMEN'S HEALTH;**8**;MAY 16, 1996
 ;;* MICHAEL REMILLARD, DDS * ALASKA NATIVE MEDICAL CENTER *
 ;;  UTILITY: PATIENT LOOKUP, SELECT FOR REPORT, KILLALL.
 ;
PATLKUP(Y,BWADD,BWCDC,DUZ2,BWPOP) ;EP
 ;---> BW PATIENT LOOKUP.
 ;---> PARAMETERS:
 ;     1 - Y             (RETURNED) PATIENT DFN OR -1 IF FAILED.
 ;     2 - BWADD         (OPTIONAL) EQUALS "ADD" IF ADD CAPABILITY
 ;     3 - BWCDC         (OPTIONAL) EQUALS "NOCDC" IF CDC UNIQUE ID#
 ;                                  SHOULD NOT BE ASKED.
 ;     4 - DUZ2=DUZ(2)   (OPTIONAL) IF NOT SET, WILL=ENVIROMENTAL DUZ(2)
 ;     5 - BWPOP         (RETURNED) BWPOP=1 IF DTOUT OR DUOUT
 ;
 ;---> EXAMPLE: D PATLKUP^BWUTL8(.Y)
 ;              D PATLKUP^BWUTL8(.Y,"ADD") - MAY ADD PATIENT TO WH.
 ;
 N DFN,DIC,X
PATLKUP1 ;---> RETURN HERE IF LOOKUP FAILED FOR SEX OR AGE.
 ;---> SET VARIABLES: Y=DFN,BWPOP=1 FOR QUIT.
 S BWPOP=0 D SETVARS^BWUTL5
 S:$G(DUZ2)]"" DUZ(2)=DUZ2
 S DIC="^AUPNPAT(",DIC(0)="AEMQ"
 S DIC("S")="I $P(^DPT(Y,0),U,2)=""F"""
 D ^DIC
 Q:$D(DUOUT)!($D(DTOUT))
 ;
 ;---> IF LOOKUP FAILS, AND IF CDC EXPORT SITE PARAMETER=YES,
 ;---> TRY LOOKUP ON CDC UNIQUE ID#.
 I Y<0,$$CDC^BWUTL5(DUZ(2)),$G(BWCDC)'="NOCDC" D UNIQUE(.Y)
 ;
 Q:Y<0
 S (DFN,Y)=+Y
 ;---> IF PATIENT ALREADY EXISTS IN BW PATIENT FILE, QUIT.
 Q:$D(^BWP(DFN,0))
 ;
 ;---> IF PATIENT IS NOT FEMALE, PROMPT USER.
 I '$$SEX^BWUTL1(DFN) D  G PATLKUP1
 .W !!?3,$$NAME^BWUTL1(DFN)," is not female." D DIRZ^BWUTL3
 ;
 ;---> QUIT IF NO ADD ("LAYGO") CAPABILITY.
 I $G(BWADD)'="ADD" D  G PATLKUP1
 .W !!?3,$$NAME^BWUTL1(DFN)
 .W " is not currently in the Women's Health database." D DIRZ^BWUTL3
 ;
 ;---> ASK TO ADD AS A NEW PATIENT.
 W !!?3,$$NAME^BWUTL1(DFN)
 ;---> IF PATIENT IS UNDER 12 YEARS, PROMPT USER.
 W:+$$AGE^BWUTL1(DFN)<12 " is under 12 years of age,",!?3,"and she"
 W " is not currently in the Women's Health database."
 W !?3,"Do you wish to add her to the Women's Health Database?"
 S DIR("?",1)="     Enter YES to ADD this patient to the Women's Health"
 S DIR("?",1)=DIR("?",1)_" database."
 S DIR("?")="     Enter NO to quit without adding her to the database."
 S DIR(0)="Y",DIR("A")="   Enter Yes or No"
 D ^DIR W !
 I $D(DIRUT) S Y=-1 Q
 G:Y=0 PATLKUP1
 D AUTOADD^BWPATE(DFN,DUZ(2),.Y,1)
 Q
 ;
UNIQUE(Y) ;EP
 ;---> TRY PATIENT LOOKUP ON CDC UNIQUE ID#.
 ;---> PARAMETERS:
 ;     1 - Y             (RETURNED) PATIENT DFN OR -1 IF FAILED.
 ;
 N D,DIC,DIR
 W !!?3,"Do you wish to search for a patient by her CDC Unique ID#?"
 S DIR("?",1)="     Enter YES to search for a patient by entering her"
 S DIR("?",1)=DIR("?",1)_" CDC Unique ID#"
 S DIR("?",2)="     as reported by the MDE Edit Program."
 S DIR("?")="     Enter NO to quit without searching by CDC Unique ID#."
 S DIR(0)="Y",DIR("A")="   Enter Yes or No",DIR("B")="NO"
 D ^DIR W !
 I $D(DIRUT) S Y=-1 Q
 I Y=0 S Y=-1 Q
 S DIC="^BWP(",DIC(0)="QEA",D="CDC"
 D IX^DIC S Y=+Y
 Q
 ;
 ;
KGBL(GBL) ;EP
 ;---> KILL A GLOBAL.  GBL SHOULD INCLUDE THE LEADING "^".
 S:GBL["(" GBL=$P(GBL,"(")
 F  S GBL=$Q(@GBL) Q:GBL=""  K @GBL
 Q
 ;
ZGBL(GBL) ;EP
 ;---> ZERO OUT (DELETE ALL DATA) IN A FILEMAN FILE.
 ;---> GBL SHOULD INCLUDE THE LEADING "^".
 N N,X
 S:GBL["(" GBL=$P(GBL,"(")
 Q:'$D(@(GBL_"(0)"))
 S N=0,X=$P(@(GBL_"(0)"),U,1,2)
 F  S N=$O(@(GBL_"("""_N_""")")) Q:N=""  K @(GBL_"("""_N_""")")
 S @(GBL_"(0)")=X
 Q
 ;
KILLALL ;EP
 ;---> CLEAN UP VARIABLES.
 ;---> MSM
 ;S X="BW" F  S X=$O(@X) Q:$E(X,1,2)'="BW"  K @X
 ;S X="DI" F  S X=$O(@X) Q:$E(X,1,2)'="BW"  K @X
 ;---> DSM
 ;S X="BW" F  S X=$ZSORT(@X) Q:$E(X,1,2)'="BW"  K @X
 ;S X="DI" F  S X=$ZSORT(@X) Q:$E(X,1,2)'="BW"  K @X
 ;
 ;---> REPLACE KILLS BELOW WITH $O COMMANDS ABOVE WHEN AVAILABLE.
 K BW,BW0,BW1,BW1DX,BW2,BW2DX,BWA,BWABBV,BWABBVS,BWACC
 K BWACCN,BWACCP,BWADD,BWAGE,BWAGENCY,BWAGRG,BWAGRP,BWAR,BWAREA
 K BWARR,BWARR1,BWASKTIM,BWATT,BWB,BWBDF,BWBEGDF,BWBEGDT
 K BWBEGDT1,BWBNEED,BWC,BWC0,BWCAPT,BWCBEDA,BWCC,BWCDC,BWCDCV,BWCHAGE
 K BWCHG,BWCHRT,BWCHSSN,BWCMGR,BWCMGR1,BWCNEED,BWCODE,BWCOLPS,BWCONF
 K BWCONFF,BWCOUNT,BWCRT,BWCUR,BWD,BWDA,BWDATE,BWDATE1,BWDD,BWDDATE
 K BWDEF,BWDFLT,BWDFN,BWDIAG,BWDR,BWDT,BWDUZ0,BWDUZ2,BWDX
 K BWE,BWEDC,BWEDCL,BWEDF,BWENDDF,BWENDDT,BWENDDT1,BWENDSAM,BWENDT
 K BWERR,BWERROR,BWERRORS,BWFAC,BWFACIL,BWFILE,BWFLNM,BWGBL
 K BWGBLN,BWHEADER,BWHCF,BWHFS,BWHLOC,BWHRCN,BWI,BWICD,BWIEN,BWION
 K BWITEM,BWITEMS,BWKDT,BWLDAT,BWLFRT,BWLINE,BWLINL,BWLOC,BWLOGO
 K BWLOOP,BWLPRG,BWM,BWMABN,BWMAM,BWMAMDT,BWMATCH,BWMENUT,BWMES,BWN
 K BWNAMAGE,BWNAME,BWNEW,BWNN,BWNODE,BWNOFAC,BWNOFOL,BWNOMAT,BWNORM
 K BWNOW,BWOFAC,BWOLD,BWOLD2,BWOUT,BWPABN,BWPAGE,BWPAP,BWPAPDA,BWPAPDT
 K BWPAPRG,BWPAPRG1,BWPAPS,BWPATH,BWPATS,BWPC,BWPCCN,BWPCCP,BWPCD,BWPCDL
 K BWPCDN,BWPCDS,BWPCDT,BWPDATE,BWPN,BWPNAME,BWPOP,BWPOP1,BWPOST,BWPPAP
 K BWPRE,BWPREG,BWPRIO,BWPRMPT,BWPRMT,BWPRMT1,BWPRMT2,BWPRMTQ,BWPROF
 K BWPROV,BWPRPCD,BWPRV,BWPSTAT,BWPTITL,BWPURP,BWPUSER,BWQUE,BWQUIT
 K BWRCVDT,BWRES,BWRESN,BWRTN,BWS,BWSAME,BWSCRN,BWSITE,BWSL,BWSPEC
 K BWSPTX,BWSS,BWSSN,BWSTART,BWSTAT,BWSTTDT,BWSUB,BWSUBH,BWUSER,BWSV
 K BWTAB,BWTEST,BWTIME,BWTIMLN,BWTITLE,BWTITLE1,BWTITLE2,BWTMP
 K BWTTAB,BWTYPE,BWUNL,BWVER,BWVFIL,BWVGBL,BWX,BWX0,BWX1,BWX2,BWX3
 K BWX4,BWX5,BWX6,BWX7,BWX8,BWX9,BWXPORT,BWXREF,BWY,BWYY
 K COL,COLLEN,LEN,NN,NODE,V,POP
 ;
 ;---> ADDED MANUALLY.
 K BWMDAT,BWMGR,BWPDAT,BWTOT
 K ^TMP("BW",$J)
 ;
 ;---> FILEMAN KILLS.
 D DKILLS^BWFMAN
 K X,Y,Z,ZTRTN,ZTSAVE
 Q
