BQICMLST ;VNGT/HS/ALA-Care Management List ; 28 May 2008  6:59 PM
 ;;2.1;ICARE MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
EN(DATA,TYPE) ;EP -- BQI GET CARE MGMT LIST
 ;
 ; Input
 ;   TYPE - type of measures to list see table 90506.5 for list
 ;
 NEW UID,II,TTYPE,SCAT,SCLIN,BQMEAS,CAT,CLIN,TITLE,BATCH,BQIMEASF,CMCOD
 NEW EDIT,NCAT,NCLIN,NVIEW,ORD,PIND,YEAR,SDIR,BQISORT
 S UID=$S($G(ZTSK):"Z"_ZTSK,1:$J)
 S DATA=$NA(^TMP("BQICMLST",UID))
 K @DATA
 ;
 S II=0
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQICMLST D UNWIND^%ZTER" ; SAC 2006 2.2.3.3.2
 ;
 S @DATA@(II)="I00010MEAS_IEN^T00030TYPE^T00030CATEGORY^T00030CLIN_GROUP^T00050DISPLAY_NAME^T00015CODE^T00001VIEW^T00001PER_DIRECT^"
 S @DATA@(II)=@DATA@(II)_"T00001EXCEPT^I00003SIZE^T00003EDIT^T00003BATCH^T00001DEFAULT_SORT"_$C(30)
 ;
 S TYPE=$G(TYPE,"")
 ;
 I TYPE'="" D   G DONE
 . N TPC,NTYPE
 . F TPC=1:1:$L(TYPE,$C(29)) S NTYPE=$P(TYPE,$C(29),TPC) I NTYPE]"" D
 .. I NTYPE'?.N S CMCOD=$$FIND1^DIC(90506.5,"","B",NTYPE,"","","ERROR")
 .. I NTYPE?.N S CMCOD=NTYPE
 .. ;
 .. S TTYPE=$P($G(^BQI(90506.5,CMCOD,0)),U,2)
 .. I TTYPE'="" D RET
 ;
 S TTYPE=""
 F  S TTYPE=$O(^BQI(90506.1,"AD",TTYPE)) Q:TTYPE=""  D
 .;
 .;Filter out Inactive entries
 .N IEN,INACTIVE S IEN=$O(^BQI(90506.5,"C",TTYPE,"")) Q:IEN=""
 .S INACTIVE=$$GET1^DIQ(90506.5,IEN_",",.1,"I") Q:INACTIVE
 .D RET
 ;
DONE ;
 S CAT=""
 F  S CAT=$O(BQISORT(CAT)) Q:CAT=""  D
 . S CLIN=""
 . F  S CLIN=$O(BQISORT(CAT,CLIN)) Q:CLIN=""  D
 .. S TITLE=""
 .. F  S TITLE=$O(BQISORT(CAT,CLIN,TITLE)) Q:TITLE=""  D
 ... S BQMEAS=""
 ... F  S BQMEAS=$O(BQISORT(CAT,CLIN,TITLE,BQMEAS)) Q:BQMEAS=""  D
 .... S II=II+1,@DATA@(II)=BQISORT(CAT,CLIN,TITLE,BQMEAS)
 S II=II+1,@DATA@(II)=$C(31)
 K BQISORT
 Q
 ;
ERR ;
 D ^%ZTER
 NEW Y,ERRDTM
 S Y=$$NOW^XLFDT() X ^DD("DD") S ERRDTM=Y
 S BMXSEC="Recording that an error occurred at "_ERRDTM
 I $D(II),$D(DATA) S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
RET ; Data retrieval
 NEW IEN,FDATA,SRC,GCAT,RCAT,DCAT,GCLIN,RCLIN,GVIEW,DVIEW,VIEW,DCLIN
 NEW CAT,CLIN,RVIEW,MVIEW,PDIR,EXCEPT,SIZE,SRCN
 ; Check for Alternate Display Order fist
 S ORD=""
 F  S ORD=$O(^BQI(90506.1,"AF",TTYPE,ORD)) Q:ORD=""  D
 . S IEN=""
 . F  S IEN=$O(^BQI(90506.1,"AF",TTYPE,ORD,IEN)) Q:IEN=""  D GET(IEN)
 ;
 ; Get normal order
 S ORD=""
 F  S ORD=$O(^BQI(90506.1,"AD",TTYPE,ORD)) Q:ORD=""  D
 . S IEN=""
 . F  S IEN=$O(^BQI(90506.1,"AD",TTYPE,ORD,IEN)) Q:IEN=""  D GET(IEN)
 Q
 ;
GET(IEN) ;EP - Get data values
 S FDATA=$G(^BQI(90506.1,IEN,0)),EXCEPT="",SIZE=""
 I $$GET1^DIQ(90506.1,IEN_",",.1,"I")=1 Q
 I $$GET1^DIQ(90506.1,IEN_",",3.07,"I")=1 Q
 S SRCN=$$GET1^DIQ(90506.1,IEN_",",3.01,"I")
 S SRC=$S($$GET1^DIQ(90506.5,SRCN_",",.06,"E")'="":$$GET1^DIQ(90506.5,SRCN_",",.06,"E"),1:$$GET1^DIQ(90506.5,SRCN_",",.01,"E"))
 ;S SRC=$$GET1^DIQ(90506.1,IEN_",",3.01,"E")
 S NCAT=$$GET1^DIQ(90506.1,IEN_",",3.03,"E")
 S NCLIN=$$GET1^DIQ(90506.1,IEN_",",3.02,"E")
 S NVIEW=$$GET1^DIQ(90506.1,IEN_",",3.04,"I")
 S PDIR=$$GET1^DIQ(90506.1,IEN_",",.14,"I")
 S SIZE=$$GET1^DIQ(90506.1,IEN_",",.15,"E")
 S EDIT=+$$GET1^DIQ(90506.1,IEN_",",.16,"I")
 S EDIT=$S(EDIT=1:"YES",1:"NO")
 S BATCH=+$$GET1^DIQ(90506.1,IEN_",",.17,"I")
 S BATCH=$S(BATCH=1:"YES",1:"NO")
 S SDIR=$$GET1^DIQ(90506.1,IEN_",",3.08,"I")
 S:SDIR="" SDIR="A"
 S VIEW=NVIEW
 S:VIEW="" VIEW="O"
 S DCLIN=""
 S CAT=NCAT
 S CLIN=NCLIN
 I TTYPE="G" D
 . NEW CODE,BQIH,BQIYR,BQMEAS,VER
 . S CODE=$P(FDATA,U,1),YEAR=$P(CODE,"_",1)
 . S BQMEAS=$P(CODE,"_",2)
 . S BQIH=$$SPM^BQIGPUTL()
 . S BQIYR=$$LKP^BQIGPUTL(YEAR)
 . S VER=$$VERSION^XPDUTL("BGP")
 . I VER<8.0 D
 .. S PIND=$O(^BQI(90508,BQIH,20,BQIYR,20,"B",BQMEAS,""))
 .. S EXCEPT=+$P(^BQI(90508,BQIH,20,BQIYR,20,PIND,0),U,4)
 .. S EXCEPT=$S(EXCEPT=1:"C",1:"N")
 . I VER>7.0 D
 .. D GFN^BQIGPUTL(BQIH,BQIYR)
 .. S EXCEPT=$$GET1^DIQ(BQIMEASF,BQMEAS_",",1704,"I")
 .. S EXCEPT=$S(EXCEPT="Y":"C",1:"N")
 .. S PDIR=$$GET1^DIQ(BQIMEASF,BQMEAS_",",1705,"E")
 S SCAT=$S(CAT="":"@",1:CAT),SCLIN=$S(CLIN="":"@",1:CLIN)
 S BQISORT(SCAT,SCLIN,$P(FDATA,U,3),IEN)=IEN_U_SRC_U_CAT_U_CLIN_U_$P(FDATA,U,3)_U_$P(FDATA,U,1)_U_VIEW_U_$G(PDIR)_U_$G(EXCEPT)_U_SIZE_U_EDIT_U_BATCH_U_SDIR_$C(30)
 ;S II=II+1,@DATA@(II)=IEN_U_SRC_U_CAT_U_CLIN_U_$P(FDATA,U,3)_U_$P(FDATA,U,1)_U_VIEW_U_PDIR_U_EXCEPT_U_SIZE_U_EDIT_U_BATCH_U_SDIR_$C(30)
 Q
