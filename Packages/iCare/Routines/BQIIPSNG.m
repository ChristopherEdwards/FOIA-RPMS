BQIIPSNG ;GDIT/HS/ALA-Update a Single Provider ; 06 Dec 2012  1:18 PM
 ;;2.3;ICARE MANAGEMENT SYSTEM;**3,5**;Apr 18, 2012;Build 17
 ;
EN(BQIPROV,BQDATE) ;EP
 ; Get current IPC
 S CRIPC=$P($G(^BQI(90508,1,11)),U,1)
 S CRN=$O(^BQI(90508,1,22,"B",CRIPC,"")) I CRN="" Q
 ; Calculate the IPC measures
 S MSN=0
 F  S MSN=$O(^BQI(90508,1,22,CRN,1,MSN)) Q:'MSN  D
 . S IDATA=^BQI(90508,1,22,CRN,1,MSN,0)
 . S CODE=$P(IDATA,U,1),TYP=$P(IDATA,U,2)
 . ; If inactive, quit
 . I $P(IDATA,U,7)=1 Q
 . ; If type is non calculable
 . I TYP="N" D NCLC Q
 . ; If type is RPMS
 . I TYP="R" Q
 . ; If type is CRS
 . I TYP="G" D CRS Q
 . I TYP="M" D MU
 ;
 ; Update the Goal Set
 NEW TPRN,PRV,TPRD,GPRN,GPRD,DEN,FAC,YEAR
 S YEAR=$$GET1^DIQ(90508,1_",",2,"E")
 S TPRN=$O(^BQIPROV(BQIPROV,30,"B","IPC_TOTP",""))
 I TPRN="" D TOTP
 I TPRN'="" S TPRD=$O(^BQIPROV(BQIPROV,30,TPRN,1,"B",BQDATE,""))
 I $G(TPRD)="" D
 . D TOTP
 . S TPRD=$O(^BQIPROV(BQIPROV,30,TPRN,1,"B",BQDATE,""))
 S GPRN=$O(^BQIPROV(BQIPROV,30,"B",YEAR_"_2452",""))
 I GPRN'="" S GPRD=$O(^BQIPROV(BQIPROV,30,GPRN,1,"B",BQDATE,""))
 I TPRN'="",TPRD'="" S DEN=$P(^BQIPROV(BQIPROV,30,TPRN,1,TPRD,0),U,2)
 I GPRN'="",GPRD'="" S $P(^BQIPROV(BQIPROV,30,GPRN,1,GPRD,0),U,2)=DEN
 S $P(^BQIPROV(BQIPROV,2),U,3)=$$NOW^XLFDT()
 ;
 ; Update the MU bundles
 NEW MPRN,IPRN,IPRD,DEN,NUM,MCOD,MPRD,MBUN
 S MPRN=$O(^BQIPROV(BQIPROV,30,"B","IPC_WGT",""))
 I MPRN'="" S MPRD=$O(^BQIPROV(BQIPROV,30,MPRN,1,"B",BQDATE,""))
 K MBUN
 F MCOD="MU_8","MU_6","MU_7" D
 . S IPRN=$O(^BQIPROV(BQIPROV,30,"B",MCOD,"")) I IPRN="" Q
 . S IPRD=$O(^BQIPROV(BQIPROV,30,IPRN,1,"B",BQDATE,"")) I IPRD="" Q
 . S DEN=$P(^BQIPROV(BQIPROV,30,IPRN,1,IPRD,0),U,2),NUM=$P(^(0),U,3)
 . S MBUN(+DEN)=+NUM
 S DEN=$O(MBUN(""))
 S:DEN="" NUM="" S:DEN'="" NUM=MBUN(DEN)
 I MPRN'="",MPRD'="" D
 . S $P(^BQIPROV(BQIPROV,30,MPRN,1,MPRD,0),U,2)=DEN,$P(^BQIPROV(BQIPROV,30,MPRN,1,MPRD,0),U,3)=NUM
 Q
 ;
NCLC ; No calculation possible
 Q
 ;
CRS ; Get values from BQIPAT
 S PRV=BQIPROV,TDEN=0,TNUM=0
 S DFN="",PDEN=0,PNUM=0
 F  S DFN=$O(^AUPNPAT("AK",PRV,DFN)) Q:DFN=""  D
 . I '$$HRN^BQIUL1(DFN) Q
 . S IEN=$O(^BQIPAT(DFN,30,"B",CODE,"")) I IEN="" Q
 . S PNUM=PNUM+$P(^BQIPAT(DFN,30,IEN,0),U,3)
 . S PDEN=PDEN+$P(^BQIPAT(DFN,30,IEN,0),U,4)
 D STORP^BQIIPUTL(PRV,CODE,BQDATE,PDEN,PNUM)
 Q
 ;
MU ; Get values for MU measures
 S PRV=BQIPROV,TDEN=0,TNUM=0
 NEW BGPBEN,BGPRTYPE,BGP0RPTH,BGPMUT,BGPMUYF,BGPBD,BGPED,BGPTP,BGPINDT
 NEW BQTDT,BQTMN,BQIGREF,DFN,CDEN,CNUM,CEXC,NUM,BQTN,MUIND,MUI
 S PDEN=0,PNUM=0
 ; Current
 S BGPBEN=3
 S BGPRTYPE=4,BGP0RPTH="A",BGPMUT="P",BGPMUYF=90595.11
 S (BGPBD,BGPED,BGPTP,BGPINDT)=""
 S BGPBD=BEGDT,BGPED=ENDT
 S BGPPBD="",BGPPED=""
 ; Baseline
 S BGPBBD=BGPPBD,BGPBED=BGPPED
 S BQIGREF=$NA(^TMP("BQICQM",$J)) K @BQIGREF
 S MUIND=$P($G(^BGPMUIND(90596.11,$P(CODE,"_",2),0)),U,1) I MUIND="" Q
 S BGPIND(MUIND)="",BGPPROV=PRV,MUI=$P(CODE,"_",2)
 D BQI^BGPMUEPD(.BQIGREF,BGPPROV)
 K CDEN,CNUM,CEXC,NUM
 S DFN=""
 F  S DFN=$O(@BQIGREF@(BGPPROV,DFN)) Q:DFN=""  D
 . S CDEN=$P($G(@BQIGREF@(BGPPROV,DFN,"C",MUI)),U,1)
 . S NUM=$P($G(@BQIGREF@(BGPPROV,DFN,"C",MUI)),U,2)
 . I NUM>1,$$FMTE^BQIUL1(NUM)'?.N S NUM=1
 . S CNUM=NUM
 . S CEXC=$P($G(@BQIGREF@(BGPPROV,DFN,"C",MUI)),U,3)
 . S PDEN=PDEN+$G(CDEN),PNUM=PNUM+$G(CNUM)
 D STORP^BQIIPUTL(PRV,CODE,BQDATE,PDEN,PNUM)
 K BQIND
 Q
 ;
TOTP ;
 NEW BQITOTP
 S BQITOTP=0
 S PIEN="" F  S PIEN=$O(^AUPNPAT("AK",BQIPROV,PIEN)) Q:PIEN=""  D
 . ;I '$$HRN^BQIUL1(PIEN) Q
 . S BQITOTP=BQITOTP+1
 D STORP^BQIIPUTL(BQIPROV,"IPC_TOTP",BQDATE,BQITOTP,0)
 Q
