BQIMUFAC ;VNGT/HS/ALA-MU Facility ; 01 Mar 2011  7:11 AM
 ;;2.3;ICARE MANAGEMENT SYSTEM;;Apr 18, 2012;Build 59
 ;
 ;
EN(DATA,TMFRAME) ; EP -- BQI MU GET FACILITY
 NEW UID,II,HDR,C1,C2,C3,C4,NAME,HEAD,HX,PEC,SORT,QFL,PCT,CT,DDATA
 S UID=$S($G(ZTSK):"Z"_ZTSK,1:$J)
 S DATA=$NA(^TMP("BQIMUFAC",UID))
 K @DATA
 S II=0
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQIMUFAC D UNWIND^%ZTER" ; SAC 2006 2.2.3.3.2
 S TMFRAME=$G(TMFRAME,90)
 S HDR="T00050HIDE_FACILITY^T00020HIDE_ID^T00050OBJECTIVE^T00010MEAS_SET^T00005CURR_DEN^T00005CURR_NUM^T00005CURR_MET^T00010STAGE_1_GOAL^"
 S HDR=HDR_"T00005PREV_DEN^T00005PREV_NUM^T00005PREV_MET^T00030HIDE_CURR_PERIOD^T00030HIDE_PREV_PERIOD"
 S @DATA@(II)=HDR_$C(30)
 S TMFRAME=$G(TMFRAME,90)
 S FC=0
 F  S FC=$O(^BQIFAC(FC)) Q:'FC  D RTE(FC)
 ;
DONE ;
 S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
ERR ;
 D ^%ZTER
 NEW Y,ERRDTM
 S Y=$$NOW^XLFDT() X ^DD("DD") S ERRDTM=Y
 S BMXSEC="Recording that an error occurred at "_ERRDTM
 I $D(II),$D(DATA) S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
RTE(FAC) ; EP
 NEW DATE,MN,MDATA,PRVR,CFROM,CTHRU,PFROM,PTHRU,IDN,CDEN,CNUM,CEXC,PDEN
 NEW PNUM,PEXC,CURR,PREV,OBJ,FACIL,DDATA,NODE,ST1G,MSET
 ;S FACIL=FAC_$C(28)_$P(^DIC(4,FAC,0),U,1)
 S FACIL=$P(^DIC(4,FAC,0),U,1)
 S DDATA=$G(^BQIFAC(FAC,0)) I DDATA="" S ^BQIFAC(FAC,0)=FAC,^BQIFAC("B",FAC,FAC)=""
 I TMFRAME=90 S NODE=20 D
 . S CFROM=$$FMTE^BQIUL1($P(DDATA,U,6)),CTHRU=$$FMTE^BQIUL1($P(DDATA,U,7)),CPER=CFROM_"-"_CTHRU
 . S PFROM=$$FMTE^BQIUL1($P(DDATA,U,8)),PTHRU=$$FMTE^BQIUL1($P(DDATA,U,9)),PPER=PFROM_"-"_PTHRU
 I TMFRAME=12 S NODE=10 D
 . S CFROM=$$FMTE^BQIUL1($P(DDATA,U,2)),CTHRU=$$FMTE^BQIUL1($P(DDATA,U,3)),CPER=CFROM_"-"_CTHRU
 . S PFROM=$$FMTE^BQIUL1($P(DDATA,U,4)),PTHRU=$$FMTE^BQIUL1($P(DDATA,U,5)),PPER=PFROM_"-"_PTHRU
 ;
 S NODE=$S(TMFRAME=90:20,1:10)
 S IDN=0
 F  S IDN=$O(^BQIFAC(FAC,NODE,IDN)) Q:'IDN  D
 . S MDATA=^BQIFAC(FAC,NODE,IDN,0)
 . S CDEN=$P(MDATA,U,2),CNUM=$P(MDATA,U,3),CEXC=$G(^BQIFAC(FAC,NODE,IDN,1))
 . S PDEN=$P(MDATA,U,4),PNUM=$P(MDATA,U,5),PEXC=$G(^BQIFAC(FAC,NODE,IDN,2))
 . S ID=$P(MDATA,U,1)
 . S IMN=$O(^APCMMUM("B",ID,""))
 . S ST1G=$G(^APCMMUM(IMN,13,1,0)),OBJ=$P(^APCMMUM(IMN,0),U,5)
 . S MSET=$$GET1^DIQ(9001300.02,IMN_",",.03,"E")
 . ;
 . I +CDEN=0,+CNUM=0 S CURR="0%"
 . I +PDEN=0,+PNUM=0 S PREV="0%"
 . I +CDEN'=0,+CNUM=0 S CURR="0%"
 . I +PDEN'=0,+PNUM=0 S PREV="0%"
 . I +CDEN'=0,+CNUM'=0 S CURR=$J((CNUM/CDEN)*100,3,0)_"%",CURR=$$TRIM^BQIUL1(CURR," ")
 . I +PDEN'=0,+PNUM'=0 S PREV=$J((PNUM/PDEN)*100,3,0)_"%",PREV=$$TRIM^BQIUL1(PREV," ")
 . I CURR="" S CURR="N/A"
 . I PREV="" S PREV="N/A"
 . I CEXC'="" S CURR="Excluded"
 . I PEXC'="" S PREV="Excluded"
 . S:CNUM="" CNUM=0 S:PNUM="" PNUM=0 S:CDEN="" CDEN=0 S:PDEN="" PDEN=0
 . S II=II+1,@DATA@(II)=FACIL_U_ID_U_OBJ_U_MSET_U_CDEN_U_CNUM_U_CURR_U_ST1G_U_PDEN_U_PNUM_U_PREV_U_CPER_U_PPER_$C(30)
 Q
 ;
CQ(DATA,TMFRAME,PERIOD) ; EP -- BQI MU GET FAC CQ
 NEW UID,II,HDR,C1,C2,C3,C4,NAME,HEAD,HX,PEC,SORT,QFL,PCT,CT,DDATA,CURN,CPER,PRVN,PPER
 S UID=$S($G(ZTSK):"Z"_ZTSK,1:$J)
 S DATA=$NA(^TMP("BQIMUFCQ",UID))
 K @DATA
 S II=0
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQIMUFAC D UNWIND^%ZTER" ; SAC 2006 2.2.3.3.2
 ;
 D GTM^BQIMUTIM
 ;
 S HDR="T00050HIDE_FACILITY^T00020HIDE_ID^T00050OBJECTIVE^T00010MEAS_SET^T00005CURR_DEN^T00005CURR_NUM^T00005CURR_MET^T00010STAGE_1_GOAL^"
 S HDR=HDR_"T00005PREV_DEN^T00005PREV_NUM^T00005PREV_MET^T00030HIDE_CURR_PERIOD^T00030HIDE_PREV_PERIOD"
 S @DATA@(II)=HDR_$C(30)
 ;
 S FC=0
 F  S FC=$O(^BQIFAC(FC)) Q:'FC  D RET(FC)
 S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
RET(FAC) ; EP
 NEW DATE,MN,PRVR,IDN,CDEN,CNUM,CEXC,PDEN,BGPIND,CRDT,CQN,PRDT
 NEW PNUM,PEXC,CURR,PREV,BQZZ,CDATA,PDATA,FACIL,X,I,ID,IMN,ST1G
 NEW MSN,MSET
 S FACIL=$P(^DIC(4,FAC,0),U,1)
 ;
 S X=0 F  S X=$O(^BGPMUIND(90595.11,"AMS","H",X)) Q:X'=+X  S BGPIND(X)=""
 S I="" F  S I=$O(BGPIND(I)) Q:I=""  D
 . S BN="" F  S BN=$O(^BGPMUIND(90596.11,"B",I,BN)) Q:BN=""  D
 .. S ID=$P(^BGPMUIND(90596.11,BN,0),U,4),BQZZ(ID,"C")="NDA",BQZZ(ID,"P")="NDA"
 ;
 S CRDT=""
 F  S CRDT=$O(BQCDAR(CRDT)) Q:CRDT=""  D
 . S CQN=$O(^BQIFAC(FAC,50,"B",CRDT,""))
 . D CAGG
 ;
 S PRDT=""
 F  S PRDT=$O(BQPDAR(PRDT)) Q:PRDT=""  D
 . S PQN=$O(^BQIFAC(FAC,50,"B",PRDT,""))
 . D PAGG
 ;
 S ID=""
 F  S ID=$O(BQZZ(ID)) Q:ID=""  D
 . S IMN=$O(^BGPMUIND(90596.11,"C",ID,"")) I IMN="" Q
 . S ST1G="",OBJ=$P($G(^BGPMUIND(90596.11,IMN,17)),U,3)
 . S MSN=$P(^BGPMUIND(90596.11,IMN,0),U,1)
 . I $P(^BGPMUIND(90595.11,MSN,0),U,4)'="H" Q
 . S MSET="HOSPITAL"
 . ;
 . S CDEN=$P($G(BQZZ(ID,"C")),U,1)
 . S CNUM=$P($G(BQZZ(ID,"C")),U,2)
 . ;
 . S PDEN=$P($G(BQZZ(ID,"P")),U,1)
 . S PNUM=$P($G(BQZZ(ID,"P")),U,2)
 . ;
 . I ID[".ED." D
 .. S CURR=""
 .. I CDEN'="" S CURR=$J(CDEN,4,0)_" mins",CURR=$$TRIM^BQIUL1(CURR," ")
 .. S PREV=""
 .. I PDEN'="" S PREV=$J(PDEN,4,0)_" mins",PREV=$$TRIM^BQIUL1(PREV," ")
 . I ID'[".ED." D
 .. I +CDEN=0,+CNUM=0 S CURR="0%"
 .. I +CDEN'=0,+CNUM=0 S CURR="0%"
 .. I +CDEN'=0,+CNUM'=0,(CDEN-CEXC)=0 S CURR="0%"
 .. I +CDEN'=0,+CNUM'=0,(CDEN-CEXC)'=0 S CURR=$J((CNUM/(CDEN-CEXC))*100,3,0)_"%",CURR=$$TRIM^BQIUL1(CURR," ")
 .. S:CNUM="" CNUM=0 S:CDEN="" CDEN=0
 .. I +PDEN=0,+PNUM=0 S PREV="0%"
 .. I +PDEN'=0,+PNUM=0 S PREV="0%"
 .. I +PDEN'=0,+PNUM'=0,(PDEN-PEXC)=0 S PREV="0%"
 .. I +PDEN'=0,+PNUM'=0,(PDEN-PEXC)'=0 S PREV=$J((PNUM/(PDEN-PEXC))*100,3,0)_"%",PREV=$$TRIM^BQIUL1(PREV," ")
 . S II=II+1,@DATA@(II)=FACIL_U_ID_U_OBJ_U_MSET_U_CDEN_U_CNUM_U_CURR_U_ST1G_U_PDEN_U_PNUM_U_PREV_U_CPER_U_PPER_$C(30)
 K BQZZ
 Q
 ;
CAGG ; Aggregate
 ;
 I CQN="" Q
 S IDN=0
 F  S IDN=$O(^BQIFAC(FAC,50,CQN,1,IDN)) Q:'IDN  D
 . S (CDEN,CNUM,CEXC,CURR)=""
 . S CDATA=^BQIFAC(FAC,50,CQN,1,IDN,0)
 . S CDEN=$P(CDATA,U,2),CNUM=$P(CDATA,U,3),CEXC=$P(CDATA,U,4)
 . S ID=$P(CDATA,U,1)
 . S $P(BQZZ(ID,"C"),U,1)=$P($G(BQZZ(ID,"C")),U,1)+CDEN
 . S $P(BQZZ(ID,"C"),U,2)=$P($G(BQZZ(ID,"C")),U,2)+CNUM
 . S $P(BQZZ(ID,"C"),U,3)=$P($G(BQZZ(ID,"C")),U,3)+CEXC
 Q
 ;
PAGG ; Aggregate
 I PQN="" Q
 S IDN=0
 F  S IDN=$O(^BQIFAC(FAC,50,PQN,1,IDN)) Q:'IDN  D
 . S (PDEN,PNUM,PEXC,PREV)=""
 . S PDATA=^BQIFAC(FAC,50,PQN,1,IDN,0)
 . S PDEN=$P(PDATA,U,2),PNUM=$P(PDATA,U,3),PEXC=$P(PDATA,U,4)
 . S ID=$P(PDATA,U,1)
 . ;
 . I +PNUM=0 S PREV=0
 . I +PDEN'=0,+PNUM'=0 S PREV=$J((PNUM/PDEN)*100,3,0),PREV=$$TRIM^BQIUL1(PREV," ")
 . I PDEN="",PNUM="",PEXC'="" S PREV="Excluded"
 . I PDEN="",PNUM="",PEXC="" S PREV=""
 . S $P(BQZZ(ID,"P"),U,1)=$P($G(BQZZ(ID,"P")),U,1)+PDEN
 . S $P(BQZZ(ID,"P"),U,2)=$P($G(BQZZ(ID,"P")),U,2)+PNUM
 . S $P(BQZZ(ID,"P"),U,3)=$P($G(BQZZ(ID,"P")),U,3)+PEXC
 Q
