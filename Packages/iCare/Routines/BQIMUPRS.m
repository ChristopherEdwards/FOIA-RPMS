BQIMUPRS ;VNGT/HS/ALA-Update a single provider ; 27 Apr 2011  8:28 AM
 ;;2.2;ICARE MANAGEMENT SYSTEM;**1**;Jul 28, 2011;Build 25
 ;
PROV(BGPPROV) ;EP -- 1 year
 ; For 1 year
 S BGPBEN=3
 S BGPRTYPE=4,BGP0RPTH="A",BGPMUT="P",BGPMUYF=90595.11
 S (BGPBD,BGPED,BGPTP,BGPINDT)=""
 ; 1 year timeframe
 ; Current
 S BGPBD=$$DATE^BQIUL1("T-365"),BGPED=DT
 ; Previous
 S BGPPBD=$$DATE^BQIUL1("T-731"),BGPPED=$$DATE^BQIUL1("T-366")
 ; Baseline
 S BGPBBD=BGPPBD,BGPBED=BGPPED
 ;
 S BQIGREF=$NA(^TMP("BQICQM",$J)) K @BQIGREF
 D IND("E")
 D BQI^BGPMUEPD(.BQIGREF,BGPPROV)
 K CDEN,CNUM,CEXC,PDEN,PNUM,PEXC,NUM
 S DFN=""
 F  S DFN=$O(@BQIGREF@(BGPPROV,DFN)) Q:DFN=""  D
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"C",I)) Q:I=""  D
 .. NEW NUM
 .. S CDEN(I)=$G(CDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,1)
 .. S NUM=+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,2) S:NUM>1 NUM=1
 .. S CNUM(I)=$G(CNUM(I))+NUM
 .. S CEXC(I)=$G(CEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,3)
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"P",I)) Q:I=""  D
 .. NEW NUM
 .. S PDEN(I)=$G(PDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,1)
 .. S NUM=+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,2) S:NUM>1 NUM=1
 .. S PNUM(I)=$G(PNUM(I))+NUM
 .. S PEXC(I)=$G(PEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,3)
 .. ; For DFN set up and store individual
 .. S PADH=$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,4)
 D STORP(11)
 K @BQIGREF
 Q
 ;
STORP(NODE) ;
 I '$D(@BQIGREF) Q
 K ^BQIPROV(BGPPROV,NODE),^BQIPROV(BGPPROV,NODE,"B")
 ; If 1 year
 I NODE=11 D
 . S $P(^BQIPROV(BGPPROV,1),U,1,4)=BGPBD_U_BGPED_U_BGPPBD_U_BGPPED
 . S $P(^BQIPROV(BGPPROV,2),U,2)=$$NOW^XLFDT()
 . S $P(^BQI(90508,1,12),U,4)=BGPPROV
 ; If 90 days
 I NODE=21 D
 . S $P(^BQIPROV(BGPPROV,1),U,5,8)=BGPBD_U_BGPED_U_BGPPBD_U_BGPPED
 . S $P(^BQIPROV(BGPPROV,2),U,1)=$$NOW^XLFDT()
 . S $P(^BQI(90508,1,12),U,3)=BGPPROV
 S CT=0
 S I=0 F  S I=$O(^BGPMUIND(90596.11,I)) Q:'I  D
 . S MSN=$P(^BGPMUIND(90596.11,I,0),U,1)
 . I $G(^BGPMUIND(90595.11,MSN,0))="" Q
 . I $P(^BGPMUIND(90595.11,MSN,0),U,4)="H" Q
 . S CT=CT+1,^BQIPROV(BGPPROV,NODE,CT,0)=$P(^BGPMUIND(90596.11,I,0),"^",4)_U_$G(CDEN(I))_U_$G(CNUM(I))_U_$G(CEXC(I))_U_$G(PDEN(I))_U_$G(PNUM(I))_U_$G(PEXC(I))
 . S ^BQIPROV(BGPPROV,NODE,"B",$P(^BGPMUIND(90596.11,I,0),"^",4),CT)=""
 Q
 ;
IND(TY) ; EP - Set indicators
 ; Input
 ;   TY = Type (H=Hospital)
 K BGPIND
 I TY'="H" D
 . S X=0 F  S X=$O(^BGPMUIND(90595.11,X)) Q:'X  I $P(^BGPMUIND(90595.11,X,0),U,4)'="H" S BGPIND(X)=""
 I TY="H" D
 . S X=0 F  S X=$O(^BGPMUIND(90595.11,X)) Q:'X  I $P(^BGPMUIND(90595.11,X,0),U,4)="H" S BGPIND(X)=""
 Q
 ;
NIN(BGPPROV) ;EP - 90 Days
 ; For 90 day timeframe
 ; Current
 S BGPBEN=3
 S BGPRTYPE=4,BGP0RPTH="A",BGPMUT="P",BGPMUYF=90595.11
 S (BGPBD,BGPED,BGPTP,BGPINDT)=""
 S BGPBD=$$DATE^BQIUL1("T-90"),BGPED=DT
 ; Previous
 S BGPPBD=$$DATE^BQIUL1("T-181"),BGPPED=$$DATE^BQIUL1("T-91")
 ; Baseline
 S BGPBBD=BGPPBD,BGPBED=BGPPED
 S BQIGREF=$NA(^TMP("BQICQM",$J)) K @BQIGREF
 D IND("E")
 D BQI^BGPMUEPD(.BQIGREF,BGPPROV)
 K CDEN,CNUM,CEXC,PDEN,PNUM,PEXC
 S DFN=""
 F  S DFN=$O(@BQIGREF@(BGPPROV,DFN)) Q:DFN=""  D
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"C",I)) Q:I=""  D
 .. S CDEN(I)=$G(CDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,1)
 .. S CNUM(I)=$G(CNUM(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,2)
 .. S CEXC(I)=$G(CEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,3)
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"P",I)) Q:I=""  D
 .. S PDEN(I)=$G(PDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,1)
 .. S PNUM(I)=$G(PNUM(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,2)
 .. S PEXC(I)=$G(PEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,3)
 .. ; For DFN set up and store individual
 .. S PADH=$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,4)
 D STORP(21)
 K @BQIGREF
 Q
