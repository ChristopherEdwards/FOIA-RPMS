BQIMUPRS ;VNGT/HS/ALA-Update a single provider ; 27 Apr 2011  8:28 AM
 ;;2.3;ICARE MANAGEMENT SYSTEM;;Apr 18, 2012;Build 59
 ;
PROV(BGPPROV) ;EP -- 1 year
 NEW BGPBEN,BGPRTYPE,BGP0RPTH,BGPMUT,BGPMUYF,BGPBD,BGPED,BGPTP,BGPINDT
 NEW BQTDT,BQTMN,BQIGREF,DFN,CDEN,CNUM,CEXC,NUM,PADH,PDEN,PNUM,PEXC
 ; For 1 year
 S BGPBEN=3
 S BGPRTYPE=4,BGP0RPTH="A",BGPMUT="P",BGPMUYF=90595.11
 S (BGPBD,BGPED,BGPTP,BGPINDT)=""
 ; 1 year timeframe
 ; Current
 S BGPBD=$$DATE^BQIUL1("T-365"),BGPED=DT
 ; Previous
 S BGPPBD=$$DATE^BQIUL1("T-731"),BGPPED=$$DATE^BQIUL1("T-366")
 ; Baseline
 S BGPBBD=BGPPBD,BGPBED=BGPPED
 ;
 S BQIGREF=$NA(^TMP("BQICQM",$J)) K @BQIGREF
 D IND("E")
 D BQI^BGPMUEPD(.BQIGREF,BGPPROV)
 K CDEN,CNUM,CEXC,PDEN,PNUM,PEXC,NUM
 S DFN=""
 F  S DFN=$O(@BQIGREF@(BGPPROV,DFN)) Q:DFN=""  D
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"C",I)) Q:I=""  D
 .. NEW NUM
 .. S CDEN(I)=$G(CDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,1)
 .. S NUM=+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,2)
 .. I NUM>1,$$FMTE^BQIUL1(NUM)'?.N S NUM=1
 .. S CNUM(I)=$G(CNUM(I))+NUM
 .. S CEXC(I)=$G(CEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,3)
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"P",I)) Q:I=""  D
 .. NEW NUM
 .. S PDEN(I)=$G(PDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,1)
 .. S NUM=+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,2)
 .. I NUM>1,$$FMTE^BQIUL1(NUM)'?.N S NUM=1
 .. S PNUM(I)=$G(PNUM(I))+NUM
 .. S PEXC(I)=$G(PEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,3)
 .. ; For DFN set up and store individual
 .. S PADH=$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,4)
 D STORP(11)
 K @BQIGREF
 Q
 ;
STORP(NODE) ;EP - Store data
 I '$D(@BQIGREF) Q
 ;
 S BQTMN=$O(^BQIPROV(BGPPROV,50,"B",BQTDT,""))
 I BQTMN="" D UPD
 I NODE=60 D  Q
 . S $P(^BQIPROV(BGPPROV,2),U,1)=$$NOW^XLFDT()
 . S CT=0
 . S I=0 F  S I=$O(^BGPMUIND(90596.11,I)) Q:'I  D
 .. S MSN=$P(^BGPMUIND(90596.11,I,0),U,1)
 .. I $G(^BGPMUIND(90595.11,MSN,0))="" Q
 .. I $P(^BGPMUIND(90595.11,MSN,0),U,4)="H" Q
 .. S CT=CT+1,^BQIPROV(BGPPROV,NODE,BQTMN,10,CT,0)=$P(^BGPMUIND(90596.11,I,0),"^",4)_U_$G(CDEN(I))_U_$G(CNUM(I))_U_$G(CEXC(I))
 .. S ^BQIPROV(BGPPROV,NODE,BQTMN,10,"B",$P(^BGPMUIND(90596.11,I,0),"^",4),CT)=""
 ;
 I NODE=50 D  Q
 . S $P(^BQIPROV(BGPPROV,2),U,1)=$$NOW^XLFDT()
 . S CT=0
 . S I=0 F  S I=$O(^BGPMUIND(90596.11,I)) Q:'I  D
 .. S MSN=$P(^BGPMUIND(90596.11,I,0),U,1)
 .. I $G(^BGPMUIND(90595.11,MSN,0))="" Q
 .. I $P(^BGPMUIND(90595.11,MSN,0),U,4)="H" Q
 .. S CT=CT+1,^BQIPROV(BGPPROV,NODE,BQTMN,1,CT,0)=$P(^BGPMUIND(90596.11,I,0),"^",4)_U_$G(CDEN(I))_U_$G(CNUM(I))_U_$G(CEXC(I))
 .. S ^BQIPROV(BGPPROV,NODE,BQTMN,1,"B",$P(^BGPMUIND(90596.11,I,0),"^",4),CT)=""
 Q
 ;
 K ^BQIPROV(BGPPROV,NODE),^BQIPROV(BGPPROV,NODE,"B")
 ; If 1 year
 I NODE=11 D
 . S $P(^BQIPROV(BGPPROV,1),U,1,4)=BGPBD_U_BGPED_U_BGPPBD_U_BGPPED
 . S $P(^BQIPROV(BGPPROV,2),U,2)=$$NOW^XLFDT()
 . S $P(^BQI(90508,1,12),U,4)=BGPPROV
 ; If 90 days
 I NODE=21 D
 . S $P(^BQIPROV(BGPPROV,1),U,5,8)=BGPBD_U_BGPED_U_BGPPBD_U_BGPPED
 . S $P(^BQIPROV(BGPPROV,2),U,1)=$$NOW^XLFDT()
 . S $P(^BQI(90508,1,12),U,3)=BGPPROV
 ;
 S CT=0
 S I=0 F  S I=$O(^BGPMUIND(90596.11,I)) Q:'I  D
 . S MSN=$P(^BGPMUIND(90596.11,I,0),U,1)
 . I $G(^BGPMUIND(90595.11,MSN,0))="" Q
 . I $P(^BGPMUIND(90595.11,MSN,0),U,4)="H" Q
 . S CT=CT+1,^BQIPROV(BGPPROV,NODE,CT,0)=$P(^BGPMUIND(90596.11,I,0),"^",4)_U_$G(CDEN(I))_U_$G(CNUM(I))_U_$G(CEXC(I))_U_$G(PDEN(I))_U_$G(PNUM(I))_U_$G(PEXC(I))
 . S ^BQIPROV(BGPPROV,NODE,"B",$P(^BGPMUIND(90596.11,I,0),"^",4),CT)=""
 Q
 ;
IND(TY) ; EP - Set indicators
 ; Input
 ;   TY = Type (H=Hospital)
 K BGPIND
 I TY'="H" D
 . S X=0 F  S X=$O(^BGPMUIND(90595.11,X)) Q:'X  I $P(^BGPMUIND(90595.11,X,0),U,4)'="H" S BGPIND(X)=""
 I TY="H" D
 . S X=0 F  S X=$O(^BGPMUIND(90595.11,X)) Q:'X  I $P(^BGPMUIND(90595.11,X,0),U,4)="H" S BGPIND(X)=""
 Q
 ;
NIN(BGPPROV) ;EP - 90 Days
 NEW BGPBEN,BGPRTYPE,BGP0RPTH,BGPMUT,BGPMUYF,BGPBD,BGPED,BGPTP,BGPINDT
 NEW BQTDT,BQTMN,BQIGREF,DFN,CDEN,CNUM,CEXC,NUM,PADH,PDEN,PNUM,PEXC
 ; Current
 S BGPBEN=3
 S BGPRTYPE=4,BGP0RPTH="A",BGPMUT="P",BGPMUYF=90595.11
 S (BGPBD,BGPED,BGPTP,BGPINDT)=""
 S BGPBD=$$DATE^BQIUL1("T-90"),BGPED=DT
 ; Previous
 S BGPPBD=$$DATE^BQIUL1("T-181"),BGPPED=$$DATE^BQIUL1("T-91")
 ; Baseline
 S BGPBBD=BGPPBD,BGPBED=BGPPED
 S BQIGREF=$NA(^TMP("BQICQM",$J)) K @BQIGREF
 D IND("E")
 D BQI^BGPMUEPD(.BQIGREF,BGPPROV)
 K CDEN,CNUM,CEXC,PDEN,PNUM,PEXC
 S DFN=""
 F  S DFN=$O(@BQIGREF@(BGPPROV,DFN)) Q:DFN=""  D
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"C",I)) Q:I=""  D
 .. S CDEN(I)=$G(CDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,1)
 .. S NUM=$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,2)
 .. I NUM>1,$$FMTE^BQIUL1(NUM)'?.N S NUM=1
 .. S CNUM(I)=$G(CNUM(I))+NUM
 .. S CEXC(I)=$G(CEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,3)
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"P",I)) Q:I=""  D
 .. S PDEN(I)=$G(PDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,1)
 .. S NUM=$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,2)
 .. I NUM>1,$$FMTE^BQIUL1(NUM)'?.N S NUM=1
 .. S PNUM(I)=$G(PNUM(I))+NUM
 .. S PEXC(I)=$G(PEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,3)
 .. ; For DFN set up and store individual
 .. S PADH=$P($G(@BQIGREF@(BGPPROV,DFN,"P",I)),U,4)
 D STORP(21)
 K @BQIGREF
 Q
 ;
MON(BGPPROV) ;EP - Monthly
 NEW BGPBEN,BGPRTYPE,BGP0RPTH,BGPMUT,BGPMUYF,BGPBD,BGPED,BGPTP,BGPINDT
 NEW BQTDT,BQTMN,BQIGREF,DFN,CDEN,CNUM,CEXC,NUM
 ; Current
 S BGPBEN=3
 S BGPRTYPE=4,BGP0RPTH="A",BGPMUT="P",BGPMUYF=90595.11
 S (BGPBD,BGPED,BGPTP,BGPINDT)=""
 ;S BQTDT=$O(^BQIPROV(BGPPROV,60,"AC",""),-1)
 ;I BQTDT="" D UPD
 ;S BQTMN=$O(^BQIPROV(BGPPROV,60,"AC",BQTDT,""))
 ;I BQTMN="" Q
 ;S BGPBD=$P(^BQIPROV(BGPPROV,60,BQTMN,0),U,2),BGPED=$P(^(0),U,3)
 S BGPBD=$P(^BQI(90508,1,12),U,8),BGPED=$P(^BQI(90508,1,12),U,9)
 S BQTDT=$E(BGPBD,1,5)_"00"
 S BQTMN=$O(^BQIPROV(BGPPROV,50,"B",BQTDT,""))
 I BQTMN="" D UPD
 ;S BGPBD=$$DATE^BQIUL1("T-90"),BGPED=DT
 ; no more Previous
 ;S BGPPBD=$$DATE^BQIUL1("T-181"),BGPPED=$$DATE^BQIUL1("T-91")
 S BGPPBD="",BGPPED=""
 ; Baseline
 S BGPBBD=BGPPBD,BGPBED=BGPPED
 S BQIGREF=$NA(^TMP("BQICQM",$J)) K @BQIGREF
 D IND("E")
 D BQI^BGPMUEPD(.BQIGREF,BGPPROV)
 K CDEN,CNUM,CEXC,NUM
 S DFN=""
 F  S DFN=$O(@BQIGREF@(BGPPROV,DFN)) Q:DFN=""  D
 . S I=""
 . F  S I=$O(@BQIGREF@(BGPPROV,DFN,"C",I)) Q:I=""  D
 .. S CDEN(I)=$G(CDEN(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,1)
 .. S NUM=$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,2)
 .. I NUM>1,$$FMTE^BQIUL1(NUM)'?.N S NUM=1
 .. S CNUM(I)=$G(CNUM(I))+NUM
 .. S CEXC(I)=$G(CEXC(I))+$P($G(@BQIGREF@(BGPPROV,DFN,"C",I)),U,3)
 .. ;S ^XTMP("BQIMCQMPT",DFN,BGPPROV,BQTDT,I)=$G(@BQIGREF@(BGPPROV,DFN,"C",I))
 D STORP(50)
 K @BQIGREF
 Q
 ;
UPD ;EP
 NEW BEGDT,ENDT,TMFRAME,XX,V,I,ERROR,BQIUPD,BQDATE,BQTMN
 S BEGDT=$P($G(^BQI(90508,1,12)),U,8),ENDT=$P($G(^BQI(90508,1,12)),U,9)
 ;S TMFRAME=$$FMTE^BQIUL1(BEGDT)_" - "_$$FMTE^BQIUL1(ENDT)
 I $G(^BQIPROV(BGPPROV,0))="" S ^BQIPROV(BGPPROV,0)=BGPPROV,^BQIPROV("B",BGPPROV,BGPPROV)=""
 ;I $G(^BQIPROV(BGPPROV,60,0))="" S ^BQIPROV(BGPPROV,60,0)="^90505.46^^"
 I $G(^BQIPROV(BGPPROV,50,0))="" S ^BQIPROV(BGPPROV,50,0)="^90505.45D^^"
 S BQDATE=$E(BEGDT,1,5)_"00"
 NEW DA,X,IENS,Y
 S DA(1)=BGPPROV,DIC="^BQIPROV("_DA(1)_",50,",X=BQDATE,DIC(0)="LNZ",DLAYGO=90505.45,DIC("P")=DLAYGO
 D ^DIC
 S DA=+Y I DA=-1 Q
 S BQTMN=DA
 ;S IENS=$$IENS^DILF(.DA)
 ;S BQIUPD(90505.46,IENS,.02)=BEGDT,BQIUPD(90505.46,IENS,.03)=ENDT
 ;D FILE^DIE("","BQIUPD","ERROR")
 ;S BQTDT=TMFRAME
 Q
