BQIPLFL1 ;VNGT/HS/ALA - Continuation of BQIPLFL ; 23 Mar 2011  3:18 PM
 ;;2.2;ICARE MANAGEMENT SYSTEM;;Jul 28, 2011;Build 37
 ;
MVL(BOWNR,BPLIEN,BDA,BVAL,BTYP) ;EP - Add a multiple value API
 ;Input
 ;  BOWNR  - Owner IEN
 ;  BPLIEN - Panel IEN
 ;  BDA    - Parameter IEN
 ;  BVAL   - Value of Parameter
 ;  BTYP   - Value type of parameter
 NEW DA,IENS,DIC,DLAYGO
 S DA(3)=BOWNR,DA(2)=BPLIEN,DA(1)=BDA,X=BVAL
 S DLAYGO=90505.1151,DIC(0)="L",DIC("P")=DLAYGO
 S DIC="^BQICARE("_DA(3)_",1,"_DA(2)_",15,"_DA(1)_",1,"
 K DO,DD D FILE^DICN
 S (DA,MDA)=+Y
 I DA=-1 S RESULT=-1,QFL=1 Q
 S IENS=$$IENS^DILF(.DA)
 I BTYP="D" S BVAL=$$DATE^BQIUL1(BVAL)
 I BTYP="T" S BQIUPD(90505.1151,IENS,.02)=BVAL
 I BTYP'="T" S BQIUPD(90505.1151,IENS,.01)=BVAL
 D FILE^DIE("","BQIUPD","ERROR")
 Q MDA
 ;
ANF(OWNR,PLIEN,NAME) ; EP - Add new filter parameter
 NEW DA,IENS,DIC,DLAYGO
 S DA(2)=OWNR,DA(1)=PLIEN,X=NAME
 S DLAYGO=90505.115,DIC(0)="L",DIC("P")=DLAYGO
 S DIC="^BQICARE("_DA(2)_",1,"_DA(1)_",15,"
 I '$D(^BQICARE(DA(2),1,DA(1),15,0)) S ^BQICARE(DA(2),1,DA(1),15,0)="^90505.115^^"
 D ^DIC
 I Y=-1 K DO,DD D FILE^DICN
 Q +Y
 ;
