BQIPTSKN ;PRXM/HC/ALA - PATIENT SKIN TESTS ; 26 Mar 2007  4:14 PM
 ;;2.1;ICARE MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
SK(DATA,DFN,DRANGE) ; EP -- BQI PATIENT SKIN TESTS
 ;
 ;Description - all the skin tests that a patient has
 ;
 ;Input
 ;  DFN - Patient internal entry number
 ;
 NEW UID,II,IEN,VISIT,VSDTM,SK,RESULTS,READ,RDTM,INJS,ORPHY,ENPHY
 S UID=$S($G(ZTSK):"Z"_ZTSK,1:$J)
 S DATA=$NA(^TMP("BQIPTSKN",UID))
 K @DATA
 ;
 S II=0
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQIPTSKN D UNWIND^%ZTER" ; SAC 2006 2.2.3.3.2
 ;
 S DRANGE=$$DATE^BQIUL1($G(DRANGE))
 S @DATA@(II)="I00010VISIT_IEN^D00030VISIT_DATETIME^T00040SKIN_TEST^T00020RESULTS^T00040READING^D00030READ_DATETIME"_$C(30)
 S IEN=""
 F  S IEN=$O(^AUPNVSK("AC",DFN,IEN),-1) Q:IEN=""  D
 . S SK=$$GET1^DIQ(9000010.12,IEN_",",.01,"E") I SK="" Q
 . S VISIT=$$GET1^DIQ(9000010.12,IEN_",",.03,"I") I VISIT="" Q
 . S VSDTM=$$GET1^DIQ(9000010,VISIT_",",.01,"I") I VSDTM=0 Q
 . I DRANGE'="",(VSDTM\1<DRANGE) Q
 . S RESULTS=$$GET1^DIQ(9000010.12,IEN_",",.04,"E")
 . S READ=$$GET1^DIQ(9000010.12,IEN_",",.05,"E")
 . S RDTM=$$GET1^DIQ(9000010.12,IEN_",",.06,"I")
 . S INJS=$$GET1^DIQ(9000010.12,IEN_",",.09,"E")
 . ;S ORPHY=$$GET1^DIQ(9000010.12,IEN_",",1202,"E")
 . ;S ENPHY=$$GET1^DIQ(9000010.12,IEN_",",1204,"E")
 . S II=II+1,@DATA@(II)=VISIT_U_$$FMTE^BQIUL1(VSDTM)_U_SK_U_RESULTS_U_READ_U_$$FMTE^BQIUL1(RDTM)_$C(30)
 ;
 ; Check for refusals
 D REF
 S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
ERR ;
 D ^%ZTER
 NEW Y,ERRDTM
 S Y=$$NOW^XLFDT() X ^DD("DD") S ERRDTM=Y
 S BMXSEC="Recording that an error occurred at "_ERRDTM
 I $D(II),$D(DATA) S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
REF ; Check for refusals
 NEW SKN,RVDT,REVDT,RFIEN,VISIT,PERCNT,SK,RESULTS
 S SKN=""
 F  S SKN=$O(^AUPNPREF("AA",DFN,9999999.28,SKN)) Q:SKN=""  D
 . S RVDT=""
 . F  S RVDT=$O(^AUPNPREF("AA",DFN,9999999.28,SKN,RVDT)) Q:RVDT=""  D
 .. ; Reverse the reverse date
 .. S REVDT=9999999-RVDT
 .. I DRANGE'="",(REVDT\1)<DRANGE Q
 .. S RFIEN=""
 .. F  S RFIEN=$O(^AUPNPREF("AA",DFN,9999999.28,SKN,RVDT,RFIEN)) Q:RFIEN=""  D
 ... S SK=$$GET1^DIQ(9999999.28,SKN_",",.01,"E")
 ... S RESULTS=$$GET1^DIQ(9000022,RFIEN_",",.07,"E")
 ... S ORPHY=$$GET1^DIQ(9000022,RFIEN_",",1204,"E")
 ... S READ="",VISIT="",RDTM=""
 ... S II=II+1,@DATA@(II)=VISIT_U_$$FMTE^BQIUL1(REVDT)_U_SK_U_RESULTS_U_READ_U_RDTM_$C(30)
 Q
