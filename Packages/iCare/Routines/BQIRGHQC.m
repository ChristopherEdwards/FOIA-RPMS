BQIRGHQC ;GDIT/HS/ALA-HMS QOC ; 26 Jun 2014  7:49 AM
 ;;2.5;ICARE MANAGEMENT SYSTEM;;May 24, 2016;Build 27
 ;
 ;
SSX(BQDFN) ;EP
 NEW LAST
 S LAST=""
 D PTEDTAX^BKMIXX1(BQDFN,"BKM SAFE SEX ED CODES","","","",.LAST)
 I LAST'="" S LAST=LAST\1
 Q $$FMTMDY^BQIUL1(LAST)
 ;
ADVD(BQDFN) ;EP
 NEW N,TEXT
 I $G(^AUPNADVD(BQDFN,0))="" Q "NONE"
 S N=0,TEXT=""
 F  S N=$O(^AUPNADVD(BQDFN,11,N)) Q:'N  D
 . I $P(^AUPNADVD(BQDFN,11,N,0),U,2)'="Y" Q
 . NEW DA,IENS
 . S DA(1)=BQDFN,DA=N,IENS=$$IENS^DILF(.DA)
 . S TEXT=TEXT_$$GET1^DIQ(9000040.11,IENS,.03,"E")_" ("_$$FMTMDY^BQIUL1($$GET1^DIQ(9000040.11,IENS,.01,"I")\1)_")"_$C(10)_$C(13)
 I TEXT="" Q "NONE"
 Q $$TKO^BQIUL1(TEXT,$C(10)_$C(13))
