BQITASK4 ;VNGT/HS/ALA-MU Task ; 23 Feb 2011  8:28 AM
 ;;2.3;ICARE MANAGEMENT SYSTEM;**1**;Apr 18, 2012;Build 43
 ;
 ;
MU ; EP -- BQI UPDATE MEAN USE 1 YEAR
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQI1POJB D UNWIND^%ZTER"
 ;
 ;   Set the DATE/TIME MU STARTED field
 NEW DA
 S DA=$O(^BQI(90508,0)) I 'DA Q
 S BQIUPD(90508,DA_",",8.04)=$$NOW^XLFDT()
 S BQIUPD(90508,DA_",",8.06)=1
 S BQIUPD(90508,DA_",",24.1)=$G(ZTSK)
 D FILE^DIE("","BQIUPD","ERROR")
 K BQIUPD
 ;
 ; 12 MONTH PERIOD
 NEW APCMED,APCMBD,APCMRPT,APCMPBD,APCMPED,APCMTIME,APCMRPTT,BQIPROV,APCMATTE,APCMIND
 NEW CADH
 ;
 ;S APCMED=DT,APCMBD=$$DATE^BQIUL1("T-12M"),APCMRPT=1
 S APCMED=DT,APCMBD=$$DATE^BQIUL1("T-365"),APCMRPT=1
 ;S APCMPBD=$E(APCMBD,1,3)-1_$E(APCMBD,4,7),APCMPED=$E(APCMED,1,3)-1_$E(APCMED,4,7)
 S APCMPED=$$DATE^BQIUL1("T-366"),APCMPBD=$$DATE^BQIUL1("T-731")
 S APCMTIME=1,APCMRPTT=1
 S GLOBAL=$NA(^TMP("BQIMUP1",$J))
 ;
PR ;Find providers
 ; Clean out providers
 NEW DA
 S DA=0 F  S DA=$O(^BQIPROV(DA)) Q:'DA  D
 . I $D(^BQI(90508,1,14,"B",DA)) Q
 . K ^BQIPROV(DA,10)
 . F BJ=2:1:5 S $P(^BQIPROV(DA,0),U,BJ)=""
 ;
 ; Objective Measures
 D FND
 ;
 F X="S1.010.EP","S1.013.EP","S1.014.EP","S1.015.EP","S1.018.EP","S1.020.EP","S1.024.EP","S1.025.EP" D
 . S PROV=""
 . F  S PROV=$O(BQIPROV(PROV)) Q:PROV=""  S APCMATTE(X,PROV)=""
 ;gather up measures for this report
 S X=0 F  S X=$O(^APCMMUM(X)) Q:X'=+X  I $P(^APCMMUM(X,0),U,2)="E" D
 . S APCMIND(X)=""
 . S ID=$P(^APCMMUM(X,0),U,1)
 . I $P(^APCMMUM(X,0),U,6)'="R" Q
 . S PROV=""
 . F  S PROV=$O(BQIPROV(PROV)) Q:PROV=""  S @GLOBAL@(PROV,ID,"CURR")="",@GLOBAL@(PROV,ID,"PREV")="",APCMATTE(ID,PROV)=""
 ;
 S APCMWPP=1
 I $T(BQI^APCM11E1)'="" D BQI^APCM11E1(.GLOBAL,.BQIPROV)
 ;
 D STORP(10)
 K @GLOBAL,BQIPROV
 ;
FC ; Facility
 NEW APCMFAC
 S GLOBAL=$NA(^TMP("BQIMUP",$J)) K @GLOBAL
 ;S APCMED=DT,APCMBD=$$DATE^BQIUL1("T-12M"),APCMRPT=1
 S APCMED=DT,APCMBD=$$DATE^BQIUL1("T-365"),APCMRPT=1
 ;S APCMPBD=$E(APCMBD,1,3)-1_$E(APCMBD,4,7),APCMPED=$E(APCMED,1,3)-1_$E(APCMED,4,7)
 S APCMPED=$$DATE^BQIUL1("T-366"),APCMPBD=$$DATE^BQIUL1("T-731")
 K APCMATTE,APCMIND
 S APCMFAC=$$HME^BQIGPUTL(),BQIPROV(APCMFAC)="",APCMRPTT=2
 K APCMATTE,APCMIND
 F X="S1.009.H","S1.012.H","S1.013.H","S1.014.H","S1.018.H","S1.022.H","S1.023.H","S1.024.H" S APCMATTE(X,APCMFAC)=""
 ;gather up measures for this report
 S X=0 F  S X=$O(^APCMMUM(X)) Q:X'=+X  I $P(^APCMMUM(X,0),U,2)="H" D
 . S APCMIND(X)=""
 . S ID=$P(^APCMMUM(X,0),U,1)
 . I $P(^APCMMUM(X,0),U,6)'="R" Q
 . S @GLOBAL@(APCMFAC,ID,"CURR")="",@GLOBAL@(APCMFAC,ID,"PREV")="",APCMATTE(ID,APCMFAC)=""
 ;
 S APCMWPP=1
 I $T(BQI^APCM11E1)'="" D BQI^APCM11E1(.GLOBAL,.BQIPROV)
 ;
 D STORF(10)
 K @GLOBAL,BQIPROV
 ;
 ;  Set the DATE/TIME MU STOPPED field
 NEW DA
 S DA=$O(^BQI(90508,0)) I 'DA Q
 S BQIUPD(90508,DA_",",8.05)=$$NOW^XLFDT()
 S BQIUPD(90508,DA_",",8.06)="@"
 S BQIUPD(90508,DA_",",24.1)="@"
 D FILE^DIE("","BQIUPD","ERROR")
 K BQIUPD
 Q
 ;
FND ;EP - Find providers
 NEW DA,USRC,PRV
 S PRV=""
 F  S PRV=$O(^BQI(90508,1,14,"B",PRV)) Q:PRV=""  D
 . I +$P($G(^VA(200,PRV,0)),U,11)>0,$P(^(0),U,11)'>DT Q
 . S BQIPROV(PRV)=""
 Q
 ;
STORP(NODE) ; EP - Store Provider data
 S PROV=""
 F  S PROV=$O(@GLOBAL@(PROV)) Q:PROV=""  D
 . ;
 . S $P(^BQIPROV(PROV,0),U,1)=PROV,^BQIPROV("B",PROV,PROV)=""
 . S TOP=APCMBD_"^"_APCMED_"^"_APCMPBD_"^"_APCMPED
 . I NODE=10 F TI=2:1:5 S $P(^BQIPROV(PROV,0),U,TI)=$P(TOP,U,(TI-1))
 . I NODE=20 F TI=6:1:9 S $P(^BQIPROV(PROV,0),U,TI)=$P(TOP,U,(TI-5))
 . S ID=""
 . F  S ID=$O(@GLOBAL@(PROV,ID)) Q:ID=""  D
 .. S IIEN=$O(^APCMMUM("B",ID,"")) I IIEN="" Q
 .. I $P(^APCMMUM(IIEN,0),"^",6)'="R" Q
 .. S CDEN=$P($G(@GLOBAL@(PROV,ID,"CURR")),"^",1),CNUM=$P($G(@GLOBAL@(PROV,ID,"CURR")),"^",2)
 .. S CEXC=$P($G(@GLOBAL@(PROV,ID,"CURR")),"^",3),CADH=$P($G(@GLOBAL@(PROV,ID,"CURR")),"^",4)
 .. S PDEN=$P($G(@GLOBAL@(PROV,ID,"PREV")),"^",1),PNUM=$P($G(@GLOBAL@(PROV,ID,"PREV")),"^",2)
 .. S PEXC=$P($G(@GLOBAL@(PROV,ID,"PREV")),"^",3)
 .. ;
 .. S ^BQIPROV(PROV,NODE,IIEN,0)=ID_"^"_CDEN_"^"_CNUM_"^"_PDEN_"^"_PNUM
 .. I CEXC'="" S ^BQIPROV(PROV,NODE,IIEN,1)=CEXC
 .. I PEXC'="" S ^BQIPROV(PROV,NODE,IIEN,2)=PEXC
 .. I CADH'="" S ^BQIPROV(PROV,NODE,IIEN,3)=CADH
 Q
 ;
STORF(NODE) ;Store data for facility/hospital
 NEW FAC,ID,IIEN,CDEN,PDEN,CNUM,PNUM,CEXC,PEXC
 S FAC=""
 F  S FAC=$O(@GLOBAL@(FAC)) Q:FAC=""  D
 . S TOP=APCMBD_"^"_APCMED_"^"_APCMPBD_"^"_APCMPED
 . S $P(^BQIFAC(FAC,0),U,1)=FAC,^BQIFAC("B",FAC,FAC)=""
 . I NODE=10 F TI=2:1:5 S $P(^BQIFAC(FAC,0),U,TI)=$P(TOP,U,(TI-1))
 . I NODE=20 F TI=6:1:9 S $P(^BQIFAC(FAC,0),U,TI)=$P(TOP,U,(TI-5))
 . S ID=""
 . F  S ID=$O(@GLOBAL@(FAC,ID)) Q:ID=""  D
 .. S IIEN=$O(^APCMMUM("B",ID,"")) I IIEN="" Q
 .. I $P(^APCMMUM(IIEN,0),"^",6)'="R" Q
 .. S CDEN=$P($G(@GLOBAL@(FAC,ID,"CURR")),"^",1),CNUM=$P($G(@GLOBAL@(FAC,ID,"CURR")),"^",2)
 .. S CEXC=$P($G(@GLOBAL@(FAC,ID,"CURR")),"^",3),CADH=$P($G(@GLOBAL@(FAC,ID,"CURR")),"^",4)
 .. S PDEN=$P($G(@GLOBAL@(FAC,ID,"PREV")),"^",1),PNUM=$P($G(@GLOBAL@(FAC,ID,"PREV")),"^",2)
 .. S PEXC=$P($G(@GLOBAL@(FAC,ID,"PREV")),"^",3)
 .. S ^BQIFAC(FAC,NODE,IIEN,0)=ID_"^"_CDEN_"^"_CNUM_"^"_PDEN_"^"_PNUM
 .. I CEXC'="" S ^BQIFAC(FAC,NODE,IIEN,1)=CEXC
 .. I PEXC'="" S ^BQIFAC(FAC,NODE,IIEN,2)=PEXC
 .. I CADH'="" S ^BQIFAC(FAC,NODE,IIEN,3)=CADH
 Q
 ;
NIN ; EP -- BQI UPDATE MEAN USE 90 DAYS
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQI1POJB D UNWIND^%ZTER"
 ;
 ;   Set the DATE/TIME MU STARTED field
 NEW DA
 S DA=$O(^BQI(90508,0)) I 'DA Q
 S BQIUPD(90508,DA_",",4.16)=$$NOW^XLFDT()
 S BQIUPD(90508,DA_",",4.18)=1
 S BQIUPD(90508,DA_",",24.09)=$G(ZTSK)
 D FILE^DIE("","BQIUPD","ERROR")
 K BQIUPD
 ;
 ; 90 DAY PERIOD
 NEW APCMED,APCMBD,APCMRPT,APCMPBD,APCMPED,APCMTIME,APCMRPTT,X,PROV,BQIPROV,APCMATTE,APCMIND,APCMWPP
 S APCMED=DT,APCMBD=$$DATE^BQIUL1("T-90"),APCMRPT=1
 S APCMPED=$$DATE^BQIUL1("T-91"),APCMPBD=$$DATE^BQIUL1("T-181")
 S APCMTIME=1,APCMRPTT=1
 S GLOBAL=$NA(^TMP("BQIMUP9",$J))
 ;
 ;Clean out providers
 S DA=0 F  S DA=$O(^BQIPROV(DA)) Q:'DA  D
 . I $D(^BQI(90508,1,14,"B",DA)) Q
 . K ^BQIPROV(DA,20)
 . F BJ=6:1:9 S $P(^BQIPROV(DA,0),U,BJ)=""
 ;
 D FND
 F X="S1.010.EP","S1.013.EP","S1.014.EP","S1.015.EP","S1.018.EP","S1.020.EP","S1.024.EP","S1.025.EP" D
 . S PROV=""
 . F  S PROV=$O(BQIPROV(PROV)) Q:PROV=""  S APCMATTE(X,PROV)=""
 ;gather up measures for this report
 S X=0 F  S X=$O(^APCMMUM(X)) Q:X'=+X  I $P(^APCMMUM(X,0),U,2)="E" D
 . S APCMIND(X)=""
 . S ID=$P(^APCMMUM(X,0),U,1)
 . I $P(^APCMMUM(X,0),U,6)'="R" Q
 . S PROV=""
 . F  S PROV=$O(BQIPROV(PROV)) Q:PROV=""  S @GLOBAL@(PROV,ID,"CURR")="",@GLOBAL@(PROV,ID,"PREV")="",APCMATTE(ID,PROV)=""
 ;
 S APCMWPP=1
 I $T(BQI^APCM11E1)'="" D BQI^APCM11E1(.GLOBAL,.BQIPROV)
 ;
 D STORP(20)
 K @GLOBAL,BQIPROV,APCMATTE
 ;
 ; 90 DAY PERIOD
 S APCMED=DT,APCMBD=$$DATE^BQIUL1("T-90"),APCMRPT=1
 S APCMPED=$$DATE^BQIUL1("T-91"),APCMPBD=$$DATE^BQIUL1("T-181")
 S APCMTIME=1,APCMRPTT=1
 S APCMFAC=$$HME^BQIGPUTL(),BQIPROV(APCMFAC)="",APCMRPTT=2
 K APCMATTE,APCMIND
 F X="S1.009.H","S1.012.H","S1.013.H","S1.014.H","S1.018.H","S1.022.H","S1.023.H","S1.024.H" S APCMATTE(X,APCMFAC)=""
 ;gather up measures for this report
 S X=0 F  S X=$O(^APCMMUM(X)) Q:X'=+X  I $P(^APCMMUM(X,0),U,2)="H" D
 . S APCMIND(X)=""
 . S ID=$P(^APCMMUM(X,0),U,1)
 . I $P(^APCMMUM(X,0),U,6)'="R" Q
 . S @GLOBAL@(APCMFAC,ID,"CURR")="",@GLOBAL@(APCMFAC,ID,"PREV")="",APCMATTE(ID,APCMFAC)=""
 ;
 S APCMWPP=1
 I $T(BQI^APCM11E1)'="" D BQI^APCM11E1(.GLOBAL,.BQIPROV)
 ;
 D STORF(20)
 K @GLOBAL,BQIPROV
 ;  Set the DATE/TIME MU STOPPED field
 NEW DA
 S DA=$O(^BQI(90508,0)) I 'DA Q
 S BQIUPD(90508,DA_",",4.17)=$$NOW^XLFDT()
 S BQIUPD(90508,DA_",",4.18)="@"
 S BQIUPD(90508,DA_",",24.09)="@"
 D FILE^DIE("","BQIUPD","ERROR")
 K BQIUPD
 Q
