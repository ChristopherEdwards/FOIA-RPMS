BQITRUT2 ;GDIT/HS/ALA-Lab search ; 03 Mar 2015  9:46 AM
 ;;2.5;ICARE MANAGEMENT SYSTEM;;May 24, 2016;Build 27
 ;
 ;
LAB(TMFRAME,RECENT,BQDFN,TAX,SEARCH,TREF) ;EP
 ; Check for a lab test result
 ;
 ; Input
 ;   TMFRAME - Time frame to search data for
 ;   RECENT  - 1=Only check most recent lab,0=Check all within timeframe
 ;   BQDFN   - Patient internal entry number
 ;   TAX     - Lab taxonomy to search
 ;   RESULT  - Lab result to check for
 ;   OPER    - Operand to use for result check
 ;   RES2    - If range, the other result value
 ;   OPER2   - If range, the other result operand
 ;   TREF    - Multiple same resulting taxonomies built
 ;             into reference (usually global)
 ;
 NEW TEMP,EDATE,BDATE,LIEN,QFL,RES,CT,VALUE,VIEN,VSDTM
 S BDATE=$$DATE^BQIUL1(TMFRAME),EDATE=DT
 S TEMP=$NA(^TMP("BQITEMP",UID)) K @TEMP
 S TAX=$G(TAX,""),RECENT=$G(RECENT,0)
 I TAX'="" D
 . S TREF=$NA(^TMP("BQITAX",UID))
 . K @TREF
 . D BLD^BQITUTL(TAX,TREF)
 ;
 S LIEN="",QFL=0,RES=0_U_"No Test",CT=0
 I $G(TMFRAME)'="" D
 . S TIEN=""
 . F  S TIEN=$O(@TREF@(TIEN)) Q:TIEN=""  D
 .. S EDT=9999999-BDATE,BDT=(9999999-EDATE)-.001
 .. I $P($G(^LAB(60,TIEN,0)),U,4)="MI" D  Q
 ... D MIC(BQDFN,TIEN,EDT,BDT,.MICRO)
 ... M @TEMP=MICRO
 ... K MICRO
 .. F  S BDT=$O(^AUPNVLAB("AA",BQDFN,TIEN,BDT)) Q:BDT=""!(BDT>EDT)  D
 ... S LIEN=""
 ... F  S LIEN=$O(^AUPNVLAB("AA",BQDFN,TIEN,BDT,LIEN)) Q:LIEN=""  D
 .... S FLAG=$P($G(^AUPNVLAB(LIEN,11)),U,9) I FLAG="" Q
 .... I FLAG'="R"&(FLAG'="M") Q
 .... S VSDTM=$$GET1^DIQ(9000010,VIEN_",",.01,"I")\1 I VSDTM=0 Q
 .... ;I $G(TMFRAME)'="",VSDTM<BDATE Q
 .... ; quit if deleted flag
 .... I $P($G(^AUPNVSIT(VIEN,0)),U,11)=1 Q
 .... I $P($G(^AUPNVLAB(LIEN,11)),U,9)="D" Q
 .... S VIEN=$P(^AUPNVLAB(LIEN,0),U,3) I VIEN="" Q
 .... NEW LN,LTMP,GLB
 .... S LN="",LTMP="BQILAB" K @LTMP
 .... F  S LN=$O(^AUPNVLAB("AD",VIEN,LN)) Q:LN=""  D
 ..... S GLB=$P($G(^AUPNVLAB(LN,12)),"^",8)
 ..... I GLB'=LIEN Q
 ..... S VALUE=$P(^AUPNVLAB(LN,0),U,4) I VALUE="" Q
 ..... I GLB'="" S @LTMP@(VSDTM,VIEN,LIEN,LN)=VALUE_U_"9000010.09"_U_TIEN
 .... ;
 .... S @TEMP@(VSDTM,VIEN,LIEN)=VALUE_U_"9000010.09"_U_TIEN
 ;
 I $G(TMFRAME)="" D
 . S LIEN="",LTMP="BQILAB" K @LTMP
 . F  S LIEN=$O(^AUPNVLAB("AC",BQDFN,LIEN),-1) Q:LIEN=""  D
 .. S TIEN=$P($G(^AUPNVLAB(LIEN,0)),U,1) I TIEN="" Q
 .. I '$D(@TREF@(TIEN)) Q
 .. ;S VALUE=$P(^AUPNVLAB(LIEN,0),U,4) I VALUE="" Q
 .. S VIEN=$P(^AUPNVLAB(LIEN,0),U,3) I VIEN="" Q
 .. S VSDTM=$$GET1^DIQ(9000010,VIEN_",",.01,"I")\1 I VSDTM=0 Q
 .. ;I $G(TMFRAME)'="",VSDTM<BDATE Q
 .. ; quit if deleted flag
 .. I $P($G(^AUPNVSIT(VIEN,0)),U,11)=1 Q
 .. I $P($G(^AUPNVLAB(LIEN,11)),U,9)="D" Q
 .. NEW LN,GLB
 .. S LN=""
 .. F  S LN=$O(^AUPNVLAB("AD",VIEN,LN)) Q:LN=""  D
 ... S GLB=$P($G(^AUPNVLAB(LN,12)),"^",8)
 ... I GLB'=LIEN Q
 ... S VALUE=$P(^AUPNVLAB(LN,0),U,4) I VALUE="" Q
 ... I GLB'="" S @LTMP@(VSDTM,LIEN,LN)=VALUE_U_"9000010.09"_U_TIEN
 .. I $D(@LTMP) D  ;
 ... S VSDTM=""
 ... S VSDTM=$O(@LTMP@(VSDTM),-1),LIEN=$O(@LTMP@(VSDTM,""),-1)
 ... S LN="" F  S LN=$O(@LTMP@(VSDTM,LIEN,LN),-1) Q:LN=""  D
 .... S VALUE=$P(@LTMP@(VSDTM,LIEN,LN),U,1)
 .... S FILE=$P(@LTMP@(VSDTM,LIEN,LN),U,2)
 .... K ROPER
 .... S RN=""
 .... F  S RN=$O(SEARCH(RN)) Q:RN=""  D  Q:QFL
 ..... S OPER=$P(SEARCH(RN),U,2),RESULT=$P(SEARCH(RN),U,1),OPER2=$P(SEARCH(RN),U,4),RES2=$P(SEARCH(RN),U,3)
 ..... D LCHK
 .. ;S @TEMP@(VSDTM,VIEN,LIEN)=VALUE_U_"9000010.09"_U_TIEN
 . F  S LIEN=$O(^AUPNVMIC("AC",BQDFN,LIEN),-1) Q:LIEN=""  D
 .. S TIEN=$P($G(^AUPNVMIC(LIEN,0)),U,1) I TIEN="" Q
 .. I '$D(@TREF@(TIEN)) Q
 .. S VALUE=$P(^AUPNVMIC(LIEN,0),U,7) I VALUE="" Q
 .. S VIEN=$P(^AUPNVMIC(LIEN,0),U,3) I VIEN="" Q
 .. S VSDTM=$$GET1^DIQ(9000010,VIEN_",",.01,"I")\1 I VSDTM=0 Q
 .. ; quit if deleted flag
 .. I $P($G(^AUPNVSIT(VIEN,0)),U,11)=1 Q
 .. S FLAG=$P($G(^AUPNVMIC(LIEN,11)),U,9)
 .. I FLAG'="R"&(FLAG'="M") Q
 .. S @TEMP@(VSDTM,VIEN,LIEN)=VALUE_U_"9000010.25"_U_TIEN
 ;
 S VSDTM=""
 F  S VSDTM=$O(@TEMP@(VSDTM),-1) Q:VSDTM=""  D  Q:QFL
 . S VIEN=""
 . F  S VIEN=$O(@TEMP@(VSDTM,""),-1) Q:VIEN=""  D  Q:QFL
 .. S LIEN=""
 .. F  S LIEN=$O(@TEMP@(VSDTM,VIEN,""),-1) Q:LIEN=""  D  Q:QFL
 ... S VALUE=$P(@TEMP@(VSDTM,VIEN,LIEN),U,1),OVALUE=VALUE
 ... S FILE=$P(@TEMP@(VSDTM,VIEN,LIEN),U,2)
 ... S TIEN=$P(@TEMP@(VSDTM,VIEN,LIEN),U,3)
 ... K ROPER
 ... S RN=""
 ... F  S RN=$O(SEARCH(RN)) Q:RN=""  D  Q:QFL
 .... S OPER=$P(SEARCH(RN),U,2),RESULT=$P(SEARCH(RN),U,1),OPER2=$P(SEARCH(RN),U,4),RES2=$P(SEARCH(RN),U,3)
 .... D RCHK
 K @TEMP
 Q RES
 ;
RCHK ;
 I OPER="'=",RESULT="",VALUE'="" S RES=1_U_$$FMTE^BQIUL1(VSDTM)_U_VALUE_U_VIEN_U_LIEN_U_TIEN_U_FILE,QFL=1 Q
 ;
 I RESULT'?.N,VALUE?.N Q
 ;
 I RESULT="POS",$E(VALUE,1)'?.N,'$$POSITIVE^BQITRUTL(VALUE) Q
 I RESULT="POS",$E(VALUE,1)'?.N,$$POSITIVE^BQITRUTL(VALUE) D  Q
 . S RES=1_U_$$FMTE^BQIUL1(VSDTM)_U_VALUE_U_VIEN_U_LIEN_U_TIEN_U_FILE,QFL=1
 I RESULT="NEG",$E(VALUE,1)'?.N,'$$NEGATIVE^BQITRUTL(VALUE) Q
 I RESULT="NEG",$E(VALUE,1)'?.N,$$NEGATIVE^BQITRUTL(VALUE) D
 . S RES=1_U_$$FMTE^BQIUL1(VSDTM)_U_VALUE_U_VIEN_U_LIEN_U_TIEN_U_FILE,QFL=1
 I VALUE'?.PN,VALUE'?.N Q
 ;I $E(VALUE,$L(VALUE))?.P S VALUE=VALUE_"0"
 I $E(VALUE,$L(VALUE),$L(VALUE))?.P S VALUE=$E(VALUE,1,$L(VALUE)-1)
 ; if value starts with a punctuation e.g. < or >
 I $E(VALUE,1,1)?.P S ROPER=$E(VALUE,1,1),VALUE=$E(VALUE,2,$L(VALUE))
 I RES2="" D
 . I $G(ROPER)="",@("VALUE"_OPER_"RESULT") D  Q
 .. S RES=1_U_$$FMTE^BQIUL1(VSDTM)_U_OVALUE_U_VIEN_U_LIEN_U_TIEN_U_FILE,QFL=1
 . I $G(ROPER)'="",OPER=ROPER,@("VALUE"_OPER_"RESULT") D  Q
 .. S RES=1_U_$$FMTE^BQIUL1(VSDTM)_U_OVALUE_U_VIEN_U_LIEN_U_TIEN_U_FILE,QFL=1
 . I $G(ROPER)'="",OPER'=ROPER Q
 I RES2'="" D
 . I @("VALUE"_OPER_"RESULT"),@("VALUE"_OPER2_"RES2") D
 .. S RES=1_U_$$FMTE^BQIUL1(VSDTM)_U_OVALUE_U_VIEN_U_LIEN_U_TIEN_U_FILE,QFL=1
 Q
 ;
LCHK ;
 I OPER="'=",RESULT="",VALUE'="" S @TEMP@(VSDTM,VIEN,LN)=VALUE_U_FILE_U_TIEN Q
 ;
 I RESULT'?.N,VALUE?.N Q
 ;
 I RESULT="POS",$E(VALUE,1)'?.N,'$$POSITIVE^BQITRUTL(VALUE) Q
 I RESULT="POS",$E(VALUE,1)'?.N,$$POSITIVE^BQITRUTL(VALUE) D  Q
 . S @TEMP@(VSDTM,VIEN,LN)=VALUE_U_"9000010.25"_U_TIEN
 I RESULT="NEG",$E(VALUE,1)'?.N,'$$NEGATIVE^BQITRUTL(VALUE) Q
 I RESULT="NEG",$E(VALUE,1)'?.N,$$NEGATIVE^BQITRUTL(VALUE) D
 . S @TEMP@(VSDTM,VIEN,LN)=VALUE_U_FILE_U_TIEN
 I VALUE'?.PN,VALUE'?.N Q
 ;I $E(VALUE,$L(VALUE))?.P S VALUE=VALUE_"0"
 I $E(VALUE,$L(VALUE),$L(VALUE))?.P S VALUE=$E(VALUE,1,$L(VALUE)-1)
 ; if value starts with a punctuation e.g. < or >
 I $E(VALUE,1,1)?.P S ROPER=$E(VALUE,1,1),VALUE=$E(VALUE,2,$L(VALUE))
 I RES2="" D
 . I $G(ROPER)="",@("VALUE"_OPER_"RESULT") D  Q
 .. S @TEMP@(VSDTM,VIEN,LN)=VALUE_U_FILE_U_TIEN
 . I $G(ROPER)'="",OPER=ROPER,@("VALUE"_OPER_"RESULT") D  Q
 .. S @TEMP@(VSDTM,VIEN,LN)=VALUE_U_FILE_U_TIEN
 . I $G(ROPER)'="",OPER'=ROPER Q
 I RES2'="" D
 . I @("VALUE"_OPER_"RESULT"),@("VALUE"_OPER2_"RES2") D
 .. S @TEMP@(VSDTM,VIEN,LN)=VALUE_U_FILE_U_TIEN
 Q
 ;
MIC(BQDFN,TIEN,EDT,BDT,MICRO) ;EP - Look through Microbiology file
 NEW FLAG,LIEN,VALUE,VIEN,VSDTM
 K MICRO
 F  S BDT=$O(^AUPNVMIC("AA",BQDFN,TIEN,BDT)) Q:BDT=""!(BDT>EDT)  D
 . S LIEN=""
 . F  S LIEN=$O(^AUPNVMIC("AA",BQDFN,TIEN,BDT,LIEN)) Q:LIEN=""  D
 .. S VALUE=$P(^AUPNVMIC(LIEN,0),U,7) I VALUE="" Q
 .. S VIEN=$P(^AUPNVMIC(LIEN,0),U,3) I VIEN="" Q
 .. S VSDTM=$$GET1^DIQ(9000010,VIEN_",",.01,"I")\1 I VSDTM=0 Q
 .. ; quit if deleted flag
 .. I $P($G(^AUPNVSIT(VIEN,0)),U,11)=1 Q
 .. S FLAG=$P($G(^AUPNVMIC(LIEN,11)),U,9)
 .. I FLAG'="R"&(FLAG'="M") Q
 .. S MICRO(VSDTM,VIEN,LIEN)=VALUE_U_"9000010.25"
 Q
 ;
LBB(TMFRAME,RECENT,DATE,BQDFN,TAX,SEARCH,TREF) ;EP
 I $G(TMFRAME)'="" D
 . I TMFRAME'["-" Q
 . S TMFRAME=$P(TMFRAME,"-",2)
 S BDATE=$$FMADD^XLFDT(DATE,-TMFRAME),EDATE=$$FMADD^XLFDT(DATE,TMFRAME)
 S TEMP=$NA(^TMP("BQITEMP",UID)) K @TEMP
 S TAX=$G(TAX,""),RECENT=$G(RECENT,0)
 I TAX'="" D
 . S TREF=$NA(^TMP("BQITAX",UID))
 . K @TREF
 . D BLD^BQITUTL(TAX,TREF)
 ;
 S LIEN="",QFL=0,RES=0_U_"No Test",CT=0
 D
 . S TIEN=""
 . F  S TIEN=$O(@TREF@(TIEN)) Q:TIEN=""  D
 .. S EDT=9999999-BDATE,BDT=(9999999-EDATE)-.001
 .. I $P($G(^LAB(60,TIEN,0)),U,4)="MI" D  Q
 ... D MIC(BQDFN,TIEN,EDT,BDT,.MICRO)
 ... M @TEMP=MICRO
 ... K MICRO
 .. F  S BDT=$O(^AUPNVLAB("AA",BQDFN,TIEN,BDT)) Q:BDT=""!(BDT>EDT)  D
 ... S LIEN=""
 ... F  S LIEN=$O(^AUPNVLAB("AA",BQDFN,TIEN,BDT,LIEN)) Q:LIEN=""  D
 .... S VALUE=$P(^AUPNVLAB(LIEN,0),U,4) I VALUE="" Q
 .... S VIEN=$P(^AUPNVLAB(LIEN,0),U,3) I VIEN="" Q
 .... S FLAG=$P($G(^AUPNVLAB(LIEN,11)),U,9) I FLAG="" Q
 .... I FLAG'="R"&(FLAG'="M") Q
 .... S VSDTM=$$GET1^DIQ(9000010,VIEN_",",.01,"I")\1 I VSDTM=0 Q
 .... ;I $G(TMFRAME)'="",VSDTM<BDATE Q
 .... ; quit if deleted flag
 .... I $P($G(^AUPNVSIT(VIEN,0)),U,11)=1 Q
 .... I $P($G(^AUPNVLAB(LIEN,11)),U,9)="D" Q
 .... S @TEMP@(VSDTM,VIEN,LIEN)=VALUE_U_"9000010.09"_U_TIEN
 ;
 S VSDTM=""
 F  S VSDTM=$O(@TEMP@(VSDTM),-1) Q:VSDTM=""  D  Q:QFL
 . S VIEN=$O(@TEMP@(VSDTM,""),-1)
 . S LIEN=$O(@TEMP@(VSDTM,VIEN,""),-1)
 . S VALUE=$P(@TEMP@(VSDTM,VIEN,LIEN),U,1),OVALUE=VALUE
 . S FILE=$P(@TEMP@(VSDTM,VIEN,LIEN),U,2)
 . S TIEN=$P(@TEMP@(VSDTM,VIEN,LIEN),U,3)
 . K ROPER
 . S RN=""
 . F  S RN=$O(SEARCH(RN)) Q:RN=""  D  Q:QFL
 .. S OPER=$P(SEARCH(RN),U,2),RESULT=$P(SEARCH(RN),U,1),OPER2=$P(SEARCH(RN),U,4),RES2=$P(SEARCH(RN),U,3)
 .. D RCHK
 K @TEMP
 Q RES
