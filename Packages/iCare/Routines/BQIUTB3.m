BQIUTB3 ;PRXM/HC/ALA-Taxonomy table ; 20 Jun 2007  11:40 AM
 ;;2.1;ICARE MANAGEMENT SYSTEM;;Feb 07, 2011
 ;
 Q
 ;
EN(DATA,TAXTY) ;EP -- BQI GET TAXONOMY LIST **for future use**
 NEW UID,II
 S UID=$S($G(ZTSK):"Z"_ZTSK,1:$J)
 S DATA=$NA(^TMP("BQIUTB3",UID))
 K @DATA
 S II=0
 NEW $ESTACK,$ETRAP S $ETRAP="D ERR^BQIUTB1 D UNWIND^%ZTER" ; SAC 2006 2.2.3.3.2
 S @DATA@(II)="I00010IEN^T00030TAX_NAME"_$C(30)
 S TAXTY=$G(TAXTY,""),FILE=""
 I TAXTY'="" S FILE=$S(TAXTY="DX":80,TAXTY="M":50,TAXTY="CP":81,TAXTY="PR":80.1,1:"")
 S TN=0
 F  S TN=$O(^ATXAX(TN)) Q:'TN  D
 . I FILE'="",$P(^ATXAX(TN,0),U,15)'=FILE Q
 . I $P(^ATXAX(TN,0),U,12)="" Q
 . I $O(^ATXAX(TN,21,0))="" Q
 . S NM=$P(^ATXAX(TN,0),U,1)
 . S Z(NM,TN)=""
 ;
 S NM=""
 F  S NM=$O(Z(NM)) Q:NM=""  D
 . S TN=""
 . F  S TN=$O(Z(NM,TN)) Q:TN=""  S II=II+1,@DATA@(II)=TN_U_NM_$C(30)
 ;
 S II=II+1,@DATA@(II)=$C(31)
 Q
 ;
ALG(DATA) ;Get a list of allergies
 NEW TEXT,IEN,VALUE
 S II=0
 S @DATA@(II)="T00080IEN^T00080"_$C(30)
 S VALUE=$NA(^TMP("BQIALGY",UID)) K @VALUE
 S IEN=0
 F  S IEN=$O(^GMR(120.8,IEN)) Q:'IEN  D
 . S TEXT=$P($G(^GMR(120.8,IEN,0)),U,2) I TEXT="" Q
 . I $P($G(^GMR(120.8,IEN,"ER")),U,1)=1 Q
 . S TEXT=$$STRIP^BQIUL1(TEXT," ")
 . S @VALUE@(TEXT)=""
 ;
 S TEXT=""
 F  S TEXT=$O(@VALUE@(TEXT)) Q:TEXT=""  S II=II+1,@DATA@(II)=TEXT_U_TEXT_$C(30)
 S II=II+1,@DATA@(II)=$C(31)
 K @VALUE
 Q
